archetype (adl_version=1.4; uid=67171bf1-2026-42d2-8152-69a0c2304580)
	openEHR-EHR-EVALUATION.smokeless_tobacco_summary.v0

concept
	[at0000]	-- Smokeless tobacco summary
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2009-06-21">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record summary or persistent information about smokeless tobacco using habit of an individual.">
			use = <"Use to record summary or persistent information about smokeless tobacco using habit of an individual.

This archetype is to be used to record information about both current and previous smokeless tobacco usage.

The specific focus of this archetype is on documentation about the use of all types of smokeless tobacco usage because of the associated health risks from this type of consumption.

The specific scope of this archetype is on documentation about the use of all types of smokeless tobacco because of the associated health risks from this kind of consumption. Amount of nicotine and tar, use of bags and additives has been left outside of scope for the core archetype, but could be added into the Episode SLOT if required. 

The 'Per type' cluster of data elements allows for recording of specific details and episodes about each type of tobacco consumed and can be repeated once per type. The list of tobacco types listed in the 'Per type' run-time name constraint identifies the type of smokeless tobacco. This name constraint can be applied during template modelling or at run-time within a software application.

In many situations the individual will only use one type of tobacco, such as snus or snuff.  If other types of tobacco are used the details will be recorded in another instance of the 'Per type' cluster.

The history of waxing and waning of use for each type of tobacco over time can be captured using the repeatable 'Per episode' cluster. This cluster of data elements allows for a very detailed pattern of tobacco usage behaviour to be recorded for each type of tobacco used such as daily usage of snus, alongside occasional use of chewing tobacco while on holiday. 

Triggers for closing one episode and commencing a new one will largely reflect local data collection preferences, including if the individual: 
- quits for a significant period of time (which will likely be locally defined); or
- significantly changes their amount of use or pattern of their usage.

Use to incorporate the narrative descriptions of smokeless tobacco usage habits within existing or legacy clinical systems into an archetyped format, using the 'Overall description' data element.">
			keywords = <"tobacco", "snus", "snuff", "chewing", "dip", "gutka">
			misuse = <"Not to be used to record event-or period-based information about usage of smokeless tobacco, such as actual daily use or the average use over a specified period of time - use the OBSERVATION.smokeless_tobacco archetype.

Not to be used for recording any other administration of nicotine, such as e-cigarettes, nicotine patches or nicotine chewing gum. Use seoparate archetypes for this purpose. 

Not to be used to record information about tobacco smoking. Use the EVALUATION.tobacco_smoking_summary archetype for this purpose.">
			copyright = <"© openEHR Foundation, Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Heather Leslie, Ocean Informatics, Australia (Editor)", "Ian McNicoll, Ocean Informatics UK, United Kingdom", "John Tore Valand, Helse Bergen HF, Norway", "Silje Ljosland Bakke, Nasjonal IKT, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"Nasjonal IKT">
		["references"] = <"Hedström, A. K., Bäärnhielm, M., Olsson, T., & Alfredsson, L. (2009). Tobacco smoking, but not Swedish snuff use, increases the risk of multiple sclerosis. Neurology, 73(9), 696-701. doi: http://dx.doi.org/10.1212/WNL.0b013e3181b59c40

Tobacco Use Summary, Draft Archetype [Internet]. nehta, Australia, nehta Clinical Knowledge Manager [cited: 2016-06-06]. Available from: http://dcm.nehta.org.au/ckm/#showArchetype_1013.1.1026.

Tobacco Use Summary, Rejected Archetype [Internet]. openEHR Foundation, openEHR Clinical Knowledge Manager [cited: 2016-06-06]. Available from: http://www.openehr.org/ckm/#showArchetype_1013.1.1520.">
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["original_namespace"] = <"no.nasjonalikt">
		["original_publisher"] = <"Nasjonal IKT">
		["custodian_namespace"] = <"no.nasjonalikt">
		["MD5-CAM-1.0.1"] = <"D8DFA947DC2E040CA464C52024BBB849">
		["build_uid"] = <"233cbb34-c065-4094-9fb8-d0b0842ac5c8">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {	-- Smokeless tobacco summary
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0089] occurrences matches {0..1} matches {	-- Overall status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0006, 	-- Never used
									at0003, 	-- Current user
									at0005]	-- Former user
								}
							}
						}
					}
					ELEMENT[at0043] occurrences matches {0..1} matches {	-- Overall description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0015] occurrences matches {0..1} matches {	-- Regular smokeless tobacco use commenced
						value matches {
							DV_DATE matches {*}
						}
					}
					ELEMENT[at0080] occurrences matches {0..1} matches {	-- Daily smokeless tobacco use commenced
						value matches {
							DV_DATE matches {*}
						}
					}
					CLUSTER[at0029] occurrences matches {0..*} matches {	-- Per type
						name matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0056, 	-- Chewing tobacco
									at0066, 	-- Dipping tobacco
									at0078, 	-- Dissolvable tobacco
									at0057, 	-- Gutka
									at0092, 	-- Iqmik
									at0062, 	-- Naswar
									at0055, 	-- Snuff
									at0093, 	-- Snuff creamy
									at0054, 	-- Snus
									at0088]	-- Topical tobacco paste
								}
							}
						}
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0052] occurrences matches {0..1} matches {	-- Status
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0061, 	-- Current user
											at0059, 	-- Former user
											at0091]	-- Never used
										}
									}
								}
							}
							ELEMENT[at0053] occurrences matches {0..1} matches {	-- Description
								value matches {
									DV_TEXT matches {*}
								}
							}
							CLUSTER[at0064] occurrences matches {0..*} matches {	-- Per episode
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0081] occurrences matches {0..1} matches {	-- Episode label
										value matches {
											DV_COUNT matches {
												magnitude matches {|>=1|}
											}
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0013] occurrences matches {0..1} matches {	-- Episode start date
										value matches {
											DV_DATE matches {*}
										}
									}
									ELEMENT[at0082] occurrences matches {0..1} matches {	-- Episode end date
										value matches {
											DV_DATE matches {*}
										}
									}
									ELEMENT[at0030] occurrences matches {0..1} matches {	-- Pattern
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0083, 	-- Daily
													at0084]	-- Non-daily
												}
											}
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0023] occurrences matches {0..1} matches {	-- Typical use (units)
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::382]>
												list = <
													["1"] = <
														units = <"1/d">
													>
													["2"] = <
														units = <"1/wk">
													>
												>
											>
										}
									}
									ELEMENT[at0065] occurrences matches {0..1} matches {	-- Typical use (mass)
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::347]>
												list = <
													["1"] = <
														units = <"g/d">
														magnitude = <|>=0.0|>
														precision = <|0|>
													>
													["2"] = <
														units = <"g/wk">
														magnitude = <|>=0.0|>
														precision = <|0|>
													>
													["3"] = <
														units = <"[oz_av]/d">
													>
													["4"] = <
														units = <"[oz_av]/wk">
													>
												>
											>
										}
									}
									ELEMENT[at0025] occurrences matches {0..1} matches {	-- Number of quit attempts
										value matches {
											DV_COUNT matches {
												magnitude matches {|>=0|}
											}
										}
									}
									allow_archetype CLUSTER[at0026] occurrences matches {0..*} matches {	-- Episode details
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.cessation_attempts(-[a-zA-Z0-9_]+)*\.v1/}
									}
									ELEMENT[at0087] occurrences matches {0..1} matches {	-- Episode comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
							ELEMENT[at0014] occurrences matches {0..1} matches {	-- Quit date
								value matches {
									DV_DATE matches {*}
								}
							}
							allow_archetype CLUSTER[at0077] occurrences matches {0..*} matches {	-- Type details
								include
									archetype_id/value matches {/openEHR-EHR-CLUSTER\.change(-[a-zA-Z0-9_]+)*\.v1/}
							}
							ELEMENT[at0017] occurrences matches {0..1} matches {	-- Pack years
								value matches {
									DV_COUNT matches {
										magnitude matches {|>=0|}
									}
								}
							}
							ELEMENT[at0069] occurrences matches {0..1} matches {	-- Comment
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
					allow_archetype CLUSTER[at0086] occurrences matches {0..*} matches {	-- Overall details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.change(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0016] occurrences matches {0..1} matches {	-- Overall quit date
						value matches {
							DV_DATE matches {*}
						}
					}
					ELEMENT[at0074] occurrences matches {0..1} matches {	-- Overall pack years
						value matches {
							DV_COUNT matches {
								magnitude matches {|>=0|}
							}
						}
					}
					ELEMENT[at0019] occurrences matches {0..1} matches {	-- Overall comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0021] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0073] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0071] occurrences matches {0..*} matches {	-- Quit date definition
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0085] occurrences matches {0..1} matches {	-- Quit attempt definition
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0075] occurrences matches {0..*} matches {	-- Current user definition
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0076] occurrences matches {0..*} matches {	-- Former  user definition
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0079] occurrences matches {0..*} matches {	-- Never used definition
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0072] occurrences matches {0..1} matches {	-- Pack definiton
						value matches {
							DV_COUNT matches {
								magnitude matches {|>=0|}
							}
							C_DV_QUANTITY <
								property = <[openehr::124]>
								list = <
									["1"] = <
										units = <"g">
										precision = <|0|>
									>
									["2"] = <
										units = <"[oz_av]">
										precision = <|0|>
									>
								>
							>
						}
					}
					ELEMENT[at0022] occurrences matches {0..1} matches {	-- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}





ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Smokeless tobacco summary">
					description = <"Summary or persistent information about smokeless tobacco using habit of an individual.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Current user">
					description = <"The individual is a current user of any type of smokeless tobacco.">
				>
				["at0005"] = <
					text = <"Former user">
					description = <"The individual has previously used smokeless tobacco, but is not a current user.">
				>
				["at0006"] = <
					text = <"Never used">
					description = <"Individual has never used any type of smokeless tobacco.">
				>
				["at0013"] = <
					text = <"Episode start date">
					description = <"Date when this episode commenced.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0014"] = <
					text = <"Quit date">
					description = <"Date when the individual last used the specified type of tobacco.">
					comment = <"Can be a partial date, for example, only a year. Definitions for a 'Quit date' vary enormously and can be defined using the 'Quit data definition' data element in the Protocol section of this archetype. This date will be identical to the 'Episode end date' for the most recent episode. This date could be used by decision support guidance to determine if the  individual is at risk of relapse, for example in the first 12 months since quitting.">
				>
				["at0015"] = <
					text = <"Regular smokeless tobacco use commenced">
					description = <"The date or partial date when the individual first started frequent or regular, but non-daily, using of smokeless tobacco of any type.">
					comment = <"Can be a partial date, for example, only a year. For example, this date could represent when the individual commenced using smokeless tobacco every Friday night or at parties.">
				>
				["at0016"] = <
					text = <"Overall quit date">
					description = <"The date when the individual last ceased using smokeless tobacco of any type.">
					comment = <"This date could be used by decision support guidance to determine if the  individual is at risk of relapse, for example in the first 12 months since quitting.">
				>
				["at0019"] = <
					text = <"Overall comment">
					description = <"Additional narrative about the all use of smokeless tobacco that has not captured in other fields.">
					comment = <"For example: stopped smoking or reduced amount on becoming pregnant.">
				>
				["at0021"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0022"] = <
					text = <"Last updated">
					description = <"The date this smokeless tobacco summary was last updated.">
				>
				["at0023"] = <
					text = <"Typical use (units)">
					description = <"Estimate of number of units of the specified type of tobacco consumed.">
					comment = <"For example: the number of 'bags' or 'portions' per day or per week. This data element is redundant if a value is recorded for 'Typical use(mass)'.">
				>
				["at0025"] = <
					text = <"Number of quit attempts">
					description = <"Number of times the individual has attempted to stop using the specified type of tobacco.">
				>
				["at0026"] = <
					text = <"Episode details">
					description = <"Additional structured details about the specified episode of tobacco using.">
				>
				["at0029"] = <
					text = <"Per type">
					description = <"Details about smoking activity for a specified type of inhaled tobacco.">
					comment = <"The run-time name constraint on this Cluster enables simple templates for each required type to be designed in templates, or the types to be managed exclusively at run-time. The list of names can be extended at run-time if additional types of tobacco smoking are identified locally.">
				>
				["at0030"] = <
					text = <"Pattern">
					description = <"The typical pattern of use for the specified type of tobacco.">
					comment = <"The typical pattern of use can be coded with a terminology or a local value set in a template.">
				>
				["at0043"] = <
					text = <"Overall description">
					description = <"Narrative summary about the individual's overall smokeless tobacco using pattern and history.">
					comment = <"Use this data element to record a narrative description only where the structured data does not adequately reflect the smokeless tobacco using habits for this individual or to incorporate unstructured smokeless tobacco using information from existing or legacy clinical systems into an archetyped format.">
				>
				["at0052"] = <
					text = <"Status">
					description = <"Statement about current smokeless tobacco behaviour for the specified type of tobacco.">
				>
				["at0053"] = <
					text = <"Description">
					description = <"Narrative summary about the using behaviour for the specified type of tobacco.">
				>
				["at0054"] = <
					text = <"Snus">
					description = <"A type tobacco that is placed under the upper lip. Comes as a loose variant or prepackaged into pouches.">
				>
				["at0055"] = <
					text = <"Snuff">
					description = <"A type of tobacco that is inhaled or \"snuffed\" into the nasal cavity.">
				>
				["at0056"] = <
					text = <"Chewing tobacco">
					description = <"A type of tobacco that is chewed. Comes in several different varieties. For example: Loose leaf, plug or twist.">
				>
				["at0057"] = <
					text = <"Gutka">
					description = <"A combination of betel leaf, areca nut, and slaked lime mixed with tobacco.">
				>
				["at0059"] = <
					text = <"Former user">
					description = <"The individual has previously used smokeless tobacco, but is not a current user.">
				>
				["at0061"] = <
					text = <"Current user">
					description = <"The individual is a current user of any type of smokeless tobacco.">
				>
				["at0062"] = <
					text = <"Naswar">
					description = <"An Afghan tobacco product similar to dipping tobacco.">
				>
				["at0064"] = <
					text = <"Per episode">
					description = <"Details about a discrete period of using activity for the specified type of tobacco.">
				>
				["at0065"] = <
					text = <"Typical use (mass)">
					description = <"Estimate of the weight of loose tobacco used.">
					comment = <"This data element will typically be used for loose tobacco and is redundant if a value is recorded for 'Typical use (units)'.">
				>
				["at0066"] = <
					text = <"Dipping tobacco">
					description = <"A type of tobacco that is placed between the lower or upper lip and gums.">
				>
				["at0069"] = <
					text = <"Comment">
					description = <"Additional narrative about smokeless tobacco use of the specified type of tobacco, not captured in other fields.">
				>
				["at0071"] = <
					text = <"Quit date definition">
					description = <"The applied definition for the 'Quit date' data elements used in this archetype.">
				>
				["at0073"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0075"] = <
					text = <"Current user definition">
					description = <"The applied definition for the 'Current user' value in each of the 'Status' data elements used in this archetype.">
					comment = <"Definition may need to be specified per type.">
				>
				["at0076"] = <
					text = <"Former  user definition">
					description = <"The applied definition for the 'Former user' value in each of the 'Status' data elements used in this archetype.">
					comment = <"Definition may need to be specified per type.">
				>
				["at0077"] = <
					text = <"Type details">
					description = <"Additional structured details about the specified type of tobacco using.">
				>
				["at0078"] = <
					text = <"Dissolvable tobacco">
					description = <"A variation on chewing tobacco that completely dissolves in the mouth.">
					comment = <"Additional details could be recorded using the Fagerstrom ">
				>
				["at0079"] = <
					text = <"Never used definition">
					description = <"The applied definition for the 'Never used' value in each of the 'Status' data elements used in this archetype.">
					comment = <"Definition may need to be specified per type. For example, the definition may not be zero, but less than a specified amount (as units or mass) smoked during a specified time interval.">
				>
				["at0080"] = <
					text = <"Daily smokeless tobacco use commenced">
					description = <"The date or partial date when the individual first started daily daily use of smokeless tobacco of any type.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0081"] = <
					text = <"Episode label">
					description = <"Identification of an episode of smokeless tobacco use - either as a number in a sequence and/or a named event.">
					comment = <"For example: '2' as the second episode within a sequence of episodes; or 'Pregnancy with twins' if describing the use of smokeless tobacco during a health event such as during a specific pregnancy.">
				>
				["at0082"] = <
					text = <"Episode end date">
					description = <"Date when this episode ceased.">
					comment = <"Can be a partial date, for example, only a year. This data field will be empty if the episode is current and ongoing.">
				>
				["at0083"] = <
					text = <"Daily">
					description = <"Use of the specified type of tobacco at least once every day.">
				>
				["at0084"] = <
					text = <"Non-daily">
					description = <"Not using the specified type of tobacco every day.">
				>
				["at0085"] = <
					text = <"Quit attempt definition">
					description = <"The applied definition for a Quit attempt used to determine value for the 'Number of quit attempts' data element used in this archetype.">
				>
				["at0086"] = <
					text = <"Overall details">
					description = <"Additional structured details about the overall smokeless tobacco using behaviour.">
				>
				["at0087"] = <
					text = <"Episode comment">
					description = <"Additional narrative about the use of tobacco during the specified episode, not captured in other fields.">
				>
				["at0088"] = <
					text = <"Topical tobacco paste">
					description = <"A paste applied to the skin and absorbed through the dermis.">
				>
				["at0089"] = <
					text = <"Overall status">
					description = <"Statement about current snus behaviour for all types of tobacco.">
				>
				["at0091"] = <
					text = <"Never used">
					description = <"Individual has never used any type of smokeless tobacco.">
				>
				["at0092"] = <
					text = <"Iqmik">
					description = <"An Alaskan tobacco product which also contains punk ash.">
				>
				["at0093"] = <
					text = <"Snuff creamy">
					description = <"A fluid tobacco mixture marketed as a dental hygiene aid, albeit used for recreation.">
				>
				["at0017"] = <
					text = <"Pack years">
					description = <"Estimate of the cumulative amount of tobacco used using the specified type of smokeless tobacco.">
					comment = <"Commonly used in assessment of snus use. It is calculated by multiplying the number of packs of snus used per day by the number of years the individual has used. One packet-year is the
equivalent of consuming one packet of snuff daily for 1 year. The definition of a pack can be recorded in the protocol of this archetype using the 'Pack definition' data element.">
				>
				["at0074"] = <
					text = <"Overall pack years">
					description = <"Estimate of the cumulative amount for all types of smokeless tobacco used.">
					comment = <"The definition of a pack can be recorded in the protocol of this archetype using the 'Pack definition' data element.">
				>
				["at0072"] = <
					text = <"Pack definiton">
					description = <"The definition of the size of pack used as part of the algorithm for calculating 'Pack years' data elements used in this archetype.">
					comment = <"Definition may need to be specified per type. For example, the number of grams in a pack used for snus may be different in different types of boxes; the mass of loose tobacco in a pack used for loose types of smokelsess tobacco.">
				>
			>
		>
	>

archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.exam_auditory_canal.v1

concept
	[at0000]	-- Examination of external auditory canal
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2012-09-03">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording the findings on physical examination of the external auditory canal by a clinician.">
			use = <"Use to record the findings on physical examination of the external auditory canal by a clinician.

Designed to be used within the CLUSTER.exam_ears archetype, but may be used within any relevant archetype SLOT related to physical examination.

Please note: this archetype has been designed specifically to reflect current practice within the NT Hearing Health program. It is recognised that this is not currently reflecting a maximal dataset and may need to be extended in future revisions as more specific requirements are identified.">
			keywords = <"inspection", "ear", "tympanic", "perforation", "drum", "canal", "otoscopic", "exam", "examination", "pneumatic", "otoscopy", "membrane">
			misuse = <"Not to be used to record findings of the physical examination of other parts of the ear, such as pinna and mastoid region - use other purpose-built archetypes for this purpose.">
			copyright = <"© National E-Health Transition Authority">
		>
	>
	lifecycle_state = <"CommitteeDraft">
	other_contributors = <"Stephen Chu, NEHTA, Australia", "Harvey Coates, UWA, Australia", "Marissa Cookson, Dept of Health, Australia", "Kimberley Crebbin, NT Hearing Services, Australia", "Graeme Crossland, Northern Territory, Australia", "Kathy Currie, Northern Territory Health, Australia", "Jacques de Haan, Alice Springs Hospital, Australia", "Jade Frederiksen, Hearing Health Program, DoH, Australia", "Sam Heard, Ocean Informatics, Australia (Editor)", "Paul Hickey, Hearing Health Program, Australia", "Oliver Hosking, Remote Health NT, Australia", "Amanda Leach, Menzies School of Health Research, Australia", "Anthony Leech, Hearing Health, Australia", "Kerrie Lee, Ngaanyatjarra Health Service, Australia", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Ian McNicoll, Ocean Informatics, United Kingdom", "Hemi Patel, Royal Darwin Hospital, Australia", "Mark Ramjan, NT Department of Health, Australia", "Fiona Wake, RAHC, Australia", "Christine Wigger, Menzies School of Health Research, Australia">
	other_details = <
		["references"] = <"Communication with clinical domain experts associated with Australia's Northern Territory Hearing Health Program.

Northern Territory Government, Department of Health: Hearing Health Program ENT Specialist Consultation Form 2011 12 07.

Northern Territory Government, Department of Health: Northern Territory Hearing Health Program Teleotology Nurse Consultation Form 2011 12 07.

Northern Territory Government, Department of Health: Northern Territory Hearing Health Program Audiologist Consultation Form 2011 12 07.

OATSIH:  The Recommendations for Clinical Care Guidelines on the Management of Otitis Media in Aboriginal and Torres Strait Islander Populations (2010). Available at: http://www.health.gov.au/internet/publications/publishing.nsf/Content/CA2578620005D57ACA25792800821BE8?OpenDocument.">
		["MD5-CAM-1.0.1"] = <"3226F0B564A0B491E8B32BBFC323B024">
	>

definition
	CLUSTER[at0000] matches {	-- Examination of external auditory canal
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0097] occurrences matches {0..1} matches {	-- Ear examined
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0098, 	-- Left
							at0099]	-- Right
						}
					}
				}
			}
			ELEMENT[at0189] occurrences matches {0..1} matches {	-- No abnormality detected
				value matches {
					DV_BOOLEAN matches {
						value matches {True}
					}
				}
			}
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0193] occurrences matches {0..1} matches {	-- Description of wax
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0106] occurrences matches {0..1} matches {	-- Consistency of wax
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0108, 	-- Soft
							at0107]	-- Hard
						}
					}
				}
			}
			ELEMENT[at0109] occurrences matches {0..1} matches {	-- State of wax
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0111, 	-- Not impacted
							at0110]	-- Impacted
						}
					}
				}
			}
			ELEMENT[at0139] occurrences matches {0..1} matches {	-- Tenderness of canal
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0140, 	-- Present
							at0141]	-- Absent
						}
					}
				}
			}
			ELEMENT[at0132] occurrences matches {0..1} matches {	-- Oedema of canal
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0133, 	-- Present
							at0134]	-- Absent
						}
					}
				}
			}
			ELEMENT[at0124] occurrences matches {0..1} matches {	-- Erythema of canal
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0125, 	-- Present
							at0126]	-- Absent
						}
					}
				}
			}
			ELEMENT[at0143] occurrences matches {0..1} matches {	-- Offensive odour
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0145, 	-- Present
							at0144]	-- Absent
						}
					}
				}
			}
			ELEMENT[at0190] occurrences matches {0..1} matches {	-- Discharge presence
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0191, 	-- Present
							at0192]	-- Absent
						}
					}
				}
			}
			ELEMENT[at0157] occurrences matches {0..1} matches {	-- Discharge type
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0151] occurrences matches {0..1} matches {	-- Discharge amount
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0153, 	-- Scant
							at0154, 	-- Moderate
							at0155]	-- Profuse
						}
					}
				}
			}
			ELEMENT[at0156] occurrences matches {0..1} matches {	-- Discharge description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0159] occurrences matches {0..1} matches {	-- Foreign body presence
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0160, 	-- Present
							at0161]	-- Absent
						}
					}
				}
			}
			ELEMENT[at0163] occurrences matches {0..1} matches {	-- Foreign body location
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0164, 	-- Outer Canal
							at0165, 	-- Deep Canal
							at0183]	-- Middle ear
						}
					}
				}
			}
			CLUSTER[at0064] occurrences matches {0..*} matches {	-- Specific finding
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0112] matches {	-- Finding
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0114, 	-- Polyp
									at0185, 	-- Fungal spores
									at0186, 	-- Exostosis
									at0187, 	-- Canal Stenosis
									at0188]	-- Mastoid Bowl
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0147] occurrences matches {0..1} matches {	-- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
			ELEMENT[at0172] occurrences matches {0..*} matches {	-- Multimedia representation
				value matches {
					DV_MULTIMEDIA matches {
						media_type matches {
							[openEHR::
							425, 
							426, 
							427, 
							428, 
							429, 
							401, 
							402, 
							404, 
							405, 
							406, 
							407, 
							414]
						}
					}
				}
			}
			ELEMENT[at0105] occurrences matches {0..*} matches {	-- Clinical interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0173] occurrences matches {0..*} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0194] occurrences matches {0..*} matches {	-- Exam not done
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
			}
			allow_archetype CLUSTER[at0195] occurrences matches {0..*} matches {	-- Tympanic membrane examination
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exam_tympanic_membrane(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination of external auditory canal">
					description = <"Physical examination of the external auditory canal
 by a clinician.">
				>
				["at0001"] = <
					text = <"Clinical description">
					description = <"Narrative description of the overall findings observed by a clinician during a physical examination of the external auditory canal.">
					comment = <"Use this data element to provide additional, narrative description of any data elements related to the external auditory canal and tympanic membrane examination as a whole, that are not represented by structured values. For example, the relative size and surface of the auditory canal; or increased vascularity of the tympanic membrane.">
				>
				["at0064"] = <
					text = <"Specific finding">
					description = <"Details about specific features noted during the examination of the external auditory canal.">
				>
				["at0097"] = <
					text = <"Ear examined">
					description = <"Identification of the ear under examination.">
				>
				["at0098"] = <
					text = <"Left">
					description = <"The left external auditory canal and tympanic membrane.">
				>
				["at0099"] = <
					text = <"Right">
					description = <"The right external auditory canal and tympanic membrane.">
				>
				["at0105"] = <
					text = <"Clinical interpretation">
					description = <"Single word, phrase or brief description representing a summary of the examination findings.">
					comment = <"Coding with a terminology is preferred, if possible. For example, normal examination or chronic otitis media.">
				>
				["at0106"] = <
					text = <"Consistency of wax">
					description = <"Description of the type of wax in the external auditory canal.">
					comment = <"Presence of hard wax has implications on treatment decisions. Presence of soft wax may be a causative factor in visual occlusion.">
				>
				["at0107"] = <
					text = <"Hard">
					description = <"The wax is hard.">
				>
				["at0108"] = <
					text = <"Soft">
					description = <"The wax is soft.">
				>
				["at0109"] = <
					text = <"State of wax">
					description = <"Description of the state of the wax observed in the external auditory canal.">
				>
				["at0110"] = <
					text = <"Impacted">
					description = <"Ear wax is tightly packed in the external auditory canal, such that it may be hard to remove or cause difficulty with hearing.">
				>
				["at0111"] = <
					text = <"Not impacted">
					description = <"Ear wax is not tightly packed in the external auditory canal.">
				>
				["at0112"] = <
					text = <"Finding">
					description = <"Name of the identified finding. ; presence of abnormalities not captured in specific structured data elements, such as presence of canal stenosis, exostoses, mastoid bowl/cavity or tumours.">
				>
				["at0114"] = <
					text = <"Polyp">
					description = <"A polyp is observed within the external auditory canal.">
				>
				["at0124"] = <
					text = <"Erythema of canal">
					description = <"Presence of erythema or redness in the external auditory canal.">
				>
				["at0125"] = <
					text = <"Present">
					description = <"Erythema, or redness, is observed in the wall of the external auditory canal.">
				>
				["at0126"] = <
					text = <"Absent">
					description = <"There is no erythema or redness observed in the wall of the external auditory canal.">
				>
				["at0132"] = <
					text = <"Oedema of canal">
					description = <"Presence of oedema in the external auditory canal.">
				>
				["at0133"] = <
					text = <"Present">
					description = <"Oedema is observed in the external auditory canal wall.">
				>
				["at0134"] = <
					text = <"Absent">
					description = <"Oedema is not observed in the external auditory canal wall.">
				>
				["at0139"] = <
					text = <"Tenderness of canal">
					description = <"Presence of tenderness in the external auditory canal.">
				>
				["at0140"] = <
					text = <"Present">
					description = <"Tenderness is noted in the external auditory canal wall.">
				>
				["at0141"] = <
					text = <"Absent">
					description = <"Tenderness is not noted in the external auditory canal wall.">
				>
				["at0143"] = <
					text = <"Offensive odour">
					description = <"Presence of any offensive odour originating from the external auditory canal.">
				>
				["at0144"] = <
					text = <"Absent">
					description = <"No odour is noted as originating from the external auditory canal.">
				>
				["at0145"] = <
					text = <"Present">
					description = <"An offensive odour is noted as originating from the external auditory canal.">
				>
				["at0147"] = <
					text = <"Description">
					description = <"Narrative description of the identified finding.">
				>
				["at0151"] = <
					text = <"Discharge amount">
					description = <"Amount of discharge observed in the external auditory canal or at the tympanic membrane perforation.">
				>
				["at0153"] = <
					text = <"Scant">
					description = <"A small amount of discharge is observed in the external auditory canal.">
				>
				["at0154"] = <
					text = <"Moderate">
					description = <"A moderate amount of discharge is observed in the external auditory canal.">
				>
				["at0155"] = <
					text = <"Profuse">
					description = <"A profuse amount of discharge is observed in the external auditory canal.">
				>
				["at0156"] = <
					text = <"Discharge description">
					description = <"Narrative description of the discharge observed in the external auditory canal or at the tympanic membrane perforation.">
				>
				["at0157"] = <
					text = <"Discharge type">
					description = <"Type of discharge observed in the external auditory canal or at the tympanic membrane.">
					comment = <"For example: serous; bloody; purulent; mucoid; mucopurulent; or haemoserous.">
				>
				["at0159"] = <
					text = <"Foreign body presence">
					description = <"Presence of a foreign body in the external auditory canal.">
				>
				["at0160"] = <
					text = <"Present">
					description = <"A foreign body is present in the external auditory canal.">
				>
				["at0161"] = <
					text = <"Absent">
					description = <"A foreign body is present in the external auditory canal.">
				>
				["at0163"] = <
					text = <"Foreign body location">
					description = <"Location of the foreign body within the external auditory canal.">
				>
				["at0164"] = <
					text = <"Outer Canal">
					description = <"The foreign body is located in the outer part of the external auditory canal.">
				>
				["at0165"] = <
					text = <"Deep Canal">
					description = <"The foreign body is located in the deep, inner part of the external auditory canal.">
				>
				["at0172"] = <
					text = <"Multimedia representation">
					description = <"Image or video taken during the physical examination of the external auditory canal and tympanic membrane.">
					comment = <"Multiple occurrences allow for pre- and post-cleaning images to be recorded as part of the examination findings.">
				>
				["at0173"] = <
					text = <"Comment">
					description = <"Additional narrative about the physical examination findings of the external auditory canal and tympanic membrane, not captured in other fields.">
				>
				["at0183"] = <
					text = <"Middle ear">
					description = <"The foreign body is located within the middle ear cavity.">
				>
				["at0185"] = <
					text = <"Fungal spores">
					description = <"Fungal Spores are observed within the external auditory canal.">
				>
				["at0186"] = <
					text = <"Exostosis">
					description = <"An exostosis is observed within the external auditory canal.">
				>
				["at0187"] = <
					text = <"Canal Stenosis">
					description = <"A narrowing of the auditory canal is observed.">
				>
				["at0188"] = <
					text = <"Mastoid Bowl">
					description = <"A mastoid bowl or cavity is observed within the auditory canal.">
				>
				["at0189"] = <
					text = <"No abnormality detected">
					description = <"Statement that no abnormality was detected (NAD) on examination of the external auditory canal and tympanic membrane.">
					comment = <"Record as True if no abnormality was detected on examination. Specific statements can be included in the 'Clinical Interpretation' data element.">
				>
				["at0190"] = <
					text = <"Discharge presence">
					description = <"Presence of a discharge observed in the external auditory canal.">
				>
				["at0191"] = <
					text = <"Present">
					description = <"Discharge is observed within the ear canal.">
				>
				["at0192"] = <
					text = <"Absent">
					description = <"Discharge is not observed within the ear canal.">
				>
				["at0193"] = <
					text = <"Description of wax">
					description = <"Narrative description of the wax findings in the external auditory canal.">
				>
				["at0194"] = <
					text = <"Exam not done">
					description = <"Details to explicitly record that this examination was not performed.">
				>
				["at0195"] = <
					text = <"Tympanic membrane examination">
					description = <"Further structured details about examination of the tympanic membrane.">
				>
			>
		>
	>

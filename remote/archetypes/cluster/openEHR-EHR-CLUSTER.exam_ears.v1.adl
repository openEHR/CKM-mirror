archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.exam_ears.v1

concept
	[at0000]	-- Examination of Ears
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2013-02-05">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording a narrative description and clinical interpretation of the findings observed during the physical examination of the ears, and to provide a framework in which to nest detailed CLUSTER archetypes, each of which can further describe specific aspects of the ear examination in more detail.">
			use = <"Use to record a narrative description of the overall findings observed during the physical examination of the ears. 

Use as a container archetype - to provide a framework in which specific, detailed CLUSTER archetypes related to ear examination findings can be nested. For example,  CLUSTER.exam_tympanic_membrane, CLUSTER.exam_auditory_canal and CLUSTER.exam.pinna can be inserted into relevant slots to record detailed physical examination findings for a specific ear. In addition the CLUSTER.exclusion_exam can be used to record that an ear examination was not done, if inserted at root level 'Other Details' SLOT, or that examination of a specified ear was not done, if inserted in the 'Details' SLOT of the relevant ear.

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical Description' data element.">
			keywords = <"examination", "physical", "clinical", "findings", "ear", "exam">
			misuse = <"Not to be used for recording data not related to the physical examination of the ears.

Not to be used to record data about ear testing - use specific purpose-built OBSERVATION archetypes. For example: OBSERVATION.audiometry_result or OBSERVATION.tympanogram_226hz.

Not to be used for recording the clinical history - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom.

Not to be used to record stand-alone clinical observations or measurements - use specific OBSERVATION archetypes. For example OBSERVATION.blood_pressure, OBSERVATION.body_weight, or OBSERVATION.height.">
			copyright = <"© National E-Health Transition Authority">
		>
	>
	lifecycle_state = <"CommitteeDraft">
	other_contributors = <>
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["MD5-CAM-1.0.1"] = <"D4F9840273427BAE5910425D0C9D0AF4">
	>

definition
	CLUSTER[at0000] matches {	-- Examination of Ears
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0006] occurrences matches {0..1} matches {	-- No abnormality detected
				value matches {
					DV_BOOLEAN matches {
						value matches {True, False}
					}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Symmetry?
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0008] occurrences matches {0..1} matches {	-- Right ear
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0009] occurrences matches {0..1} matches {	-- No abnormality detected
						value matches {
							DV_BOOLEAN matches {
								value matches {True, False}
							}
						}
					}
					allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {	-- Details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.exam_tympanic_membrane(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.exam_pinna(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.exam_auditory_canal(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
					}
				}
			}
			CLUSTER[at0010] occurrences matches {0..1} matches {	-- Left ear
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0011] occurrences matches {0..1} matches {	-- No abnormality detected
						value matches {
							DV_BOOLEAN matches {
								value matches {True, False}
							}
						}
					}
					allow_archetype CLUSTER[at0013] occurrences matches {0..*} matches {	-- Details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.exam_auditory_canal(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.exam_tympanic_membrane(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.exam_pinna(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
					}
				}
			}
			allow_archetype CLUSTER[at0002] occurrences matches {0..*} matches {	-- Other details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0003] occurrences matches {0..*} matches {	-- Clinical interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0005] occurrences matches {0..*} matches {	-- Multimedia representation
				value matches {
					DV_MULTIMEDIA matches {
						media_type matches {
							[openEHR::
							425, 
							426, 
							427, 
							428, 
							429, 
							401, 
							402, 
							404, 
							405, 
							406, 
							407, 
							414, 
							518]
						}
					}
				}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination of Ears">
					description = <"Findings observed during the physical examination of the ears.">
				>
				["at0001"] = <
					text = <"Clinical description">
					description = <"Narrative description of the overall findings observed during a physical examination of the ears.">
					comment = <"May be used to record a narrative summary of the complete clinical examination of the body system or anatomical strucutre or key aspects of physical examination findings, which will be further supported by the additional structured data, using CLUSTER archetypes in the 'Examination Detail' slot. This data element may be used to capture legacy data that is not available in a structured format.">
				>
				["at0002"] = <
					text = <"Other details">
					description = <"Structured details about the physical examination of the ears not captured elsewhere in this archetype.">
					comment = <"For example: information required to capture local context; information required to align with other reference models/formalisms; or recording that the examination was not performed.">
				>
				["at0003"] = <
					text = <"Clinical interpretation">
					description = <"Single word, phrase or brief description represents the clinical meaning and significance of the physical examination findings.">
					comment = <"Coding with a terminology is preferred, if possible. For example, normal examination or a specific physical finding.">
				>
				["at0005"] = <
					text = <"Multimedia representation">
					description = <"Digital image or video taken, or a diagram drawn, during the physical examination of the ears.">
				>
				["at0006"] = <
					text = <"No abnormality detected">
					description = <"Statement that no abnormality was detected (NAD) on examination of both ears.">
				>
				["at0007"] = <
					text = <"Symmetry?">
					description = <"Description of the appearance of symmetry of both ears in comparison to one another.">
				>
				["at0008"] = <
					text = <"Right ear">
					description = <"Details about physical examination findings for the right ear.">
				>
				["at0009"] = <
					text = <"No abnormality detected">
					description = <"Statement that no abnormality was detected (NAD) on overall examination of the right ear.">
				>
				["at0010"] = <
					text = <"Left ear">
					description = <"Details about physical examination findings for the left ear.">
				>
				["at0011"] = <
					text = <"No abnormality detected">
					description = <"Statement that no abnormality was detected (NAD) on overall examination of the left ear.">
				>
				["at0013"] = <
					text = <"Details">
					description = <"Structured details of the physical examination for the left ear.">
				>
				["at0014"] = <
					text = <"Details">
					description = <"Structured details of the physical examination for the right ear.">
				>
			>
		>
	>

archetype (adl_version=1.4)
	openEHR-EHR-EVALUATION.exclusion.v1

concept
	[at0000]	-- Exclusion Statement
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2010-02-03">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record generic, positive statement/s about the absence or exclusion of any clinical findings or evaluations within the health record at a specific point in time.">
			use = <"Use to record the positive exclusion or absence of clinical findings or evaluations within the health record at a specific point in time. This archetype has specifically been designed to avoid the need to use flags or terminology to express negation about any item within the health record. 

This archetype has been deliberately kept simple and open in order to capture simple statements about anything that may be usefully recorded as absent or excluded within the health record. Specialisations of this archetype will capture specific and more detailed information about common exclusions, such as problems or diagnoses. 

Exclusion statements can only be considered to be current and accurate at the time of recording.">
			keywords = <"exclusion", ...>
			misuse = <"Not to be used to record the exclusion or absence of adverse reactions, medication use, procedures, problems or diagnoses - use specific specialisations of this archetype for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Stephen Chu, NEHTA, Australia", "Sam Heard, Ocean Informatics, Australia", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Ian McNicoll, Ocean Informatics, United Kingdom (Editor)", "Stephen Royce, NEHTA, Australia">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["references"] = <"Exclusion Statement, draft archetype, NEHTA Clinical Knowledge Manager [Internet]. NEHTA. Authored: 02 Mar 2010. Available at: 
http://dcm.nehta.org.au/ckm/#showArchetype_1013.1.1301_2 (accessed 3 Sept 2013)">
		["MD5-CAM-1.0.1"] = <"A4B741F9AE92F3C9B5BE363F05DFAD19">
	>

definition
	EVALUATION[at0000] matches {	-- Exclusion Statement
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0002] occurrences matches {1..*} matches {	-- Exclusion Statement
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0003] occurrences matches {0..*} matches {	-- Specific Object
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0006] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0004] occurrences matches {0..1} matches {	-- Date First Verified
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {	-- Date Last Verified
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Exclusion Statement">
					description = <"Generic, positive statement/s that need to be recorded as clinically excluded from the health record at a specific point in time.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Exclusion Statement">
					description = <"The statement about an exclusion in the health record.">
					comment = <"Use to capture any information that is needed to be positively recorded as being excluded within the record. The statement can support recording global statements or statements about a specified object. See specialisations for Medications, Adverse Reactions, Problem/Diagnosis and Procedures as common examples that have been explicitly modelled.">
				>
				["at0003"] = <
					text = <"Specific Object">
					description = <"Identification of the specific object/s to which the Exclusion Statement applies.">
				>
				["at0004"] = <
					text = <"Date First Verified">
					description = <"The earliest date at which the exclusion was clinically asserted, affirmed or confirmed.">
				>
				["at0005"] = <
					text = <"Date Last Verified">
					description = <"The date at which the original confirmation was last reviewed and deemed to be correct.">
				>
				["at0006"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
			>
		>
	>

archetype (adl_version=1.4; uid=f7eaa3df-0411-4b98-8164-96a9f1f66174)
	openEHR-EHR-OBSERVATION.tympanogram_226hz.v0

concept
	[at0000]	-- Tympanogram test result - 226Hz
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["ar-sy"] = <
			language = <[ISO_639-1::ar-sy]>
			author = <
				["name"] = <"Mona Saleh">
			>
		>
	>
description
	original_author = <
		["name"] = <"Kerrie Lee">
		["email"] = <"kerrie.lee2@bigpond.com">
		["date"] = <"2012-11-20">
	>
	details = <
		["ar-sy"] = <
			language = <[ISO_639-1::ar-sy]>
			purpose = <"لتسجيل تفاصيل نتيجة اختبار المُخَطَّط الطبلي">
			use = <"">
			keywords = <"المخطط الطبلي", "قياس الطبل

السمع">
			misuse = <"">
			copyright = <"© Northern Territory Department of Health (Australia), openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record measurements based on a 226 Hz probe tone, of the admittance or impedance of the middle ear system as a function of changing the external ear canal pressure.

To infer middle ear function from objective measurements based on a fixed 226Hz probe tone, taken at the tympanic membrane of the mobility of the tympanic membrane, in reponse to varying air pressure in the ear canal.">
			use = <"Use to record measurements taken during tympanometry with a 226 Hz probe tone.

Use to record outcome of tympanometry screening based on 226 Hz probe tone tympanometry.

Use to record the interpretation all tympanogram measurements taken with a 226 Hz probe tone, to infer middle ear function for each ear, plus an overall interpretation (or tympanometric diagnosis). 

All of the data elements are recorded using a single method or protocol. If, during the test, any of the protocol parameters need to be modified, then the subsequent part of the test will need to be recorded within a separate instance of the test data, using the updated protocol parameters.

Each 'Pressure vs Compliance Measurement' pair will comprise one pressure measurment with one corresponding compliance measurement. Compliance measurement has been represented in the archetype twice, each with different units. Compliance measurements will only be recorded in data using the unit that is recorded by the tympanometer in use - either volume units OR conductance units, but not both.

Similarly, Static Compliance has been represented in the archetype twice, each with different units. Static Compliance measurements will only be recorded in data using the unit that is recorded by the tympanometer in use - either volume units OR conductance units, but not both.

If the tympanogram subtypes are to be used in an implementation, these should be substituted for the global Type B category in the 'Tympanogram Type' data element. Both 'Type B' and 'Type B - low/high compliance' should not be used in the same implementation. 'Type B - low complicance' is equivalent to the unqualified 'Type B' in the 'Tympanogram Type' data element. 

Similarly, if Type C subtypes describing the Tympanogram curve are to be used in an implementation, these should be substituted for the global Type C category in the 'Tympanogram Type' data element. Both Type C and Types C1-3 should not be used in the same implementation.">
			keywords = <"tympanogram", "tympanometry", "tympanometer", "admittance", "pressure", "compliance", "immittance", "static">
			misuse = <"Not to be used to record other hearing tests. Use other archetypes as appropriate, for example OBSERVATION.audiogram.

Not to be used to record high frequency tympanometry. Use the OBSERVATION.tympanogram_hf archetype instead.

Not to be used to record multifrequency tympanometry. Use the OBSERVATION.tympanogram_hf archetype instead.">
			copyright = <"© Northern Territory Department of Health (Australia), openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Amarjit Anand, NT Department of Health/NT Hearing, Australia", "Judith Boswell, Adelaide Hearing Consultants, Australia", "Stephen Chu, NEHTA, Australia", "Kimberley Crebbin, NT Hearing Services, Australia", "Kathy Currie, Northern Territory Health, Australia", "Jade Frederiksen, Hearing Health Program, DoH, Australia", "Sam Harkus, Australian Hearing, Australia", "Sam Heard, Ocean Informatics, Australia (Editor)", "Oliver Hosking, Remote Health NT, Australia", "Anthony Leech, Hearing Health, Australia", "Kerrie Lee, Ngaanyatjarra Health Service, Australia", "Heather Leslie, Atomica Informatics, Australia (Editor)", "Rebecca Matthews, NT Hearing Services - NTG, Australia", "Ian McNicoll, Ocean Informatics UK, United Kingdom", "Mark Ramjan, NT Department of Health, Australia", "Kelly Simmons, Department of Health, Australia", "Prasha Sooful, Royal Darwin Hospital, Australia">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Derived from: Tympanogram Result – 226Hz, Draft archetype [Internet]. Australian Digital Health Agency (NEHTA), ADHA Clinical Knowledge Manager. Authored: 2012 Nov 20. Available at: http://dcm.nehta.org.au/ckm#showArchetype_1013.1.1111_5 (discontinued).¶ Previously derived from: Tympanogram, Draft archetype [Internet]. openEHR Foundation, openEHR Clinical Knowledge Manager. Authored: 2012 Nov 20. Available at: http://www.openehr.org/knowledge/OKM.html#showarchetype_1013.1.73_2.

American Academy of Audiology. (1997) Identification of hearing loss & middle-ear dysfunction in preschool & school-age children. Audiol Today. 9, 21–23.

American National Standards Institute. (1987) Specifications for instruments to measure aural acoustic impedance and admittance (aural acoustic immittance). ANSI S3.39–1987. New York: American National Standards Institute.

American Speech-Language-Hearing Association. (1997) Guidelines for audiologic screening. Rockville MD: American Speech-Language-Hearing Association.

Fowler, C. G., & Shanks, J. E. (2002). Tympanometry. In J. Katz (Ed.), Handbook of clinical audiology (5th ed.). (pp. 175 – 204). Baltimore: Lippincott Williams & Wilkins.

Margolis, R. H., & Hunter, L. L. (2000). Acoustic Immittance Measurements. In R. J. Roeser, M. Valente & H. Hosford-Dunn (Ed.), Audiology diagnosis. (pp. 381 - 423). New York: Thieme Medical Publishers, Inc.

Margolis, R. H., Bass-Ringdahl, S., Hanks, W. D., Holte, K., & Zapala, D. A. (2003). Tympanometry in newborn infants—1 kHz norms. Journal of the American Academy of Audiology, 14, 383–392

Onusko, E. (2004). Tympanometry. American Family Physician, 70 (9), 1713 – 1720.">
		["current_contact"] = <"Heather Leslie, Atomica Informatics, Australia<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"6D4625A5EF7E5D665AFD565BE88D5059">
		["build_uid"] = <"1d10b5a1-e325-4992-978e-2040850877e9">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {	-- Tympanogram test result - 226Hz
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..1} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									CLUSTER[at0012] occurrences matches {0..2} matches {	-- Result details
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0009] matches {	-- Test ear
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0010, 	-- Left ear
															at0011]	-- Right ear
														}
													}
												}
											}
											CLUSTER[at0027] occurrences matches {0..1} matches {	-- Measurement details
												items cardinality matches {2..*; unordered} matches {
													CLUSTER[at0028] occurrences matches {0..*} matches {	-- Pressure vs compliance measurement
														items cardinality matches {1..2; unordered} matches {
															ELEMENT[at0055] matches {	-- Pressure
																value matches {
																	C_DV_QUANTITY <
																		property = <[openehr::125]>
																		list = <
																			["1"] = <
																				units = <"daPa">
																				magnitude = <|-600.0..200.0|>
																				precision = <|0|>
																			>
																			["2"] = <
																				units = <"mm[h2O]">
																			>
																		>
																	>
																}
															}
															ELEMENT[at0069] occurrences matches {0..1} matches {	-- Compliance (volume)
																value matches {
																	C_DV_QUANTITY <
																		property = <[openehr::129]>
																		list = <
																			["1"] = <
																				units = <"ml">
																				magnitude = <|0.0..5.0|>
																				precision = <|2|>
																			>
																			["2"] = <
																				units = <"cc3">
																				magnitude = <|0.0..5.0|>
																				precision = <|2|>
																			>
																		>
																	>
																}
															}
															ELEMENT[at0056] occurrences matches {0..1} matches {	-- Compliance (conductance)
																value matches {
																	C_DV_QUANTITY <
																		property = <[openehr::502]>
																		list = <
																			["1"] = <
																				units = <"mmho">
																				magnitude = <|0.0..5.0|>
																				precision = <|2|>
																			>
																		>
																	>
																}
															}
														}
													}
													ELEMENT[at0013] occurrences matches {0..1} matches {	-- Peak pressure
														value matches {
															C_DV_QUANTITY <
																property = <[openehr::125]>
																list = <
																	["1"] = <
																		units = <"daPa">
																		magnitude = <|-400.0..200.0|>
																		precision = <|0|>
																	>
																	["2"] = <
																		units = <"mm[h2O]">
																	>
																>
															>
														}
													}
													ELEMENT[at0074] occurrences matches {0..1} matches {	-- Static compliance (volume)
														value matches {
															C_DV_QUANTITY <
																property = <[openehr::129]>
																list = <
																	["1"] = <
																		units = <"ml">
																		magnitude = <|0.0..5.0|>
																		precision = <|2|>
																	>
																	["2"] = <
																		units = <"cc3">
																		magnitude = <|0.0..5.0|>
																		precision = <|2|>
																	>
																>
															>
														}
													}
													ELEMENT[at0014] occurrences matches {0..1} matches {	-- Static compliance (conductance)
														value matches {
															C_DV_QUANTITY <
																property = <[openehr::502]>
																list = <
																	["1"] = <
																		units = <"mmho">
																		magnitude = <|0.0..5.0|>
																		precision = <|2|>
																	>
																>
															>
														}
													}
													ELEMENT[at0018] occurrences matches {0..1} matches {	-- Ear canal volume
														value matches {
															C_DV_QUANTITY <
																property = <[openehr::129]>
																list = <
																	["1"] = <
																		units = <"cc3">
																		magnitude = <|0.0..5.0|>
																		precision = <|2|>
																	>
																	["2"] = <
																		units = <"ml">
																		magnitude = <|0.0..5.0|>
																		precision = <|2|>
																	>
																>
															>
														}
													}
													ELEMENT[at0029] occurrences matches {0..1} matches {	-- Tympanometric width
														value matches {
															C_DV_QUANTITY <
																property = <[openehr::125]>
																list = <
																	["1"] = <
																		units = <"daPa">
																		magnitude = <|0.0..400.0|>
																		precision = <|0|>
																	>
																>
															>
														}
													}
													ELEMENT[at0030] occurrences matches {0..1} matches {	-- Tympanometric gradient
														value matches {
															C_DV_QUANTITY <
																property = <[openehr::502]>
																list = <
																	["1"] = <
																		units = <"mmho">
																	>
																>
															>
															DV_PROPORTION matches {
																numerator matches {|0.0..1.0|}
																is_integral matches {True}
																type matches {1}
															}
														}
													}
												}
											}
											ELEMENT[at0031] occurrences matches {0..1} matches {	-- No test result
												value matches {
													DV_BOOLEAN matches {
														value matches {True}
													}
												}
											}
											ELEMENT[at0032] occurrences matches {0..1} matches {	-- Reason for no test result
												value matches {
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0004] occurrences matches {0..1} matches {	-- Tympanogram type
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0005, 	-- Type A
															at0006, 	-- Type B
															at0057, 	-- Type B with high ECV
															at0007, 	-- Type C
															at0070, 	-- Type D
															at0008, 	-- Type As
															at0034, 	-- Type Ad
															at0035]	-- Indeterminate
														}
													}
												}
											}
											ELEMENT[at0071] occurrences matches {0..1} matches {	-- Tympanogram type B subtype
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0072, 	-- Type B - high volume
															at0073]	-- Type B - low volume
														}
													}
												}
											}
											ELEMENT[at0058] occurrences matches {0..1} matches {	-- Tympanogram type C subtype
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0059, 	-- Type C1
															at0060, 	-- Type C2
															at0061]	-- Type C3
														}
													}
												}
											}
											ELEMENT[at0033] occurrences matches {0..1} matches {	-- Clinical interpretation
												value matches {
													DV_TEXT matches {*}
												}
											}
										}
									}
									ELEMENT[at0036] occurrences matches {0..1} matches {	-- Overall interpretation
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0020] occurrences matches {0..*} matches {	-- Test result image
										value matches {
											DV_MULTIMEDIA matches {
												media_type matches {
													[openEHR::
													425, 
													426, 
													427, 
													428, 
													429]
												}
											}
										}
									}
									ELEMENT[at0021] occurrences matches {0..1} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
					POINT_EVENT[at0037] occurrences matches {0..1} matches {	-- Baseline
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]	-- /data[Event Series]/events[Any event]/data[Tree]
						}
					}
					POINT_EVENT[at0038] occurrences matches {0..1} matches {	-- Post-Valsalva
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]	-- /data[Event Series]/events[Any event]/data[Tree]
						}
					}
					POINT_EVENT[at0039] occurrences matches {0..1} matches {	-- Post-Toynbee
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]	-- /data[Event Series]/events[Any event]/data[Tree]
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0040] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0043] occurrences matches {0..1} matches {	-- Direction of pressure change
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0044, 	-- Descending
									at0045]	-- Ascending
								}
							}
						}
					}
					ELEMENT[at0046] occurrences matches {0..1} matches {	-- Rate of pressure change
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::708]>
								list = <
									["1"] = <
										units = <"daPa/s">
									>
									["2"] = <
										units = <"mm[h2O]/s">
									>
								>
							>
						}
					}
					ELEMENT[at0047] occurrences matches {0..1} matches {	-- Admittance measurement
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0048, 	-- Peak compensated static
									at0049]	-- Ambient compensated static
								}
							}
						}
					}
					CLUSTER[at0050] occurrences matches {0..1} matches {	-- Pressure range
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0051] occurrences matches {0..1} matches {	-- Start pressure
								value matches {
									C_DV_QUANTITY <
										property = <[openehr::125]>
										list = <
											["1"] = <
												units = <"daPa">
											>
											["2"] = <
												units = <"mm[h2O]">
											>
										>
									>
								}
							}
							ELEMENT[at0052] occurrences matches {0..1} matches {	-- Stop pressure
								value matches {
									C_DV_QUANTITY <
										property = <[openehr::125]>
										list = <
											["1"] = <
												units = <"daPa">
											>
											["2"] = <
												units = <"mm[h2O]">
											>
										>
									>
								}
							}
						}
					}
					allow_archetype CLUSTER[at0053] occurrences matches {0..*} matches {	-- Tympanometer
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0041] occurrences matches {0..*} matches {	-- Pre-test calibration cavity size
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::129]>
								list = <
									["1"] = <
										units = <"cc3">
										magnitude = <|0.5..5.0|>
										precision = <|2|>
									>
								>
								assumed_value = <
									units = <"cc3">
									precision = <2>
									magnitude = <0.5>
								>
							>
						}
					}
					ELEMENT[at0054] occurrences matches {0..1} matches {	-- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["ar-sy"] = <
			items = <
				["at0000"] = <
					text = <"*Tympanogram Result(en)">
					description = <"*Record of measurements of movement at the tympanic membrane in response to changes in air pressure in the ear canal and their clinical interpretation as an indication of middle ear function.(en)">
				>
				["at0001"] = <
					text = <"*Event Series(en)">
					description = <"*@ internal @(en)">
				>
				["at0002"] = <
					text = <"*Any event(en)">
					description = <"*Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.(en)">
				>
				["at0003"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0004"] = <
					text = <"*Tympanogram Type(en)">
					description = <"*Description of the the shape of the tympanogram curve, based on the Jerger categories.(en)">
					comment = <"*If Type C subtypes are to be used in an implementation, these should be substituted for the global Type C category here. Both Type C and Types C1-3 should not be used in the same implementation.(en)">
				>
				["at0005"] = <
					text = <"*Type A(en)">
					description = <"*A distinctive sharp peak, around atmospheric pressure; normal static compliance; normal ear canal volume.(en)">
				>
				["at0006"] = <
					text = <"*Type B(en)">
					description = <"*Flat or rounded curve with no measurable peak pressure, reduced static compliance; ear canal volume within normal range.(en)">
				>
				["at0007"] = <
					text = <"*Type C(en)">
					description = <"*Curve similar to Type A , but with the sharp peak at a negative pressure.(en)">
				>
				["at0008"] = <
					text = <"*Type As(en)">
					description = <"*A Type A curve but with reduced static compliance.(en)">
				>
				["at0009"] = <
					text = <"*Test Ear(en)">
					description = <"*Identification of the ear being tested.(en)">
				>
				["at0010"] = <
					text = <"*Left Ear(en)">
					description = <"*The probe tone was presented to the left ear.(en)">
				>
				["at0011"] = <
					text = <"*Right Ear(en)">
					description = <"*The probe tone was presented to the right ear.(en)">
				>
				["at0012"] = <
					text = <"*Result Details(en)">
					description = <"*The tympanogram test result measurements and interpretations, recorded per ear.(en)">
				>
				["at0013"] = <
					text = <"*Peak Pressure(en)">
					description = <"*Peak pressure (also called tympanometric peak pressure or middle ear pressure or MEP) is the ear canal pressure at which the peak of the tympanogram occurs.(en)">
					comment = <"*Also known as Middle Ear Pressure (MEP). It is plotted on the horizontal axis on a tympanogram.(en)">
				>
				["at0014"] = <
					text = <"*Static Compliance (Conductance)(en)">
					description = <"*Static compliance (SC) is the greatest amount of acoustic energy absorbed by the middle ear system (the vertical peak of the tympanogram curve).(en)">
					comment = <"*Also known as 'Static Admittance'. It is plotted on the verical axis of a tympanogram.(en)">
				>
				["at0018"] = <
					text = <"*Ear Canal Volume(en)">
					description = <"*An estimate of the volume of air between the probe tip and the tympanic membrane if the tympanic membrane is intact, or the volume of the ear canal and the middle ear space if the tympanic membrane is perforated. (Fowler & Shanks, 2002, p. 180).(en)">
				>
				["at0020"] = <
					text = <"*Test Result Image(en)">
					description = <"*Digital representation of the entire result.(en)">
				>
				["at0021"] = <
					text = <"*Comment(en)">
					description = <"*Additional narrative about the test results and intepretation not captured in other fields.(en)">
				>
				["at0027"] = <
					text = <"*Measurement Details(en)">
					description = <"*Measurements of the tympanogram to infer middle ear function.(en)">
					comment = <"*Static compliance has been represented in the archetype twice, each with different units. Static compliance will only be recorded in data using the unit that is captured by the tympanometer in use - either volume units OR conductance units, but not both.
 (en)">
				>
				["at0028"] = <
					text = <"*Pressure vs Compliance Measurement(en)">
					description = <"*The Pressure/Compliance measurement pair, captured from a tympanometer and used to define a continuous tympanogram curve.(en)">
					comment = <"*Each measurement pair will comprise one pressure measurment with one corresponding compliance measurement. Compliance has been represented in the archetype twice, each with different units. Compliance will only be recorded in data using the unit that is captured by the tympanometer in use - either volume units OR conductance units, but not both.
 Multiple instances of this measurement pair will form the basis for the graphical display of the tympanogram. (en)">
				>
				["at0029"] = <
					text = <"*Tympanometric Width(en)">
					description = <"*The pressure interval corresponding to a 50% reduction in the peak static admittance.(en)">
					comment = <"*This value is typically derived from the Tympanogram curve. In some immittance machines, for example the Grason Stadler GSI 39, this data element is known as 'gradient'.(en)">
				>
				["at0030"] = <
					text = <"*Tympanometric Gradient(en)">
					description = <"*The steepness of the slope of the tympanogram near the peak.(en)">
					comment = <"*This value is typically derived from the Tympanogram curve.(en)">
				>
				["at0031"] = <
					text = <"*No Test Result(en)">
					description = <"*No test result is available for the test ear.(en)">
					comment = <"*Record as True if the clinician was unable to record a result for the test ear.(en)">
				>
				["at0032"] = <
					text = <"*Reason for No Test Result(en)">
					description = <"*Reason why no result is available for the test ear.(en)">
					comment = <"*Coding with a terminology is preferred, where possible. For example, patient was not co-operative; patient was not capable; ran out of time; no seal; presence of  tympanic membrane perforation, impacted wax or discharge.(en)">
				>
				["at0033"] = <
					text = <"*Clinical Interpretation(en)">
					description = <"*Clinical interpretation of all measurements for the test ear.(en)">
					comment = <"*Coding with a terminology is preferred, where possible.(en)">
				>
				["at0034"] = <
					text = <"*Type Ad(en)">
					description = <"*A Type A curve but with increased static compliance.(en)">
				>
				["at0035"] = <
					text = <"*Indeterminate(en)">
					description = <"*It is not possible to determine the type of tympanogram Type.(en)">
				>
				["at0036"] = <
					text = <"*Overall Interpretation(en)">
					description = <"*Overall clinical interpretation of the measurements and related findings using an tympanometer.(en)">
					comment = <"*Coding with a terminology is preferred, where possible. This data element is effectively an tympanometric diagnosis.(en)">
				>
				["at0037"] = <
					text = <"*Baseline(en)">
					description = <"*Measurement collection recorded as the basis for comparison with following measurement collections that may include other variables of time or patient state.(en)">
				>
				["at0038"] = <
					text = <"*Post Valsalva(en)">
					description = <"*Measurements are recorded after the patient has performed a valsalva manoeuvre.(en)">
				>
				["at0039"] = <
					text = <"*Post Toynbee(en)">
					description = <"*Measurements are recorded after the patient has performed a toynbee manoeuvre.(en)">
				>
				["at0040"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0041"] = <
					text = <"*Pre-test Calibration Cavity Size(en)">
					description = <"*A cavity with a known volume used to check the calibration of the tympanometer.(en)">
					comment = <"*ANSI S3.39 requires that three calibration cavities (0.5, 2.0, and 5.0 cm3) be checked with each device.(en)">
				>
				["at0043"] = <
					text = <"*Direction of Pressure Change(en)">
					description = <"*The dirction of change of pressure administered via the ear canal.(en)">
				>
				["at0044"] = <
					text = <"*Descending(en)">
					description = <"*The pressure changes from positive to negative.(en)">
				>
				["at0045"] = <
					text = <"*Ascending(en)">
					description = <"*The pressure changes from negative to positive.(en)">
				>
				["at0046"] = <
					text = <"*Rate of Pressure Change(en)">
					description = <"*The rate of change of pressure used in tympanometry.(en)">
				>
				["at0047"] = <
					text = <"*Admittance Measurement(en)">
					description = <"*Method of acoustic admittance measurement used in the test.(en)">
				>
				["at0048"] = <
					text = <"*Peak compensated static(en)">
					description = <"*The peak pressure value of the tympanogram used to compensate for ear canal volume in measurement of static acoustic admittance.(en)">
				>
				["at0049"] = <
					text = <"*Ambient compensated static(en)">
					description = <"*Admittance was measured using the Ambient compensated static acoustic admittance.(en)">
				>
				["at0050"] = <
					text = <"*Pressure Range(en)">
					description = <"*The range of air pressure used to determine the tympanogram.(en)">
				>
				["at0051"] = <
					text = <"*Start Pressure(en)">
					description = <"*The pressure value in daPa at which the pressure sweep for tympanometry begins. It is a positive pressure if the direction of pressure change is descending and a negative pressure if the direction of pressure change is ascending.(en)">
				>
				["at0052"] = <
					text = <"*Stop Pressure(en)">
					description = <"*The pressure value in daPa at which the pressure sweep for tympanometry ends. It is a negative pressure if the direction of pressure change is descending and a positive pressure if the direction of pressure change is ascending.(en)">
				>
				["at0053"] = <
					text = <"*Tympanometer(en)">
					description = <"*Details of tympanometer used to conduct the test.(en)">
				>
				["at0054"] = <
					text = <"*Comment(en)">
					description = <"*Additional narrative about the protocol for the tympanogram not captured in other fields.(en)">
				>
				["at0055"] = <
					text = <"*Pressure(en)">
					description = <"*The pressure measured in the ear canal.(en)">
				>
				["at0056"] = <
					text = <"*Compliance (Conductance)(en)">
					description = <"*The amount of acoustic energy absorbed by the middle ear system at a specified pressure, measured in mmHo.(en)">
					comment = <"*If the tympanometer records compliance in mmho, use this data element. If the tympanometer records compliance in cc3 or ml, use the 'Compliance (Volume) data element.(en)">
				>
				["at0057"] = <
					text = <"*Type B with high ECV(en)">
					description = <"*Rounded line/no measurable peak pressure; reduced static compliance; ear canal volume above the normal range.(en)">
				>
				["at0058"] = <
					text = <"*Tympanogram Type C Subtype(en)">
					description = <"*Subclassification of the tympanograms with negative peak pressure.(en)">
					comment = <"*If the subtypes are to be used in an implementation, these should be substituted for the global Type C category in the 'Tympanogram Type' data element. Both Type C and Types C1-3 should not be used in the same implementation.(en)">
				>
				["at0059"] = <
					text = <"*Type C1(en)">
					description = <"*A slight negative peak pressure, for example in range -100 to -199daPa.(en)">
				>
				["at0060"] = <
					text = <"*Type C2(en)">
					description = <"*A significant negative peak pressure, for example -200 daPa or greater.(en)">
				>
				["at0061"] = <
					text = <"*Type C3(en)">
					description = <"*A negative peak pressure and low-normal static compliance.(en)">
				>
				["at0069"] = <
					text = <"*Compliance (Volume)(en)">
					description = <"*The amount of acoustic energy absorbed by the middle ear system at a specified pressure, measured in cc3 or ml.(en)">
					comment = <"*If the tympanometer records compliance in cc3 or ml, use this data element. If the tympanometer records compliance in mmho, use the 'Compliance (Conductance) data element.(en)">
				>
				["at0070"] = <
					text = <"*Type D(en)">
					description = <"*Double peak(en)">
				>
				["at0071"] = <
					text = <"*New element(en)">
					description = <"**(en)">
				>
				["at0072"] = <
					text = <"*Type B - High Volume(en)">
					description = <"*Rounded curve with no measurable peak pressure; static compliance on lower limits of normal range; ear canal volume within normal range.(en)">
				>
				["at0073"] = <
					text = <"*Type B - Low Volume(en)">
					description = <"*Rounded curve with no measurable peak pressure, reduced static compliance; ear canal volume within normal range. Equivalent to the unqualified Type B classification.(en)">
				>
				["at0074"] = <
					text = <"*Static Compliance (Volume)(en)">
					description = <"*Static compliance (SC) is the greatest amount of acoustic energy absorbed by the middle ear system (the vertical peak of the tympanogram curve).(en)">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Tympanogram test result - 226Hz">
					description = <"Record of measurements of movement at the tympanic membrane in response to a 226Hz probe tone and changes in air pressure in the ear canal, and their clinical interpretation as an indication of middle ear function.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Tympanogram type">
					description = <"Description of the the shape of the tympanogram curve, based on the Jerger categories.">
					comment = <"If Type B or C subtypes are to be used in an implementation, these should be substituted for the global 'Type B' or 'Type C' classification here. 
Both 'Type B' and 'Type B - low compliance'/'Type B - high compliance' should not be used in the same implementation. Similarly, both 'Type C' and Types C1-3 should not be used in the same implementation. ">
				>
				["at0005"] = <
					text = <"Type A">
					description = <"Curve with a distinctive sharp peak, around atmospheric pressure; normal static compliance; normal ear canal volume.">
				>
				["at0006"] = <
					text = <"Type B">
					description = <"Flat or rounded curve with no measurable peak pressure, reduced static compliance; ear canal volume within normal range.">
				>
				["at0007"] = <
					text = <"Type C">
					description = <"Curve similar to Type A , but with the sharp peak at a negative pressure.">
				>
				["at0008"] = <
					text = <"Type As">
					description = <"A Type A curve but with reduced static compliance.">
				>
				["at0009"] = <
					text = <"Test ear">
					description = <"Identification of the ear being tested.">
				>
				["at0010"] = <
					text = <"Left ear">
					description = <"The probe tone was presented to the left ear.">
				>
				["at0011"] = <
					text = <"Right ear">
					description = <"The probe tone was presented to the right ear.">
				>
				["at0012"] = <
					text = <"Result details">
					description = <"The tympanogram test result measurements and interpretations, recorded per ear.">
				>
				["at0013"] = <
					text = <"Peak pressure">
					description = <"Peak pressure (also called tympanometric peak pressure or middle ear pressure or MEP) is the ear canal pressure at which the peak of the tympanogram occurs.">
					comment = <"Also known as Middle Ear Pressure (MEP). It is plotted on the horizontal axis on a tympanogram.">
				>
				["at0014"] = <
					text = <"Static compliance (conductance)">
					description = <"Static compliance (SC) is the greatest amount of acoustic energy absorbed by the middle ear system (the vertical peak of the tympanogram curve).">
					comment = <"Also known as 'Static Admittance'. It is plotted on the verical axis of a tympanogram.">
				>
				["at0018"] = <
					text = <"Ear canal volume">
					description = <"An estimate of the volume of air between the probe tip and the tympanic membrane if the tympanic membrane is intact, or the volume of the ear canal and the middle ear space if the tympanic membrane is perforated. (Fowler & Shanks, 2002, p. 180).">
				>
				["at0020"] = <
					text = <"Test result image">
					description = <"Digital representation of the entire result.">
				>
				["at0021"] = <
					text = <"Comment">
					description = <"Additional narrative about the test results and intepretation not captured in other fields.">
				>
				["at0027"] = <
					text = <"Measurement details">
					description = <"Measurements of the tympanogram to infer middle ear function.">
					comment = <"Static compliance has been represented in the archetype twice, each with different units. Static compliance will only be recorded in data using the unit that is captured by the tympanometer in use - either volume units OR conductance units, but not both.
 ">
				>
				["at0028"] = <
					text = <"Pressure vs compliance measurement">
					description = <"The Pressure/Compliance measurement pair, captured from a tympanometer and used to define a continuous tympanogram curve.">
					comment = <"Each measurement pair will comprise one pressure measurment with one corresponding compliance measurement. Compliance has been represented in the archetype twice, each with different units. Compliance will only be recorded in data using the unit that is captured by the tympanometer in use - either volume units OR conductance units, but not both.
 Multiple instances of this measurement pair will form the basis for the graphical display of the tympanogram. ">
				>
				["at0029"] = <
					text = <"Tympanometric width">
					description = <"The pressure interval corresponding to a 50% reduction in the peak static admittance.">
					comment = <"This value is typically derived from the Tympanogram curve. In some immittance machines, for example the Grason Stadler GSI 39, this data element is known as 'gradient'.">
				>
				["at0030"] = <
					text = <"Tympanometric gradient">
					description = <"The steepness of the slope of the tympanogram near the peak.">
					comment = <"This value is typically derived from the Tympanogram curve.">
				>
				["at0031"] = <
					text = <"No test result">
					description = <"No test result is available for the test ear.">
					comment = <"Record as True if the clinician was unable to record a result for the test ear.">
				>
				["at0032"] = <
					text = <"Reason for no test result">
					description = <"Reason why no result is available for the test ear.">
					comment = <"Coding with a terminology is preferred, where possible. For example, patient was not co-operative; patient was not capable; ran out of time; no seal; presence of  tympanic membrane perforation, impacted wax or discharge.">
				>
				["at0033"] = <
					text = <"Clinical interpretation">
					description = <"Clinical interpretation of all measurements for the test ear.">
					comment = <"Coding with a terminology is preferred, where possible.">
				>
				["at0034"] = <
					text = <"Type Ad">
					description = <"A Type A curve but with increased static compliance.">
				>
				["at0035"] = <
					text = <"Indeterminate">
					description = <"It is not possible to determine the type of tympanogram Type.">
				>
				["at0036"] = <
					text = <"Overall interpretation">
					description = <"Overall clinical interpretation of the measurements and related findings using an tympanometer.">
					comment = <"Coding with a terminology is preferred, where possible. This data element is effectively an tympanometric diagnosis.">
				>
				["at0037"] = <
					text = <"Baseline">
					description = <"Measurement collection recorded as the basis for comparison with following measurement collections that may include other variables of time or patient state.">
				>
				["at0038"] = <
					text = <"Post-Valsalva">
					description = <"Measurements are recorded after the patient has performed a valsalva manoeuvre.">
				>
				["at0039"] = <
					text = <"Post-Toynbee">
					description = <"Measurements are recorded after the patient has performed a toynbee manoeuvre.">
				>
				["at0040"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0041"] = <
					text = <"Pre-test calibration cavity size">
					description = <"A cavity with a known volume used to check the calibration of the tympanometer.">
					comment = <"ANSI S3.39 requires that three calibration cavities (0.5, 2.0, and 5.0 cm3) be checked with each device.">
				>
				["at0043"] = <
					text = <"Direction of pressure change">
					description = <"The dirction of change of pressure administered via the ear canal.">
				>
				["at0044"] = <
					text = <"Descending">
					description = <"The pressure changes from positive to negative.">
				>
				["at0045"] = <
					text = <"Ascending">
					description = <"The pressure changes from negative to positive.">
				>
				["at0046"] = <
					text = <"Rate of pressure change">
					description = <"The rate of change of pressure used in tympanometry.">
				>
				["at0047"] = <
					text = <"Admittance measurement">
					description = <"Method of acoustic admittance measurement used in the test.">
				>
				["at0048"] = <
					text = <"Peak compensated static">
					description = <"The peak pressure value of the tympanogram used to compensate for ear canal volume in measurement of static acoustic admittance.">
				>
				["at0049"] = <
					text = <"Ambient compensated static">
					description = <"Admittance was measured using the Ambient compensated static acoustic admittance.">
				>
				["at0050"] = <
					text = <"Pressure range">
					description = <"The range of air pressure used to determine the tympanogram.">
				>
				["at0051"] = <
					text = <"Start pressure">
					description = <"The pressure value in daPa at which the pressure sweep for tympanometry begins. It is a positive pressure if the direction of pressure change is descending and a negative pressure if the direction of pressure change is ascending.">
				>
				["at0052"] = <
					text = <"Stop pressure">
					description = <"The pressure value in daPa at which the pressure sweep for tympanometry ends. It is a negative pressure if the direction of pressure change is descending and a positive pressure if the direction of pressure change is ascending.">
				>
				["at0053"] = <
					text = <"Tympanometer">
					description = <"Details of tympanometer used to conduct the test.">
				>
				["at0054"] = <
					text = <"Comment">
					description = <"Additional narrative about the protocol for the tympanogram not captured in other fields.">
				>
				["at0055"] = <
					text = <"Pressure">
					description = <"The pressure measured in the ear canal.">
				>
				["at0056"] = <
					text = <"Compliance (conductance)">
					description = <"The amount of acoustic energy absorbed by the middle ear system at a specified pressure, measured in mmHo.">
					comment = <"If the tympanometer records compliance in mmho, use this data element. If the tympanometer records compliance in cc3 or ml, use the 'Compliance (Volume) data element.">
				>
				["at0057"] = <
					text = <"Type B with high ECV">
					description = <"Rounded curve with measurable peak pressure; reduced static compliance; ear canal volume above the normal range.">
				>
				["at0058"] = <
					text = <"Tympanogram type C subtype">
					description = <"Subclassification of the tympanograms with negative peak pressure.">
					comment = <"If the subtypes are to be used in an implementation, these should be substituted for the global Type C category in the 'Tympanogram Type' data element. Both Type C and Types C1-3 should not be used in the same implementation.">
				>
				["at0059"] = <
					text = <"Type C1">
					description = <"A slight negative peak pressure, for example in range -100 to -199 daPa.">
				>
				["at0060"] = <
					text = <"Type C2">
					description = <"A significant negative peak pressure, for example -200 daPa or greater.">
				>
				["at0061"] = <
					text = <"Type C3">
					description = <"A negative peak pressure and low-normal static compliance.">
				>
				["at0069"] = <
					text = <"Compliance (volume)">
					description = <"The amount of acoustic energy absorbed by the middle ear system at a specified pressure, measured in cc3 or ml.">
					comment = <"If the tympanometer records compliance in cc3 or ml, use this data element. If the tympanometer records compliance in mmho, use the 'Compliance (Conductance) data element.">
				>
				["at0070"] = <
					text = <"Type D">
					description = <"A curve with a double peak; normal or reduced static compliance; normal ear canal volume.">
				>
				["at0071"] = <
					text = <"Tympanogram type B subtype">
					description = <"Subclassification of the tympanograms with a flat curve.">
					comment = <"If the subtypes are to be used in an implementation, these should be substituted for the global Type B category in the 'Tympanogram Type' data element. Both 'Type B' and 'Type B - low/high compliance' should not be used in the same implementation. 'Type B - low complicance' is equivalent to the unqualified 'Type B' in the 'Tympanogram Type' data element.">
				>
				["at0072"] = <
					text = <"Type B - high volume">
					description = <"Rounded curve with no measurable peak pressure; static compliance on lower limits of normal range; ear canal volume within normal range.">
				>
				["at0073"] = <
					text = <"Type B - low volume">
					description = <"Rounded curve with no measurable peak pressure, reduced static compliance; ear canal volume within normal range. Equivalent to the unqualified Type B classification.">
				>
				["at0074"] = <
					text = <"Static compliance (volume)">
					description = <"Static compliance (SC) is the greatest amount of acoustic energy absorbed by the middle ear system (the vertical peak of the tympanogram curve).">
				>
			>
		>
	>

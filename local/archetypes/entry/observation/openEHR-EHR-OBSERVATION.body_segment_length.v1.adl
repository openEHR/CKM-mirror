archetype (adl_version=1.4; uid=3af37d47-2a31-4bfd-bb2c-13d5cc457888)
	openEHR-EHR-OBSERVATION.body_segment_length.v1

concept
	[at0000]	-- Body segment length
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"John Tore Valand, Vebjørn Arntzen">
				["organisation"] = <"Helse Bergen HF, Oslo universitetssykehus HF">
				["email"] = <"john.tore.valand@helse-bergen.no; varntzen@ous-hf.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics, Australia">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2019-03-07">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere måling av avstanden mellom to definerte punkter på kroppsoverflaten.">
			use = <"Brukes for å registrere måling av avstanden mellom to definerte punkter på kroppsoverflaten.

Antropometriske målinger som er vanlige, og ofte er brukt som en del av vekstdiagrammer i barndommen, er modellert som egne arketyper. Hensikten med denne arketypen er å tilby en standard måte å registrere en eller alle mulige målinger mellom to identifiserte punkter på kroppen, identifisert av dataelementelementet \"Kroppsdel\", heller enn å lage en egen/ separat arketype for alle mulige målinger.

Denne målingen kan registreres og følges over tid, for eksempel å følge utvikling av et barns vekst, eller for å gi innblikk i formen og proporsjonene av et individs kropp i en klinisk, antropometrisk eller ergonomisk vurdering. Man kan også følge utviklingen av en tilstand eller sykdom ved å registrere målinger av unormale kliniske funn eller observasjoner i denne arketypen.

De vanligste parametrene som blir målt mellom to definerte punkter er lagt til DV_CODED_TEXT-datatype i elementet \"Kroppsdel\". Når behovet flere målinger blir identifisert kan disse etter hvert bli lagt til listen.">
			keywords = <"antropometri", "måling", "lengde", "høyde", "bredde", "avstand", "spenn">
			misuse = <"Brukes ikke til å registrere allment utbredte kliniske målinger, som har sine egne spesifikke arketyper, for eksempel:

- bruk OBSERVATION.height (Høyde/lengde) for for å måle en persons høyde eller lengde.
- bruk OBSERVATION.head_circumference (Hodeomkrets) for å måle hodeomkrets.
- bruk OBSERVATION.waist_circumference (Midjeomkrets) for å måle midjeomkrets.
- bruk OBSERVATION.hip_circumference (Hofteomkrets) for å måle hofteomkrets.
- bruk OBSERVATION.chest_circumference for å måle brystomkrets.

Skal ikke brukes for å registrere omkretsen av en kroppsdel eller del av en kroppsdel. Bruk OBSERVATION.body_segment_circumference (Kroppsmåling - omkrets) for dette.

Skal ikke brukes til å registrere areal av kroppsegmenter, bruk arketypen OBSERVATION.body_segment_area (Kroppsmåling - areal) for dette formålet.

Skal ikke brukes til å registrere dimensjonene på et indre organ eller en lesjon/skade.

Skal ikke brukes til å registrere målinger av hudfolder - bruk arketyper spesifikke for dette formålet.

Skal ikke brukes til å registrere Isse - sete lengde på foster når det er brukt bildedannende utstyr.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the measurement of the distance between two points of surface anatomy.">
			use = <"Use to record the measurement of the distance between two points of surface anatomy.

Body segment measurements that are ubiquitous and, most commonly, used as part of growth charting in childhood have been modelled as unique and discrete archetypes. Rather than create an archetype for every single possible body segment measurement, the intent of this archetype is to use a common recording pattern to capture any, or all, other possible measurements between two identified body points, identified by the 'Body segment name' data element.

This measurement may be recorded and tracked over time, for example tracking child growth parameters, or used to provide insight into the shape and proportions of an individual's body for purposes of clinical, anthropometric or ergonomic assessment. It may also be used to record the measurement of abnormal clinical findings or observations in order to track the progress of a condition or disease.

Common parameters that are measured between two identified points have been added to the DV_CODED_TEXT data type in the 'Body segment name' data element. As further parameters are identified, these can be added to this list over time.">
			keywords = <"anthropometry", "measurement", "length", "height", "width", "span">
			misuse = <"Not to be used to record common body segment measurements that have been modelled as separate archetypes, including, but not limited to:
- for Head circumference - use OBSERVATION.head_circumference;
- for Hip circumference - use OBSERVATION.hip_circumference;
- for Waist circumference - use OBSERVATION.waist_circumference; and
- for Chest circumference - use OBSERVATION.chest_circumference.

Not to be used to record the circumference of a body segment - use OBSERVATION.body_segment_circumference for this purpose.

Not to be used to record the area of a body segment - use OBSERVATION.body_segment_area for this purpose.

Not to be used to record the dimensions of an internal body organ or a lesion.

Not to be used to record skinfold measurements - use archetypes that are specific for this purpose.

Not to be used to measure the Crown-rump length for a fetus, using imaging technology.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <"Andreas Abildgaard, OUS, Norway", "Marit Alice Venheim, Helse Vest IKT, Norway", "Stein Arne Rimehaug, Sunnaas sykehus, Norway", "Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Heidi Aursand, Oslo universitetssykehus, Norway", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "Malin Berg, DIPS ASA, Norway", "Randi Brendberg, Helse Nord RHF, Norway", "Angela Crovetti, CDC/NIOSH, United States", "Mikkel Gaup Grønmo, FSE, Helse Nord, Norway (Nasjonal IKT redaktør)", "Heather Grain, Llewelyn Grain Informatics, Australia", "Alfred Honore, Haukeland, Norway", "Evelyn Hovenga, EJSH Consulting, Australia", "Lars Ivar Mehlum, Nasjonal IKT HF, Norway", "Brit Jorun Liseth, Haukeland Universitetssykehus, Norway", "Lars Morgan Karlsen, DIPS ASA, Norway", "Heidi Koikkalainen, United Kingdom (openEHR Editor)", "Ronald Krawec, Alberta Health Services, Canada", "Tomi Laptoš, Marand, Slovenia", "Liv Laugen, Oslo universitetssykehus, Norway", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Anne M Gromsrud, DIPS AS, Norway", "Priscila Maranhão, MEDCIDS-FMUP, Portugal", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Johannes Michael Strehle, Helse-Nord, Norway", "Svenne Naumann, Finnmarkssykehuset, Norway", "Bjørn Næss, DIPS ASA, Norway", "Jayashree Panickar, Karolinska Institute, Sweden", "Line Silsand, univertsity hospital of northern norway, Norway (openEHR Editor)", "Trine Strand, Oslo Universitetssykehus (OUS), Norway", "Norwegian Review Summary, Nasjonal IKT HF, Norway", "Micaela Thierley, Helse Bergen, Norway", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"De Mits S, Coorevits P, De Clercq D, Elewaut D, Woodburn J, Roosen P. Reliability and validity of the INFOOT three-dimensional foot digitizer for patients with rheumatoid arthritis. J Am Podiatr Med Assoc. 2011 May-Jun;101(3):198-207. PubMed PMID: 21622631.

Drillis R, Contini R, Bluestein M. Body Segment Parameters; a survey of measurement techniques. Artif Limbs. 1964;8:44-66. PubMed PMID: 14208177. Available from: http://www.oandplibrary.org/al/1964_01_044.asp.

Gordon, Claire C. et. al. 1988 Anthropometric Survey of U.S. Personnel: Summary Statistics Interim Report [Internet]. March 1989 [cited 2019 Mar 25]. Available from: https://multisite.eos.ncsu.edu/www-ergocenter-ncsu-edu/wp-content/uploads/sites/18/2016/06/Anthropometric-Detailed-Data-Tables.pdf.

Júlíusson, P.B., Vinsjansen, S., Nilsen, B., Sælensminde, H., Vågset, R. and Eide, G.E., 2005. Måling av vekst og vekt: En oversikt over anbefalte teknikker. Pediatrisk Endokrinologi, 19, pp.23-9 [cited 2019 June 18]. Available from: http://pediatrisk-endokrinologi.no/2005/1/Juliusson_et_al_2005_1.pdf.

Man-Systems Integration Standards,Volume 1, Section 3. [Internet]. National Aeronautics and Space Administration. Anthropometry and biomechanics; [cited 2019 Mar 25]. Available from: https://msis.jsc.nasa.gov/sections/section03.htm.

National health and nutrition examination survey III: Body Measurements (Anthropometry) [Internet]. Rockville, MD: Westat, Inc; 1988 Oct [cited 2019 May 10]. Available from: https://wwwn.cdc.gov/nchs/data/nhanes3/manuals/anthro.pdf.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"C6EE1F2FA157E2E60BF4C597F0FA4AC0">
		["build_uid"] = <"3e94f8f5-9046-4e6c-81aa-8629c66c8055">
		["revision"] = <"1.0.1">
	>

definition
	OBSERVATION[at0000] matches {    -- Body segment length
		data matches {
			HISTORY[at0001] matches {    -- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- ItemTree
								items cardinality matches {2..*; unordered} matches {
									ELEMENT[at0004] matches {    -- Body segment name
										value matches {
											DV_TEXT matches {*}
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0017,    -- Sitting height
													at0024,    -- Armspan
													at0018,    -- Upper arm length
													at0019,    -- Leg length
													at0020,    -- Upper leg length
													at0021,    -- Foot length
													at0022,    -- Crown-rump length
													at0023,    -- Knee height
													at0041,    -- Forearm length
													at0042]    -- Lower leg length
												}
											}
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {    -- Laterality
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0006,    -- Left
													at0007]    -- Right
												}
											}
										}
									}
									ELEMENT[at0008] matches {    -- Length
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::122]>
												list = <
													["1"] = <
														units = <"cm">
														magnitude = <|>=0.0|>
														precision = <|1|>
													>
													["2"] = <
														units = <"[in_i]">
														magnitude = <|>=0.0|>
														precision = <|1|>
													>
													["3"] = <
														units = <"mm">
														magnitude = <|>=0.0|>
														precision = <|1|>
													>
												>
											>
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {    -- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0010] matches {    -- ItemTree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0011] occurrences matches {0..1} matches {    -- Confounding factors
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0031] occurrences matches {0..1} matches {    -- Body position
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0036,    -- Standing
													at0037,    -- Sitting
													at0039]    -- Lying
												}
											}
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
					INTERVAL_EVENT[at0012] occurrences matches {0..1} matches {    -- Average measurement
						math_function matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[openehr::146]
								}
							}
						}
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]
						}
						state matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/state[at0010]
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0014] matches {    -- ItemTree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0015] occurrences matches {0..*} matches {    -- Measuring device
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0027] occurrences matches {0..1} matches {    -- Measurement method
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0029] occurrences matches {0..1} matches {    -- Measurement origin
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0030] occurrences matches {0..*} matches {    -- Structured origin
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0033] occurrences matches {0..1} matches {    -- Measurement endpoint
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0035] occurrences matches {0..*} matches {    -- Structured endpoint
						include
							archetype_id/value matches {/.*/}
					}
					allow_archetype CLUSTER[at0016] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Body segment length">
					description = <"Measurement of the distance between two points of surface anatomy.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Body segment name">
					description = <"Name of the measured body segment.">
					comment = <"Use the 'Measurement method' element and/or recording the 'Measurement origin' and/or 'Measurement endpoint' to record how the body segment is measured.">
				>
				["at0005"] = <
					text = <"Laterality">
					description = <"Side of the segment measured.">
				>
				["at0006"] = <
					text = <"Left">
					description = <"Left side of the body.">
				>
				["at0007"] = <
					text = <"Right">
					description = <"Right side of the body.">
				>
				["at0008"] = <
					text = <"Length">
					description = <"Measured value of the length of the body segment.">
				>
				["at0009"] = <
					text = <"Comment">
					description = <"Additional narrative about the length measurement of the body segment, not captured in other fields.">
				>
				["at0010"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0011"] = <
					text = <"Confounding factors">
					description = <"Record any issues or factors that may impact on the measurement of the body segment.">
					comment = <"For example: an uncooperative child.">
				>
				["at0012"] = <
					text = <"Average measurement">
					description = <"The average of several measurements of the same body segment taken during a single examination.">
				>
				["at0014"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0015"] = <
					text = <"Measuring device">
					description = <"Details of the device used for measuring the body part.">
				>
				["at0016"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0017"] = <
					text = <"Sitting height">
					description = <"Vertical distance between the sitting surface and the vertex of the head. A measure of trunk length, performed with the individual sitting erect.">
				>
				["at0018"] = <
					text = <"Upper arm length">
					description = <"Distance between the shoulder and the elbow.">
				>
				["at0019"] = <
					text = <"Leg length">
					description = <"Distance between the hip joint and the ankle joint.">
				>
				["at0020"] = <
					text = <"Upper leg length">
					description = <"Distance between the hip joint and the knee joint.">
				>
				["at0021"] = <
					text = <"Foot length">
					description = <"Distance between the base of the heel to the tip of the longest toe.">
				>
				["at0022"] = <
					text = <"Crown-rump length">
					description = <"Distance between vertex of head to the lowest part of the trunk.">
				>
				["at0023"] = <
					text = <"Knee height">
					description = <"Distance between the sole of the foot and the top of the lower leg.">
				>
				["at0024"] = <
					text = <"Armspan">
					description = <"Distance from fingertip to fingertip with both arms fully extended at right angles to the body.">
				>
				["at0027"] = <
					text = <"Measurement method">
					description = <"Narrative description of the method used for measurement.">
					comment = <"Specify in order to ensure that a sequence of measurements are taken at the same point and in the same way. For example: 'Sitting with knee bent at 90 degrees'.">
				>
				["at0029"] = <
					text = <"Measurement origin">
					description = <"Identification of a simple body site for the location of the starting point of the measurement.">
				>
				["at0030"] = <
					text = <"Structured origin">
					description = <"A structured anatomical location for the starting point of the measurement.">
				>
				["at0031"] = <
					text = <"Body position">
					description = <"The position of the individual at the time of measurement.">
				>
				["at0033"] = <
					text = <"Measurement endpoint">
					description = <"Identification of a simple body site for the location of the ending point of the measurement.">
				>
				["at0035"] = <
					text = <"Structured endpoint">
					description = <"A structured anatomical location for the endpoint of the measurement.">
				>
				["at0036"] = <
					text = <"Standing">
					description = <"Standing at the time of measurement.">
				>
				["at0037"] = <
					text = <"Sitting">
					description = <"Sitting (for example on bed or chair) at the time of measurement.">
				>
				["at0039"] = <
					text = <"Lying">
					description = <"Lying flat at the time of measurement.">
				>
				["at0041"] = <
					text = <"Forearm length">
					description = <"Distance from the elbow to the wrist.">
				>
				["at0042"] = <
					text = <"Lower leg length">
					description = <"Distance between the knee joint and the ankle joint.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Kroppsmåling - lengde">
					description = <"Måling av avstanden mellom to definerte punkter på kroppsoverflaten.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert hendelse">
					description = <"Standard, uspesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i et templat eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Navn på kroppsdel">
					description = <"Navn på del av kroppen som måles.">
					comment = <"Måten kroppsdelen måles på skal registreres ved hjelp av elementet \"Metode\" og/eller av \"Startmålepunkt\" og \"Endemålepunkt\".">
				>
				["at0005"] = <
					text = <"Lateralitet">
					description = <"Hvilken side av kroppen kroppsdelen er på.">
				>
				["at0006"] = <
					text = <"Venstre">
					description = <"Venstre side av kroppen.">
				>
				["at0007"] = <
					text = <"Høyre">
					description = <"Høyre side av kroppen.">
				>
				["at0008"] = <
					text = <"Lengde">
					description = <"Den målte lengden av kroppsdelen.">
				>
				["at0009"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om målingen av en kroppsdel som ikke er fanget i andre felt.">
				>
				["at0010"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0011"] = <
					text = <"Konfunderende faktorer">
					description = <"Forhold eller omstendigheter som kan påvirke målingen av kroppsdelen.">
					comment = <"For eksempel: et barn som ikke samarbeider, eller pasienter med skoliose/ kyfose.">
				>
				["at0012"] = <
					text = <"Gjennomsnittsmåling">
					description = <"Gjennomsnittet av flere målinger av den samme kroppsdelen tatt under en enkelt undersøkelse.">
				>
				["at0014"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0015"] = <
					text = <"Måleinstrument">
					description = <"Detaljer om måleinstrumentet som ble brukt i måling av kroppsdelen.">
				>
				["at0016"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR eller CIMI.">
				>
				["at0017"] = <
					text = <"Sittehøyde">
					description = <"Vertikal avstand mellom sitteflaten og toppen av hodet. Måling av overkroppens lengde mens en person sitter med rak rygg.">
				>
				["at0018"] = <
					text = <"Lengde av overarm">
					description = <"Avstanden mellom skulder og albue.">
				>
				["at0019"] = <
					text = <"Benlengde">
					description = <"Avstanden fra hofteledd til ankelledd.">
				>
				["at0020"] = <
					text = <"Lårlengde">
					description = <"Avstanden mellom hofteledd og kneledd.">
				>
				["at0021"] = <
					text = <"Fotlengde">
					description = <"Avstanden fra hælen til spissen av den lengste tåen.">
				>
				["at0022"] = <
					text = <"Isse - sete lengde">
					description = <"Avstanden fra toppen av hodet til den nederste delen av overkroppen.">
				>
				["at0023"] = <
					text = <"Knehøyde">
					description = <"Avstanden fra sålen til toppen av leggen.">
				>
				["at0024"] = <
					text = <"Armspenn">
					description = <"Avstanden fra fingertupp til fingertupp med begge armer strukket ut vinkelrett fra kroppen.">
				>
				["at0027"] = <
					text = <"Målemetode">
					description = <"Fritekstbeskrivelse av metoden som ble benyttet for målingen.">
					comment = <"Spesifiser for å sikre at en sekvens av målinger blir gjort på de samme punktene og på samme måte. For eksempel \"Sittende med kneet bøyd 90 grader\".">
				>
				["at0029"] = <
					text = <"Startmålepunkt">
					description = <"En fritekstbeskrivelse som identifiserer stedet på kroppen som er startpunkt for målingen.">
				>
				["at0030"] = <
					text = <"Strukturert startmålepunkt">
					description = <"En strukturert anatomisk identifikasjon av stedet på kroppen som er startpunkt for målingen.">
				>
				["at0031"] = <
					text = <"Stilling">
					description = <"Individets posisjon ved tidspunktet for målingen.">
				>
				["at0033"] = <
					text = <"Sluttmålepunkt">
					description = <"En fritekstbeskrivelse som identifiserer stedet på kroppen som er sluttpunkt for målingen.">
				>
				["at0035"] = <
					text = <"Strukturert sluttpunkt">
					description = <"En strukturert anatomisk identifikasjon av stedet på kroppen som er sluttpunkt for målingen.">
				>
				["at0036"] = <
					text = <"Stående">
					description = <"Stående ved tidspunktet for målingen.">
				>
				["at0037"] = <
					text = <"Sittende">
					description = <"Sittende (for eksempel på en stol eller på en seng med føttene på gulvet) på tidspunkt for målingen.">
				>
				["at0039"] = <
					text = <"Liggende">
					description = <"Liggende flatt på tidspunkt for målingen.">
				>
				["at0041"] = <
					text = <"Underarmslengde">
					description = <"Avstanden fra albue til håndledd.">
				>
				["at0042"] = <
					text = <"Legglengde">
					description = <"Avstanden fra kneledd til ankelledd.">
				>
			>
		>
	>

archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.braden_scale_neonate.v1

concept
	[at0000]	-- Neonatal Braden scale (NSRAS)
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Dr Ian McNicoll">
		["organisation"] = <"Ocean Informatics, UK">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"2011-08-08">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details of an assemment based on the Neonatal Skin Risk Assessment Scale (NSRAS). 
The NSRAS is designed to allow the assessment of neonates at risk of skin breakdown or pressure sore development, based on the Braden Scale for predicting Pressure Sore Risk in adults.

It should be noted that although the purpose of the scale is similar to that of the adult Braden Scale and Braden-Q scale for older children, the scoring methodology is completely different, particuarly that a high score is accociated with high risk, the opposite from these other Braden scales where a low score equates to high risk.">
			use = <"The Neonatal Skin Risk Assessment Scale (NSRAS) should only be used to assess nenoates under 21 days of age.">
			keywords = <"braden", "neonate", "score", "skin", "pressure", "ulcer", "sore">
			misuse = <"Do not use for pressure sore risk assessment in children aged over 21 days.
Use the Braden-Q scale for children between 21 days and 5 years of age - OPEN-EHR-EHR-OBSERVATION.braden_child.v1
Use the adult Braden Scale for children over 5 years of age and adults - OPEN-EHR-OBSERVATION.braden_scale.v1">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <>
	other_details = <
		["references"] = <"Huffines B, Logsdon MC. The neonatal skin risk assessment scale for predicting skin breakdown in neonates. Issues in Comprehensive Pediatric Nursing 1997;20(2):103-114.">
		["current_contact"] = <"Dr Ian McNicoll, Ocean Informatics, UK, ian.mcnicoll@oceaninformatics.com">
		["MD5-CAM-1.0.1"] = <"81DFA726CC9BF96D198F7FEFB30B5895">
	>

definition
	OBSERVATION[at0000] matches {	-- Neonatal Braden scale (NSRAS)
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..1} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0011] occurrences matches {0..1} matches {	-- General physical condition
										value matches {
											1|[local::at0012], 	-- Best
											2|[local::at0013], 	-- Good
											3|[local::at0014], 	-- Poor
											4|[local::at0015]  	-- Very poor
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Moisture
										value matches {
											1|[local::at0017], 	-- Rarely moist
											2|[local::at0018], 	-- Occasionally moist
											3|[local::at0019], 	-- Moist
											4|[local::at0020]  	-- Constantly moist
										}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {	-- Nutrition
										value matches {
											1|[local::at0021], 	-- Excellent
											2|[local::at0022], 	-- Adequate
											3|[local::at0023], 	-- Inadequate
											4|[local::at0024]  	-- Very poor
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {	-- Activity
										value matches {
											1|[local::at0025], 	-- Unlimited
											2|[local::at0026], 	-- Slightly limited
											3|[local::at0027], 	-- Limited bed-bound
											4|[local::at0028]  	-- Completely bed-bound
										}
									}
									ELEMENT[at0008] occurrences matches {0..1} matches {	-- Mobility
										value matches {
											1|[local::at0029], 	-- No limitation
											2|[local::at0030], 	-- Slightly limited
											3|[local::at0031], 	-- Very limited
											4|[local::at0032]  	-- Completely immobile
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {	-- Mental status
										value matches {
											1|[local::at0033], 	-- No impairment
											2|[local::at0034], 	-- Slightly limited
											3|[local::at0035], 	-- Very limited
											4|[local::at0036]  	-- Completely limited
										}
									}
									ELEMENT[at0016] occurrences matches {0..1} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0010] occurrences matches {0..1} matches {	-- Total score
										value matches {
											DV_COUNT matches {
												magnitude matches {|2..24|}
											}
										}
									}
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Graded risk
										value matches {
											0|[local::at0038], 	-- Not at risk - Total score less than 13
											1|[local::at0037]  	-- At risk - Total score 13 or over
										}
									}
								}
							}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Neonatal Braden scale (NSRAS)">
					description = <"Neonatal Skin Risk Assessment Scale (NSRAS), an instrument to assess neonates at risk for skin breakdown, based on the Braden Scale for Predicting Pressure Sore Risk in adults.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Any event.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Graded risk">
					description = <"A graded risk derived from the summed Total score.">
				>
				["at0005"] = <
					text = <"Moisture">
					description = <"The dgree to which the infant's skin is exposed to moisture.">
				>
				["at0006"] = <
					text = <"Nutrition">
					description = <"The usual food intake of the neonate.">
				>
				["at0007"] = <
					text = <"Activity">
					description = <"The amount of physical activity of the neonate.">
				>
				["at0008"] = <
					text = <"Mobility">
					description = <"The neonate's ability ot change and control body position.">
				>
				["at0009"] = <
					text = <"Mental status">
					description = <"An evaluation of the mental status or sensory preception of the neonate.">
				>
				["at0010"] = <
					text = <"Total score">
					description = <"The total score, derived from the sum of all sub-scores for identifying if a neonate is at risk for pressure ulcers.">
				>
				["at0011"] = <
					text = <"General physical condition">
					description = <"An assessment of general condition, based on gestational age.">
				>
				["at0012"] = <
					text = <"Best">
					description = <"Gestational Age > 38 Weeks To Posterm.">
				>
				["at0013"] = <
					text = <"Good">
					description = <"Gestational Age > 33 Weeks But < 38 weeks.">
				>
				["at0014"] = <
					text = <"Poor">
					description = <"Gestational Age > 28 Weeks But < 33 weeks.">
				>
				["at0015"] = <
					text = <"Very poor">
					description = <"Gestational Age > 28 Weeks But < 33 weeks.">
				>
				["at0016"] = <
					text = <"Comment">
					description = <"An additional comment about the neonatal pressure risk assessment.">
				>
				["at0017"] = <
					text = <"Rarely moist">
					description = <"Skin is usually dry, linen requires changing only every 24 hours.">
				>
				["at0018"] = <
					text = <"Occasionally moist">
					description = <"Skin is occasionally moist/damp. Requiring an extra linen change approximately once a day.">
				>
				["at0019"] = <
					text = <"Moist">
					description = <"Skin is often but not always moist/damp; linen must be changed at least once a shift.">
				>
				["at0020"] = <
					text = <"Constantly moist">
					description = <"Skin is moist/damp every time infant is moved or turned.">
				>
				["at0021"] = <
					text = <"Excellent">
					description = <"Bottlehreastfeeds every meal which  meets nutritional needs  for growth.">
				>
				["at0022"] = <
					text = <"Adequate">
					description = <"Is on tube feedings which  meet nutritional needs for growth.">
				>
				["at0023"] = <
					text = <"Inadequate">
					description = <"Receives less than  optimum amount of liquid diet for growth (formula/breast milk) and supplemented with intravenous fluids.">
				>
				["at0024"] = <
					text = <"Very poor">
					description = <"NPO on intravenous  fluids.">
				>
				["at0025"] = <
					text = <"Unlimited">
					description = <"In an open crib.">
				>
				["at0026"] = <
					text = <"Slightly limited">
					description = <"In a double walled isolette.">
				>
				["at0027"] = <
					text = <"Limited bed-bound">
					description = <"In a radiant warmer without a clear plastic “saran” tent.">
				>
				["at0028"] = <
					text = <"Completely bed-bound">
					description = <"In a radiant warmer with a clear plastic “saran” tent.">
				>
				["at0029"] = <
					text = <"No limitation">
					description = <"Makes major and frequent changes in position without assistance (e.g., turn  head).">
				>
				["at0030"] = <
					text = <"Slightly limited">
					description = <"Makes frequent though slight changes in body or extremity position independently.">
				>
				["at0031"] = <
					text = <"Very limited">
					description = <"Makes occasional slight changes in body or extremity position but unable to make frequent or significant changes independently.">
				>
				["at0032"] = <
					text = <"Completely immobile">
					description = <"Does not make even slight changes in body or extremity position without assistance (e.g., Pavulon).">
				>
				["at0033"] = <
					text = <"No impairment">
					description = <"Alert and active.">
				>
				["at0034"] = <
					text = <"Slightly limited">
					description = <"Lethargic.">
				>
				["at0035"] = <
					text = <"Very limited">
					description = <"Responds only to painful stimuli (flinches, grasps, moans, increased blood pressure or heart rate).">
				>
				["at0036"] = <
					text = <"Completely limited">
					description = <"Unresponsive (does not flinch, grasp, moan, increase blood pressure, or heart rate) to painful stimuli due to diminished level of consciousness or sedation.">
				>
				["at0037"] = <
					text = <"At risk - Total score 13 or over">
					description = <"The neonate is not at risk of developing a pressure ulcer.">
				>
				["at0038"] = <
					text = <"Not at risk - Total score less than 13">
					description = <"The neonate is at risk of developing a pressure ulcer.">
				>
			>
		>
	>

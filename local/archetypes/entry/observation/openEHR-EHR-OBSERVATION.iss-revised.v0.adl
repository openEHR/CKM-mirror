archetype (adl_version=1.4; uid=0e6143ea-715b-4d6a-a5b7-f23b91ccf8cd)
	openEHR-EHR-OBSERVATION.iss-revised.v0
specialise
	openEHR-EHR-OBSERVATION.iss.v0

concept
	[at0000.1]	-- Revised International Staging System for Multiple Myeloma (R-ISS)
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Maryam Razavi">
		["organisation"] = <"Cambio CDS">
		["email"] = <"maryam.razavi@cambio.se">
		["date"] = <"2020-12-22">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the results for each component parameter and the resulting R-ISS stage.">
			use = <"Use to record the results for each component parameter and the resulting R-ISS stage.">
			keywords = <"albumin", "multiple", "myeloma", "staging", "system">
			misuse = <"">
			copyright = <"© Cambio CDS, openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Durie BG, Salmon SE. A clinical staging system for multiple myeloma. Correlation of measured myeloma cell mass with presenting clinical features, response to treatment, and survival. Cancer. 1975 Sep;36(3):842-54. doi: 10.1002/1097-0142(197509)36:3<842::aid-cncr2820360303>3.0.co;2-u. PMID: 1182674.

Greipp PR, San Miguel J, Durie BG, Crowley JJ, Barlogie B, Bladé J, Boccadoro M, Child JA, Avet-Loiseau H, Kyle RA, Lahuerta JJ, Ludwig H, Morgan G, Powles R, Shimizu K, Shustik C, Sonneveld P, Tosi P, Turesson I, Westin J. International staging system for multiple myeloma. J Clin Oncol. 2005 May 20;23(15):3412-20. doi: 10.1200/JCO.2005.04.242. Epub 2005 Apr 4. Erratum in: J Clin Oncol. 2005 Sep 1;23(25):6281. Harousseau, Jean-Luc [corrected to Avet-Loiseau, Herve]. PMID: 15809451.

Palumbo A, Avet-Loiseau H, Oliva S, Lokhorst HM, Goldschmidt H, Rosinol L, Richardson P, Caltagirone S, Lahuerta JJ, Facon T, Bringhen S, Gay F, Attal M, Passera R, Spencer A, Offidani M, Kumar S, Musto P, Lonial S, Petrucci MT, Orlowski RZ, Zamagni E, Morgan G, Dimopoulos MA, Durie BG, Anderson KC, Sonneveld P, San Miguel J, Cavo M, Rajkumar SV, Moreau P. Revised International Staging System for Multiple Myeloma: A Report From International Myeloma Working Group. J Clin Oncol. 2015 Sep 10;33(26):2863-9. doi: 10.1200/JCO.2015.61.2267. Epub 2015 Aug 3. PMID: 26240224; PMCID: PMC4846284.">
		["current_contact"] = <"maryam.razavi@cambio.se">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"D3CA7BEB1993FE4B9875ED5F320A7A8B">
		["build_uid"] = <"4b7a5d0a-e304-40d5-a440-19f406e1af59">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000.1] matches {    -- Revised International Staging System for Multiple Myeloma (R-ISS)
		data matches {
			HISTORY[at0001] matches {    -- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..1} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {    -- Serum β2 microglobulin
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0005,    -- <3.5 mg/L
													at0006,    -- 3.5-5.4 mg/L
													at0007]    -- >5.4 mg/L
												}
											}
										}
									}
									ELEMENT[at0008] occurrences matches {0..1} matches {    -- Serum albumin
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0009,    -- <3.5 g/dL
													at0010]    -- ≥3.5 g/dL
												}
											}
										}
									}
									ELEMENT[at0011] occurrences matches {0..1} matches {    -- ISS stage
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0012,    -- ISS I
													at0013,    -- ISS II
													at0014]    -- ISS III
												}
											}
										}
									}
									ELEMENT[at0.1] occurrences matches {0..1} matches {    -- CA by iFISH
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0.2,    -- Standard risk
													at0.3]    -- High risk
												}
											}
										}
									}
									ELEMENT[at0.4] occurrences matches {0..1} matches {    -- Serum LDH
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0.5,    -- Normal
													at0.6]    -- High
												}
											}
										}
									}
									ELEMENT[at0.7] occurrences matches {0..1} matches {    -- Revised ISS stage
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0.8,    -- R-ISS I
													at0.9,    -- R-ISS II
													at0.10]    -- R-ISS III
												}
											}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0015] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0016] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}




ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0.1"] = <
					text = <"CA by iFISH">
					description = <"Chromosomal abnormalities (CA) detected by
interphase fluorescent in situ hybridization (iFISH).">
				>
				["at0.10"] = <
					text = <"R-ISS III">
					description = <"ISS stage III and either high-risk CA by iFISH or high LDH.">
				>
				["at0.2"] = <
					text = <"Standard risk">
					description = <"No high risk CA.">
				>
				["at0.3"] = <
					text = <"High risk">
					description = <"Presence of del(17p) and/or translocation t(4;14) and/or translocation t(14;16).">
				>
				["at0.4"] = <
					text = <"Serum LDH">
					description = <"">
				>
				["at0.5"] = <
					text = <"Normal">
					description = <"Serum LDH < the upper limit of normal.">
				>
				["at0.6"] = <
					text = <"High">
					description = <"Serum LDH > the upper limit of normal.">
				>
				["at0.7"] = <
					text = <"Revised ISS stage">
					description = <"">
				>
				["at0.8"] = <
					text = <"R-ISS I">
					description = <"ISS stage I and standard-risk CA by iFISH and normal LDH.">
				>
				["at0.9"] = <
					text = <"R-ISS II">
					description = <"Not R-ISS stage I or III.">
				>
				["at0000.1"] = <
					text = <"Revised International Staging System for Multiple Myeloma (R-ISS)">
					description = <"A staging system for classifying and stratifying patients with multiple myeloma.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Serum β2 microglobulin">
					description = <"">
				>
				["at0005"] = <
					text = <"<3.5 mg/L">
					description = <"">
				>
				["at0006"] = <
					text = <"3.5-5.4 mg/L">
					description = <"">
				>
				["at0007"] = <
					text = <">5.4 mg/L">
					description = <"">
				>
				["at0008"] = <
					text = <"Serum albumin">
					description = <"">
				>
				["at0009"] = <
					text = <"<3.5 g/dL">
					description = <"">
				>
				["at0010"] = <
					text = <"≥3.5 g/dL">
					description = <"">
				>
				["at0011"] = <
					text = <"ISS stage">
					description = <"">
				>
				["at0012"] = <
					text = <"ISS I">
					description = <"Serum β2-microglobulin <3.5 mg/L and serum albumin ≥3.5 g/dL.">
				>
				["at0013"] = <
					text = <"ISS II">
					description = <"Not stage I or III.">
				>
				["at0014"] = <
					text = <"ISS III">
					description = <"Serum β2-microglobulin ≥5.5 mg/L.">
				>
				["at0015"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0016"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
			>
		>
	>

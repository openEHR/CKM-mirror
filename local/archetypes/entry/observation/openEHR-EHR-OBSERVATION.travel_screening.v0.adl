archetype (adl_version=1.4; uid=6ba068a7-4b42-4727-a75d-ac0e5125048d)
	openEHR-EHR-OBSERVATION.travel_screening.v0

concept
	[at0000]	-- Travel screening questionnaire
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2022-10-21">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To create a framework for recording answers to pre-defined screening questions about travel activity.">
			use = <"Use to create a framework for recording answers to pre-defined screening questions about travel activity.

Common use cases include, but are not limited to:
- Infectious disease surveillance
- Creating a patient profile in a disease registry
- Systematic questioning in any consultation related to patterns of disease.

The semantics of this archetype are intentionally loose, and querying this archetype would normally only be useful or safe within the context of each specific template. Each data element would usually be renamed in a template to represent the specific question asked. Where value sets have been proposed for common use cases, these can be adapted to align with local requirements by using the DV_TEXT or the DV_BOOLEAN datatypes choice to match each specific use case. 

Utilising this framework within a template can enable documentation of a broad range of question/answer pairs such as:
- Have you been overseas in the past 6 weeks? Yes, No, Unknown.
- Have you travelled to Africa in the past 1 year? Yes, No, Unknown.
- Have you travelled to an area where malaria is endemic in the past 2 weeks? Yes, No, Unknown.
- Have you ever travelled to an active war zone?

The EVENT structure from the reference model can be used to specify whether the questions relate to a point in time or over a period of time. Use a separate instance of this archetype to distinguish between a questionnaire recording information about an investigation or test that has been done at any time in the past and information about an investigation or test done within a specified time interval - for example, the difference between \"Have you ever travelled to an area with high radiation activity?\" compared to \"Have you travelled to an area with high radiation activity during the last four weeks?\".

The source of the information in a questionnaire response may vary in different contexts but can be specifically identified using the 'Information provider' element in the Reference Model.

This archetype has been designed to be used as a screening tool or to record simple questionnaire-format data for use in situations such as a disease registry. If the screening questionnaire identifies an investigation has been carried out, additional details required for persistence as part of a clinical record can be captured using specific test result archetypes.">
			keywords = <"travel, overseas, domestic, local, national, abroad, international, abroad", ...>
			misuse = <"Not to be used for recording a travel history - use ADMIN_ENTRY.travel_event for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Helse Vest, Norway", ...>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"95E3C4F194431BC110C3815930197056">
		["build_uid"] = <"fd696796-6807-434c-90ea-ab5fb8b9597d">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact https://www.snomed.org/snomed-ct/get-snomed or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Travel screening questionnaire
		data matches {
			HISTORY[at0022] matches {    -- Event Series
				events cardinality matches {0..*; unordered} matches {
					EVENT[at0023] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0001] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0040] occurrences matches {0..1} matches {    -- Screening purpose
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0027] occurrences matches {0..*} matches {    -- Any travel?
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0028,    -- Yes
													at0029,    -- No
													at0030]    -- Unknown
												}
											}
											DV_TEXT matches {*}
											DV_BOOLEAN matches {*}
										}
									}
									ELEMENT[at0043] occurrences matches {0..1} matches {    -- Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									CLUSTER[at0026] occurrences matches {0..*} matches {    -- Specific travel
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0021] matches {    -- Travel activity name
												value matches {
													DV_TEXT matches {*}
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0045,    -- National travel
															at0046]    -- International travel
														}
													}
												}
											}
											ELEMENT[at0024] occurrences matches {0..1} matches {    -- Occurred?
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0036,    -- Yes
															at0037,    -- No
															at0039]    -- Unknown
														}
													}
													DV_TEXT matches {*}
													DV_BOOLEAN matches {*}
												}
											}
											ELEMENT[at0003] occurrences matches {0..*} matches {    -- Timing
												value matches {
													DV_DATE_TIME matches {*}
													DV_TEXT matches {*}
													DV_INTERVAL<DV_DATE_TIME> matches {*}
													DV_INTERVAL<DV_DURATION> matches {*}
													DV_DURATION matches {*}
												}
											}
											allow_archetype CLUSTER[at0041] occurrences matches {0..*} matches {    -- Additional details
												include
													archetype_id/value matches {/.*/}
											}
											ELEMENT[at0025] occurrences matches {0..1} matches {    -- Comment
												value matches {
													DV_TEXT matches {*}
												}
											}
										}
									}
									allow_archetype CLUSTER[at0044] occurrences matches {0..1} matches {    -- Additional details
										include
											archetype_id/value matches {/.*/}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0005] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0019] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Travel screening questionnaire">
					description = <"Series of questions and associated answers used to screen for travel activity.">
					comment = <"The answers may be self-reported.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Timing">
					description = <"Indication of timing related to the travel.">
					comment = <"The 'Timing' data element has deliberately been loosely modelled to support the myriad of ways that it can be used in questionnaires to capture when an investigation was carried out. The specific and intended semantics can be further clarified in a template. For example: the actual date and/or time of travel; the departure and return time for the trip; the interval of time during which the individual was travelling; the duration of the trip; the age of the individual at the time of the trip; or the duration of time since the trip occurred. A partial date is valid, using the DV_DATE_TIME data type, to record only a year.">
				>
				["at0005"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0019"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0021"] = <
					text = <"Travel activity name">
					description = <"Name of the type of travel.">
					comment = <"For example: 'Local', 'National', 'Overseas', 'Cruise ship'; or 'War zone'.
Coding of the 'Investigation name' with a terminology is preferred, where possible.">
				>
				["at0022"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0023"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0024"] = <
					text = <"Occurred?">
					description = <"Is there a history of the the travel activity occurring?">
					comment = <"In a template, the data element would usually be renamed to the specific question asked. The proposed value set can be adapted for local use by choosing between the DV_TEXT or the DV_BOOLEAN datatypes to match each specific use case.">
				>
				["at0025"] = <
					text = <"Comment">
					description = <"Additional narrative about the travel activity not captured in other fields.">
				>
				["at0026"] = <
					text = <"Specific travel">
					description = <"Details about a specified travel activity relevant for the screening purpose.">
				>
				["at0027"] = <
					text = <"Any travel?">
					description = <"Is there a history of any travel activity related to the screening purpose?">
					comment = <"In a template, the data element would usually be renamed to the specific question asked. The proposed value set can be adapted for local use by  by choosing between the DV_TEXT or the DV_BOOLEAN datatypes to match each specific use case.">
				>
				["at0028"] = <
					text = <"Yes">
					description = <"">
				>
				["at0029"] = <
					text = <"No">
					description = <"">
				>
				["at0030"] = <
					text = <"Unknown">
					description = <"">
				>
				["at0036"] = <
					text = <"Yes">
					description = <"">
				>
				["at0037"] = <
					text = <"No">
					description = <"">
				>
				["at0039"] = <
					text = <"Unknown">
					description = <"">
				>
				["at0040"] = <
					text = <"Screening purpose">
					description = <"The context or reason for screening.">
					comment = <"This data element is intended to provide collection context for the question/answer groups when queried at a later date. It is not expected that this data element will be exposed to the individual, but only stored in data. For example: pre-admission screening, the name of the actual questionnaire or screening for previous investigations.">
				>
				["at0041"] = <
					text = <"Additional details">
					description = <"Structured details or questions about the specific travel activity.">
				>
				["at0043"] = <
					text = <"Description">
					description = <"Narrative description about the history of any travel activity relevant for the screening purpose.">
				>
				["at0044"] = <
					text = <"Additional details">
					description = <"Structured details or questions about screening for all travel activities.">
				>
				["at0045"] = <
					text = <"National travel">
					description = <"Travel occurred to places or regions within the usual country of residence.">
				>
				["at0046"] = <
					text = <"International travel">
					description = <"Travel occurred to places or regions within countries other than the usual country of residence.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0030"] = <[SNOMED-CT::261665006]>
				["at0028"] = <[SNOMED-CT::373066001]>
				["at0029"] = <[SNOMED-CT::373067005]>
				["at0036"] = <[SNOMED-CT::373066001]>
				["at0037"] = <[SNOMED-CT::373067005]>
				["at0039"] = <[SNOMED-CT::261665006]>
			>
		>
	>

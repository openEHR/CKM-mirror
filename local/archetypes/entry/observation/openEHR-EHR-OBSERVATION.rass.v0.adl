archetype (adl_version=1.4; uid=c4b087e6-22ae-4a8c-afe2-1bffa9d68163)
	openEHR-EHR-OBSERVATION.rass.v0

concept
	[at0000]	-- Richmond Agitation-Sedation Scale (RASS)
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
			>
		>
	>
description
	original_author = <
		["date"] = <"2025-02-04">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere resultatene for Richmond Agitation-Sedation Scale (RASS).">
			use = <"Brukes for å registrere resultatene for Richmond Agitation-Sedation Scale (RASS).">
			keywords = <"agitasjon, sedasjon, våkenhet ,bevisthet", ...>
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the result for the Richmond Agitation-Sedation Scale (RASS).">
			use = <"Used to record the result for the Richmond Agitation-Sedation Scale (RASS).">
			keywords = <"agitation, sedation, alertness, cognition,", ...>
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Curtis N. Sessler, Mark S. Gosnell, Mary Jo Grap, Gretchen M. Brophy, Pam V. O'Neal, Kimberly A. Keane, Eljim P. Tesoro, and R. K. Elswick \"The Richmond Agitation–Sedation Scale\", American Journal of Respiratory and Critical Care Medicine, Vol. 166, No. 10 (2002), pp. 1338-1344. doi: 10.1164/rccm.2107138

©Norsk oversettelse godkjent av Curtis Sessler november 2008 / Hilde Wøien, Hanne Alfheim, Anne Kathrine Langerud og Audun Stubhaug,
Anestesi- og Intensivklinikken Rikshospitalet HF">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"06297BB5D1275AA53F2101F3C8FA0A4F">
		["build_uid"] = <"bf92128e-adbf-4422-b5b8-eb0f5109a07b">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Richmond Agitation-Sedation Scale (RASS)
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {    -- The Richmond Agitation–Sedation Scale
										value matches {
											4|[local::at0005],
											3|[local::at0006],
											2|[local::at0007],
											1|[local::at0008],
											0|[local::at0009],
											-1|[local::at0010],
											-2|[local::at0011],
											-3|[local::at0012],
											-4|[local::at0013],
											-5|[local::at0014]
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0015] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0016] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Richmond Agitation-Sedation Scale (RASS)">
					description = <"The Richmond Agitation-Sedation Scale (RASS) is a tool used to measure the agitation or sedation level of a patient.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"The Richmond Agitation–Sedation Scale">
					description = <"">
				>
				["at0005"] = <
					text = <"Combative">
					description = <"Overtly combative or violent; immediate danger to staff">
				>
				["at0006"] = <
					text = <"Very agitated">
					description = <"Pulls on or removes tube(s) or catheter(s) or has aggressive behavior toward staff">
				>
				["at0007"] = <
					text = <"Agitated">
					description = <"Frequent nonpurposeful movement or patient–ventilator dyssynchrony">
				>
				["at0008"] = <
					text = <"Restless">
					description = <"Anxious or apprehensive but movements not aggressive or vigorous">
				>
				["at0009"] = <
					text = <"Alert and calm">
					description = <"Spontaneously pays attention to caregiver">
				>
				["at0010"] = <
					text = <"Drowsy">
					description = <"Not fully alert, but has sustained (more than 10 seconds) awakening, with eye contact, to voice">
				>
				["at0011"] = <
					text = <"Light sedation">
					description = <"Briefly (less than 10 seconds) awakens with eye contact to voice">
				>
				["at0012"] = <
					text = <"Moderate sedation">
					description = <"Any movement (but no eye contact) to voice">
				>
				["at0013"] = <
					text = <"Deep sedation">
					description = <"No response to voice, but any movement to physical stimulation">
				>
				["at0014"] = <
					text = <"Unarousable">
					description = <"No response to voice or physical stimulation">
				>
				["at0015"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0016"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Richmond Agitation-Sedation Scale (RASS)">
					description = <"Richmond Agitation-Sedation Scale (RASS) er et verktøy som brukes til å måle graden av sedasjons- eller agitasjonsnivået til en pasient.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"The Richmond Agitation–Sedation Scale">
					description = <"">
				>
				["at0005"] = <
					text = <"Aggressiv">
					description = <"Åpenlyst aggressiv, voldelig, umiddelbart til fare for  personalet">
				>
				["at0006"] = <
					text = <"Meget agitert">
					description = <"Drar i eller fjerner tube(r) eller kateter(e); aggressiv">
				>
				["at0007"] = <
					text = <"Agitert">
					description = <"Hyppige bevegelser uten formål, slåss mot respirator">
				>
				["at0008"] = <
					text = <"Rastløs">
					description = <"Engstelig eller urolig, men bevegelsene ikke aggressive">
				>
				["at0009"] = <
					text = <"Våken og rolig">
					description = <"">
				>
				["at0010"] = <
					text = <"Døsig">
					description = <"Ikke helt våken, men kan holde seg våken (åpner øynene/  øyekontakt) på tiltale (<10 sekunder)">
				>
				["at0011"] = <
					text = <"Lett sedert">
					description = <"Lar seg vekke kortvarig med øyekontakt på tiltale (<10  sekunder)">
				>
				["at0012"] = <
					text = <"Moderat sedert">
					description = <"Bevegelse eller åpner øynene på tiltale (men ingen  øyekontakt)">
				>
				["at0013"] = <
					text = <"Dypt sedert">
					description = <"Ingen respons på verbal oppfordring, men bevegelse eller  åpner øynene ved fysisk stimulering">
				>
				["at0014"] = <
					text = <"Ikke vekkbar">
					description = <"Ingen respons på verbal eller fysisk stimulering">
				>
				["at0015"] = <
					text = <"*Item tree (en)">
					description = <"@ internal @">
				>
				["at0016"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
			>
		>
	>

archetype (adl_version=1.4; uid=8062543c-f26b-47dc-b2b9-137268597904)
	openEHR-EHR-OBSERVATION.lab_test-blood_match.v1
specialise
	openEHR-EHR-OBSERVATION.lab_test.v1

concept
	[at0000.1]	-- Blood matching
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics, United Kingdom">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"07/11/2009">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record blood group and antibody findings.">
			use = <"Reporting by blood transfusion laboratory.
">
			keywords = <"lab", "pathology", "haematology", "group", "transfusion", "cross-match">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"rejected">
	other_contributors = <"Ian McNicoll, Ocean Informatics, UK", ...>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["references"] = <"NEHTA, Australia. Pathology Episode v1.0 - Data Group library [Internet]. 2009;[cited 2009 Dec 8 ] Available from: http://www.nehta.gov.au/data-group-library/data-specifications/dgl-pathology

IHE. IHE, Laboratory Technical Framework, Volume 3 Content [Internet]. 2008;Available from: http://www.ihe.net/Technical_Framework/upload/ihe_lab_TF_rel2_1-Vol-3_FT_2008-08-08.pdf.">
		["build_uid"] = <"d8d3612b-1138-4277-a634-7306eb9a7368">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["MD5-CAM-1.0.1"] = <"3DEA2538FA82ED87E92296B5D5C409E7">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	OBSERVATION[at0000.1] matches {	-- Blood matching
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Test name
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0077] occurrences matches {0..1} matches {	-- Diagnostic service
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0073] occurrences matches {0..1} matches {	-- Test status
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0037, 	-- Interim
													at0038, 	-- Final
													at0039, 	-- Supplementary
													at0040, 	-- Corrected (amended)
													at0074, 	-- Aborted
													at0079]	-- Never performed
												}
											}
										}
									}
									allow_archetype CLUSTER[at0065] occurrences matches {0..*} matches {	-- Specimen detail
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.specimen\.v1/}
										exclude
											archetype_id/value matches {/.*/}
									}
									ELEMENT[at0078] occurrences matches {0..*} matches {*}
									allow_archetype CLUSTER[at0089] occurrences matches {0..*} matches {	-- Per-result annotation
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.lab_result_annotation(-[a-zA-Z0-9_]+)*\.v1/}
										exclude
											archetype_id/value matches {/.*/}
									}
									ELEMENT[at0078.3] occurrences matches {0..1} matches {	-- ABO
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0078.2] occurrences matches {0..1} matches {	-- Rhesus
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0.90] occurrences matches {0..1} matches {	-- Antibodies detected
										value matches {
											DV_BOOLEAN matches {
												value matches {True, False}
											}
										}
									}
									CLUSTER[at0.91] occurrences matches {0..1} matches {	-- Antibodies
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0078.1] occurrences matches {0..1} matches {	-- Antibody
												value matches {
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0.92] occurrences matches {0..1} matches {	-- Details
												value matches {
													DV_TEXT matches {*}
												}
											}
										}
									}
									ELEMENT[at0057] occurrences matches {0..1} matches {	-- Overall interpretation
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0010] occurrences matches {0..*} matches {	-- Multimedia representation
										value matches {
											DV_MULTIMEDIA matches {
												media_type matches {[openEHR::]}
											}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0004] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0062] occurrences matches {0..1} matches {	-- Requestor order identifier
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0063] occurrences matches {0..1} matches {	-- Receiver order Identifier
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0068] occurrences matches {0..1} matches {	-- Laboratory test result identifier
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0017] occurrences matches {0..*} matches {	-- Receiving laboratory
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.organisation\.v1/}
						exclude
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0075] occurrences matches {0..1} matches {	-- Datetime result issued
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0.90"] = <
					text = <"Antibodies detected">
					description = <"Antibodies have been detected.">
				>
				["at0.91"] = <
					text = <"Antibodies">
					description = <"Group of findings relating to one antibody.">
				>
				["at0.92"] = <
					text = <"Details">
					description = <"Details about the antibody.">
				>
				["at0000"] = <
					text = <"Laboratory test">
					description = <"To record the result of a laboratory test which may be used to record a single valued test but will often be specialised or templated to represent multiple value or 'panel' tests.
This archetype also acts as the parent for specialisations appropriate for more specific laboratory tests microbiology, histopathology.">
				>
				["at0000.1"] = <
					text = <"Blood matching">
					description = <"Blood group information including ABO, Rhesus and antibodies etc.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Any event.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0005"] = <
					text = <"Test name">
					description = <"Specific identifier for this lab test. e.g. Full blood count , blood glucose, urine microbiology.  May equate to the result name for a single value result. Commonly a coded term e.g from LOINC or SNOMED-CT.">
				>
				["at0010"] = <
					text = <"Multimedia representation">
					description = <"Representations of the whole test in mutlimedia e.g image, audio, video.">
				>
				["at0017"] = <
					text = <"Receiving laboratory">
					description = <"Demographic details of the laboratory with responsibility for the test. Details of secondary laboratories may also be included.">
				>
				["at0037"] = <
					text = <"Interim">
					description = <"This is an initial or interim result - further updates are anticipated.">
				>
				["at0038"] = <
					text = <"Final">
					description = <"This is the final result. No further alterations are anticipated.">
				>
				["at0039"] = <
					text = <"Supplementary">
					description = <"This is a supplementary result ehich should be appended to a previous result.">
				>
				["at0040"] = <
					text = <"Corrected (amended)">
					description = <"This is a correction which should completely replace any previous results.">
				>
				["at0057"] = <
					text = <"Overall interpretation">
					description = <"An overall interpretative comment on this test.">
				>
				["at0062"] = <
					text = <"Requestor order identifier">
					description = <"The local ID assigned to the order by the order requester. Equivalent to the Placer Order Identifier.">
				>
				["at0063"] = <
					text = <"Receiver order Identifier">
					description = <"The local ID assigned to the test order by the order filler, usually by the (LIS) Laboratory Information System. Equivalent to the DICOM Accession Number and the Filler Order Identifier.">
				>
				["at0065"] = <
					text = <"Specimen detail">
					description = <"Details of the specimen being reported where all individual results are derived from the same specimen.">
				>
				["at0068"] = <
					text = <"Laboratory test result identifier">
					description = <"The identifier given to the laboratory test result of a pathology investigation.">
				>
				["at0073"] = <
					text = <"Test status">
					description = <"The status of the lab test as a whole.">
				>
				["at0074"] = <
					text = <"Aborted">
					description = <"The test was aborted and never completed.">
				>
				["at0075"] = <
					text = <"Datetime result issued">
					description = <"The date and/or time that the result was issued for the recorded ‘test status’.">
					comment = <"The date and time related to the results status is useful for version control and cumulative results for the report.">
				>
				["at0077"] = <
					text = <"Diagnostic service">
					description = <"The type of high-level diagnostic service e.g. biochemistry, haematology.">
				>
				["at0078"] = <
					text = <"Result">
					description = <"The result of the test.">
				>
				["at0078.1"] = <
					text = <"Antibody">
					description = <"The nature of the antibody.">
				>
				["at0078.2"] = <
					text = <"Rhesus">
					description = <"Rhesus blood group information.">
				>
				["at0078.3"] = <
					text = <"ABO">
					description = <"ABO blood group information.">
				>
				["at0079"] = <
					text = <"Never performed">
					description = <"The test was never performed.">
				>
				["at0089"] = <
					text = <"Per-result annotation">
					description = <"Slot to allow an annotation to be added to a particular test result at run-time.">
				>
				["at0090"] = <
					text = <"Requestor">
					description = <"Details of the clinician or organisation requesting the laboratory test.">
				>
			>
		>
	>

archetype (adl_version=1.4; uid=7b60fa4a-c8a8-46d4-af75-ee770e3a9104)
	openEHR-EHR-OBSERVATION.mayo_score.v0

concept
	[at0000]	-- Mayo score
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Vebjørn Arntzen">
				["organisation"] = <"Oslo University Hospital">
				["email"] = <"varntzen@ous-hf.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Bjørn Næss">
		["organisation"] = <"DIPS AS">
		["email"] = <"bna@dips.no">
		["date"] = <"2020-12-01">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å evaluere hvor aktiv en ulcerøs colitt er.">
			use = <"Bruk for å evaluere hvor aktiv en ulcerøs colitt er.">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To make an assessment of ulcerative colitis activity.">
			use = <"Use to make an assessment of ulcerative colitis activity.">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway", "Heather Leslie, Atomica informatics, Australia", "John Tore Valand, Helse Bergen HF, Norway", "Ørjan Næss, Diakonhjemmet sykehus, Norway", "Vendel Kristensen, Oslo University Hospital, Norway", "Liv Laugen, Oslo University Hospital, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Schroeder KW, Tremaine WJ, Ilstrup DM. Coated oral 5-aminosalicylic acid therapy for mildly to moderately active ulcerative colitis. A randomized study. N Engl J Med. 1987;317(26):1625-9.

https://www.mdcalc.com/mayo-score-disease-activity-index-dai-ulcerative-colitis

https://bestpractice.bmj.com/calculators/en-gb/MayoScore.htm">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"5164E40F683EBFA75E8478D0A7BE12AC">
		["build_uid"] = <"4edadcef-5840-4545-aab5-2da54c8405c0">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Mayo_score
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {0..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0009] occurrences matches {0..1} matches {    -- Stool frequency
										value matches {
											0|[local::at0010],
											1|[local::at0011],
											2|[local::at0012],
											3|[local::at0013]
										}
									}
									ELEMENT[at0014] occurrences matches {0..1} matches {    -- Rectal bleeding
										value matches {
											0|[local::at0015],
											1|[local::at0016],
											2|[local::at0017],
											3|[local::at0018]
										}
									}
									ELEMENT[at0019] occurrences matches {0..1} matches {    -- Mucosal appearance at endoscopy
										value matches {
											0|[local::at0020],
											1|[local::at0021],
											2|[local::at0022],
											3|[local::at0023]
										}
									}
									ELEMENT[at0024] occurrences matches {0..1} matches {    -- Physician rating of desease activity
										value matches {
											0|[local::at0025],
											1|[local::at0026],
											2|[local::at0027],
											3|[local::at0028]
										}
									}
									ELEMENT[at0029] occurrences matches {0..1} matches {    -- Total Mayo score
										value matches {
											DV_COUNT matches {
												magnitude matches {|0..12|}
											}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0030] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0032] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Mayo score">
					description = <"Scoring system for assessment of ulcerative colitis activity.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Stool frequency">
					description = <"Category for number stools per day exceeding what is normal for the individual.">
				>
				["at0010"] = <
					text = <"Normal number of stools/day">
					description = <"Normal number of stools/day for this patient.">
				>
				["at0011"] = <
					text = <"1-2 stools/day more than normal">
					description = <"1-2 stools/day more than normal for this patient.">
				>
				["at0012"] = <
					text = <"3-4 stools/day more than normal">
					description = <"3-4 stools/day more than normal for this patient.">
				>
				["at0013"] = <
					text = <">4 stools/day more than normal">
					description = <">4 stools/day more than normal for this patient.">
				>
				["at0014"] = <
					text = <"Rectal bleeding">
					description = <"Category for number of stools per day with visible blood, if any.">
					comment = <"3 points requires patients to have ≥50% of BMs with visible blood AND ≥1 BM with blood alone.">
				>
				["at0015"] = <
					text = <"No blood seen">
					description = <"">
				>
				["at0016"] = <
					text = <"Visible blood with stool less than half the time">
					description = <"">
				>
				["at0017"] = <
					text = <"Visible blood with stool half the time or more">
					description = <"">
				>
				["at0018"] = <
					text = <"Passing blood alone">
					description = <"">
				>
				["at0019"] = <
					text = <"Mucosal appearance at endoscopy">
					description = <"Category for findings of mucosal appearance during endoscopy.">
				>
				["at0020"] = <
					text = <"Normal or inactive disease">
					description = <"">
				>
				["at0021"] = <
					text = <"Mild disease">
					description = <"Mild disease (erythema, decreased vascular pattern, mild friability).">
				>
				["at0022"] = <
					text = <"Moderate disease">
					description = <"Moderate disease (marked erythema, absent vascular pattern, friability, erosions).">
				>
				["at0023"] = <
					text = <"Severe disease">
					description = <"Severe disease (spontaneous bleeding, ulceration).">
				>
				["at0024"] = <
					text = <"Physician rating of disease activity">
					description = <"Category for the overall assessment of ulcerative colitis activity made by physician.">
				>
				["at0025"] = <
					text = <"Normal">
					description = <"">
				>
				["at0026"] = <
					text = <"Mild">
					description = <"Mild disease.">
				>
				["at0027"] = <
					text = <"Moderate">
					description = <"Moderate disease.">
				>
				["at0028"] = <
					text = <"Severe">
					description = <"Severe disease.">
				>
				["at0029"] = <
					text = <"Total Mayo score">
					description = <"The sum of points assigned for each of the component variables.">
					comment = <"Scores range from zero to 12, with higher scores correlating with more severe disease.">
				>
				["at0030"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0032"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Mayo skår">
					description = <"Skåringssystem for å evaluere hvor aktiv en ulcerøs colitt er.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Tidspunkt">
					description = <"Spesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i en templat eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Avføringsfrekvens">
					description = <"Kategori for antall avføringer per dag som overstiger det som er normalt for individet.">
				>
				["at0010"] = <
					text = <"Normalt">
					description = <"">
				>
				["at0011"] = <
					text = <"1-2 tømminger/dag flere enn normalt">
					description = <"1-2 tømminger/dag flere enn normalt for denne pasienten.">
				>
				["at0012"] = <
					text = <"3-4 tømminger/dag flere enn normalt">
					description = <"3-4 tømminger/dag flere enn normalt for denne pasienten.">
				>
				["at0013"] = <
					text = <">5 tømminger/dag flere enn normalt">
					description = <">5 tømminger/dag flere enn normalt for denne pasienten.">
				>
				["at0014"] = <
					text = <"Rektale blødninger">
					description = <"Kategori for antall avføringer per dag med synlig blod, om noen.">
					comment = <"For at man skal skåre 3 poeng kreves at ≥50% av avføringene har synlig blod, OG at ≥1 har kun blod.">
				>
				["at0015"] = <
					text = <"Ikke synlig blod">
					description = <"">
				>
				["at0016"] = <
					text = <"Synlig blod i avføringen mindre enn halvparten av tiden">
					description = <"">
				>
				["at0017"] = <
					text = <"Synlig blod i avføringen mer enn halvparten av tiden">
					description = <"">
				>
				["at0018"] = <
					text = <"Tømming består utelukkende av blod">
					description = <"">
				>
				["at0019"] = <
					text = <"Slimhinnevurdering ved koloskopi">
					description = <"Kategori for vurdering av slimhinne ved koloskopi.">
				>
				["at0020"] = <
					text = <"Normal eller inaktiv sykdom">
					description = <"Normal eller inaktiv sykdom">
				>
				["at0021"] = <
					text = <"Mild sykdom">
					description = <"Mild sykdom (erytem, redusert karmønster, mild lederbarhet)">
				>
				["at0022"] = <
					text = <"Moderat sykdom">
					description = <"Moderat sykdom (markert erytem, utviskete karmønster, lederbarhet, erosjoner)">
				>
				["at0023"] = <
					text = <"Alvorlig sykdom">
					description = <"Alvorlig sykdom (spontan blødning, ulcerasjoner)">
				>
				["at0024"] = <
					text = <"Legens vurdering av tilstanden">
					description = <"Kategori for den overordnede vurderingen av ulcerøs colitt gjort av lege.">
				>
				["at0025"] = <
					text = <"Normal">
					description = <"">
				>
				["at0026"] = <
					text = <"Mild">
					description = <"">
				>
				["at0027"] = <
					text = <"Moderat">
					description = <"">
				>
				["at0028"] = <
					text = <"Alvorlig">
					description = <"">
				>
				["at0029"] = <
					text = <"Total Mayo skår">
					description = <"Summen av skår av hver enkelt kategori.">
					comment = <"Skåren går fra 0 til 12, der høyere skår korrelerer med mer alvorlig sykdom.">
				>
				["at0030"] = <
					text = <"*Item tree (en)">
					description = <"@ internal @">
				>
				["at0032"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel: lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
			>
		>
	>

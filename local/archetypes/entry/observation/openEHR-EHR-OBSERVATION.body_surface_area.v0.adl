archetype (adl_version=1.4; uid=99f4ffc4-cc9f-4328-98a1-24e66d270c5b)
	openEHR-EHR-OBSERVATION.body_surface_area.v0

concept
	[at0000]	-- Body Surface Area
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Cailin Lowry">
		["organisation"] = <"The Chris O'Brien Lifehouse at RPA">
		["email"] = <"cailin.lowry@lifehouserpa.org.au">
		["date"] = <"2012-12-07">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Body surface area (BSA) is the measured or calculated surface area of a human body. It is used in many measurements in medicine, including the calculation of drug dosages and the amount of fluids to be administered intravenously.">
			use = <"Use to measure or calculate the surface area of the human body. BSA can be used to support clinical decision-making, for example determining dosages of chemotherapy, glucocorticoids and other drugs with a narrow therapeutic index, calculating the cardiac index and in wider secondary uses such as policy-making and clinical research.">
			keywords = <"body", "surface", "area", "BSA", "surface area", "skin">
			misuse = <"Do not use to record the percentage of body surface area involvement in burns or other skin damage etc. This information would be carried in separate specific archetypes.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjoern Arntzen, Oslo university hospital, Norway", "Koray Atalag, University of Auckland, New Zealand", "Gustavo Bacelar-Silva, Healthcare Designs, Brazil (openEHR Editor)", "Silje Ljosland Bakke, National ICT Norway, Norway (openEHR Editor)", "Lars Bitsch-Larsen, Haukeland University hospital, Norway", "Rong Chen, Cambio Healthcare Systems, Sweden", "Einar Fosse, National Centre for Integrated Care and Telemedicine, Norway", "Sebastian Garde, Ocean Informatics, Germany", "Christian Ghan, Lifehouse, Australia", "Heather Grain, Llewelyn Grain Informatics, Australia", "Sam Heard, Ocean Informatics, Australia", "Lars Karlsen, DIPS ASA, Norway", "Heather Leslie, Ocean Informatics, Australia (openEHR Editor)", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Bjoern Naess, DIPS ASA, Norway", "Andrej Orel, Marand d.o.o., Slovenia", "Jussara Rotzsch, UNB, Brazil", "Dennis Valk, Code24 BV, Netherlands">
	other_details = <
		["revision"] = <"0.0.1-alpha">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["references"] = <"Wikipedia contributors. Body surface area. Wikipedia, The Free Encyclopedia. July 18, 2010, 10:20 UTC. Available at: http://en.wikipedia.org/w/index.php?title=Body_surface_area&oldid=374115233. Accessed October 19, 2010.

Mosteller RD: Simplified Calculation of Body Surface Area. N Engl J Med 1987 Oct 22;317(17):1098 (letter)

DuBois D; DuBois EF: A formula to estimate the approximate surface area if height and weight be known. Arch Int Med 1916 17:863-71.
Haycock G.B., Schwartz G.J.,Wisotsky D.H.  Geometric method for measuring body surface area: A height weight formula validated in infants, children and adults.   The Journal of Pediatrics 1978  93:1:62-66

Gehan EA, George SL, Estimation of human body surface area from height and weight.   Cancer Chemother Rep 1970 54:225-35.
Boyd E, The growth of the surface area of the human body. Minneapolis: university of Minnesota Press, 1935.  (Formula from: http://www.ispub.com/journals/IJA/Vol2N2/bsa.htm )

Lam TK, Leung DT: More on simplified calculation of body-surface area. N Engl J Med 1988 Apr 28;318(17):1130, (letter)
Wang Y, Moss J, Thisted R. Predictors of body surface area. J Clin Anesth. 1992; 4(1):4-10.">
		["build_uid"] = <"d73ebe79-cf2f-4710-bd82-134922657499">
		["original_publisher"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"AAFFA3B5EB38864BFDD22FF78A3A17FD">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	OBSERVATION[at0000] matches {	-- Body Surface Area
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..1} matches {	-- Point in time
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0004] matches {	-- Body Surface Area
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::335]>
												list = <
													["1"] = <
														units = <"m2">
														precision = <|2|>
													>
												>
											>
										}
									}
									ELEMENT[at0019] occurrences matches {0..1} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0005] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0009] occurrences matches {0..1} matches {	-- Method
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0010, 	-- Automatic entry
									at0011]	-- Direct entry
								}
							}
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {	-- Formula
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0012, 	-- Dubois and Dubois
									at0013, 	-- Mosteller
									at0014, 	-- Haycock
									at0015, 	-- Gehan and George
									at0016, 	-- Boyd
									at0017, 	-- Fujimoto
									at0018]	-- Takihara
								}
							}
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0008] occurrences matches {0..*} matches {	-- Device
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
					allow_archetype CLUSTER[at0007] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Body Surface Area">
					description = <"Body surface area (BSA) is the measured or calculated surface area of a human body.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Point in time">
					description = <"Specified point in time at which the BSA was recorded, and  which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Body Surface Area">
					description = <"The measured or calculated surface area of a human body.">
					comment = <"For example: '1.81 m2'">
				>
				["at0005"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0006"] = <
					text = <"Formula">
					description = <"Formula used to calculate the BSA.  A number of different formulae have been devised.">
					comment = <"For example: Mosteller, DuBois and DuBois, Haycock and Boyd.">
				>
				["at0007"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0008"] = <
					text = <"Device">
					description = <"The device used to estimate body surface area.">
				>
				["at0009"] = <
					text = <"Method">
					description = <"The method by which the body surface area was estimated.">
				>
				["at0010"] = <
					text = <"Automatic entry">
					description = <"The body surface area was calculated and entered automatically without user intervention.">
				>
				["at0011"] = <
					text = <"Direct entry">
					description = <"The body surface area was calculated and entered directly by the user.">
				>
				["at0012"] = <
					text = <"Dubois and Dubois">
					description = <"The Dubois and Dubois formula was used to estimate body surface area.">
				>
				["at0013"] = <
					text = <"Mosteller">
					description = <"The Mosteller formula was used to estimate body surface area.">
				>
				["at0014"] = <
					text = <"Haycock">
					description = <"The Haycock formula was used to estimate body surface area.">
				>
				["at0015"] = <
					text = <"Gehan and George">
					description = <"The Gehan and George formula was used to estimate body surface area.">
				>
				["at0016"] = <
					text = <"Boyd">
					description = <"The Boyd formula was used to estimate body surface area.">
				>
				["at0017"] = <
					text = <"Fujimoto">
					description = <"The Fujimoto formula to estimate body surface area.">
				>
				["at0018"] = <
					text = <"Takihara">
					description = <"The Takihara formula was used to estimate body surface area.">
				>
				["at0019"] = <
					text = <"Comment">
					description = <"Any additional comment about the body surface area estimation.">
				>
			>
		>
	>

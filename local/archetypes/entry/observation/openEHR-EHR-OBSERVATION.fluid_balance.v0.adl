archetype (adl_version=1.4; uid=035370a4-35a4-40c8-9c48-3bb2b2d26437)
	openEHR-EHR-OBSERVATION.fluid_balance.v0

concept
	[at0000]	-- Fluid Balance
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Vebjoern Arntzen">
				["organisation"] = <"Oslo Univeristy Hospital HF, Norway">
				["email"] = <"varntzen@ous-hf.no">
			>
			accreditation = <"Enterprise Architect, RN">
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2013-02-22">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere oppsamlet eller total mengde væskeinntak og væsketap, samt estimat over væskebalansen til et individ i et gitt tidsintervall.">
			use = <"Brukes for å registrere oppsamlet eller total mengde væskeinntak og væsketap, samt estimat over væskebalanse til et individ i et gitt tidsintervall.

Et væskeregnskap vil vanligvis bestå av en kombinasjon av OBSERVATION.fluid_input (Norsk: Væskeinntak), OBSERVATION.fluid_output (Norsk: Væsketap) og denne arketypen for å fange alle nødvendige komponenter.">
			keywords = <"væske, balanse, væskeinntak, væsketap, tap, inntak, svette, perspiratio, fordamping", ...>
			misuse = <"Skal ikke brukes for å registrere enkeltmålinger av væskeinntak eller væsketap. Bruk OBSERVATION.fluid_input (Væskeinntak) eller OBSERVATION.fluid_output (Væsketap) for dette.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the cumulative or total amounts of fluid input and output, plus an estimation of the fluid balance status of a subject over specified intervals of time.">
			use = <"Use to record cumulative or total amounts of fluid input and output, plus an estimation of the fluid balance status of a subject over specified periods of time.

A fluid balance chart will typically require a combination of OBSERVATION.fluid_input, OBSERVATION.fluid_output and this archetype to capture all the required components.">
			keywords = <"fluid", "balance", "input", "output", "loss", "sweat", "insensible", "evaporation", "perspiratio">
			misuse = <"Not to be used to record individual measurements of fluid input or output. Use OBSERVATION.fluid_input or OBSERVATION.fluid_output for these purposes.">
			copyright = <"© National E-Health Transition Authority, openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Ian McNicoll, Ocean Informatics, United Kingdom", "Vebjørn Arntzen, Oslo University Hospital HF, Norway">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["revision"] = <"0.0.1-alpha">
		["references"] = <"Derived from: Fluid Balance, Draft Archetype [Internet]. nehta, Australia, nehta Clinical Knowledge Manager [cited: 2015-11-08]. Available from: http://dcm.nehta.org.au/ckm/#showArchetype_1013.1.1203">
		["build_uid"] = <"afa935f9-ede7-44a3-9eb1-5480e43bba1c">
		["original_publisher"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"EE1648672678D5755B3B30BD3EEA13FE">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	OBSERVATION[at0000] matches {	-- Fluid Balance
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					INTERVAL_EVENT[at0002] occurrences matches {0..*} matches {	-- Any Interval Total
						math_function matches {
							DV_CODED_TEXT matches {
								defining_code matches {[openehr::148]}
							}
						}
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Insensible Loss
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::129]>
												list = <
													["1"] = <
														units = <"ml">
													>
													["2"] = <
														units = <"l">
													>
													["3"] = <
														units = <"[foz_us]">
													>
												>
											>
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Total Input
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::129]>
												list = <
													["1"] = <
														units = <"ml">
													>
													["2"] = <
														units = <"l">
													>
													["3"] = <
														units = <"[foz_us]">
													>
												>
											>
										}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {	-- Total Output
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::129]>
												list = <
													["1"] = <
														units = <"ml">
													>
													["2"] = <
														units = <"l">
													>
													["3"] = <
														units = <"[foz_us]">
													>
												>
											>
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {	-- Fluid Balance
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::129]>
												list = <
													["1"] = <
														units = <"ml">
													>
													["2"] = <
														units = <"l">
													>
													["3"] = <
														units = <"[foz_us]">
													>
												>
											>
										}
									}
								}
							}
						}
					}
					INTERVAL_EVENT[at0008] occurrences matches {0..1} matches {	-- 24 Hour Total
						math_function matches {
							DV_CODED_TEXT matches {
								defining_code matches {[openehr::148]}
							}
						}
						width matches {
							DV_DURATION matches {
								value matches {|PT24H|}
							}
						}
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]	-- /data[Event Series]/events[Any Interval Total]/data[Tree]
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0009] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0010] occurrences matches {0..1} matches {	-- Insensible Loss Estimation Method
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0011] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Fluid Balance">
					description = <"Estimation of the fluid balance status of a subject by subtracting the total fluid output plus insensible loss from the total fluid input.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any Interval Total">
					description = <"The total amount representing the total over a specified interval of time, or a cumulative total to a specified time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Insensible Loss">
					description = <"Estimation of the amount of fluid loss from water lost by evaporation from the skin and respiratory tract.">
					comment = <"The insensible loss depends on a range of parameters, among others like body weight, body temperature, time in open surgery and whether the individual is on medical ventilator or not. The estimate should comply with a standardized method.">
				>
				["at0005"] = <
					text = <"Total Input">
					description = <"The total amount of fluid input, for a specified interval of time.">
				>
				["at0006"] = <
					text = <"Total Output">
					description = <"The total amount of fluid output for a specified interval of time.">
				>
				["at0007"] = <
					text = <"Fluid Balance">
					description = <"Overall fluid balance for a specified period of time, calculated by subtracting the total fluid output plus insensible loss from the total fluid input.">
					comment = <"Fluid Balance = Total Fluid Input - (Total Fluid Output + Insensible Loss).">
				>
				["at0008"] = <
					text = <"24 Hour Total">
					description = <"The total amount over a specified 24 hour period.">
				>
				["at0009"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0010"] = <
					text = <"Insensible Loss Estimation Method">
					description = <"The method for estimation of insensible loss.">
				>
				["at0011"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"e.g. Local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Væskebalanse">
					description = <"Estimat over væskebalansestatus til et individ ved å trekke totalt væsketap pluss perspiratio fra totalt væskeinntak.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Væskebalanse valgfritt intervall">
					description = <"Væskebalansen over et gitt intervall, eller den totale oppsamlede væskebalansen over en gitt tid.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Perspiratio">
					description = <"Estimat over mengden væsketap ved fordampning fra hud og luftveier.">
					comment = <"Væsketap ved perspiratio avhenger av blant annet hudoverflate, kroppstemperatur, lengden på åpen kirurgi og eventuell respiratorbehandling. Estimat bør skje i henhold til skjema for beregning av perspiratio.">
				>
				["at0005"] = <
					text = <"Totalt væskeinntak">
					description = <"Den totale mengden væskeinntak i et gitt tidsintervall.">
				>
				["at0006"] = <
					text = <"Totalt væsketap">
					description = <"Den totale mengden væsketap i et gitt tidsintervall.">
				>
				["at0007"] = <
					text = <"Væskebalanse">
					description = <"Overordnet væskebalanse for en gitt tidsperiode, fremkommet ved å trekke det totale væsketapet pluss perspiratio fra det totale væskeinntaket.">
					comment = <"Væskebalanse = Totalt væskeinntak - (Totalt væsketap + perspiratio).">
				>
				["at0008"] = <
					text = <"Væskebalanse døgn">
					description = <"Væskebalansen over en periode på 24 timer.">
				>
				["at0009"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0010"] = <
					text = <"Metode estimat perspiratio">
					description = <"Metoden som er brukt for å estimere perspiratio.">
				>
				["at0011"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR eller CIMI.">
				>
			>
		>
	>

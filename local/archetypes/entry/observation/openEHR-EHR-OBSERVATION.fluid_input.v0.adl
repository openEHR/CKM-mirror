archetype (adl_version=1.4; uid=01abbbb2-3973-4ca7-9b7a-2485025469af)
	openEHR-EHR-OBSERVATION.fluid_input.v0

concept
	[at0000]	-- Fluid Input
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["sl"] = <
			language = <[ISO_639-1::sl]>
			author = <
				["name"] = <"?">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Vebjørn Arntzen">
				["organisation"] = <"Oslo University Hospital">
				["email"] = <"varntzen@ous-hf.no">
			>
			accreditation = <"Enterprise architect, RN">
		>
	>
description
	original_author = <
		["name"] = <"Dr Ian McNicoll">
		["organisation"] = <"Ocean Informatics, United Kingdom">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"2009-11-11">
	>
	details = <
		["sl"] = <
			language = <[ISO_639-1::sl]>
			purpose = <"*To record details of fluid input, often used as part of fluid balance estimation.(en)">
			use = <"*Use to record details of fluid input as part of routine nursing care.

Use to record details of individual fluid input activities, in order to allow fluid balance estimations. For example: \"Oral\", \"Cup of Tea\", 220ml; or 1 litre Normal Saline infusion.

May also be used to record descriptive assessments of input.

(en)">
			keywords = <"*fluid(en)", "*fluid balance(en)", "*input output(en)", "*io(en)", "*input(en)">
			misuse = <"*Do not use as a primary record of the administration of fluids e.g intravascular, dialyisis. May be used alongside administration recrods ,normally an ACTION.(en)">
			copyright = <"© National E-Health Transition Authority, openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details of fluid input, often used as part of fluid balance estimation.">
			use = <"Use to record details of fluid input as part of routine nursing care.

Use to record details of individual fluid input activities, in order to allow fluid balance estimations. For example: \"Oral\", \"Cup of Tea\", 220ml; or 1 litre Normal Saline infusion.

May also be used to record descriptive assessments of fluid input.

A fluid balance chart will typically require a combination of OBSERVATION.fluid_output, OBSERVATION.fluid_balance and this archetype to capture all the required components.">
			keywords = <"fluid", "io", "input", "balance">
			misuse = <"Not to be used as a primary record of the fluid administration such as intravascular therapy or haemodialyisis.

Not to be used to record fluid output. Use OBSERVATION.fluid_output for this purpose.

Not to be used to record fluid balance calculations. Use OBSERVATION.fluid_balance for this purpose.">
			copyright = <"© National E-Health Transition Authority, openEHR Foundation">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere detaljerte opplysninger om væskeinntak, ofte brukt som del av væskebalanse.">
			use = <"	
Brukes for å registrere detaljer om et individs væskeinntak. Dette gjøres ofte rutinemessig i forbindelse med et væskeregnskap.

Brukes for å registrere detaljer om væskeinntaket, for eksempel \"Per os\", \"Kaffe\", \"150 ml\"; eller 1000 ml NaCl 0,9%.

Et væskeregnskap vil trenge data fra denne arketypen, OBSERVATION.fluid_input, i tillegg til arketypene OBSERVATION.fluid_output og OBSERVATION.fluid_balance for å få med seg alle nødvendige komponenter.">
			keywords = <"væske", "inn/ut", "væskebalanse", "inntak", "væskeinntak">
			misuse = <"Skal ikke brukes for å registrere væsketap. Bruk OBSERVATION.fluid_output (Væsketap) til dette.

Skal ikke brukes for å registrere væskeregnskap. Bruk OBSERVATION.fluid_balance (Væskeregnskap) til dette.">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Sam Heard, Ocean Informatics, Australia", "Heather Leslie, Ocean Informatics, Australia">
	other_details = <
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Derived from: Fluid Input, Draft Archetype [Internet]. nehta, Australia, nehta Clinical Knowledge Manager [cited: 2015-11-08]. Available from: http://dcm.nehta.org.au/ckm/#showArchetype_1013.1.1205">
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"D737933A5E3FFF41243F3CBD21F96F8F">
		["build_uid"] = <"cba7788c-2bbc-4eec-a311-609e39e2338e">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {	-- Fluid Input
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..*} matches {	-- Measurement Time
						data matches {
							ITEM_TREE[at0003] matches {	-- List
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0034] occurrences matches {0..1} matches {	-- Route
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0036] occurrences matches {0..1} matches {	-- Fluid
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0035] occurrences matches {0..1} matches {	-- Volume
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::129]>
												list = <
													["1"] = <
														units = <"ml">
														magnitude = <|>=0.0|>
														precision = <|0|>
													>
													["2"] = <
														units = <"l">
														magnitude = <|>=0.0|>
														precision = <|2|>
													>
													["3"] = <
														units = <"[foz_us]">
													>
												>
											>
										}
									}
									ELEMENT[at0032] occurrences matches {0..1} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at0039] occurrences matches {0..*} matches {	-- Fluid details
										include
											archetype_id/value matches {/.*/}
									}
								}
							}
						}
					}
					INTERVAL_EVENT[at0038] occurrences matches {0..*} matches {	-- Measurement Interval
						math_function matches {
							DV_CODED_TEXT matches {
								defining_code matches {[openehr::640]}
							}
						}
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]	-- /data[Event Series]/events[Measurement Time]/data[List]
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0008] matches {	-- List
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0031] occurrences matches {0..1} matches {	-- Measurement Precision
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0018, 	-- Estimated
									at0019, 	-- Measured
									at0041]	-- Reported
								}
							}
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0033] occurrences matches {0..1} matches {	-- Input Device
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
						exclude
							archetype_id/value matches {/.*/}
					}
					allow_archetype CLUSTER[at0028] occurrences matches {0..1} matches {	-- Measurement Device
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
						exclude
							archetype_id/value matches {/.*/}
					}
					allow_archetype CLUSTER[at0040] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Fluid Input">
					description = <"Record of fluid being ingested by, or administered to, the subject.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Measurement Time">
					description = <"The measurement, or estimation, of the volume of a specified fluid input at a specified point-in-time.">
				>
				["at0003"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0008"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0018"] = <
					text = <"Estimated">
					description = <"The fluid volume has been estimated.">
				>
				["at0019"] = <
					text = <"Measured">
					description = <"The fluid volume has been directly measured.">
				>
				["at0028"] = <
					text = <"Measurement Device">
					description = <"Details of device used to measure the fluid input.">
				>
				["at0031"] = <
					text = <"Measurement Precision">
					description = <"Records the level of precision of the fluid input observation.">
				>
				["at0032"] = <
					text = <"Comment">
					description = <"Additional narrative about the fluid input not captured in other fields.">
				>
				["at0033"] = <
					text = <"Input Device">
					description = <"The input device, for example a nasogastric tube or a jejunal catheter.">
				>
				["at0034"] = <
					text = <"Route">
					description = <"The delivery route of the fluid.">
					comment = <"For example: nasogastric tube; oral; intravenous or intraosseous. The route can be entered manually or fetched from an application, for example from ACTION.medication; especially for fluids given intraveniously or parenteral. A link can be used for this purpose.">
				>
				["at0035"] = <
					text = <"Volume">
					description = <"The volume of fluid ingested or administered.">
					comment = <"The volume can be entered manually or fetched from an application, for example from ACTION.medication; especially for fluids given intraveniously or parenteral. A link can be used for this purpose.">
				>
				["at0036"] = <
					text = <"Fluid">
					description = <"Identification of the fluid ingested or administered.">
					comment = <"For example: Normal Saline; cup of tea; or parenteral nutrition. The fluid name can be entered manually or fetched from an application, for example from ACTION.medication; especially for fluids given intraveniously or parenteral. A link can be used for this purpose.">
				>
				["at0038"] = <
					text = <"Measurement Interval">
					description = <"The measurement, or estimation, of the volume of a specified fluid input during a specified interval of time.">
				>
				["at0039"] = <
					text = <"Fluid details">
					description = <"Details about the fluid input.">
					comment = <"For example: further details about the fluid, such as nutritional value.">
				>
				["at0040"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"e.g. Local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0041"] = <
					text = <"Reported">
					description = <"The fluid volume has been reported by a third party.">
				>
			>
		>
		["sl"] = <
			items = <
				["at0000"] = <
					text = <"*Fluid Input(en)">
					description = <"*Record of fluid being ingested by, or administered to, the subject.(en)">
				>
				["at0001"] = <
					text = <"*Event Series(en)">
					description = <"*@ internal @(en)">
				>
				["at0002"] = <
					text = <"*Measurement Time(en)">
					description = <"*The measurement, or estimation, of the volume of a specified fluid input at a specified point-in-time.(en)">
				>
				["at0003"] = <
					text = <"*List(en)">
					description = <"*@ internal @(en)">
				>
				["at0008"] = <
					text = <"*List(en)">
					description = <"*@ internal @(en)">
				>
				["at0018"] = <
					text = <"*Estimated(en)">
					description = <"*The fluid volume has been estimated.(en)">
				>
				["at0019"] = <
					text = <"*Measured(en)">
					description = <"*The fluid volume has been directly measured.(en)">
				>
				["at0028"] = <
					text = <"*Measurement Device(en)">
					description = <"*Details of device used to measure the fluid input.(en)">
				>
				["at0031"] = <
					text = <"*Measurement Precision(en)">
					description = <"*Records the level of precision of the fluid input observation.(en)">
				>
				["at0032"] = <
					text = <"*Comment(en)">
					description = <"*Additional narrative about the fluid input not captured in other fields.(en)">
				>
				["at0033"] = <
					text = <"*Input Device(en)">
					description = <"*The input device, for example a nasogastric tube or a jejunal catheter.(en)">
				>
				["at0034"] = <
					text = <"*Route(en)">
					description = <"*The delivery route of the fluid.(en)">
					comment = <"*For example: nasogastric tube; oral; intravenous or intraosseous.(en)">
				>
				["at0035"] = <
					text = <"*Volume(en)">
					description = <"*The volume of fluid ingested or administered.(en)">
				>
				["at0036"] = <
					text = <"*Fluid(en)">
					description = <"*Identification of the fluid ingested or administered.(en)">
					comment = <"*For example: Normal Saline; cup of tea; or parenteral nutrition.(en)">
				>
				["at0038"] = <
					text = <"*Measurement Interval(en)">
					description = <"*The measurement, or estimation, of the volume of a specified fluid input during a specified interval of time.(en)">
				>
				["at0039"] = <
					text = <"*Cluster(en)">
					description = <"**(en)">
				>
				["at0040"] = <
					text = <"*Cluster(en)">
					description = <"**(en)">
				>
				["at0041"] = <
					text = <"*Reported(en)">
					description = <"*The fluid volume has been reported by a third party.(en)">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Væskeinntak">
					description = <"Registrering av væske som blir drukket av, eller administrert til, et individ.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Måletidspunkt">
					description = <"Målingen eller estimatet av mengden av en spesifisert væske som er drukket eller administrert på et gitt tidspunkt.">
				>
				["at0003"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0008"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0018"] = <
					text = <"Estimert">
					description = <"Mengden væskeinntak er estimert.">
				>
				["at0019"] = <
					text = <"Målt">
					description = <"Mengden væskeinntak er direkte observert og målt.">
				>
				["at0028"] = <
					text = <"Måleutstyr">
					description = <"Detaljer om måleutstyret som er brukt for å måle væskeinntaket.">
				>
				["at0031"] = <
					text = <"Presisjonsnivå på målingen">
					description = <"Presisjonsnivået til registreringen av væskeinntaket.">
				>
				["at0032"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om væsketapet som ikke er registrert i andre felt.">
				>
				["at0033"] = <
					text = <"Utstyr">
					description = <"Typen utstyr som er brukt for å administrere væsken, for eksempel ernæringssonde eller en jejunal sonde.">
				>
				["at0034"] = <
					text = <"Administrasjonsvei">
					description = <"Administrasjonsveien til væsken.">
					comment = <"For eksempel ernæringssonde, per os, intravenøst eller intraossøst. Administrasjonsveien kan bli registrert manuelt eller hentet fra en applikasjon, for eksempel via ACTION.medication; spesielt for væsker som er gitt intravenøst eller parenteralt. En lenke kan bruks for dette formålet.">
				>
				["at0035"] = <
					text = <"Mengde">
					description = <"Mengden væske som er drukket eller administrert.">
					comment = <"Mengden kan bli registrert manuelt eller hentet fra en applikasjon, for eksempel via ACTION.medication; spesielt for væsker som er gitt intravenøst eller parenteralt. En lenke kan bruks for dette formålet.">
				>
				["at0036"] = <
					text = <"Væske">
					description = <"Identifisering av væsken som er drukket eller administrert.">
					comment = <"For eksempel fysiologisk saltvann; en kopp kaffe eller parenteral ernæring. Navnet på væsken kan bli registrert manuelt eller hentet fra en applikasjon, for eksempel via ACTION.medication; spesielt for væsker som er gitt intravenøst eller parenteralt. En lenke kan bruks for dette formålet.">
				>
				["at0038"] = <
					text = <"Målingsintervall">
					description = <"Målingen eller estimatet av mengden av et spesifikt væskeinntaki løpet av et gitt intervall.">
				>
				["at0039"] = <
					text = <"Detaljer om væsken">
					description = <"Detaljer om væskeinntaket.">
					comment = <"For eksempel ytterligere detaljer om væsken, som ernæringsinnhold.">
				>
				["at0040"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR eller CIMI.">
				>
				["at0041"] = <
					text = <"Rapportert">
					description = <"Mengden væskeinntak har blitt rapportert av en tredjepart.">
				>
			>
		>
	>

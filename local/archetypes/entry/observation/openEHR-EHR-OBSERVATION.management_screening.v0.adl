archetype (adl_version=1.4; uid=f2debd59-f888-4729-87dc-e398a8c496d2)
	openEHR-EHR-OBSERVATION.management_screening.v0

concept
	[at0000]	-- Management/treatment screening questionnaire
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Silje Ljosland Bakke, Marit Alice Venheim">
				["organisation"] = <"Helse Vest IKT AS">
				["email"] = <"silje.ljosland.bakke@helse-vest-ikt.no, marit.alice.venheim@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2020-03-13">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere responsen i et individuelt eller selvrapportert registreringsskjema for sykdomshåndtering eller behandling som blir utført.">
			use = <"Brukes for å registrere responsen i et individuelt eller selvrapportert registreringsskjema for sykdomshåndtering eller behandling som blir utført.

Vanlige bruksområder inkluderer, men er ikke begrenset til:
- Systematisk spørring i enhver konsultasjon; eller
- Spesifikk spørring relatert til overvåkning av infeksjonssykdom.">
			misuse = <"">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the responses to an individual- or self-reported questionnaire screening for management or treatment carried out.">
			use = <"Use to record the responses to an individual- or self-reported questionnaire screening for management or treatment carried out.

If additional details about a specific activity need to be recorded, use the RM LINK class to define a relationship between this archetype and an instance of the appropriate ENTRY archetype archetype within the same template.

Common use cases include, but are not limited to:
- Systematic questioning in any consultation; or
- Specific questioning related to infectious disease surveillance.">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"62794FD978B6E0509B2026AC23D5C446">
		["build_uid"] = <"4c14b210-1ccb-407e-a7e4-1c5a99dd657a">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact http://www.snomed.org/snomed-ct/get-snomedct or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Management/treatment screening questionnaire
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {0..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0034] occurrences matches {0..1} matches {    -- Screening purpose
										value matches {
											DV_TEXT matches {*}
										}
									}
									CLUSTER[at0022] occurrences matches {0..*} matches {    -- Specific management/treatment activity
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0004] matches {    -- Activity name
												value matches {
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0005] occurrences matches {0..1} matches {    -- Actioned?
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0023,    -- Actioned
															at0024,    -- Not actioned
															at0027]    -- Unknown
														}
													}
												}
											}
											ELEMENT[at0006] occurrences matches {0..1} matches {    -- Date actioned
												value matches {
													DV_DATE_TIME matches {*}
												}
											}
											allow_archetype CLUSTER[at0036] occurrences matches {0..*} matches {    -- Activity details
												include
													archetype_id/value matches {/openEHR-EHR-CLUSTER\.inspired_oxygen\.v1/}
											}
											ELEMENT[at0035] occurrences matches {0..1} matches {    -- Comment
												value matches {
													DV_TEXT matches {*}
												}
											}
										}
									}
									ELEMENT[at0025] occurrences matches {0..1} matches {    -- Overall comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0007] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0021] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Management/treatment screening questionnaire">
					description = <"An individual- or self-reported questionnaire screening for management or treatment carried out.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Activity name">
					description = <"Name of the management or treatment activity being screened.">
				>
				["at0005"] = <
					text = <"Actioned?">
					description = <"Did the management or treatment activity take place?">
				>
				["at0006"] = <
					text = <"Date actioned">
					description = <"The date/time when the management or treatment activity was carried out.">
					comment = <"Partial dates are allowed.">
				>
				["at0007"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0021"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0022"] = <
					text = <"Specific management/treatment activity">
					description = <"Grouping of data elements related to screening for a single management or treatment activity.">
				>
				["at0023"] = <
					text = <"Actioned">
					description = <"The specific management activity was carried out.">
				>
				["at0024"] = <
					text = <"Not actioned">
					description = <"The specific management activity was not carried out.">
				>
				["at0025"] = <
					text = <"Overall comment">
					description = <"Additional narrative about all management and treatment activities, not captured in other fields.">
				>
				["at0027"] = <
					text = <"Unknown">
					description = <"It is not known whether the management activity was carried out.">
				>
				["at0034"] = <
					text = <"Screening purpose">
					description = <"The reason for overall screening.">
					comment = <"For example: screening for an infectious disease, such as SARS-COV-2.">
				>
				["at0035"] = <
					text = <"Comment">
					description = <"Additional narrative about the specific management or treatment activity, not captured in other fields.">
				>
				["at0036"] = <
					text = <"Activity details">
					description = <"Additional details about the specific activity.">
					comment = <"For example: Details about oxygen therapy.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Registreringsskjema for sykdomshåndtering/behandling">
					description = <"Et individuelt eller selvrapportert registreringsskjema for sykdomshåndtering eller behandling som blir utført.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert hendelse">
					description = <"Standard, uspesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i et templat eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Navn på aktiviteten">
					description = <"Navnet på sykdomshåndteringen (tiltaket) eller behandlingsaktiviteten som blir registrert.">
				>
				["at0005"] = <
					text = <"Iverksatt?">
					description = <"Ble tiltaket eller aktiviteten iverksatt?">
				>
				["at0006"] = <
					text = <"Dato">
					description = <"Dato/klokkeslett da sykdomshåndteringen (tiltaket) eller behandlingen ble utført.">
					comment = <"Delvis dato er tillatt.">
				>
				["at0007"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0021"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR eller CIMI.">
				>
				["at0022"] = <
					text = <"Spesifikk håndterings-/behandlingsaktivitet">
					description = <"Gruppering av dataelementer relatert til registreringen for en enkel sykdomshåndtering eller behandlingsaktivitet.">
				>
				["at0023"] = <
					text = <"Iverksatt">
					description = <"Det spesifikke tiltaket ble iverksatt.">
				>
				["at0024"] = <
					text = <"Ikke iverksatt">
					description = <"Det spesifikke tiltaket ble ikke iverksatt.">
				>
				["at0025"] = <
					text = <"Kommentar">
					description = <"Ytterligere kommentar til all sykdomshåndtering og behandlingsaktivitet som ikke er fanget opp i andre felt.">
				>
				["at0027"] = <
					text = <"Ukjent">
					description = <"Det er ukjent om tiltaket ble iverksatt.">
				>
				["at0034"] = <
					text = <"Registreringsformål">
					description = <"Årsaken til registreringen.">
					comment = <"For eksempel: Screening for en infeksjonssykdom, som SARS-COV-2.">
				>
				["at0035"] = <
					text = <"Kommentar">
					description = <"Ytterligere kommentar til den spesifikke sykdomshåndteringen eller behandlingen som ikke er fanget opp i andre felt.">
				>
				["at0036"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere detaljer om den spesifikke aktiviteten.">
					comment = <"For eksempel detaljer om oksygenbehandling.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0027"] = <[SNOMED-CT::261665006]>
				["at0023"] = <[SNOMED-CT::385652002]>
				["at0024"] = <[SNOMED-CT::385660001]>
			>
		>
	>

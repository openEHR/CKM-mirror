archetype (adl_version=1.4; uid=1673ecda-0271-49ba-924b-af836e07bf09)
	openEHR-EHR-OBSERVATION.body_segment_length_discrepancy.v0

concept
	[at0000]	-- Body segment length discrepancy
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"John Tore Valand, Vebjørn Arntzen">
				["organisation"] = <"Helse Bergen HF, Oslo universitetssykehus HF">
				["email"] = <"john.tore.valand@helse-bergen.no; varntzen@ous-hf.no, john.tore.valand@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"John Tore Valand">
		["organisation"] = <"Helse Vest IKT AS">
		["email"] = <"John.tore.valand@helse-vest-ikt.no">
		["date"] = <"2021-12-13">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere måling av ulikhet i lengde mellom parvise kroppsdeler.">
			use = <"Brukes for å registrere måling av ulikhet i lengde mellom parvise kroppsdeler.

Hensikten med denne arketypen er å tilby en standard måte å registrere en eller alle mulige målinger for ulikhet i lengde mellom parvise kroppsdeler, identifisert av dataelementelementet \"Kroppsdel\", heller enn å lage en egen/ separat arketype for alle mulige målinger.

Denne målingen kan registreres og følges over tid, for eksempel å følge utvikling av et barns vekst, eller for å gi innblikk i formen og proporsjonene av et individs kropp i en klinisk, antropometrisk eller ergonomisk vurdering. Man kan også følge utviklingen av en tilstand eller sykdom ved å registrere målinger av unormale kliniske funn eller observasjoner i denne arketypen.

De vanligste parametrene som blir målt mellom to definerte punkter er lagt til DV_CODED_TEXT-datatype i elementet \"Kroppsdel\". Når behovet flere målinger blir identifisert kan disse etter hvert bli lagt til listen.

Dataelementene \"Målemetode\", \"Startmålepunkt\", \"Strukturert startmålepunkt\", \"Sluttmålepunkt\", \"Struktuert sluttpunkt\" er overflødige om disse allerede er registrert i arketypen OBSERVATION.body_segment_length ved måling av den faktiske lengden av kroppsdelene.">
			keywords = <"antropometri, måling, omkrets, omfang", ...>
			misuse = <"Skal ikke brukes til å registrere lengden av en kroppsdel. Bruk arketypen OBSERVATION.body_segment_length (Kroppsmåling - lengde) for dette formålet.">
			copyright = <"© openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the measurement of the difference in length between paired body segments.">
			use = <"Use to record the measurement of the difference in length between paired body segments.

Body segment measurements that are ubiquitous and, most commonly, used as part of growth charting in childhood have been modelled as unique and discrete archetypes. Rather than create an archetype for every single possible body segment measurement, the intent of this archetype is to use a common recording pattern to capture any, or all, other possible measurements of discrepancy between two paired body segments, identified by the 'Paired body segment name' data element.

This measurement may be recorded and tracked over time, for example tracking child growth parameters, or used to provide insight into the shape and proportions of an individual's body for purposes of clinical, anthropometric or ergonomic assessment. It may also be used to record the measurement of abnormal clinical findings or observations in order to track the progress of a condition or disease.

Common parameters that are measured between two identified points have been added to the DV_CODED_TEXT data type in the 'Body segment name' data element. As further parameters are identified, these can be added to this list over time.

The elements 'Measurement method', 'Measurement origin', 'Structured origin', 'Measurement endpoint' and 'Structured endpoint' is reduntant if already recorded in the OBSERVATION.body_segment_length when recording the actual length of the body segments.">
			keywords = <"anthropometry", "measurement", "length", "height", "width", "span">
			misuse = <"Not to be used to record the length of a body segment - use OBSERVATION.body_segment_length for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Andreas Abildgaard, OUS, Norway", "Vebjørn Arntzen, Oslo universitetssykehus HF, Norway (Nasjonal IKT redaktør)", "Heidi Aursand, Oslo universitetssykehus, Norway", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (Nasjonal IKT redaktør)", "Malin Berg, DIPS ASA, Norway", "Randi Brendberg, Helse Nord RHF, Norway", "Angela Crovetti, CDC/NIOSH, United States", "Heather Grain, Llewelyn Grain Informatics, Australia", "Anne M Gromsrud, DIPS AS, Norway", "Mikkel Gaup Grønmo, FSE, Helse Nord, Norway (Nasjonal IKT redaktør)", "Alfred Honore, Haukeland, Norway", "Evelyn Hovenga, EJSH Consulting, Australia", "Lars Morgan Karlsen, Nordlandssykehuset Bodø, Norway", "Heidi Koikkalainen, United Kingdom (openEHR Editor)", "Ronald Krawec, Alberta Health Services, Canada", "Tomi Laptoš, Marand, Slovenia", "Liv Laugen, Oslo universitetssykehus, Norway", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Brit Jorun Liseth, Haukeland Universitetssykehus, Norway", "Priscila Maranhão, MEDCIDS-FMUP, Portugal", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Lars Ivar Mehlum, Nasjonal IKT HF, Norway", "Svenne Naumann, Finnmarkssykehuset, Norway", "Bjørn Næss, DIPS ASA, Norway", "Jayashree Panickar, Karolinska Institute, Sweden", "Norwegian Review Summary, Nasjonal IKT HF, Norway", "Stein Arne Rimehaug, Sunnaas sykehus, Norway", "Line Silsand, Universitetssykehuset i Nord-Norge, Norway", "Trine Strand, Oslo Universitetssykehus (OUS), Norway", "Johannes Michael Strehle, Helse-Nord, Norway", "Micaela Thierley, Helse Bergen, Norway", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Nasjonal IKT redaktør)", "Marit Alice Venheim, Helse Vest IKT, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Qiu XS, Ma WW, Li WG, Wang B, Yu Y, Zhu ZZ, Qian BP, Zhu F, Sun X, Ng BK, Cheng JC, Qiu Y. Discrepancy between radiographic shoulder balance and cosmetic shoulder balance in adolescent idiopathic scoliosis patients with double thoracic curve. Eur Spine J. 2009 Jan;18(1):45-51. doi: 10.1007/s00586-008-0833-4. Epub 2008 Nov 29. PMID: 19043746; PMCID: PMC2615117.

Leg Length Discrepancy, Physiopedia. Internet. Retrieved from: https://www.physio-pedia.com/Leg_Length_Discrepancy. Access date 06.10.22.
">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"91047201365B990BBEB748A8B57C105F">
		["build_uid"] = <"8ef07eca-773d-497a-be55-f9cbe1ea15f9">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Body segment length discrepancy
		data matches {
			HISTORY[at0001] matches {    -- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- ItemTree
								items cardinality matches {2..*; unordered} matches {
									ELEMENT[at0004] matches {    -- Body segment name
										value matches {
											DV_TEXT matches {*}
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0049,    -- Upper arm length
													at0050,    -- Leg length
													at0051,    -- Upper leg length
													at0052,    -- Foot length
													at0053,    -- Knee height
													at0054,    -- Forearm length
													at0055,    -- Lower leg length
													at0056,    -- Shoulder height
													at0057]    -- Sitting shoulder height
												}
											}
										}
									}
									ELEMENT[at0008] matches {    -- Discrepancy
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::122]>
												list = <
													["1"] = <
														units = <"cm">
														magnitude = <|>=0.0|>
														precision = <|1|>
													>
													["2"] = <
														units = <"[in_i]">
														magnitude = <|>=0.0|>
														precision = <|1|>
													>
													["3"] = <
														units = <"mm">
														magnitude = <|>=0.0|>
														precision = <|1|>
													>
												>
											>
										}
									}
									ELEMENT[at0046] occurrences matches {0..1} matches {    -- Longest side
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0047,    -- Left
													at0048]    -- Right
												}
											}
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {    -- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0014] matches {    -- ItemTree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0015] occurrences matches {0..*} matches {    -- Measuring device
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0027] occurrences matches {0..1} matches {    -- Measurement method
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0029] occurrences matches {0..1} matches {    -- Measurement origin
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0030] occurrences matches {0..*} matches {    -- Structured origin
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0033] occurrences matches {0..1} matches {    -- Measurement endpoint
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0035] occurrences matches {0..*} matches {    -- Structured endpoint
						include
							archetype_id/value matches {/.*/}
					}
					allow_archetype CLUSTER[at0016] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Body segment length discrepancy">
					description = <"Measurement of the difference in length between paired body segments.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Body segment name">
					description = <"Name of the measured difference in length in paired body segments.">
					comment = <"For exampel: Leg length discrepancy or shoulder height discrepancy.
Use the 'Measurement method' element and/or recording the 'Measurement origin' and/or 'Measurement endpoint' to record how the body segment is measured.">
				>
				["at0008"] = <
					text = <"Discrepancy">
					description = <"Measured or calculated length of the discrepancy between paired body segments.">
				>
				["at0009"] = <
					text = <"Comment">
					description = <"Additional narrative about the discrepancy, not captured in other fields.">
				>
				["at0014"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0015"] = <
					text = <"Measuring device">
					description = <"Details of the device used for measuring the body segment.">
				>
				["at0016"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0027"] = <
					text = <"Measurement method">
					description = <"Narrative description of the method used for measurement.">
					comment = <"Specify in order to ensure that a sequence of measurements are taken at the same point and in the same way. For example: 'Sitting with knee bent at 90 degrees'.">
				>
				["at0029"] = <
					text = <"Measurement origin">
					description = <"Identification of a simple body site for the location of the starting point of the measurement.">
				>
				["at0030"] = <
					text = <"Structured origin">
					description = <"A structured anatomical location for the starting point of the measurement.">
				>
				["at0033"] = <
					text = <"Measurement endpoint">
					description = <"Identification of a simple body site for the location of the ending point of the measurement.">
				>
				["at0035"] = <
					text = <"Structured endpoint">
					description = <"A structured anatomical location for the endpoint of the measurement.">
				>
				["at0046"] = <
					text = <"Longest side">
					description = <"The laterality of the longest of the body segments.">
				>
				["at0047"] = <
					text = <"Left">
					description = <"">
				>
				["at0048"] = <
					text = <"Right">
					description = <"">
				>
				["at0049"] = <
					text = <"Upper arm length">
					description = <"Distance between the shoulder and the elbow.">
				>
				["at0050"] = <
					text = <"Leg length">
					description = <"Distance between the hip joint and the ankle joint.">
				>
				["at0051"] = <
					text = <"Upper leg length">
					description = <"Distance between the hip joint and the knee joint.">
				>
				["at0052"] = <
					text = <"Foot length">
					description = <"Distance between the base of the heel to the tip of the longest toe.">
				>
				["at0053"] = <
					text = <"Knee height">
					description = <"Distance between the sole of the foot and the top of the lower leg.">
				>
				["at0054"] = <
					text = <"Forearm length">
					description = <"Distance from the elbow to the wrist.">
				>
				["at0055"] = <
					text = <"Lower leg length">
					description = <"Distance between the knee joint and the ankle joint.">
				>
				["at0056"] = <
					text = <"Shoulder height">
					description = <"Vertical distance from the standing surface to the acromion.">
				>
				["at0057"] = <
					text = <"Sitting shoulder height">
					description = <"Vertical distance from the sitting surface to the acromion.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Kroppsmåling - lengde - sideforskjell">
					description = <"Måling av ulikhet i lengde mellom parvise kroppsdeler.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert hendelse">
					description = <"Standard, uspesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i et templat eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Navn på kroppsdel">
					description = <"Navnet på målingen for ulikhet mellom parvise kroppsdeler.">
					comment = <"For eksempel: \"Benlengdeforskjell\" eller \"Skulderhøydeforskjell\".
Måten kroppsdelen måles på skal registreres ved hjelp av elementet \"Metode\" og/eller av \"Start/Endemålepunkt\".">
				>
				["at0008"] = <
					text = <"Sideforskjell">
					description = <"Målt eller kalkulert lengde på sideforskjell mellom parvise kroppsdeler.">
				>
				["at0009"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om sideulikheten som ikke er registrert i andre felt.">
				>
				["at0014"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0015"] = <
					text = <"Måleinstrument">
					description = <"Detaljer om måleinstrumentet som ble brukt i måling av kroppsdelen.">
				>
				["at0016"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR eller CIMI.">
				>
				["at0027"] = <
					text = <"Målemetode">
					description = <"Fritekstbeskrivelse av metoden som ble benyttet for målingen.">
					comment = <"Spesifiser for å sikre at en sekvens av målinger blir gjort på de samme punktene og på samme måte. For eksempel \"Sittende med kneet bøyd 90 grader\".">
				>
				["at0029"] = <
					text = <"Startmålepunkt">
					description = <"En fritekstbeskrivelse som identifiserer stedet på kroppen som er startpunkt for målingen.">
				>
				["at0030"] = <
					text = <"Strukturert startmålepunkt">
					description = <"En strukturert anatomisk identifikasjon av stedet på kroppen som er startpunkt for målingen.">
				>
				["at0033"] = <
					text = <"Sluttmålepunkt">
					description = <"En fritekstbeskrivelse som identifiserer stedet på kroppen som er sluttpunkt for målingen.">
				>
				["at0035"] = <
					text = <"Strukturert sluttpunkt">
					description = <"En strukturert anatomisk identifikasjon av stedet på kroppen som er sluttpunkt for målingen.">
				>
				["at0046"] = <
					text = <"Lengste side">
					description = <"Lateraliteten til det lengste av kroppssegmentene.">
				>
				["at0047"] = <
					text = <"Venstre">
					description = <"">
				>
				["at0048"] = <
					text = <"Høyre">
					description = <"">
				>
				["at0049"] = <
					text = <"Lengde av overarm">
					description = <"Avstanden mellom skulder og albue.">
				>
				["at0050"] = <
					text = <"Benlengde">
					description = <"Avstanden fra hofteledd til ankelledd.">
				>
				["at0051"] = <
					text = <"Lårlengde">
					description = <"Avstanden mellom hofteledd og kneledd.">
				>
				["at0052"] = <
					text = <"Fotlengde">
					description = <"Avstanden fra hælen til spissen av den lengste tåen.">
				>
				["at0053"] = <
					text = <"Knehøyde">
					description = <"Avstanden fra sålen til toppen av leggen.">
				>
				["at0054"] = <
					text = <"Underarmslengde">
					description = <"Avstanden fra albue til håndledd.">
				>
				["at0055"] = <
					text = <"Legglengde">
					description = <"Avstanden fra kneledd til ankelledd.">
				>
				["at0056"] = <
					text = <"Skulderhøyde">
					description = <"Vertikal avstand fra gulv til acromion.">
				>
				["at0057"] = <
					text = <"Sittende skulderhøyde">
					description = <"Vertikal avstand fra sitteflate til acromion.">
				>
			>
		>
	>

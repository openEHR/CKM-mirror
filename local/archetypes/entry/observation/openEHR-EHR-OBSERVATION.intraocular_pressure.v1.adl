archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.intraocular_pressure.v1

concept
	[at0000]	-- Intraocular pressure measurement
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Gustavo M Bacelar-Silva">
		["organisation"] = <"Faculty of Medicine - University of Porto">
		["email"] = <"mail@gustavobacelar.com">
		["date"] = <"2012-06-05">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the results of intraocular pressure of an individual, normally using a tonometry device.">
			use = <"Use to record the results of measurement of intraocular pressure of a single eye.  It is common  for different toniometry methods to be required for each eye because of patient intolerance. This precludes recording the results from both eyes within the same Entry record i.e. each eye will need a separate instance of the archetype to record the result for each eye.

The 'Clinical Description' data element can be used to record simple narrative summary or as a means to integrate legacy data. 

Detailed, structured intraocular pressure results are preferred.

The 'Absent Test Result' element may be used to record circumstances where a test could not be performed or completed. Further details may be recorded under 'Confounding Factors' or 'Additional Comment'.">
			keywords = <"IOP", "toniometry", "glaucoma", "eye">
			misuse = <"Where details of methodology carried under protocol change between the examination of each eye, a new Entry must be created at run-time.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Ian McNicoll, Ocean Informatics, UK ", ...>
	other_details = <
		["current_contact"] = <"Gustavo M Bacelar-Silva, mail@gustavobacelar.com">
		["references"] = <"IHE Eye Care Domain. General Eye Evaluation (GEE) [Internet]. IHE; 2012. Available from: http://www.ihe.net/Technical_Framework/upload/IHE_EyeCare_Supp_GEE_Rev1-1_TI_2012-06-29.pdf

Royal College of Opthalmologists. Cataract National Dataset for Adults [Internet]. 2011. Available from: http://www.rcophth.ac.uk/page.asp?section=583&sectionTitle=Cataract+National+Data+Set+for+Adults

CfH, UK. Do Once & Share - Glaucoma [Internet]. [cited 2012 Sep 26]. Available from: http://www.doasglaucoma.org/

DICOM Supplement 30 [Internet]. [cited 2012 Sep 26]. Available from: http://medical.nema.org/

Lamparter, J., & Hoffmann, E. M. (2009). Messung des Augeninnendrucks (Tonometrie) mit unterschiedlichen Verfahren. Der Ophthalmologe, 106(8), 676-682.">
		["MD5-CAM-1.0.1"] = <"A3881B6AA67F5DCAF9BF6763D2BF7934">
	>

definition
	OBSERVATION[at0000] matches {	-- Intraocular pressure measurement
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..1} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0062] occurrences matches {0..1} matches {	-- Clinical Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									CLUSTER[at0076] occurrences matches {0..*} matches {	-- Test Result
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0057] occurrences matches {0..1} matches {	-- Eye
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0058, 	-- Left eye
															at0059]	-- Right eye
														}
													}
												}
											}
											ELEMENT[at0071] occurrences matches {0..1} matches {	-- Absent Result
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0072, 	-- Never performed
															at0073]	-- Not completed.
														}
													}
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0042] occurrences matches {0..1} matches {	-- Intraocular Pressure
												value matches {
													C_DV_QUANTITY <
														property = <[openehr::125]>
														list = <
															["1"] = <
																units = <"mm[Hg]">
																magnitude = <|0.0..90.0|>
																precision = <|1|>
															>
														>
													>
												}
											}
											ELEMENT[at0065] occurrences matches {0..1} matches {	-- Applanation Time
												value matches {
													C_DV_QUANTITY <
														property = <[openehr::128]>
														list = <
															["1"] = <
																units = <"millisec">
																magnitude = <|>=0.0|>
																precision = <|0|>
															>
														>
													>
												}
											}
											ELEMENT[at0061] occurrences matches {0..*} matches {	-- Clinical Interpretation
												value matches {
													DV_TEXT matches {*}
												}
											}
										}
									}
									ELEMENT[at0077] occurrences matches {0..*} matches {	-- Overall Interpretation
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0063] occurrences matches {0..1} matches {	-- Additional Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0007] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0074] occurrences matches {0..*} matches {	-- Confounding Factors
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0068] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0046] occurrences matches {0..1} matches {	-- Tonometry Method
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0047, 	-- Goldmann
									at0048, 	-- Perkins
									at0049, 	-- Tono-Pen
									at0050, 	-- Icare (Rebound)
									at0051, 	-- Dynamic Contour
									at0052, 	-- Ocular Response Analyzer
									at0053, 	-- TGDc-01
									at0066]	-- Non-contact tonometry
								}
							}
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0055] occurrences matches {0..1} matches {	-- Device Details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Intraocular pressure measurement">
					description = <"Intraocular pressure measurement of a single eye, normally using a tonometry device.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Any measurement event.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0007"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0042"] = <
					text = <"Intraocular Pressure">
					description = <"Measured values of the intraocular pressure.">
				>
				["at0046"] = <
					text = <"Tonometry Method">
					description = <"Type of tonometery used to measure intracoular pressure.">
				>
				["at0047"] = <
					text = <"Goldmann">
					description = <"Goldmann tonometry.">
				>
				["at0048"] = <
					text = <"Perkins">
					description = <"Perkins tonometry.">
				>
				["at0049"] = <
					text = <"Tono-Pen">
					description = <"Tono-Pen tonometry.">
				>
				["at0050"] = <
					text = <"Icare (Rebound)">
					description = <"Icare (Rebound) tonometry.">
				>
				["at0051"] = <
					text = <"Dynamic Contour">
					description = <"Dynamic Contour tonometry.">
				>
				["at0052"] = <
					text = <"Ocular Response Analyzer">
					description = <"Ocular Response Analyzer.">
				>
				["at0053"] = <
					text = <"TGDc-01">
					description = <"A TGDc-01 device was used to perform the test.">
				>
				["at0055"] = <
					text = <"Device Details">
					description = <"Details of the toniometry device used to measure intraocular pressure.">
				>
				["at0057"] = <
					text = <"Eye">
					description = <"The eye that was examined.">
				>
				["at0058"] = <
					text = <"Left eye">
					description = <"The left eye was examined.">
				>
				["at0059"] = <
					text = <"Right eye">
					description = <"The right eye was examined.">
				>
				["at0061"] = <
					text = <"Clinical Interpretation">
					description = <"A textual intepretation, generally coded, of the test result.">
				>
				["at0062"] = <
					text = <"Clinical Description">
					description = <"An overall narrative description of the intraocular pressure test result.">
				>
				["at0063"] = <
					text = <"Additional Comment">
					description = <"An additional comment about the measurement.">
				>
				["at0065"] = <
					text = <"Applanation Time">
					description = <"The time taken for a non-contact tonometer to flatten the cornea, used to calculate intraocular pressure.">
				>
				["at0066"] = <
					text = <"Non-contact tonometry">
					description = <"Non-contact tonometry was used to perfrom the test.">
				>
				["at0068"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0071"] = <
					text = <"Absent Result">
					description = <"Details of a test result which could not be recorded. Specific detailed reasons should be captured in 'Additional Comment' or 'Confounding Factors' if these relate to the patient's state.">
				>
				["at0072"] = <
					text = <"Never performed">
					description = <"The test was never performed.">
				>
				["at0073"] = <
					text = <"Not completed.">
					description = <"The test was started but could not be completed.">
				>
				["at0074"] = <
					text = <"Confounding Factors">
					description = <"Factors related to the state of the subject which may effect interpretation of the result.">
				>
				["at0076"] = <
					text = <"Test Result">
					description = <"Details of the visual field test result for each eye.">
				>
				["at0077"] = <
					text = <"Overall Interpretation">
					description = <"A textual intepretation, generally coded, of all the test results.">
				>
			>
		>
	>

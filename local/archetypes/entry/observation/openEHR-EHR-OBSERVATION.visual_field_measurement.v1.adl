archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.visual_field_measurement.v1

concept
	[at0000]	-- Visual field measurement
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Ian McNicoll">
		["organisation"] = <"Ocean Informatics UK">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"2012-09-19">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording the results of visual field testing or perimetry.">
			use = <"To record the results of visual field testing or perimetry. 

The 'Clinical Description' data element can be used to record simple narrative summary or as a means to integrate legacy data. 

Detailed, structured visual acuity results are preferred and are generally recorded for each eye in turn.">
			keywords = <"perimetry", "ophthalmic", "optometry", "eye", "vision", "visual">
			misuse = <"Where the perimetry method or device changes between each eye, a new record Entry should be created at runtime.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Gustavo Bacelar-Silva, Brazil", ...>
	other_details = <
		["current_contact"] = <"Ian McNicoll, Ocean Informatics, UK, ian.mcnicoll@oceaninformatics.com">
		["references"] = <"IHE Eye Care Domain. General Eye Evaluation (GEE) [Internet]. IHE; 2012. Available from: http://www.ihe.net/Technical_Framework/upload/IHE_EyeCare_Supp_GEE_Rev1-1_TI_2012-06-29.pdf

Royal College of Opthalmologists. Cataract National Dataset for Adults [Internet]. 2011. Available from: http://www.rcophth.ac.uk/page.asp?section=583&sectionTitle=Cataract+National+Data+Set+for+Adults

CfH, UK. Do Once & Share - Glaucoma [Internet]. [cited 2012 Sep 26]. Available from: http://www.doasglaucoma.org/

DICOM Supplement 30 [Internet]. [cited 2012 Sep 26]. Available from: http://medical.nema.org/

http://www.slideshare.net/hmirzaeee/glaucoma-hemifield-test

Standard Automated Perimetry, EyeWiki, http://eyewiki.aao.org/Standard_Automated_Perimetry">
		["MD5-CAM-1.0.1"] = <"AD0AF52EC0B9C0BFA404538C7C818C1E">
	>

definition
	OBSERVATION[at0000] matches {	-- Visual field measurement
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..1} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Clinical Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									CLUSTER[at0007] occurrences matches {0..2} matches {	-- Test Result
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0008] occurrences matches {0..1} matches {	-- Eye
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0019, 	-- Left eye
															at0020]	-- Right eye
														}
													}
												}
											}
											ELEMENT[at0027] occurrences matches {0..1} matches {	-- Glaucoma Hemifield Test (GHT)
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0028, 	-- Outside normal limits
															at0029, 	-- Borderline
															at0030, 	-- General reduction of sensitivity
															at0031, 	-- Abnormally high sensitivity
															at0032]	-- Within normal limits
														}
													}
												}
											}
											ELEMENT[at0037] occurrences matches {0..1} matches {	-- Visual Field Index
												value matches {
													DV_PROPORTION matches {
														numerator matches {|0.0..100.0|}
														is_integral matches {True}
														type matches {2}
													}
												}
											}
											ELEMENT[at0034] occurrences matches {0..1} matches {	-- Mean deviation
												value matches {
													C_DV_QUANTITY <
														property = <[openehr::123]>
														list = <
															["1"] = <
																units = <"dB">
																magnitude = <|-30.0..10.0|>
																precision = <|2|>
															>
														>
													>
												}
											}
											ELEMENT[at0038] occurrences matches {0..1} matches {	-- Mean deviation P
												value matches {
													DV_PROPORTION matches {
														numerator matches {|0.5..5.0|}
														type matches {2}
													}
												}
											}
											ELEMENT[at0035] occurrences matches {0..1} matches {	-- Pattern Standard deviation
												value matches {
													C_DV_QUANTITY <
														property = <[openehr::123]>
														list = <
															["1"] = <
																units = <"dB">
																magnitude = <|0.0..25.0|>
																precision = <|2|>
															>
														>
													>
												}
											}
											ELEMENT[at0039] occurrences matches {0..1} matches {	-- Pattern Standard deviation P
												value matches {
													DV_PROPORTION matches {
														numerator matches {|0.5..5.0|}
														type matches {2}
													}
												}
											}
											ELEMENT[at0009] occurrences matches {0..*} matches {	-- Clinical Interpretation
												value matches {
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0006] occurrences matches {0..1} matches {	-- Image
												value matches {
													DV_MULTIMEDIA matches {
														media_type matches {
															[openEHR::
															425, 
															426, 
															427, 
															428, 
															429]
														}
													}
												}
											}
										}
									}
									ELEMENT[at0025] occurrences matches {0..1} matches {	-- Overall Interpretation
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0024] occurrences matches {0..1} matches {	-- Additional Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0022] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0023] occurrences matches {0..*} matches {	-- Confounding Factors
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at0036] occurrences matches {0..2} matches {	-- Refractive correction
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.refraction_details(-[a-zA-Z0-9_]+)*\.v1/}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0010] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0011] occurrences matches {0..1} matches {	-- Perimetry Method
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0012, 	-- Goldman perimetry
									at0013, 	-- Dicon
									at0014, 	-- Henson
									at0015, 	-- Octopus
									at0016, 	-- Humphrey
									at0017, 	-- Frequency Doubling Perimetry (FDP)
									at0040]	-- FASTPAC automated standard perimetry
								}
							}
						}
					}
					allow_archetype CLUSTER[at0026] occurrences matches {0..1} matches {	-- Device Details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
				}
			}
		}
	}


ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Visual field measurement">
					description = <"Results of visual field testing / perimetry.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Any measurement event.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0005"] = <
					text = <"Clinical Description">
					description = <"A term, commonly coded, expressing an overall interpretation of the visual field test.">
				>
				["at0006"] = <
					text = <"Image">
					description = <"A multimedia representaion of the visual field test.">
				>
				["at0007"] = <
					text = <"Test Result">
					description = <"Details of the visual field test result for each eye.">
				>
				["at0008"] = <
					text = <"Eye">
					description = <"The eye which is being examined.">
				>
				["at0009"] = <
					text = <"Clinical Interpretation">
					description = <"The test result expressed as a qualitative term, normally coded.">
				>
				["at0010"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0011"] = <
					text = <"Perimetry Method">
					description = <"The method used to record the perimetry result.">
				>
				["at0012"] = <
					text = <"Goldman perimetry">
					description = <"Goldman perimetry was used to measure the visual fields.">
				>
				["at0013"] = <
					text = <"Dicon">
					description = <"The Dicon device was used to measure the visual fields.">
				>
				["at0014"] = <
					text = <"Henson">
					description = <"A Henson device was used to measure the visual fields.">
				>
				["at0015"] = <
					text = <"Octopus">
					description = <"An Octopus device was used to measure the visual fields.">
				>
				["at0016"] = <
					text = <"Humphrey">
					description = <"A Humphrey device was used to measure the visual fields.">
				>
				["at0017"] = <
					text = <"Frequency Doubling Perimetry (FDP)">
					description = <"Frequency Doubling Perimetry was used to measure the visual fields.">
				>
				["at0019"] = <
					text = <"Left eye">
					description = <"The left eye was examined.">
				>
				["at0020"] = <
					text = <"Right eye">
					description = <"The right eye was examined.">
				>
				["at0022"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0023"] = <
					text = <"Confounding Factors">
					description = <"Patient circumstances which may affect interpretation of the result.">
				>
				["at0024"] = <
					text = <"Additional Comment">
					description = <"Any additional narrative comment about the visual field test.">
				>
				["at0025"] = <
					text = <"Overall Interpretation">
					description = <"A term, commonly coded, expressing an overall interpretation of the visual field test.">
				>
				["at0026"] = <
					text = <"Device Details">
					description = <"Details of the device used to measure visual fields.">
				>
				["at0027"] = <
					text = <"Glaucoma Hemifield Test (GHT)">
					description = <"A coded intepretation of the Glaucoma Hemifield Test (GHT).">
				>
				["at0028"] = <
					text = <"Outside normal limits">
					description = <"Differences between a matched pair of corresponding zones exceeds the difference found in 99% of the normal population, or when both members of a pair of zones are more abnormal than 99.5% of the individuals with the normative population.">
				>
				["at0029"] = <
					text = <"Borderline">
					description = <"Matched pairs of zones are abnormal at the 97th percentile within the normative database.">
				>
				["at0030"] = <
					text = <"General reduction of sensitivity">
					description = <"Conditions for “outside normal limits” are not met, and the best region of the visual field is at or below the 99.5th percentile of the normative population.">
				>
				["at0031"] = <
					text = <"Abnormally high sensitivity">
					description = <"Overall sensitivity in the affected  region of the VF is better than 99.5% of individuals within the normative population.">
				>
				["at0032"] = <
					text = <"Within normal limits">
					description = <"None of the abnormal conditions are met.">
				>
				["at0034"] = <
					text = <"Mean deviation">
					description = <"The average of deviation in vusual field across all test areas.">
				>
				["at0035"] = <
					text = <"Pattern Standard deviation">
					description = <"Average of non-uniform visual field loss.">
				>
				["at0036"] = <
					text = <"Refractive correction">
					description = <"Details of refractive correction applied to each eye.">
				>
				["at0037"] = <
					text = <"Visual Field Index">
					description = <"Visual Field Index result.">
				>
				["at0038"] = <
					text = <"Mean deviation P">
					description = <"The P value of the Mean Deviation result.">
				>
				["at0039"] = <
					text = <"Pattern Standard deviation P">
					description = <"The P value of the Pattern Standard deviation result.">
				>
				["at0040"] = <
					text = <"FASTPAC automated standard perimetry">
					description = <"FASTPAC automated standard perimetry was used to measure the visual fields.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0016"] = <[SNOMED-CT::252806005]>
				["at0017"] = <[SNOMED-CT::392017002]>
				["at0027"] = <[SNOMED-CT::423083007]>
				["at0034"] = <[SNOMED-CT::422859007]>
				["at0035"] = <[SNOMED-CT::421362004]>
				["at0037"] = <[SNOMED-CT::421140005]>
				["at0040"] = <[SNOMED-CT::392132006]>
			>
		>
	>

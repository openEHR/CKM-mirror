archetype (adl_version=1.4; uid=c16556dd-c527-4f61-bdbd-3f912fbe5e01)
	openEHR-EHR-OBSERVATION.age.v0

concept
	[at0000]	-- Age assertion
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Sarah Ballout, Natalia Strauch, Darin Leonhardt">
				["organisation"] = <"MHH-Hannover, Medizinische Hochschule Hannover, PLRI, PLRI für medizinische Informatik/ Medizinische Hochschule">
				["email"] = <"ballout.sarah@mh-hannover.de, Strauch.Natalia@mh-hannover.de, leonhardt.darin@mh-hannover.de">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Silje Ljosland Bakke, Vebjørn Arntzen, John Tore Valand">
				["organisation"] = <"Helse Vest IKT AS, Oslo University Hospital">
				["email"] = <"silje.ljosland.bakke@helse-vest-ikt.no, varntzen@ous-hf.no, john.tore.valand@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2018-07-23">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Zur Aufzeichnung einer Aussage über das Alter einer Person zum Zeitpunkt der Dokumentation, als klinischer Kontext für andere Informationen in derselben COMPOSITION.">
			use = <"Zur Aufzeichnung einer Aussage über das Alter einer Person zum Zeitpunkt der Dokumentation, als klinischer Kontext für andere Informationen in derselben COMPOSITION.
Dieser Archetyp ist so konzipiert, dass sowohl das chronologische als auch das angepasste Alter zur gleichen Zeit und im gleichen Kontext erfasst werden können. Zum Beispiel ein Ereignis oder ein ganzes Dokument wie ein Beratungsgespräch, eine Überweisung oder ein Dokument zum Austausch.

Verwendung als eigenständiger Archetyp innerhalb eines Templates neben anderen kontextuell relevanten Archetypen. 

Zu den Anwendungsfällen gehören unter anderem;
- gleichzeitige Erfassung des chronologischen Alters und des angepassten Alters zusammen mit Wachstumsparametern des Kindes;
- Aufzeichnung des Alters einer Person zum Zeitpunkt der Aufnahme als Kontext für andere Inhalte in einem Dokument, z. B. das Alter eines Kindes zum Zeitpunkt der Überweisung; oder
- das Alter einer Person zum Zeitpunkt des Ausfüllens eines Fragebogens zur Gesundheitsbewertung.">
			keywords = <"chronologisch", "korrigiert", "angepasst", "Alter">
			misuse = <"Nicht zur Darstellung des aktuellen Alters einer Person in der Benutzeroberfläche einer elektronischen Patientenakte zu verwenden. Verwenden Sie zu diesem Zweck ein formales Patientenregister oder Archetypen, die auf dem openEHR-Modell für demografische Informationen basieren.

Nicht zu verwenden, um das biologische Alter (basierend auf zellulären und physiologischen Faktoren) einer Person zu erfassen - verwenden Sie hierfür entsprechende Archetypen.

Nicht für die Erfassung des Skelettalters zu verwenden, verwenden Sie zu diesem Zweck OBSERVATION.skeletal_age.

Nicht zur Aufzeichnung des geschätzten oder bekannten Zeitraums oder der Dauer der Schwangerschaft oder des Gestationsalters des Fötus oder des Neugeborenen zu verwenden, verwenden Sie zu diesem Zweck OBSERVATION.gestational_age.

Nicht zu verwenden als geschätztes Alter in einem forensischen Bericht oder für eine bewusstlose Person - verwenden Sie für diesen Zweck einen entsprechenden Archetyp.">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere en påstand om en persons alder på dokumentasjonstidspunktet, for å gi klinisk kontekst for den øvrige informasjonen i samme COMPOSITION arketype.">
			use = <"Brukes for å registrere en påstand om en persons alder på dokumentasjonstidspunktet, for å gi klinisk kontekst for den øvrige informasjonen i samme COMPOSITION arketype.

Denne arketypen er designet for å støtte registrering av både kronologisk og justert alder på samme tid og innenfor samme kontekst. For eksempel en enkelt hendelse eller et helt dokument som en konsultasjon, henvisning eller dokument for utveksling.

Bruk som en frittstående arketype i et templat sammen med andre relevante arketyper.

Eksempler på bruksområder for arketypen:
- å samtidig registrere kronologisk alder og justert alder sammen med vekstparametere hos barn;
- å registrere alderen til et individ på tidspunktet for registrering som kontekst for det andre innholdet i et dokument, for eksempel alderen til et barn på tidspunktet for henvisningen; 
- alderen til et individ på tidspunktet for utfylling av et spørreskjema for helsevurdering.">
			keywords = <"kronologisk", "justert", "alder">
			misuse = <"Skal ikke brukes til å vise individets nåværende alder i brukergrensesnittet i et elektronisk journalsystem. Bruk et formelt pasientregister eller arketyper basert på openEHR demografisk informasjonsmodell til dette formålet.

Skal ikke brukes til å registrere den biologiske alderen (basert på cellulære og fysiologiske faktorer) til et individ - bruk passende arketyper for dette formålet.

Skal ikke brukes til å registrere skjelettalder, bruk OBSERVATION.skeletal_age til dette formålet.

Skal ikke brukes til å registrere et anslag eller kjent varighet av et svangerskapet eller svangerskapsalderen til fosteret eller nyfødte, bruk OBSERVATION.gestational_age for dette formålet.

Skal ikke brukes til å registrere estimert alder i en rettsmedisinsk rapport eller for en bevisstløs person - bruk en passende arketype for dette formålet.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record an assertion about the age of an individual at the time of documentation, as clinical context for the other information in the same COMPOSITION.">
			use = <"Use to record an assertion about the age of an individual at the time of documentation, as clinical context for the other information in the same COMPOSITION.
This archetype is designed to support recording both the chronological and adjusted age at the same time and within the same context. For example an event or entire document such as a consultation, referral or document for exchange.

Use as a standalone archetype within a template alongside other contextually relevant archetypes. 

Use cases include, but are not limited to;
- to simultaneously record the chronological age and adjusted age alongside child growth parameters;
- to record the age of an individual at the time of recording as context for the other content in a document, such as the age of a child at the time of referral; or
- the age of an individual at the time of filling out a health assessment questionnaire.">
			keywords = <"chronological", "corrected", "adjusted", "age">
			misuse = <"Not to be used to display the individual's current age in the user interface of an electronic health record. Use a formal patient registry or archetypes based on the openEHR demographic information model for this purpose.

Not to be used to record the biological age (based on cellular and physiological factors) of an individual - use approriate archetypes for this purpose.

Not to be used to record skeletal age, use OBSERVATION.skeletal_age for this purpose.

Not to be used to record the estimate or known period or duration of the pregnancy or gestational age of the foetus or new born, use OBSERVATION.gestational_age for this purpose.

Not to be used to record an estimated age in a forensic report or for an unconscious inidividual - use an approriate archetype for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "Ivar Berge, Oslo University Hospital, Norway (openEHR Editor)", "SB Bhattacharyya, Bhattacharyyas Clinical Records Research & Informatics LLP, India", "Tara Bonet Chinillach, Catalan Health Service, Spain", "Hugo Claudio Briceño García, Catsalut, Spain", "Hanne Marte Bårholm, Helse Vest IKT, Norway (openEHR Editor)", "Clara Calleja Vega, CatSalut. Servei Català de la Salut., Spain", "Christian Chevalley, ADOC, Thailand", "Julio de Sosa, Servei Català de la Salut, Spain", "giovanni delussu, crs4, Italy", "Mona Didriksen, Helse Vest IKT, Norway", "Manuela Domingo, hospital general universitario dr. balmis, Spain", "Stefan Dubois, Dep't of Pathology and Genetics, University Hospitals of Lund and Malmö, Sweden", "Emmanuel Eschmann, CISTEC, Switzerland", "Kåre Flø, DIPS ASA, Norway", "Fabian Franzeck, University Hospital Basel, Switzerland", "Santiago Frid, Hospital Clínic de Barcelona, Spain", "Rosane Gotardo, Systema Ltda., Brazil", "Mikkel Johan Gaup Grønmo, Helse Nord IKT, Norway (openEHR Editor)", "Atle Hansen, Universitetssykehuset Nord-Norge, Norway", "Evelyn Hovenga, EJSH Consulting, Australia", "Lars Ivar Mehlum, Helse Vest IKT AS, Norway", "Mika Kiviaho, Tietoevry, Finland", "June Marie Nepstad Knappskog, Helse Nord IKT AS, Norway (openEHR Editor), Norway (openEHR Editor)", "Ronald Krawec, Alberta Health Services, Canada", "Lise Kristin Knutsen, Oslo universitetssykehus, Norway", "Anjali Kulkarni, Karkinos, India", "Kanika Kuwelker, Helse Vest IKT, Norway (openEHR Editor)", "Jörgen Kuylenstierna, eWeave AB, Sweden", "michel laji!, Karolinska Institutet, Sweden", "Liv Laugen, ​Oslo University Hospital, Norway, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Brad Littleton, Big Picture Medical, Australia", "Ruth Lochan, Akademiska Sjukhuset Uppsala, Sweden", "June Marie Knappskog, Helse Nord IKT AS, Norway (Nasjonal IKT redaktør), Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "Laura Moral Lopez, Sistema de Salut de Catalunya, Spain", "Arunakiry Natarajan, medondo, Germany", "Svenne Naumann, Finnmarkssykehuset, Norway", "Anne-Berit Norman Paulsen, Universitetssykehuset Nord-Norge, Norway", "Mikael Nyström, Cambio Healthcare Systems AB, Sweden", "Mariana Pais, Faculty of Medicine of the University of Porto, Portugal", "Xavier Pastor, Hospital Clínic - University of Barcelona, Spain", "Terje Sagmyr, Helse Vest IKT, Norway (openEHR Editor)", "Benjamin Senst, Germany", "Videha Sharma, University of Manchester, United Kingdom", "Norwegian Review Summary, Norwegian Public Hospitals, Norway", "Anders Thurin, VGR, Sweden", "John Tore Valand, Helse Vest IKT, Norway (openEHR Editor)", "Marit Alice Venheim, Helse Vest IKT, Norway (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"0CEA21778519261AC848ED165E697FE4">
		["build_uid"] = <"9d18d17e-3971-4b10-824f-379a5a8f616b">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Age assertion
		data matches {
			HISTORY[at0001] matches {    -- Event Series
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..1} matches {    -- Point in time event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..*} matches {    -- Chronological age
										value matches {
											DV_DURATION matches {
												value matches {|>=PT0S|}
											}
											C_DV_QUANTITY <
												
												list = <
													["1"] = <
														units = <"a">
														magnitude = <|0.0..<200.0|>
													>
													["2"] = <
														units = <"a_j">
														magnitude = <|0.0..<200.0|>
													>
												>
											>
											DV_TEXT matches {*}
											DV_INTERVAL<DV_DURATION> matches {
												lower matches {
													DV_DURATION occurrences matches {0..1} matches {
														value matches {|PT0S..<P200Y|}
													}
												}
												upper matches {
													DV_DURATION occurrences matches {0..1} matches {
														value matches {|PT0S..<P200Y|}
													}
												}
											}
										}
									}
									ELEMENT[at0005] occurrences matches {0..*} matches {    -- Adjusted age
										value matches {
											DV_DURATION matches {
												value matches {|>-P1Y|}
											}
											C_DV_QUANTITY <
												
												list = <
													["1"] = <
														units = <"a">
														magnitude = <|>-1.0..<10.0|>
													>
													["2"] = <
														units = <"a_j">
														magnitude = <|>-1.0..<10.0|>
													>
												>
											>
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {    -- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0008] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0009] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Age assertion">
					description = <"An assertion about the age of an individual at the time of documentation.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Point in time event">
					description = <"Default, unspecified point in time event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Chronological age">
					description = <"The age of an individual calculated from their birth date.">
					comment = <"In most use cases the DV_DURATION data type is used to record the Chronological age. The DV_INTERVAL_DURATION is used for recording a structured age interval in cases where there's uncertainty about the individual's exact age. The DV_QUANTITY data type is used for recording a decimal age in years or mean Julian years (365.25 days). The DV_TEXT data type is used for recording a textual representation of the individual's age, or age category often found in questionnaires. For example: 'P3M2W', '0,2884615384615385 a_j', '3 months 2 weeks' or '31-40 years' or 'Teenager'.">
				>
				["at0005"] = <
					text = <"Adjusted age">
					description = <"The age of a premature infant calculated from their due date.">
					comment = <"Also known as 'Corrected age'. In most use cases the DV_DURATION data type is used to record the Adjusted age. The DV_QUANTITY data type is used for recording a decimal age in years or mean Julian years (365.25 days), and the DV_TEXT data type is used for recording a textual representation of the individual's age. For example: 'P3M2W', '0,2884615384615385 a_j' or '3 months 2 weeks'.">
				>
				["at0006"] = <
					text = <"Comment">
					description = <"Additional narrative about the age of an individual, not captured in other fields.">
				>
				["at0008"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Altersangabe">
					description = <"Eine Aussage über das Alter einer Person zum Zeitpunkt der Dokumentation.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Ereigniszeitpunkt">
					description = <"Standardwert, ein undefinierter Zeitpunkt des Ereignisses, das explizit im Template oder zur Laufzeit der Anwendung definiert werden kann.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Chronologisches Alter">
					description = <"Zeitdauer seit der Geburt.">
					comment = <"In den meisten Anwendungsfällen wird der Datentyp DV_DURATION zur Erfassung des chronologischen Alters verwendet. Der Datentyp DV_INTERVAL_DURATION wird für die Aufzeichnung eines strukturierten Altersintervalls in Fällen verwendet, in denen Unsicherheit über das genaue Alter der Person besteht. Der Datentyp DV_QUANTITY wird für die Erfassung eines dezimalen Alters in Jahren oder mittleren julianischen Jahren (365,25 Tage) verwendet. Der Datentyp DV_TEXT wird für die Aufzeichnung einer textuellen Darstellung des Alters der Person oder einer Alterskategorie verwendet, die häufig in Fragebögen zu finden ist. Zum Beispiel: \"P3M2W\", \"0,2884615384615385 a_j\", \"3 Monate 2 Wochen\" oder \"31-40 Jahre\" oder \"Teenager\".">
				>
				["at0005"] = <
					text = <"Korrigiertes Alter">
					description = <"Das Alter eines Frühgeborenen, gerechnet ab seinem Geburtstermin.">
					comment = <"Auch bekannt als 'Korrigiertes Alter'. In den meisten Anwendungsfällen wird der Datentyp DV_DURATION zur Erfassung des korrigierten Alters verwendet. Der Datentyp DV_QUANTITY wird für die Aufzeichnung eines dezimalen Alters in Jahren oder mittleren julianischen Jahren (365,25 Tage) verwendet, und der Datentyp DV_TEXT wird für die Aufzeichnung einer textuellen Darstellung des Alters der Person verwendet. Zum Beispiel: \"P3M2W\", \"0,2884615384615385 a_j\" oder \"3 Monate 2 Wochen\".">
				>
				["at0006"] = <
					text = <"Kommentar">
					description = <"Beschreibung des Alters einer Person, das nicht in anderen Datenelementen dargestellt wird.">
				>
				["at0008"] = <
					text = <"*Item tree(en)">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Erweiterung">
					description = <"Zusätzliche Informationen zur Erfassung lokaler Inhalte oder Anpassung an andere Referenzmodelle/Formalismen.">
					comment = <"Zum Beispiel: Lokaler Informationsbedarf oder zusätzliche Metadaten zur Anpassung an FHIR-Ressourcen.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Alder">
					description = <"En påstand om alderen til en person på dokumentasjonstidspunktet.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert tidspunkthendelse">
					description = <"Uspesifisert tidspunkt som kan defineres mer eksplisitt i en templat eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Kronologisk alder">
					description = <"Alderen til en person regnet fra fødselsdatoen.">
					comment = <"I de fleste tilfeller benyttes datatypen DV_DURATION for å registrere den kronologiske alderen. Datatypen DV_INTERVAL_DURATION brukes for å registrere et strukturert aldersintervall i tilfeller der det er usikkerhet om individets nøyaktige alder. Datatypen DV_QUANTITY brukes til å registrere en alder med desimaler i år eller gjennomsnittlige julianske år (365,25 dager), datatypen DV_TEXT brukes til å registrere en tekstlig representasjon av individets alder eller alderskategori ofte benyttet i spørreskjema. 
For eksempel: \"P3M2W\", \"0,2884615384615385 a_j\" eller \"3 måneder 2 uker\", \"31-40 år\" eller \"Tenåring\".">
				>
				["at0005"] = <
					text = <"Justert alder">
					description = <"Alderen til et prematurt spedbarn regnet fra termindatoen.">
					comment = <"I de fleste tilfeller benyttes datatypen DV_DURATION for å registrere justert alder. Datatypen DV_QUANTITY brukes til å registrere en alder med desimaler i år eller gjennomsnittlige julianske år (365,25 dager), datatypen DV_TEXT brukes til å registrere en tekstlig representasjon av individets alder. 
For eksempel: \"P3M2W\", \"0,2884615384615385 a_j\" eller \"3 måneder 2 uker\".">
				>
				["at0006"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om et individs alder som ikke fanges opp i andre felt.">
				>
				["at0008"] = <
					text = <"*Item tree (en)">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
			>
		>
	>

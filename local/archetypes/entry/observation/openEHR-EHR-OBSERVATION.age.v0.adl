archetype (adl_version=1.4; uid=c16556dd-c527-4f61-bdbd-3f912fbe5e01)
	openEHR-EHR-OBSERVATION.age.v0

concept
	[at0000]	-- Age
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Sarah Ballout, Natalia Strauch, Darin Leonhardt">
				["organisation"] = <"MHH-Hannover, Medizinische Hochschule Hannover, PLRI">
				["email"] = <"ballout.sarah@mh-hannover.de, Strauch.Natalia@mh-hannover.de, leonhardt.darin@mh-hannover.de">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Silje Ljosland Bakke, Vebjørn Arntzen, John Tore Valand">
				["organisation"] = <"Helse Vest IKT AS, Oslo University Hospital">
				["email"] = <"silje.ljosland.bakke@helse-vest-ikt.no, varntzen@ous-hf.no, john.tore.valand@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2018-07-23">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Zur Darstellung der Altersparameter einer Person zu einem bestimmten Zeitpunkt.">
			use = <"Verwendung zur Erfassung von Parametern über das Alter einer Person zu einem bestimmten Zeitpunkt.

Verwendung innerhalb eines Templates neben anderen kontextuell relevanten Archetypen. Einschließlich, aber nicht beschränkt auf;
- zur Aufzeichnung des angepassten Alters eines Säuglings als Teil der Überwachung von Wachstumsmustern bei Kindern oder;
- zur Aufzeichnung des tatsächlichen Alters zum Zeitpunkt einer körperlichen Untersuchung oder;
- zur Erfassung einer Kategorie oder eines Bereichs, in dem die Person erstmals Symptome wahrgenommen hat.">
			keywords = <"Alter, Chronologisches Alter, Korrigiertes alter, Altersklasse, Alterkategorie, Altersgruppe", ...>
			misuse = <"Nicht zu verwenden, um das aktuelle Alter der Person in der Benutzeroberfläche einer elektronischen Patientenakte darzustellen. Verwenden Sie zu diesem Zweck ein formales Patientenregister oder Archetypen, die auf dem openEHR-Modell für demografische Informationen basieren.">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere detaljer om alderen til et individ på et gitt tidspunkt.">
			use = <"Brukes for å registrere parametere om alderen til et individ på et bestemt tidspunkt.

Bruk i et templat sammen med andre relevante arketyper. For eksempel:
- å registrere den justerte alderen til et spedbarn som en del av overvåking av barnets vekstmønster,
- å registrere alderen på tidspunktet for en fysisk undersøkelse,
- å registrere en tidskategori eller periode der personen først ble oppmerksom på symptomer.">
			misuse = <"Skal ikke brukes til å vise individets nåværende alder i brukergrensesnittet i et elektronisk journalsystem. Bruk et formelt pasientregister eller arketyper basert på openEHR demografisk informasjonsmodell til dette formålet.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about the age of an individual at a specific point in time.">
			use = <"Use to record parameters about the age of an individual at a specific point in time.

Use within a template alongside other contextually relevant archetypes. Including, but not limited to;
- to record the adjusted age of an infant as part of monitoring child growth patterns or;
- to record the actual age at the time of a physical examination or;
- to record a category or range in which the individual first became aware of symptoms.">
			misuse = <"Not to be used to display the individuals current age in the user interface of an electronic health record. Use a formal patient registry or archetypes based on the openEHR demographic information model for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"D07E8E63ADF77740A9BF20DB5E9FCDEF">
		["build_uid"] = <"7da76d3a-dddb-4254-81bf-a0615dbcf2fa">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Age
		data matches {
			HISTORY[at0001] matches {    -- Event Series
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..1} matches {    -- Point in time event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..*} matches {    -- Chronological age
										value matches {
											DV_DURATION matches {
												value matches {|PT0S..<P200Y|}
											}
											C_DV_QUANTITY <
												property = <[openehr::128]>
												list = <
													["1"] = <
														units = <"a">
														magnitude = <|0.0..<200.0|>
													>
													["2"] = <
														units = <"a_j">
														magnitude = <|0.0..<200.0|>
													>
												>
											>
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0005] occurrences matches {0..*} matches {    -- Adjusted age
										value matches {
											DV_DURATION matches {
												value matches {|>-P1Y..<P10Y|}
											}
											C_DV_QUANTITY <
												property = <[openehr::128]>
												list = <
													["1"] = <
														units = <"a">
														magnitude = <|>-1.0..<10.0|>
													>
													["2"] = <
														units = <"a_j">
														magnitude = <|>-1.0..<10.0|>
													>
												>
											>
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {    -- Age category
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {    -- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0008] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0009] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Age">
					description = <"Details about the age of an individual at a specific point in time.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Point in time event">
					description = <"Default, unspecified point in time event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Chronological age">
					description = <"Duration of time since birth.">
					comment = <"In most use cases the DV_DURATION data type is used to record the Chronological age. The DV_QUANTITY data type is used for recording a decimal age in years or mean Julian years (365.25 days), and the DV_TEXT data type is used for recording a textual representation of the individual's age. For example: \"P3M2W\", \"0,2884615384615385 a_j\" or \"3 months 2 weeks\".">
				>
				["at0005"] = <
					text = <"Adjusted age">
					description = <"Chronological age of an infant, less the number of weeks or months they were born premature.">
					comment = <"Also known as 'Corrected age'. In most use cases the DV_DURATION data type is used to record the Adjusted age. The DV_QUANTITY data type is used for recording a decimal age in years or mean Julian years (365.25 days), and the DV_TEXT data type is used for recording a textual representation of the individual's age. For example: \"P3M2W\", \"0,2884615384615385 a_j\" or \"3 months 2 weeks\".">
				>
				["at0006"] = <
					text = <"Comment">
					description = <"Additional narrative about the age of an individual, not captured in other fields.">
				>
				["at0007"] = <
					text = <"Age category">
					description = <"Duration of time since birth, expressed as a text description of a specified age range or grouping.">
					comment = <"For example: 'Age 10-20' or 'Teenager'. Categories for each specific use case will be defined within a template.">
				>
				["at0008"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Alter">
					description = <"Angaben über das Alter einer Person zu einem bestimmten Zeitpunkt.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Ereigniszeitpunkt">
					description = <"Standardwert, ein undefinierter Zeitpunkt des Ereignisses, das explizit im Template oder zur Laufzeit der Anwendung definiert werden kann.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Chronologisches Alter">
					description = <"Zeitdauer seit der Geburt.">
					comment = <"In den meisten Anwendungsfällen wird der Datentyp Dauer zur Erfassung des chronologischen Alters verwendet. Der Datentyp Menge wird für die Aufzeichnung eines dezimalen Alters in Jahren oder mittleren julianischen Jahren (365,25 Tage) verwendet, und der Datentyp Text wird für die Aufzeichnung einer textuellen Darstellung des Alters der Person verwendet. Zum Beispiel: \"P3M2W\", \"0,2884615384615385 a_j\" oder \"3 Monate 2 Wochen\".">
				>
				["at0005"] = <
					text = <"Korrigiertes Alter">
					description = <"Chronologisches Alter eines Säuglings, abzüglich der Anzahl der Wochen oder Monate, in denen es zu früh geboren wurde.">
					comment = <"In den meisten Anwendungsfällen wird der Datentyp DV_DURATION zur Erfassung des korrigierten Alters verwendet. Der Datentyp DV_QUANTITY wird für die Aufzeichnung eines dezimalen Alters in Jahren oder mittleren julianischen Jahren (365,25 Tage) verwendet, und der Datentyp DV_TEXT wird für die Aufzeichnung einer textuellen Darstellung des Alters der Person verwendet. Zum Beispiel: \"P3M2W\", \"0,2884615384615385 a_j\" oder \"3 Monate 2 Wochen\".">
				>
				["at0006"] = <
					text = <"Kommentar">
					description = <"Beschreibung des Alters einer Person, das nicht in anderen Datenelementen dargestellt wird.">
				>
				["at0007"] = <
					text = <"Altersklasse">
					description = <"Dauer der Zeit seit der Geburt, ausgedrückt als Textbeschreibung eines bestimmten Altersbereichs oder einer bestimmten Gruppe.">
					comment = <"Zum Beispiel: \"Alter 10-20\" oder \"Teenager\". Die Kategorien für jeden spezifischen Anwendungsfall werden in einen Template definiert.">
				>
				["at0008"] = <
					text = <"*Item tree (en)">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Erweiterung">
					description = <"Zusätzliche Informationen zur Erfassung lokaler Inhalte oder Anpassung an andere Referenzmodelle/Formalismen.">
					comment = <"Zum Beispiel: Lokaler Informationsbedarf oder zusätzliche Metadaten zur Anpassung an FHIR-Ressourcen.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Alder">
					description = <"Detaljer om alderen til et individ på et gitt tidspunkt.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert tidspunkthendelse">
					description = <"Uspesifisert tidspunkt som kan defineres mer eksplisitt i en templat eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Kronologisk alder">
					description = <"Varighet siden fødsel.">
					comment = <"I de fleste tilfeller benyttes datatypen DV_DURATION for å registrere den kronologiske alderen. Datatypen DV_QUANTITY brukes til å registrere en alder med desimaler i år eller gjennomsnittlige julianske år (365,25 dager), datatypen DV_TEXT brukes til å registrere en tekstlig representasjon av individets alder. For eksempel: \"P3M2W\", \"0,2884615384615385 a_j\" eller \"3 måneder 2 uker\".">
				>
				["at0005"] = <
					text = <"Justert alder">
					description = <"Kronologisk alder for et spedbarn, minus antall uker eller måneder det ble født for tidlig.">
					comment = <"I de fleste tilfeller benyttes datatypen DV_DURATION for å registrere justert alder. Datatypen DV_QUANTITY brukes til å registrere en alder med desimaler i år eller gjennomsnittlige julianske år (365,25 dager), datatypen DV_TEXT brukes til å registrere en tekstlig representasjon av individets alder. For eksempel: \"P3M2W\", \"0,2884615384615385 a_j\" eller \"3 måneder 2 uker\".">
				>
				["at0006"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om et individs alder som ikke fanges opp i andre felt.">
				>
				["at0007"] = <
					text = <"Alderskategori">
					description = <"Varighet av tid siden fødselen, uttrykt som en tekstbeskrivelse av et spesifisert aldersintervall eller gruppering.
">
					comment = <"For exampel; \"10-20 år\" eller \"Tenåring. Kategorier vil bli definert i et templat for hvert enkelt brukstilfelle.">
				>
				["at0008"] = <
					text = <"*Item tree (en)">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
			>
		>
	>

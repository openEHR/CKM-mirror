archetype (adl_version=1.4; uid=95e92636-812d-472e-aa6a-8e5315a34315)
	openEHR-EHR-OBSERVATION.medication_screening.v1

concept
	[at0000]	-- Medication screening questionnaire
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Kim Sommer, Natalia Strauch, Alina Rehberg">
				["organisation"] = <"Medizinische Hochschule Hannover">
				["email"] = <"sommer.kimkatrin@mh-hannover.de, Strauch.Natalia@mh-hannover.de, rehberg.alina@mh-hannover.de">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Silje Ljosland Bakke, John Tore Valand">
				["organisation"] = <"Helse Vest IKT AS, Helse Bergen">
				["email"] = <"silje.ljosland.bakke@helse-vest-ikt.no, john.tore.valand@helse-bergen.no, john.tore.valand@helse-vest-ikt.no">
			>
		>
		["nl"] = <
			language = <[ISO_639-1::nl]>
			author = <
				["name"] = <"Martijn van Eenennaam">
				["organisation"] = <"Nedap Healthcare">
				["email"] = <"martijn.vaneenennaam@nedap.com">
			>
			accreditation = <"PhD">
		>
	>
description
	original_author = <
		["name"] = <"Silje Ljosland Bakke">
		["organisation"] = <"Nasjonal IKT HF">
		["email"] = <"silje.ljosland.bakke@nasjonalikt.no">
		["date"] = <"2018-11-07">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Schaffung von Rahmenbedingungen für die Darstellung von Antworten auf vordefinierte Screening-Fragen zur Anwendung eines bestimmten Medikaments oder einer Medikamentengruppe.">
			use = <"Verwendung als Rahmenbedingung für die Darstellung von Antworten auf vordefinierte Screening-Fragen zur Anwendung eines bestimmten Medikaments oder einer Medikamentengruppe (einschließliche Medikamentenkasse).

Beispiele für Medikamente, Gruppierungen und Klassen von Medikamenten sind „Alendronsäure“, „Osteoporose-Medikamente“ bzw. „Bisphosphonate“.

Häufige Anwendungsfälle umfassen, sind aber nicht beschränkt auf:
- Systematische Befragung in jedem Beratungsgespräch zu dem Medikamenteneinnahme-Verhalten, zum Beispiel:
--- Verwenden Sie Paracetamol? Ja, Nein, Unbekannt.
--- Haben Sie in den letzten vier Wochen Antikoagulanzien verwendet? Ja, Nein, Unbekannt.

Die Semantik dieses Archetyps ist absichtlich locker, und die Abfrage dieses Archetyps wäre normalerweise nur im Kontext jedes spezifischen Templates nützlich oder sicher. In einem Template würde normalerweise jedes Datenelement in die spezifische gestellte Frage umbenannt. Wo Wertesätze für allgemeine Anwendungsfälle vorgeschlagen wurden, können diese für die lokale Verwendung angepasst werden, indem die „Text“-Auswahl verwendet wird, um jedem spezifischen Anwendungsfall zu entsprechen.

Über die EVENT-Struktur aus dem Referenzmodell kann festgelegt werden, ob sich die Fragen auf einen Zeitpunkt oder einen Zeitraum beziehen. Verwenden Sie eine separate Instanz dieses Archetyps, um zwischen einer Befragung zu unterscheiden, der Informationen über ein Medikament enthält, das zu einem beliebigen Zeitpunkt in der Vergangenheit angewendet wurde, und Informationen über ein Medikament, das innerhalb eines bestimmten Zeitintervalls durchgeführt wurde - zum Beispiel den Unterschied zwischen \"Verwenden Sie Paracetamol? \" im Vergleich zu \"Haben Sie in den letzten vier Wochen Antikoagulanzien verwendet?\".

Die Quelle der Informationen in einer Fragebogenantwort kann in verschiedenen Kontexten variieren, kann jedoch mithilfe des Elements „Information provider“ im Referenzmodell spezifisch identifiziert werden.

Dieser Archetyp wurde entwickelt, um als Screening-Tool oder zur Darstellung einfacher Daten im Fragebogenformat zur Verwendung in Situationen wie einem Krankheitsregister verwendet zu werden. Wenn der Screening-Fragebogen das Vorhandensein eines Medikaments identifiziert, wird empfohlen, dass das klinische System die spezifischen Details über das Medikament unter Verwendung eines relevanten Medikations-Archetyps darstellt und beibehält, zum Beispiel das OBSERVATION.medication_statement, um eine detaillierte Momentaufnahme über die tatsächliche Verwendung des spezifischen Medikaments darzustellen.">
			keywords = <"Medikation, Screening, Fragebogen, Medikament, Arzneimittel, Behandlung", ...>
			misuse = <"Nicht zur Darstellung einer Anforderung für ein zu verabreichendes oder zu konsumierendes Medikament zu verwenden - verwenden Sie zu diesem Zweck INSTRUCTION.medication_order.

Nicht zur Darstellung der Verabreichung, Abgabe oder Einnahme eines Medikaments verwenden - verwenden Sie ACTION.medication für diesen Zweck.

Nicht zur Darstellung einer Zusammenfassung der Verwendung eines Medikaments über die Lebenszeit der Person zu verwenden – verwenden Sie zu diesem Zweck EVALUATION.medication_summary.

Darf nicht verwendet werden, um eine detaillierte Momentaufnahme über die tatsächliche Verwendung eines einzelnen angegebenen Medikaments außerhalb eines Screening-Kontexts darzustellen. - Verwenden Sie zu diesem Zweck OBSERVATION.medication_statement.

Darf nicht verwendet werden, um Details über das Fehlen eines bestimmten Medikaments oder einer Medikamentengruppe außerhalb eines Screening-Kontexts darzustellen. Verwenden Sie zu diesem Zweck EVALUATION.exclusion_specific.

Nicht zu verwenden, um einen Rahmen für die Darstellung von Antworten auf vordefinierte Screening-Fragen zu Nebenwirkungen zu schaffen, verwenden Sie zu diesem Zweck einen geeigneten Archetyp.

Darf nicht verwendet werden, um Details zu einer einfachen Auswahlliste darzustellen, in der eine Frage entweder als \"vorhanden\" oder \"unbestimmt\" aufgezeichnet werden kann. Verwenden Sie dazu OBSERVATION.selection_list.">
			copyright = <"© openEHR Foundation, Nasjonal IKT HF">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å lage et rammeverk for registrering av svar på forhåndsdefinerte spørsmål om bruken av et hvilket som helst legemiddel eller gruppe av legemidler.">
			use = <"Brukes for å lage et rammeverk for registrering av svar på forhåndsdefinerte spørsmål om bruken av et hvilket som helst legemiddel eller gruppe av legemidler.

Eksempler på legemidler, grupper og klasser av legemidler er \"alendronsyre\", \"osteoporosemedisiner\" og \"bisfosfonater\".

Vanlige bruksområder inkluderer, men er ikke begrenset til:
- Systematiske spørsmål ved konsultasjoner, for eksempel:
--- Bruker du paracetamol? Ja, Nei, Ukjent.
--- Har du brukt noen blodfortynnende medisiner de siste 4 ukene? Ja, Nei, Ukjent.

Semantikken til denne arketypen er med vilje løst definert, og spørringer etter data i denne arketypen vil normalt bare være hensiktsmessig eller sikkert i konteksten av det spesifikke templatet. I et templat vil hvert dataelement vanligvis døpes om til det (spesifikke) spørsmålet man ønsker svar på. Flere steder er det foreslått verdisett som passer til de vanligste bruksområder, men det er allikevel mulig å tilpasse/endre verdisett for lokal bruk ved å benytte de alternative datatypene DV_TEXT eller DV_BOOLEAN for å harmonere verdisettet til hvert enkelt brukstilfelle.

EVENT-strukturen fra arketypens referansemodell kan brukes for å spesifisere om spørsmålene relateres til et tidspunkt eller til et tidsintervall.
Bruk egne instanser av denne arketypen for å skille mellom spørsmål om bruk av et legemiddel på et hvilket som helst tidspunkt og spørsmål om legemidler som er brukt i et tidsintervall. For eksempel; \"Bruker du paracetamol nå?\" sammenlignet med \"Har du brukt noen smertestillende medisin de siste 4 ukene?\".

Kilden til informasjonen i et spørreskjema kan variere i ulike kontekster, men kan identifiseres spesifikt ved å benytte \"Information provider\" elementet i openEHR referansemodellen.

Denne arketypen er laget for å bli brukt i et spørreskjema eller for å registrere enkle strukturerte data til for eksempel et sykdomsregister. Dersom det i spørreskjemaet identifiseres at et legemiddel er tilstede, anbefales det at spesifikke detaljer om legemiddelet registreres og lagres i pasientjournalen i en egnet legemiddelarketype, for eksempel arketypen OBSERVATION.medication_statement for å registrere et detaljert øyeblikksbilde om bruken av et enkelt spesifisert legemiddel.">
			keywords = <"legemiddel, kartlegging, spørreskjema, legemiddelgruppe, medikament", ...>
			misuse = <"Brukes ikke for å dokumentere forordningen av et legemiddel - bruk arketypen INSTRUCTION.medication_order (Legemiddelordinering) til dette formålet.

Brukes ikke for å dokumentere administreringen av et legemiddel - bruk arketypen ACTION.medication (Legemiddelhåndtering) til dette formålet.

Brukes ikke for å registrere et sammendrag om all bruk av et spesifisert legemiddel over et individs livstid, bruk arketypen EVALUATION.medication_summary (Legemiddelsammendrag) til dette formålet.

Brukes ikke for å registrere et detaljert øyeblikksbilde om bruken av et enkelt spesifisert legemiddel, utenfor konteksten av kartleggingen. Bruk arketypen OBSERVATION.medication_statement for dette formålet.

Brukes ikke for å registrere detaljer om etablert fravær av et spesifikt legemiddel eller gruppe av legemidler i pasientjournalen, utenfor konteksten av kartleggingen. Bruk arketypen EVALUATION.exclusion_specific for dette formålet.

Brukes ikke for å lage et rammeverk for registrering av svar på forhåndsdefinerte spørsmål om overfølsomhetsreaksjoner, bruk en egnet arketype for dette formålet.

Brukes ikke for å registrere detaljer om en enkel utvalgsliste der et spørsmål kan registreres som enten \"Tilstede\" eller \"Ubestemt\". Bruk arketypen OBSERVATION.selection_list for dette formålet.">
			copyright = <"© openEHR Foundation, Nasjonal IKT HF, openEHR Foundation, openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To create a framework for recording answers to pre-defined screening questions about the use of any specified medication or grouping of medications.">
			use = <"Use to create a framework for recording answers to pre-defined screening questions about the use of any specified medication or grouping (including classes) of medications.

Examples of medications, groupings and classes of medications are 'alendronic acid', 'anti osteoporosis medications' and 'bisphosphonates', respectively.

Common use cases include, but are not limited to:
- Systematic questioning in any consultation related to patterns of medication usage, for example:
--- Do you use paracetamol? Yes, No, Unknown.
--- Have you been using any anticoagulants the last four weeks? Yes, No, Unknown.

The semantics of this archetype are intentionally loose, and querying this archetype would normally only be useful or safe within the context of each specific template. In a template, each data element would usually be renamed to the specific question asked. Where value sets have been proposed for common use cases, these can be adapted for local use by using the DV_TEXT or the DV_BOOLEAN datatypes choice to match each specific use case.

The EVENT structure from the reference model can be used to specify whether the questions relate to point in time or over a period of time. Use a separate instance of this archetype to distinguish between a questionnaire recording information about a medication that has been used at any time in the past and information about a medication used within a specified time interval - for example the difference between \"Do you use paracetamol?\" compared to \"Have you been using any anticoagulants during the last four weeks?\".

The source of the information in a questionnaire response may vary in different contexts but can be specifically identified using the 'Information provider' element in the Reference Model.

This archetype has been designed to be used as a screening tool or to record simple questionnaire-format data for use in situations such as a disease registry. If the screening questionnaire identifies the presence of a medication it is recommended that clinical system record and persist the specific details about the medication using a relevant medication archetype, for example the OBSERVATION.medication_statement to record a detailed snapshot view about the actual use of a single specified medication.">
			keywords = <"medication, screening, questionnaire, drug, treatment", ...>
			misuse = <"Not to be used for recording an order for a medication to be administered or consumed - use INSTRUCTION.medication_order for this purpose.

Not to be used for recording the administration, dispensing or consumption of a medication - use ACTION.medication for this purpose.

Not to be used for recording a summary of use of a medication over the lifetime of the individual - use EVALUATION.medication_summary for this purpose.

Not to be used to record a detailed snapshot view about the actual use of a single specified medication, outside of a screening context. - use OBSERVATION.medication_statement for this purpose.

Not to be used to record details about the positive absence of a specific medication or grouping of medication, outside of a screening context. Use EVALUATION.exclusion_specific for this purpose.

Not to be used to to create a framework for recording answers to pre-defined screening questions about adverse reactions, use an appropriate archetype for this purpose.

Not to be used to record details about a simple selection list where a question may be recorded as either \"present\" or \"indeterminate\". Use OBSERVATION.selection_list for this purpose.">
			copyright = <"© openEHR Foundation, Nasjonal IKT HF">
		>
		["nl"] = <
			language = <[ISO_639-1::nl]>
			purpose = <"*To create a framework for recording answers to pre-defined screening questions about the use of a any medication. (en)">
			use = <"*Use to create a framework for recording answers to pre-defined screening questions about the use of any specified medication or grouping (including classes) of medications.

Examples of medications, groupings and classes of medications are 'alendronic acid', 'anti osteoporosis medications' and 'bisphosphonates', respectively.

Common use cases include, but are not limited to:
- Systematic questioning in any consultation related to patterns of medication usage , for example:
--- Do you use paracetamol? Yes, No, Unknown.
--- Have you been using any anticoagulants the last four weeks? Yes, No, Unknown.

The semantics of this archetype are intentionally loose, and querying this archetype would normally only be useful or safe within the context of each specific template. In a template, each data element would usually be renamed to the specific question asked. Where value sets have been proposed for common use cases, these can be adapted for local use by using the 'text' choice to match each specific use case.

The EVENT structure from the reference model can be used to specify whether the questions relate to point in time or over a period of time. Use a separate instance of this archetype to distinguish between a questionnaire recording information about a procedure that has been performed at any time in the past and information about a procedure performed within a specified time interval - for example the difference between \"Do you use paracetamol?\" compared to \"Have you been using any anticoagulants the last four weeks?\".

The source of the information in a questionnaire response may vary in different contexts but can be specifically identified using the 'Information provider' element in the Reference Model.

This archetype has been designed to be used as a screening tool or to record simple questionnaire-format data for use in situations such as a disease registry. If the screening questionnaire identifies the presence of a medication it is recommended that clinical system record and persist the specific details about the medication using a relevant medication archetype, for example the OBSERVATION.medication_statement to record a detailed snapshot view about the actual use of a single specified medication. (en)">
			keywords = <"*medication, screening, questionnaire, drug, treatment (en)", ...>
			misuse = <"*Not to be used for recording an order for a medication to be administered or consumed - use INSTRUCTION.medication_order for this purpose.

Not to be used for recording the administration, dispensing or consumption of a medication - use ACTION.medication for this purpose.

Not to be used for recording a summary of use of a medication over the lifetime of the individual - use EVALUATION.medication_summary for this purpose.

Not to be used to record a detailed snapshot view about the actual use of a single specified medication, outside of a screening context. - use OBSERVATION.medication_statement for this purpose.

Not to be used to record details about the positive absence of a specific medication or grouping of medication, outside of a screening context. Use EVALUATION.exclusion_specific for this purpose.

Not to be used to to create a framework for recording answers to pre-defined screening questions about adverse reactions, use an appropriate archetype for this purpose.

Not to be used to record details about a simple selection list where a question may be recorded as either \"present\" or \"indeterminate\". Use OBSERVATION.selection_list for this purpose. (en)">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Astrid Askeland, Dips AS, Norway", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "SB Bhattacharyya, Bhattacharyyas Clinical Records Research & Informatics LLP, India", "Randi Brendberg, Helse Nord RHF, Norway", "Yexuan Cheng, 浙江大学, China", "Are Edvardsen, SKDE, Helse Nord RHF, Norway", "Alexander Eikrem-Lüthi, Lovisenberg Diakonale Sykehus, Norway", "Kåre Flø, DIPS ASA, Norway", "Grant Forrest, Lunaria Ltd, United Kingdom", "Anca Heyd, DIPS ASA, Norway", "Joost Holslag, Nedap, Netherlands", "Evelyn Hovenga, EJSH Consulting, Australia", "Mikkel Johan Gaup Grønmo, Regional forvaltning EPJ, Helse Nord, Norway", "Gunnar Jårvik, Helse Vest IKT AS, Norway", "Anjali Kulkarni, Karkinos, India", "Kanika Kuwelker, Helse Vest IKT, Norway", "Jörgen Kuylenstierna, eWeave AB, Sweden", "Liv Laugen, ​Oslo University Hospital, Norway, Norway", "Øygunn Leite Kallevik, Helse Bergen, Norway", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Mikael Nyström, Cambio Healthcare Systems AB, Sweden", "Jussara Rotzsch, Hospital Alemão Oswaldo Cruz, Brazil", "Andre Smitt-Ingebretsen, Sørlandet sykehus HF, Norway", "Natalia Strauch, Medizinische Hochschule Hannover, Germany", "Norwegian Review Summary, Norwegian Public Hospitals, Norway", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)", "Marit Alice Venheim, Helse Vest IKT, Norway (openEHR Editor)", "Ina Wille, Helse-Vest RHF, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Avgrenet fra: Medication screening questionnaire, Published archetype [Internet]. openEHR Foundation, openEHR Clinical Knowledge Manager [cited: 2023-07-12]. Available from: https://ckm.openehr.org/ckm/archetypes/1013.1.4677">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"876BA56A1E975E7F68DC58E6EB81EB33">
		["build_uid"] = <"dcd86caa-80f4-4dbf-9162-cc61880219ad">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact https://www.snomed.org/snomed-ct/get-snomed or info@snomed.org.">
		["revision"] = <"1.0.2">
	>

definition
	OBSERVATION[at0000] matches {    -- Medication screening questionnaire
		data matches {
			HISTORY[at0022] matches {    -- Event Series
				events cardinality matches {0..*; unordered} matches {
					EVENT[at0023] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0001] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0040] occurrences matches {0..1} matches {    -- Screening purpose
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0027] occurrences matches {0..*} matches {    -- Any medications used?
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0028,    -- Yes
													at0029,    -- No
													at0030]    -- Unknown
												}
											}
											DV_TEXT matches {*}
											DV_BOOLEAN matches {*}
										}
									}
									ELEMENT[at0043] occurrences matches {0..1} matches {    -- Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									CLUSTER[at0026] occurrences matches {0..*} matches {    -- Specific medication
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0021] matches {    -- Medication name
												value matches {
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0024] occurrences matches {0..1} matches {    -- Used
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0036,    -- Yes
															at0037,    -- No
															at0039]    -- Unknown
														}
													}
													DV_TEXT matches {*}
													DV_BOOLEAN matches {*}
												}
											}
											ELEMENT[at0003] occurrences matches {0..1} matches {    -- Latest dose
												value matches {
													DV_DATE_TIME matches {*}
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0002] occurrences matches {0..*} matches {    -- Timing
												value matches {
													DV_DURATION matches {*}
													DV_INTERVAL<DV_DURATION> matches {*}
													DV_TEXT matches {*}
													DV_DATE_TIME matches {*}
													DV_INTERVAL<DV_DATE_TIME> matches {*}
												}
											}
											allow_archetype CLUSTER[at0041] occurrences matches {0..*} matches {    -- Additional details
												include
													archetype_id/value matches {/openEHR-EHR-CLUSTER\.dosage(-[a-zA-Z0-9_]+)*\.v2/}
											}
											ELEMENT[at0025] occurrences matches {0..1} matches {    -- Comment
												value matches {
													DV_TEXT matches {*}
												}
											}
										}
									}
									allow_archetype CLUSTER[at0042] occurrences matches {0..*} matches {    -- Additional details
										include
											archetype_id/value matches {/.*/}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0005] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0019] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Medication screening questionnaire">
					description = <"Series of questions and associated answers used to screen for the use of medications.">
					comment = <"The answers may be self-reported.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Timing">
					description = <"Indication of timing related to the use of the medication or grouping of medications.">
					comment = <"The 'Timing' data element has deliberately been loosely modelled to support the myriad of ways that it can be used in questionnaires to capture when the medication or grouping of medications were used. The specific and intended semantics can be further clarified in a template. For example: the actual date and/or time; the start and stop time for the use of the medication or grouping of medications; the interval of time during which the medication or grouping of medications were used; the duration of the medication or grouping of medications were used; the age of the individual at the time the medication or grouping of medications were used; or the duration of time since it were used. A partial date is valid, using the DV_DATE_TIME data type, to record only a year.">
				>
				["at0003"] = <
					text = <"Latest dose">
					description = <"The date and/or time of administation of the most recent dose of the medication or group of medications.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0005"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0019"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0021"] = <
					text = <"Medication name">
					description = <"Name of medication or grouping of medication.">
					comment = <"For example: 'alendronic acid', 'anti osteoporosis medications' or 'bisphosphonates'.
Coding of the 'Medication name' with a terminology is preferred, where possible.">
				>
				["at0022"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0023"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0024"] = <
					text = <"Used">
					description = <"Is there a history of use of a specific medication or group of medications?">
					comment = <"In a template, the data element would usually be renamed to the specific question asked. The proposed value set can be adapted for local use by using the DV_TEXT or the DV_BOOLEAN datatypes choice to match each specific use case. 
For example an alternative valueset using the DV_TEXT datatype can be: 
Currently using [The individual currently uses the specific medication either on a regular basis or as required.]
Never used [The individual has never used the specific medication.]
Used in the past [The individual has used the specific medication in the past, but isn't currently using it.]
Unknown [It is not known whether the individual uses or has used the specific medication.]">
				>
				["at0025"] = <
					text = <"Comment">
					description = <"Additional narrative about the specific medication question, not captured in other fields.">
				>
				["at0026"] = <
					text = <"Specific medication">
					description = <"Details about a specified medication or grouping of medications relevant for the screening purpose.">
					comment = <"Use separate instances of this CLUSTER to differentiate between specific medications or groupings of medication.">
				>
				["at0027"] = <
					text = <"Any medications used?">
					description = <"Is there a history of use of any medication related to the screening purpose?">
					comment = <"In a template, the data element would usually be renamed to the specific question asked. The proposed value set can be adapted for local use by using the DV_TEXT or the DV_BOOLEAN datatypes choice to match each specific use case.">
				>
				["at0028"] = <
					text = <"Yes">
					description = <"">
				>
				["at0029"] = <
					text = <"No">
					description = <"">
				>
				["at0030"] = <
					text = <"Unknown">
					description = <"">
				>
				["at0036"] = <
					text = <"Yes">
					description = <"">
				>
				["at0037"] = <
					text = <"No">
					description = <"">
				>
				["at0039"] = <
					text = <"Unknown">
					description = <"">
				>
				["at0040"] = <
					text = <"Screening purpose">
					description = <"The context or reason for screening.">
					comment = <"This data element is intended to provide collection context for the question/answer groups when queried at a later date. It is not expected that this data element will be exposed to the individual, but only stored in data. For example: pre-admission screening, the name of the actual questionnaire or screening for previous use of a class of medications, such as bisphosphonates.">
				>
				["at0041"] = <
					text = <"Additional details">
					description = <"Structured details or questions about the specific medication or grouping of medications.">
				>
				["at0042"] = <
					text = <"Additional details">
					description = <"Structured details or questions about the screening for medications.">
				>
				["at0043"] = <
					text = <"Description">
					description = <"Narrative description about the history of use of any medication relevant for the screening purpose.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Kartleggingsspørsmål om legemidler">
					description = <"Spørsmål og tilhørende svar som brukes til å kartlegge bruken av legemidler.">
					comment = <"Svarene kan være selvrapporterte.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Tidsangivelse">
					description = <"Tidsangivelse for bruken av legemiddelet eller gruppen av legemidler.">
					comment = <"Dataelementet \"Tidsangivelse\" er med hensikt løst modellert for å støtte ulike måter å angi tidsangivelser for bruken av legemiddelet eller gruppen av legemidler i et spørreskjema. Den konkrete semantikken defineres i templatet. For eksempel: Bruken av legemiddelet eller gruppen av legemidler faktiske dato og/eller klokkeslett, start- og slutttid for bruken av legemiddelet eller gruppen av legemidler, et tidsintervall for når legemiddelet eller gruppen av legemidler ble brukt, varigheten av bruken av legemiddelet eller gruppen av legemidler, individets alder på tidspunktet for bruken av legemiddelet eller gruppen av legemidler, eller hvor lenge det siden det/de ble brukt. Kan være en deldato, for eksempel bare år, ved å bruke datatypen DV_DATE_TIME.">
				>
				["at0003"] = <
					text = <"Siste dose">
					description = <"Dato og/eller klokkeslett for administreringen av den siste dosen av legemiddelet eller gruppen av legemidler.">
					comment = <"Kan være en deldato, for eksempel kun årstall.">
				>
				["at0005"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0019"] = <
					text = <"Ytterligere informasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
				["at0021"] = <
					text = <"Legemiddelnavn">
					description = <"Navnet på legemiddelet eller gruppen av legemidler.">
					comment = <"For eksempel: \"alendronsyre\", \"osteoporosemedisin\" og \"bisfosfonater\".
Koding av legemiddelnavnet med en terminologi foretrekkes, der det er mulig.">
				>
				["at0022"] = <
					text = <"*HISTORY (en)">
					description = <"*">
				>
				["at0023"] = <
					text = <"Uspesifisert hendelse">
					description = <"Standard, uspesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i et templat eller i en applikasjon.">
				>
				["at0024"] = <
					text = <"Brukt">
					description = <"Er det spesifikke legemiddelet eller gruppen av legemidler noen gang brukt?">
					comment = <"Navnet på dataelementet kan omformuleres til det spesifikke spørsmålet i et templat eller brukergrensesnitt. Det foreslåtte verdisettet kan tilpasses/endres i det enkelte bruksområde ved å benytte de alternative datatypene DV_TEXT eller DV_BOOLEAN.

Eksempel på et alternativt verdisett ved å benytte datatypen DV_TEXT kan være:
Nåværende bruk [Individet bruker for tiden det spesifikke legemiddelet enten fast eller ved behov.],
Aldri brukt [Individet har aldri brukt det spesifikke legemiddelet.],
Tidligere bruk [Individet har tidligere brukt det spesifikke legemiddelet, men bruker det for tiden ikke.],
Usikker [Det er ikke kjent hvorvidt individet bruker eller har brukt det spesifikke legemiddelet.].">
				>
				["at0025"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om det spesifikke legemiddelspørsmålet som ikke er omfattet av andre felt.">
				>
				["at0026"] = <
					text = <"Spesifikt legemiddel">
					description = <"Detaljer om et spesifikt legemiddel eller gruppe av legemidler er innenfor kartleggingsformålet.">
					comment = <"Bruk separate instanser av dette clusteret for å skille mellom spesifikke legemidler eller grupper av legemidler.">
				>
				["at0027"] = <
					text = <"Bruk av noen legemidler?">
					description = <"Finnes det noen tidligere legemiddelbruk som er innenfor kartleggingsformålet?">
					comment = <"Navnet på dataelementet kan omformuleres til det spesifikke spørsmålet i et templat eller brukergrensesnitt. Det foreslåtte verdisettet kan tilpasses/endres i det enkelte bruksområde ved å benytte de alternative datatypene DV_TEXT eller DV_BOOLEAN.">
				>
				["at0028"] = <
					text = <"Ja">
					description = <"">
				>
				["at0029"] = <
					text = <"Nei">
					description = <"">
				>
				["at0030"] = <
					text = <"Usikker">
					description = <"">
				>
				["at0036"] = <
					text = <"Ja">
					description = <"">
				>
				["at0037"] = <
					text = <"Nei">
					description = <"">
				>
				["at0039"] = <
					text = <"Usikker">
					description = <"">
				>
				["at0040"] = <
					text = <"Kartleggingsformål">
					description = <"Konteksten eller årsaken for kartleggingen.">
					comment = <"Dette dataelementet er ment for å sette en kontekst for spørsmålene om man senere ønsker å gjøre spørringer på dataene. Det er ikke forventet at dette dataelementet skal være synlig for en bruker av skjemaet, men kun tilgjengelig i de lagrede dataene. 
For eksempel: \"Preoperativ screening\" eller navnet på det faktiske spørreskjemaet eller screening for tidligere bruk av en legemiddelklasse, som bisfosfonater.">
				>
				["at0041"] = <
					text = <"Ytterligere detaljer">
					description = <"Strukturerte detaljer eller spørsmål om bruken av det spesifikke legemiddelet eller gruppen av legemidler.">
				>
				["at0042"] = <
					text = <"Ytterligere detaljer">
					description = <"Strukturerte detaljer eller spørsmål om kartleggingen av legemidler.">
				>
				["at0043"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse om bruken av noen legemidler som er innenfor kartleggingsformålet.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Fragebogen zum Medikamenten-Screening">
					description = <"Eine Reihe von Fragen und zugehörigen Antworten, die zum Screening auf die Anwendung von Medikamenten verwendet werden.">
					comment = <"Die Antworten können selbst berichtet werden.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Zeitangaben">
					description = <"*Indication of timing related to the use of the medication or grouping of medications. (en)">
					comment = <"*The 'Timing' data element has deliberately been loosely modelled to support the myriad of ways that it can be used in questionnaires to capture when the medication or grouping of medications were used. The specific and intended semantics can be further clarified in a template. For example: the actual date and/or time; the start and stop time for the use of the medication or grouping of medications; the interval of time during which the medication or grouping of medications were used; the duration of the medication or grouping of medications were used; the age of the individual at the time the medication or grouping of medications were used; or the duration of time since it were used. A partial date is valid, using the DV_DATE_TIME data type, to record only a year. (en)">
				>
				["at0003"] = <
					text = <"Letzte Dosis">
					description = <"Das Datum und/oder die Uhrzeit der Verabreichung der letzten Dosis des Medikaments oder der Medikamentengruppe.">
					comment = <"Kann ein Teildatum sein, zum Beispiel nur ein Jahr.">
				>
				["at0005"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0019"] = <
					text = <"Erweiterung">
					description = <"Zusätzliche Informationen zur Erfassung lokaler Inhalte oder Anpassung an andere Referenzmodelle/Formalismen.">
					comment = <"Zum Beispiel: Lokaler Informationsbedarf oder zusätzliche Metadaten zur Anpassung an FHIR-Ressourcen.">
				>
				["at0021"] = <
					text = <"Name des Medikaments">
					description = <"Name des Medikaments oder Medikamentengruppe.">
					comment = <"Zum Beispiel: „Alendronsäure“, „Medikamente gegen Osteoporose“ oder „Bisphosphonate“.
Die Kodierung des „Name des Medikaments“ mit einer Terminologie wird nach Möglichkeit bevorzugt.">
				>
				["at0022"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0023"] = <
					text = <"Beliebiges Ereignis">
					description = <"Standardwert, ein undefinierter/s Zeitpunkt oder Intervallereignis, das explizit im Template oder zur Laufzeit der Anwendung definiert werden kann.">
				>
				["at0024"] = <
					text = <"Angewendet">
					description = <"Gibt es eine Vorgeschichte der Verwendung eines bestimmten Medikaments oder einer Medikamentengruppe?">
					comment = <"In einem Template würde das Datenelement normalerweise in die spezifische gestellte Frage umbenannt. Der vorgeschlagene Wertesatz kann für die lokale Verwendung angepasst werden, indem die Datentypauswahl DV_TEXT oder DV_BOOLEAN verwendet wird, um jedem spezifischen Anwendungsfall zu entsprechen.
Ein alternativer Wertesatz mit dem Datentyp DV_TEXT kann beispielsweise sein:
Derzeitige Anwendung [Die Person wendet derzeit das spezifische Medikament entweder regelmäßig oder nach Bedarf an.]
Nie angewendet [Die Person hat das spezifische Medikament nie angewendet.]
In der Vergangenheit angewendet [Die Person hat das spezifische Medikament in der Vergangenheit angewendet, wendet es jedoch derzeit nicht an.]
Unbekannt [Es ist nicht bekannt, ob die Person das spezifische Medikament anwendet oder angewendet hat.]">
				>
				["at0025"] = <
					text = <"Kommentar">
					description = <"Zusätzliche Beschreibung über die spezifische Frage zum Medikament, die nicht in anderen Bereichen erfasst wird.">
				>
				["at0026"] = <
					text = <"Spezifisches Medikament">
					description = <"Details zu einem bestimmten Medikament oder einer Medikamentengruppe, die für das Screening relevant sind.">
					comment = <"Verwenden Sie separate Instanzen dieses CLUSTERs, um zwischen bestimmten Medikamenten oder Medikamentengruppen zu unterscheiden.">
				>
				["at0027"] = <
					text = <"Irgendwelche Medikamente angewendet?">
					description = <"Gibt es eine Vorgeschichte der Anwendung von Medikamenten im Zusammenhang mit dem Screening-Zweck?">
					comment = <"In einem Template würde das Datenelement normalerweise in die spezifische gestellte Frage umbenannt. Der vorgeschlagene Wertesatz kann für die lokale Verwendung angepasst werden, indem die Datentypauswahl DV_TEXT oder DV_BOOLEAN verwendet wird, um jedem spezifischen Anwendungsfall zu entsprechen.">
				>
				["at0028"] = <
					text = <"Ja">
					description = <"">
				>
				["at0029"] = <
					text = <"Nein">
					description = <"">
				>
				["at0030"] = <
					text = <"Unbekannt">
					description = <"">
				>
				["at0036"] = <
					text = <"Ja">
					description = <"">
				>
				["at0037"] = <
					text = <"Nein">
					description = <"">
				>
				["at0039"] = <
					text = <"Unbekannt">
					description = <"">
				>
				["at0040"] = <
					text = <"Zweck des Screenings">
					description = <"Der Kontext oder Grund für das Screening.">
					comment = <"Dieses Datenelement soll bei einer späteren Abfrage den Erhebungskontext für die Frage/Antwort-Gruppen bereitstellen. Es wird nicht erwartet, dass dieses Datenelement der Person belichtet wird, sondern nur in Daten gespeichert wird. Zum Beispiel: Screening vor der Aufnahme, der Name des eigentlichen Fragebogens oder Screening auf die frühere Anwendung einer Medikamentenklasse, wie z. B. Bisphosphonate.">
				>
				["at0041"] = <
					text = <"Zusätzliche Angaben">
					description = <"Strukturierte Angaben oder Fragen zum konkreten Medikament oder zur Medikamentengruppe.">
				>
				["at0042"] = <
					text = <"Zusätzliche Angaben">
					description = <"Strukturierte Angaben oder Fragen zum Medikamenten-Screening.">
				>
				["at0043"] = <
					text = <"Beschreibung">
					description = <"Beschreibung über die Geschichte der Anwendung von Medikamenten, die für das Screening relevant sind.">
				>
			>
		>
		["nl"] = <
			items = <
				["at0000"] = <
					text = <"Medicatiegebruik screening vragenlijst">
					description = <"*Series of questions and associated answers used to screen for the use of medication or grouping of medications. (en)">
					comment = <"*The answers may be self-reported. (en)">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"*Timing (en)">
					description = <"*Indication of timing related to the use of the medication or grouping of medications. (en)">
					comment = <"*The 'Timing' data element has deliberately been loosely modelled to support the myriad of ways that it can be used in questionnaires to capture when the medication or grouping of medications were used. The specific and intended semantics can be further clarified in a template. For example: the actual date and/or time; the start and stop time for the use of the medication or grouping of medications; the interval of time during which the medication or grouping of medications were used; the duration of the medication or grouping of medications were used; the age of the individual at the time the medication or grouping of medications were used; or the duration of time since it were used. A partial date is valid, using the DV_DATE_TIME data type, to record only a year. (en)">
				>
				["at0003"] = <
					text = <"*Previous dose (en)">
					description = <"*The date and/or time of administation of the most recent dose of the medication or group of medications. (en)">
					comment = <"*Can be a partial date, for example, only a year. (en)">
				>
				["at0005"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0019"] = <
					text = <"Uitbreiding">
					description = <"Aanvullende informatie vereist voor uitbreiding van het model met lokale context of om aan te sluiten bij andere referentiemodellen of formalismen.">
					comment = <"Bijvoorbeeld: lokale informatie vereisten; of aanvullende metadata om aan te sluiten bij FHIR.">
				>
				["at0021"] = <
					text = <"Medicatie naam">
					description = <"*Name of medication or grouping of medication. (en)">
					comment = <"*For example: 'alendronic acid', 'anti osteoporosis medications' or 'bisphosphonates'.
Coding of the 'Medication name' with a terminology is preferred, where possible. (en)">
				>
				["at0022"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0023"] = <
					text = <"Willekeurig event">
					description = <"Standaard, niet nader gedefiniëerd moment in tijd of tijdsinterval dat expliciet gedefiniëerd kan worden in een template of tijdens run-time.">
				>
				["at0024"] = <
					text = <"*Used (en)">
					description = <"*Is there a history of use of a specific medication or group of medications? (en)">
					comment = <"*In a template, the data element would usually be renamed to the specific question asked. The proposed value set can be adapted for local use by using the DV_TEXT or the DV_BOOLEAN datatypes choice to match each specific use case. 
For example an alternative valueset using the DV_TEXT datatype can be: 
Currently using [The individual currently uses the specific medication either on a regular basis or as required.]
Never used [The individual has never used the specific medication.]
Used in the past [The individual has used the specific medication in the past, but isn't currently using it.]
Unknown [It is not known whether the individual uses or has used the specific medication.] (en)">
				>
				["at0025"] = <
					text = <"Commentaar">
					description = <"*Additional narrative about the specific medication, grouping or class of medications. not captured in other fields. (en)">
				>
				["at0026"] = <
					text = <"*Specific medication (en)">
					description = <"*Details about a specified medication or grouping of medications relevant for the screening purpose. (en)">
					comment = <"*Use separate instances of this CLUSTER to differentiate between specific medications, groupings, or classes of medication. (en)">
				>
				["at0027"] = <
					text = <"*Any medications used? (en)">
					description = <"*Is there a history of use of any medication related to the screening purpose? (en)">
					comment = <"*In a template, the data element would usually be renamed to the specific question asked. The proposed value set can be adapted for local use by using the DV_TEXT or the DV_BOOLEAN datatypes choice to match each specific use case. (en)">
				>
				["at0028"] = <
					text = <"*Yes (en)">
					description = <"">
				>
				["at0029"] = <
					text = <"*No (en)">
					description = <"">
				>
				["at0030"] = <
					text = <"Onbekend">
					description = <"">
				>
				["at0036"] = <
					text = <"*Yes (en)">
					description = <"">
				>
				["at0037"] = <
					text = <"*No (en)">
					description = <"">
				>
				["at0039"] = <
					text = <"Onbekend">
					description = <"">
				>
				["at0040"] = <
					text = <"Doel van screening">
					description = <"*The context or reason for screening. (en)">
					comment = <"*This data element is intended to provide collection context for the question/answer groups when queried at a later date. It is not expected that this data element will be exposed to the individual, but only stored in data. For example: pre-admission screening or the name of the actual questionnaire.
For example: screening for previous use of a class of medications, such as bisphosphonates. (en)">
				>
				["at0041"] = <
					text = <"*Additional details (en)">
					description = <"*Structured details or questions about the specific medication, grouping or class of medications. (en)">
				>
				["at0042"] = <
					text = <"*Additional details (en)">
					description = <"*Structured details or questions about the screening for medications. (en)">
				>
				["at0043"] = <
					text = <"*Description (en)">
					description = <"*Narrative description about the history of use of any medication relevant for the screening purpose. (en)">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0030"] = <[SNOMED-CT::261665006]>
				["at0028"] = <[SNOMED-CT::373066001]>
				["at0029"] = <[SNOMED-CT::373067005]>
				["at0036"] = <[SNOMED-CT::373066001]>
				["at0037"] = <[SNOMED-CT::373067005]>
				["at0039"] = <[SNOMED-CT::261665006]>
			>
		>
	>

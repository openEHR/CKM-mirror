archetype (adl_version=1.4; uid=2b50f15c-f3c9-473b-8e9c-f57c00507561)
	openEHR-EHR-OBSERVATION.glasgow_coma_scale.v1

concept
	[at0000]	-- Glasgow coma scale
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["ar-sy"] = <
			language = <[ISO_639-1::ar-sy]>
			author = <
				["name"] = <"Mona Saleh">
			>
		>
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			author = <
				["name"] = <"Vladimir Pizzo">
				["organisation"] = <"Hospital Sirio Libanes, Brazil">
				["email"] = <"vladimir.pizzo@hsl.org.br">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2007-03-13">
	>
	details = <
		["ar-sy"] = <
			language = <[ISO_639-1::ar-sy]>
			purpose = <"لتسجيل الملاحظات السريرية المتعلقة بمدى استجابة الشخص للمنبهات لاعتبارها تقييما موضوعيا لدرجة وعي المريض.">
			use = <"*Use to record clinical observations regarding the reponsiveness of an adult subject to stimuli. It is commonly used to establish a baseline conscious state and neurological function assessment and/or to detect patients who may require immediate medical intervention.

In practical use, Glasgow Coma Scale is frequently calculated as a monitoring variable, using sequential and repeated measurements. Date and time should be recorded for each measurement, as well as any factors that may influence interpretation of changes. Changes in Total score or any E, M or V values may have as much clinical significance as the value recorded initially.

The Total Score can be derived as the sum of the recorded eye, motor and verbal response scores. 

The EMV Score can be derived as a concatenation of each of the recorded eye, motor and verbal response scores. For example, E3 M2 V4 represents the conscious state of a subject who opens eyes to speech, has an extensor response to pain and utters incomprehensible sounds.

The three response values are considered separately as well as their sum. 

The minimum possible Total Score value is 3 (equivalent to E1 M1 V1) and the maximum possible is 15 (equivalent to E4 M6 V5).

A recorded response for each of E, M and V is mandatory. If a response cannot be determined at all, then use of \"Not Applicable\" as a null flavour is appropriate to record. If any one of the eye, motor or verbal responses cannot be assessed - for example if the subject is intubated and not able to respond verbally, or paralysed and not able to provide a motor response - the score recorded for the response is 1 and a modifier attached. If the subject is intubated the verbal response component can be represented as V1t, where t = tube, or V1Intubated. If the subject has severe facial/eye swelling or injuries and can't open their eyes, the eye response component can be represented as E1c, where c=closed. Sometimes the numeric one may be omitted, in which case the eye response component might be expressed as Ec - for example, Ec M2 V4. The reason for not being able to assess a response can be recorded in the 'Comment' data element.

Clinical decision support should be based upon each of the specific records of eye, motor and verbal responses and the explicit statements that a response cannot be assessed, rather than the derived and composite EMV Score.(en)">
			keywords = <"الاستجابة", "حركي", "لفظي", "العين", "المنبه", "غلاسغو", "غيبوبة", "سُلَّم", "عصبيّ", "مدى الاستجابة", "العين الحركة و اللفظ", "واعي">
			misuse = <"لا يستخدم لتقييم الرضع و الأطفال صغار السن - استخدم لذلك نموذج ملاحظة. حرز غلاسغو - الغيبوبة - فيما يتعلق بطب الأطفال. للتأكد من أن الخيارات المتعلقة بردود فعل العين و الحركة و التلفظ مناسبة لعمر و قدرة الطفل.">
			copyright = <"*© National E-Health Transition Authority(en)">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record clinical responses of the subject of care to defined stimuli as an objective assessment of the level of consciousness.">
			use = <"Use to record clinical responses of an adult subject of care to stimuli. It is commonly used to establish a baseline conscious state and neurological function assessment and/or to detect patients who may require immediate medical intervention.

The Glasgow coma scale has three subscales E (eye), V (verbal) and M (motor). In clinical practice all three subscales are reported individually plus the 'Total score', if applicable.

A recorded response for each of E, V and M is mandatory. If a response cannot be tested, then the 'Not Applicable' null flavour should be recorded; do not use the 'None' ordinal value to record a missing component. Details about the reason for not being able to test a response can be recorded in the 'Confounding factors' data element.

The 'Total score' can be derived as the sum of the recorded eye, motor and verbal response scores. It is not appropriate to report a 'Total score' when one or more components are not testable because the score will be artificially low - in this situation record the EVM profile instead.

The three response values are considered separately as well as their sum. The 'EVM profile' can be derived as a concatenation of each of the recorded eye, motor and verbal response scores. For example, E3 V4 M2 represents the conscious state of a subject who opens eyes to speech, utters incomprehensible sounds and has an extensor response to stimulation.

The minimum possible 'Total score' value is 3 (equivalent to E1 V1 M1) and the maximum possible is 15 (equivalent to E4 V5 M6).

In practical use, Glasgow coma scale is recorded as one component of clinical monitoring, using sequential and repeated point-in-time measurements. Date and time should be recorded for each measurement, as well as any factors that may influence interpretation of changes. Changes in 'Total score' or any E, V or M values may have as much clinical significance as the value recorded initially.">
			keywords = <"response", "motor", "verbal", "eye", "stimulus", "glasgow", "coma", "scale", "neurological", "responsiveness", "EMV", "conscious", "GCS", "trauma", "central nervous system", "consciousness">
			misuse = <"Not to be used for assessing infants and young children - an archetype for Paediatric Glasgow Coma Scale will be required to ensure that the eye, motor and verbal response choices are appropriate for the age and ability of the child.">
			copyright = <"© openEHR Foundation">
		>
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Para registar informações clínicas sobre a resposta de um sujeito a estímulos como uma avaliação objetiva do estado de consciência de um sujeito.
A escala de coma de Glasgow (ECG) é uma escala neurológica que parece constituir-se num método confiável e objetivo de registrar o nível de consciência de uma pessoa, para avaliação inicial e contínua após um traumatismo craniano.">
			use = <"Use para registrar observações clínicas relacionadas a responsividade a estímulos de um indivíduo adulto. É utilizada comumente para estabelecer um nível de consciência basal e avaliação neurológica funcional e ou para detectar pacientes que possam necessitar intervenções médicas imediatas.

Em termos práticos, a Escala de Coma de Glasgow é frequentemente calculada como variável de monitoramento, utilizando reavaliações sequenciais. Data e hora devem ser registradas para cada medida assim como fatores que possam influenciar na interpretação do score. Modificações no score total ou nos valores O, V ou M podem ter maior significado clínico que os valores registrados inicialmente.

O Score Total é obtido como o somatório das respostas ocular, verbal e motora.

Os três valores de resposta são considerados, bem como sua soma.

O Score OVM é obtido da concatenação de cada score registrado para cada resposta (ocular, verbal e motora). Por exemplo, O3 V2 M2 representa indivíduo que abre os olhos ao chamado, emite sons incompreensíveis e apresenta padrão de extensão frente a estímulo doloroso.

O menor valor possível do score é 3 (equivalente a O1 V1 M1) e o máximo possível é 15 (equivalente a O4 V5 M6).

Um registro de resposta para cada O, V e M é mandatório. Se a resposta não puder ser determinada então a opção \"não se aplica\" é o registro apropriado. Se uma das respostas não puder ser acessada - por exemplo o indivíduo está entubado e não é capaz de responder verbalmente, ou paralisado e não é esperada resposta motora - o score registrado para esta resposta é 1 e um detalhe é registrado. Se um indivíduo está entubado o componente de resposta verbal pode ser representado como V1t, em que t = tubo, ou V1entubado. Se o indivíduo tem um edema facial ou periorbitário intenso ou lesões que o impeçam de abrir os olhos, o componente de resposta ocular pode ser representado como O1f, em que f=fechado. Ás vezes o valor numérico pode ser omitido, neste caso o componente de resposta ocular deve ser expresso como Of - por exemplo, Of V4 M2. O motivo de não ser possível acessar a resposta pode ser registrado no campo ´comentários´.

Suporte a decisão clínica pode ser baseado em registros específicos para cada um dos padrões de respostas O, V e M em detrimento dos valores derivados do score composto.">
			keywords = <"resposta", "motora", "verbal", "visual", "estímulo", "glasgow", "coma", "escala", "neurológicas", "responsividade", "OVM", "consciência", "ECG", "trauma", "sistema nervoso central", "inconsciente">
			misuse = <"Não deve ser utilizado para avaliar recém nascidos e e crianças de colo - para tal, utilizar OBSERVATION.glasgow_coma-paediatric.v1.adl para garantir respostas apropriados para a idade e capacidade da criança.">
			copyright = <"© National E-Health Transition Authority">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <"Tomas Alme, DIPS, Norway", "Nadim Anani, Karolinska Institutet, Sweden", "Vebjoern Arntzen, Oslo university hospital, Norway", "Koray Atalag, University of Auckland, New Zealand", "Silje Ljosland Bakke, Bergen Hospital Trust, Norway", "Lars Bitsch-Larsen, Haukeland University hospital, Norway", "Martin Boeker, Medical Center - University of Freiburg, Germany", "Marja Buur, Medisch Centrum Alkmaar/ Code24, Netherlands", "Margaret Campbell, Queensland Health, Australia", "Rong Chen, Cambio Healthcare Systems, Sweden", "Stephen Chu, Queensland Health, Australia", "Tamsin Cockayne, Australia", "Marc Cotran, identity vision systems, Canada", "Angela de Zwart, Orion Health, New Zealand", "Einar Fosse, National Centre for Integrated Care and Telemedicine, Norway", "Sebastian Garde, Ocean Informatics, Germany", "Christian Ghan, The Chris O'Brien Lifehouse at RPA, Australia", "William Goossen, Results 4 Care, Netherlands", "Heather Grain, Llewelyn Grain Informatics, Australia", "Birger Haarbrandt, Hannover Medical School, Germany", "Sam Heard, Ocean Informatics, Australia", "Oliver Hosking, Remote Health NT, Australia", "Evelyn Hovenga, EJSH Consulting, Australia", "Eugene Igras, IRIS Systems, Inc., Canada", "Sundaresan Jagannathan, Scottish NHS, United Kingdom", "Konstantinos Kalliamvakos, Cambio Healthcare Systems, Sweden", "Lars Karlsen, DIPS ASA, Norway", "Shinji Kobayashi, Kyoto University, Japan", "Sergey Kovalenko, Chelyabinsk Regional Children Hospital, Russia", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Hallvard Lærum, Oslo University Hospital, Norway", "Luis Marco Ruiz, Norwegian Center for Integrated Care and Telemedicine, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (Editor)", "Jeroen Meintjens, Medisch Centrum Alkmaar, Netherlands", "Andrej Orel, Marand d.o.o., Slovenia", "Michael Reynolds, Great Lakes Pediatric Associates, PLLC, United States", "Hossein Riazi, Iran", "Arturo Romero, SESCAM, Spain", "Jussara Rotzsch, UNB, Brazil", "Anoop Shah, University College London, United Kingdom", "Tony Shannon, NHS, United Kingdom", "Gary Sinclair, NT DoH, Australia", "Tim Sturgill, United States", "Soon Ghee Yap, Singapore General Hospital, Singapore">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["revision"] = <"1.0.0">
		["references"] = <"Teasdale G. Glasgow Coma Scale: The Glasgow structured approach to Assessment of the Glasgow Coma Scale [Internet]. Sir Graham Teasdale; 2014 {cited 2015 Apr 20]. Available from: http://www.glasgowcomascale.org/ and http://www.glasgowcomascale.org/downloads/GCS-Assessment-Aid.pdf.

Glasgow Coma Scale, draft archetype, NEHTA Clinical Knowledge Manager [Internet]. Australia: National eHealth Transition Authority. Authored: 2007 Mar 13. Available at: http://dcm.nehta.org.au/ckm/#showArchetype_1013.1.1160 (accessed 2015 Mar 05).

Crippen DW. Head Trauma - Presentation: Medscape Reference: Drugs, Diseases & Procedures [Internet]. WebMD LLC: c1994-2013; [updated 2012 Jun 21; accessed 2015 Mar 05]. Available from: http://emedicine.medscape.com/article/433855-overview#a0112.

Teasdale G, Jennett B. Assessment of coma and impaired consciousness. A practical scale. Lancet 1974,2:81-84.

Teasdale GM, Murray L. Revisiting the Glasgow Coma Scale and Coma Score. Intensive Care Medicine. 2000; 26: 153-154.

V3 DCModels R1 I1 2010 Sep - Glasgow Coma Scale v0.75 [Internet]. Health Level Seven International. Published Jan 2014. Available at: https://github.com/DetailedClinicalModels/Detailed-Clinical-Models/blob/master/Glasgow%20Coma%20Scale%20(GCS)/org.hl7.GlasgowComaScale(15pointversion)English-v0.75.pdf (accessed 22 Mar 2015)">
		["build_uid"] = <"9b344656-e0c3-4bf3-aaaa-4d18292e74d1">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["MD5-CAM-1.0.1"] = <"F96DF5407AD3C630AB9F3CA94F5886AB">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	OBSERVATION[at0000] matches {	-- Glasgow coma scale
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..1} matches {	-- Point in time
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {3..*; unordered} matches {
									ELEMENT[at0009] matches {	-- Best eye response (E)
										value matches {
											1|[local::at0010], 	-- None
											2|[local::at0011], 	-- To pressure
											3|[local::at0012], 	-- To sound
											4|[local::at0013]  	-- Spontaneous
										}
										null_flavour existence matches {0..1} matches {
											DV_CODED_TEXT matches {
												defining_code matches {[openehr::273]}
											}
										}
									}
									ELEMENT[at0007] matches {	-- Best verbal response (V)
										value matches {
											1|[local::at0014], 	-- None
											2|[local::at0015], 	-- Sounds
											3|[local::at0016], 	-- Words
											4|[local::at0017], 	-- Confused
											5|[local::at0018]  	-- Orientated
										}
										null_flavour existence matches {0..1} matches {
											DV_CODED_TEXT matches {
												defining_code matches {[openehr::273]}
											}
										}
									}
									ELEMENT[at0008] matches {	-- Best motor response (M)
										value matches {
											1|[local::at0019], 	-- None
											2|[local::at0020], 	-- Extension
											3|[local::at0021], 	-- Abnormal flexion
											4|[local::at0022], 	-- Normal flexion
											5|[local::at0023], 	-- Localising
											6|[local::at0024]  	-- Obeys commands
										}
										null_flavour existence matches {0..1} matches {
											DV_CODED_TEXT matches {
												defining_code matches {[openehr::273]}
											}
										}
									}
									ELEMENT[at0026] occurrences matches {0..1} matches {	-- Total score
										value matches {
											DV_COUNT matches {
												magnitude matches {|3..15|}
											}
										}
									}
									ELEMENT[at0030] occurrences matches {0..1} matches {	-- EVM profile
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0037] occurrences matches {0..*} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0040] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0041] occurrences matches {0..*} matches {	-- Confounding factors
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0038] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0039] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["ar-sy"] = <
			items = <
				["at0000"] = <
					text = <"*Glasgow coma scale(en)">
					description = <"*Fifteen point scale used to assess impairment of consciousness in response to defined stimuli. More correctly known as the Modified Glasgow coma scale.(en)">
				>
				["at0001"] = <
					text = <"*Event Series(en)">
					description = <"*@ internal @(en)">
				>
				["at0002"] = <
					text = <"*Point in time(en)">
					description = <"*Specified point in time which may be explicitly defined in a template or at run-time.(en)">
				>
				["at0003"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0007"] = <
					text = <"*Best verbal response (V)(en)">
					description = <"*Best verbal response to test stimulus.(en)">
					comment = <"*Most commonly, the score for verbal response will be selected from one of the ordinal values, however if a response cannot be tested, for example if the subject of care is intubated, then the \"Not Applicable\" null flavour should be recorded. (en)">
				>
				["at0008"] = <
					text = <"*Best motor response (M)(en)">
					description = <"*Best motor response to test stimulus.(en)">
					comment = <"*Most commonly, the score for motor response will be selected from one of the ordinal values, however if a response cannot be tested, for example if the subject of care cannot move their limbs due to injury or paralysis, then the \"Not Applicable\" null flavour should be recorded. (en)">
				>
				["at0009"] = <
					text = <"*Best eye response (E)(en)">
					description = <"*Best response of eyes to test stimulus.(en)">
					comment = <"*Most commonly, the score for eye response will be selected from one of the ordinal values, however if a response cannot be tested, for example if the subject of care cannot physically open their eyes due to other injuries, then the \"Not Applicable\" null flavour should be recorded. (en)">
				>
				["at0010"] = <
					text = <"*None(en)">
					description = <"*No eye opening at any time, no interfering factor. For example: eyes closed by local swelling.(en)">
				>
				["at0011"] = <
					text = <"*To pressure(en)">
					description = <"*Eyes opening after finger tip stimulus.(en)">
				>
				["at0012"] = <
					text = <"*To sound(en)">
					description = <"*Eyes opening after spoken or shouted request. Not to be confused with wakening of a sleeping person.(en)">
				>
				["at0013"] = <
					text = <"*Spontaneous(en)">
					description = <"*Eyes open before stimulus.(en)">
				>
				["at0014"] = <
					text = <"*None(en)">
					description = <"*No audible response, no interfering factor. For example: intubation; profound deafness.(en)">
				>
				["at0015"] = <
					text = <"*Sounds(en)">
					description = <"*Only moans/groans.(en)">
				>
				["at0016"] = <
					text = <"*Words(en)">
					description = <"*Intelligible single words.(en)">
				>
				["at0017"] = <
					text = <"*Confused(en)">
					description = <"*Not orientated but communicates coherently.(en)">
				>
				["at0018"] = <
					text = <"*Orientated(en)">
					description = <"*Correctly gives name, place and date.(en)">
				>
				["at0019"] = <
					text = <"*None(en)">
					description = <"*No movement in arms/legs, no interfering factor. For example: paralysed.(en)">
				>
				["at0020"] = <
					text = <"*Extension(en)">
					description = <"*Decerebrate extension of arms and/or legs in response to stimuli. For example: extends arm at elbow.(en)">
				>
				["at0021"] = <
					text = <"*Abnormal flexion(en)">
					description = <"*Slow, decorticate flexion of arms and/or legs. For example: bends arm at elbow,  but features predominantly abnormal.(en)">
				>
				["at0022"] = <
					text = <"*Normal flexion(en)">
					description = <"*Rapid flexion in response to stimuli but features predominantly normal. For example: flexion of wrist when supra-orbital pressure applied; pulls part of body away when nailbed pinched.(en)">
				>
				["at0023"] = <
					text = <"*Localising(en)">
					description = <"*Purposeful flexion towards painful stimuli. For example: brings hand above the clavicle when supra-orbital pressure is applied.(en)">
				>
				["at0024"] = <
					text = <"*Obeys commands(en)">
					description = <"*Follows verbal request for movement.(en)">
				>
				["at0026"] = <
					text = <"*Total score(en)">
					description = <"*The sum of the ordinal scores recorded for each of the three component responses.(en)">
					comment = <"*The Total Score may be derived as the sum of the three response data elements and, if so, should be validated by the clinical information system against the individual scores entered by the clinician to ensure there is no conflict or inconsistency. Do not report a total score when one or more components are not testable because the score will be artificially low - in this situation record the EVM profile.(en)">
				>
				["at0030"] = <
					text = <"*EVM profile(en)">
					description = <"*Alternative assessment to 'Total Score' where  the Eye Verbal Motor profile is expressed as three discrete components.(en)">
					comment = <"*The EMV Score may be automatically derived as a concatenation of the three response data elements and, if so, should be validated by the clinical information system against the individual scores entered by the clinician to ensure there is no conflict or inconsistency. The 'Not applicable' null flavour value should be recorded as 'NT', for 'not tested' as per http://www.glasgowcomascale.org/.  (en)">
				>
				["at0037"] = <
					text = <"*Comment(en)">
					description = <"*Additional narrative about the measurement of the scale not captured in other fields.(en)">
					comment = <"*Record the reasons for inability to test in the 'Confounding factors' data element.(en)">
				>
				["at0038"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0039"] = <
					text = <"*Extension(en)">
					description = <"*Additional information required to capture local content or to align with other reference models/formalisms.(en)">
					comment = <"*For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.(en)">
				>
				["at0040"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0041"] = <
					text = <"*Confounding factors(en)">
					description = <"*Narrative record of factors that may have contributed to the GCS scores.(en)">
					comment = <"*For example: deafness; aphasia; language issues; use of sedatives; hypothermia; or paralysis, severe facial/eye swelling or tracheal intubation as reasons for inability to record any one of the ordinal E, V or M assessments.(en)">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Glasgow coma scale">
					description = <"Fifteen point scale used to assess impairment of consciousness in response to defined stimuli. More correctly known as the Modified Glasgow coma scale.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Point in time">
					description = <"Specified point in time which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0007"] = <
					text = <"Best verbal response (V)">
					description = <"Best verbal response to test stimulus.">
					comment = <"Most commonly, the score for verbal response will be selected from one of the ordinal values, however if a response cannot be tested, for example if the subject of care is intubated, then the \"Not Applicable\" null flavour should be recorded. ">
				>
				["at0008"] = <
					text = <"Best motor response (M)">
					description = <"Best motor response to test stimulus.">
					comment = <"Most commonly, the score for motor response will be selected from one of the ordinal values, however if a response cannot be tested, for example if the subject of care cannot move their limbs due to injury or paralysis, then the \"Not Applicable\" null flavour should be recorded. ">
				>
				["at0009"] = <
					text = <"Best eye response (E)">
					description = <"Best response of eyes to test stimulus.">
					comment = <"Most commonly, the score for eye response will be selected from one of the ordinal values, however if a response cannot be tested, for example if the subject of care cannot physically open their eyes due to other injuries, then the \"Not Applicable\" null flavour should be recorded. ">
				>
				["at0010"] = <
					text = <"None">
					description = <"No eye opening at any time, no interfering factor. For example: eyes closed by local swelling.">
				>
				["at0011"] = <
					text = <"To pressure">
					description = <"Eyes opening after finger tip stimulus.">
				>
				["at0012"] = <
					text = <"To sound">
					description = <"Eyes opening after spoken or shouted request. Not to be confused with wakening of a sleeping person.">
				>
				["at0013"] = <
					text = <"Spontaneous">
					description = <"Eyes open before stimulus.">
				>
				["at0014"] = <
					text = <"None">
					description = <"No audible response, no interfering factor. For example: intubation; profound deafness.">
				>
				["at0015"] = <
					text = <"Sounds">
					description = <"Only moans/groans.">
				>
				["at0016"] = <
					text = <"Words">
					description = <"Intelligible single words.">
				>
				["at0017"] = <
					text = <"Confused">
					description = <"Not orientated but communicates coherently.">
				>
				["at0018"] = <
					text = <"Orientated">
					description = <"Correctly gives name, place and date.">
				>
				["at0019"] = <
					text = <"None">
					description = <"No movement in arms/legs, no interfering factor. For example: paralysed.">
				>
				["at0020"] = <
					text = <"Extension">
					description = <"Decerebrate extension of arms and/or legs in response to stimuli. For example: extends arm at elbow.">
				>
				["at0021"] = <
					text = <"Abnormal flexion">
					description = <"Slow, decorticate flexion of arms and/or legs. For example: bends arm at elbow,  but features predominantly abnormal.">
				>
				["at0022"] = <
					text = <"Normal flexion">
					description = <"Rapid flexion in response to stimuli but features predominantly normal. For example: flexion of wrist when supra-orbital pressure applied; pulls part of body away when nailbed pinched.">
				>
				["at0023"] = <
					text = <"Localising">
					description = <"Purposeful flexion towards painful stimuli. For example: brings hand above the clavicle when supra-orbital pressure is applied.">
				>
				["at0024"] = <
					text = <"Obeys commands">
					description = <"Follows verbal request for movement.">
				>
				["at0026"] = <
					text = <"Total score">
					description = <"The sum of the ordinal scores recorded for each of the three component responses.">
					comment = <"The Total Score may be derived as the sum of the three response data elements and, if so, should be validated by the clinical information system against the individual scores entered by the clinician to ensure there is no conflict or inconsistency. Do not report a total score when one or more components are not testable because the score will be artificially low - in this situation record the EVM profile.">
				>
				["at0030"] = <
					text = <"EVM profile">
					description = <"Alternative assessment to 'Total Score' where  the Eye Verbal Motor profile is expressed as three discrete components.">
					comment = <"The EMV Score may be automatically derived as a concatenation of the three response data elements and, if so, should be validated by the clinical information system against the individual scores entered by the clinician to ensure there is no conflict or inconsistency. The 'Not applicable' null flavour value should be recorded as 'NT', for 'not tested' as per http://www.glasgowcomascale.org/.  ">
				>
				["at0037"] = <
					text = <"Comment">
					description = <"Additional narrative about the measurement of the scale not captured in other fields.">
					comment = <"Record the reasons for inability to test in the 'Confounding factors' data element.">
				>
				["at0038"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0039"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0040"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0041"] = <
					text = <"Confounding factors">
					description = <"Narrative record of factors that may have contributed to the GCS scores.">
					comment = <"For example: deafness; aphasia; language issues; use of sedatives; hypothermia; or paralysis, severe facial/eye swelling or tracheal intubation as reasons for inability to record any one of the ordinal E, V or M assessments.">
				>
			>
		>
		["pt-br"] = <
			items = <
				["at0000"] = <
					text = <"Escala de Coma de Glasgow">
					description = <"Escala de 15 pontos utilizada para avaliar alterações de consciência relacionadas a determinados estímulos. Melhor descrita como Escala de Coma de Glasgow modificada.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Momento">
					description = <"Momento específico que pode ser definido.">
				>
				["at0003"] = <
					text = <"*Tree(en)">
					description = <"@ internal @">
				>
				["at0007"] = <
					text = <"Melhor Resposta Verbal (V)">
					description = <"Melhor resposta verbal ao estímulo-teste.">
					comment = <"Mais comumente, o score da resposta verbal é selecionado entre valores ordinais, entretanto se uma resposta não puder ser testada, por exemplo se o sujeito do cuidado estiver intubado, então o valor nulo \"Não Aplicável\" deve ser registrado.">
				>
				["at0008"] = <
					text = <"Melhor Resposta Motora (M)">
					description = <"Melhor resposta motora ao estímulo-teste.">
					comment = <"Mais comumente, o score da resposta verbal é selecionado entre valores ordinais, entretanto se uma resposta não puder ser testada, por exemplo se o sujeito do cuidado não puder mexer as pernas em decorrência de uma lesão ou paralisia, então o valor nulo \"Não Aplicável\" deve ser registrado.">
				>
				["at0009"] = <
					text = <"Melhor Resposta Ocular (O)">
					description = <"Melhor resposta ocular ao estímulo-teste.">
					comment = <"Mais comumente, o score da resposta ocular é selecionado entre valores ordinais, entretanto se uma resposta não puder ser testada, por exemplo se o sujeito do cuidado estiver impossibilitado de abrir os olhos por outras lesões, então o valor nulo \"Não Aplicável\" deve ser registrado.">
				>
				["at0010"] = <
					text = <"Ausente">
					description = <"Ausência de abertura ocular em qualquer momento, na ausência de fatores interferentes. Por exemplo: olhos fechados por edema local.">
				>
				["at0011"] = <
					text = <"À pressão">
					description = <"Abertura ocular após estímulo digital.">
				>
				["at0012"] = <
					text = <"A sons">
					description = <"Abertura ocular após solicitação oral. Não confundir com despertar de uma pessoa dormindo.">
				>
				["at0013"] = <
					text = <"Espontânea">
					description = <"Olhos abertos mesmo antes de qualquer estímulo.">
				>
				["at0014"] = <
					text = <"Ausente">
					description = <"Ausência de resposta audível na ausência de fatores interferentes. Por exemplo: intubação orotraqueal ou surdez.">
				>
				["at0015"] = <
					text = <"Sons">
					description = <"Apenas gemidos, grunhidos, sons incompreensíveis.">
				>
				["at0016"] = <
					text = <"Palavras">
					description = <"Palavras inteligíveis.">
				>
				["at0017"] = <
					text = <"Confuso">
					description = <"Desorientado mas comunica-se coerentemente.">
				>
				["at0018"] = <
					text = <"Orientado">
					description = <"Diz o nome, local e data corretamente.">
				>
				["at0019"] = <
					text = <"Ausente">
					description = <"Ausência de movimento de braços e pernas na ausência de fatores interferentes. Por exemplo: paralisia.">
				>
				["at0020"] = <
					text = <"Extensão">
					description = <"Postura em descerebração de braços e pernas em resposta ao estímulo. Extensão do antebraço e cotovelo.">
				>
				["at0021"] = <
					text = <"Flexão anormal">
					description = <"Postura em decorticação, lenta, de braços e/ou pernas. Por exemplo: flexão do cotovelo porém predominantemente anormal.">
				>
				["at0022"] = <
					text = <"Flexão normal">
					description = <"Flexão rápida em resposta ao estímulo, normal. Por exemplo: retirada da mão quando estimulado o leito ungueal.">
				>
				["at0023"] = <
					text = <"Localiza">
					description = <"Flexão organizada em resposta a estímulo doloroso. Por exemplo: traz a mão para tentar retirar a fonte do estímulo.">
				>
				["at0024"] = <
					text = <"Obedece comandos">
					description = <"Segue uma ordem para movimento.">
				>
				["at0026"] = <
					text = <"Score Total">
					description = <"A soma dos pontos referentes a cada componente testado.">
					comment = <"O Score Total pode ser obtido através da soma da resposta dos três elementos, como também, pode ser validado pelos sistemas de informações clínicas contra scores registrados por clínicos para garantir ausência de conflitos ou inconsistências. Não relatar um score total quando um ou mais dos componentes não forem testados porque o score ficará artificialmente baixo - nesta situação registrar o perfil OVM.">
				>
				["at0030"] = <
					text = <"Perfil OVM">
					description = <"Alternativa ao 'Score Total' em que o perfil Ocular Verbal Motor é expresso em três componentes.">
					comment = <"O Score OVM pode ser derivado automaticamente através da concatenação das respostas de cada elemento; neste contexto deve ser validado pelos sistemas de informações clínicas contra scores registrados por clínicos para garantir ausência de conflitos ou inconsistências. O valor nulo 'Não Aplicável' deve ser registrado como 'NT', para 'não testado' como sugerido em http://www.glasgowcomascale.org/.">
				>
				["at0037"] = <
					text = <"Comentários">
					description = <"Narrativa adicional sobre a aquisição do score não pertinente a outros campos.">
					comment = <"Registrar as razões para a impossibilidade de testar no elemento 'Fatores de confusão'.">
				>
				["at0038"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0039"] = <
					text = <"Extensão">
					description = <"Informações adicionais necessárias para alinhar com outros modelos/formalismo de referência.">
					comment = <"Por exemplo: informações requeridas localmente ou metadados adicionais para alinhar com equivalentes a FHIR ou CIMI.">
				>
				["at0040"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0041"] = <
					text = <"Fatores de confusão">
					description = <"Relato de fatores que podem ter contribuído para o score.">
					comment = <"Por exemplo: surdez, afasia, questões de linguagem, uso de sedativos, hipotermia, paralisia, edema facial ou periorbitário grave ou intubação oro-traqueal como razões de impossibilidade de registrar um dos valores ordinais nos domínios O, V ou M.">
				>
			>
		>
	>

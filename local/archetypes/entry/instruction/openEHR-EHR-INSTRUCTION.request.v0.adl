archetype (adl_version=1.4; uid=a14624d5-2176-47b7-a923-34c016c2e53d)
	openEHR-EHR-INSTRUCTION.request.v0

concept
	[at0000]	-- Service request
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Lars Bitsch-Larsen">
				["organisation"] = <"Haukeland University Hospital of Bergen, Norway">
			>
			accreditation = <"MD, DEAA, MBA, spec in anesthesia, spec in tropical medicine.">
		>
	>
description
	original_author = <
		["name"] = <"Dr Ian McNicoll">
		["organisation"] = <"Ocean Informatics, United Kingdom">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"2009-12-08">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"Generisk mønster for rekvirering av en hvilken som helst helsetjeneste.">
			use = <"Brukes for å registrere en generell rekvirering/bestilling av helsetjenester. Det lages spesialiserte arketyper for vanlige rekvirerbare tjenester basert på denne arketypen, f.eks. laboratorierekvisisjon eller henvisning.">
			keywords = <"rekvisisjon", "bestilling", "foreskriving", "tjeneste", "tjenesteyter", "rekvirere", "bestille", "anmodning", "forespørre", "forespørsel", "anmode", "tilsyn">
			misuse = <"Skal ikke brukes for rekvisisjoner der det eksisterer eksplisitte spesialiseringer, f.eks:
- Laboratorierekvisisjon - bruk arketypen INSTRUCTION.request-laboratory_test
- Rekvisisjon av billeddiagnostikk - bruk arketypen INSTRUCTION.request-imaging_exam
- Prosedyrerekvisisjon - bruk arketypen INSTRUCTION.request-procedure
- Henvisning - bruk arketypen INSTRUCTION.request-referral.">
			copyright = <"© Nasjonal IKT HF">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Generic request framework for a health-related service or activity to be delivered by a clinician, organisation or agency.">
			use = <"Use to record a request for a single health-related service or a well-defined package of services that have identical attributes. If multiple or a group of services are ordered at the same time but with different attributes, each services should be represented using a separate instance of this archetype.

This archetype has been designed as a framework that can be used as the basis for:
- a request from one clinician, organisation or agency to another clinician, organisation or agency for a health-related service. For example: a referral to a specialist clinician for treatment or a second clinical opinion; transfer of care to an emergency department; four hourly vital signs monitoring; and provision of home services from a municipal council; or
- a request for a follow up service to be scheduled for the same clinician, organisation or agency. For example: a review appointment in outpatients in 6 weeks. 

Clinical use cases:
- consider a clinician ordering a followup appointment in 6 weeks. 'Followup appointment' will be the 'Service name'. If they enter '6 weeks' as the proposed timing for the appointment in the User Interface, the clinical system will record the date six weeks from today in the 'Service due' data element.
- consider a clinician ordering Diabetes Education as the 'Service name'. The values for 'Reason for request' may be  'New diagnosis' and 'Prevention of Ketoacidosis'. The 'Clinical indication' will be 'Diabetes Type 1', which may be linked to the Problem Diagnosis and/or Laboratory test results. If a 4 week course is required then the more complex timing will be recorded using the CLUSTER.timing_nondaily archetype - weekly intervals for 4 services.
- consider a clinician ordering a recurring blood test, such as an INR. The complex timing for this requires use of the CLUSTER.service_direction and associated CLUSTER.timing_nondaily archetype to define each timing in a sequence of tests, such as 'daily for one week, weekly for 4 weeks, monthly for 6 months'.

In many situations it will be possible to record the steps that occur as part of this request being carried out using the corresponding generic ACTION.service. However, there will be many occasions where the required ACTION archetype will be very specific for purpose, as the data requirements for recording provision of many health-related services will need quite unique data elements, recording patterns or pathway steps. For example: ACTION.screening or ACTION.health_education.">
			keywords = <"request", "order", "service", "provide", "referral">
			misuse = <"Not to be used for requests which have a specific specialisation - for example:
- Laboratory test request - use the INSTRUCTION.request-laboratory_test archetype;
- Imaging examination request - use the INSTRUCTION.request-imaging_exam archetype; and
- Procedure request - use the INSTRUCTION.request-procedure archetype.">
			copyright = <"© openEHR Foundation, Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Fatima Almeida, Critical SW, Portugal", "Tomas Alme, DIPS ASA, Norway", "Vebjørn Arntzen, Oslo University Hospital, Norway", "Koray Atalag, University of Auckland, New Zealand", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (openEHR Editor)", "Lars Bitsch-Larsen, Haukeland University hospital, Norway", "Anita Bjørnnes, Helse Bergen, Norway", "Lisbeth Dahlhaug, Helse Midt - Norge IT, Norway", "Einar Fosse, UNN HF, Norwegian Centre for Integrated Care and Telemedicine, Norway", "Hildegard Franke, freshEHR Clinical Informatics Ltd., United Kingdom", "Heather Grain, Llewelyn Grain Informatics, Australia", "Knut Harboe, Stavanger Universitetssjukehus, Norway", "Ingrid Heitmann, Oslo universitetssykehus HF, Norway", "Andreas Hering, Helse Bergen HF, Haukeland universitetssjukehus, Norway", "Anca Heyd, DIPS ASA, Norway", "Hilde Hollås, Norway", "Evelyn Hovenga, EJSH Consulting, Australia", "Lars Ivar Mehlum, Helse Bergen HF, Norway", "Lars Karlsen, DIPS ASA, Norway", "Lars Morgan Karlsen, DIPS ASA, Norway", "Shinji Kobayashi, Kyoto University, Japan", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Hallvard Lærum, Oslo Universitetssykehus HF, Norway", "Hildegard McNicoll, freshEHR Clinical Informatics Ltd., United Kingdom", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Bjørn Næss, DIPS ASA, Norway", "Andrej Orel, Marand d.o.o., Slovenia", "Anne Pauline Anderssen, Helse Nord RHF, Norway", "Pablo Pazos, CaboLabs.com Health Informatics, Uruguay", "Rune Pedersen, Universitetssykehuset i Nord Norge, Norway", "Jussara Rotzsch, UNB, Brazil", "Line Sæle, Nasjonal IKT HF, Norway", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Editor)", "Richard Townley-O'Neill, Australian Digital Health Agency, Australia">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"DB1F47798CF40C5FFCC7B89B01EE232F">
		["build_uid"] = <"c905ead9-cbee-4200-a94a-1416829555f7">
		["revision"] = <"0.0.1-alpha">
	>

definition
	INSTRUCTION[at0000] matches {	-- Service request
		activities cardinality matches {0..*; unordered} matches {
			ACTIVITY[at0001] occurrences matches {1..*} matches {	-- Current Activity
				description matches {
					ITEM_TREE[at0009] matches {	-- Tree
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0121] matches {	-- Service name
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0148] occurrences matches {0..1} matches {	-- Service type
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0153] occurrences matches {0..1} matches {	-- Initial request
								value matches {
									DV_BOOLEAN matches {
										value matches {True}
									}
								}
							}
							ELEMENT[at0135] occurrences matches {0..1} matches {	-- Description
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0062] occurrences matches {0..*} matches {	-- Reason for request
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0064] occurrences matches {0..1} matches {	-- Reason description
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0152] occurrences matches {0..1} matches {	-- Clinical indication
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0065] occurrences matches {0..*} matches {	-- Intent
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0068] occurrences matches {0..1} matches {	-- Urgency
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0136, 	-- Emergency
											at0137, 	-- Urgent
											at0138]	-- Routine
										}
									}
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0040] occurrences matches {0..1} matches {	-- Service due
								value matches {
									DV_DATE_TIME matches {*}
									DV_INTERVAL<DV_DATE_TIME> matches {
										upper matches {
											DV_DATE_TIME matches {*}
										}
										lower matches {
											DV_DATE_TIME matches {*}
										}
									}
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0151] occurrences matches {0..*} matches {	-- Complex timing
								include
									archetype_id/value matches {/openEHR-EHR-CLUSTER\.service_direction(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.timing_daily(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.timing_nondaily(-[a-zA-Z0-9_]+)*\.v1/}
							}
							ELEMENT[at0145] occurrences matches {0..1} matches {	-- Service period start
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0144] occurrences matches {0..1} matches {	-- Service period expiry
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0147] occurrences matches {0..1} matches {	-- Indefinite?
								value matches {
									DV_BOOLEAN matches {
										value matches {True}
									}
								}
							}
							allow_archetype CLUSTER[at0132] occurrences matches {0..*} matches {	-- Specific details
								include
									archetype_id/value matches {/.*/}
							}
							allow_archetype CLUSTER[at0149] occurrences matches {0..*} matches {	-- Supporting information
								include
									archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
							}
							ELEMENT[at0076] occurrences matches {0..1} matches {	-- Supplementary information
								value matches {
									DV_BOOLEAN matches {
										value matches {True}
									}
								}
							}
							ELEMENT[at0078] occurrences matches {0..1} matches {	-- Information description
								value matches {
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0116] occurrences matches {0..*} matches {	-- Patient requirements
								include
									archetype_id/value matches {/.*/}
							}
							ELEMENT[at0150] occurrences matches {0..1} matches {	-- Comment
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0008] matches {	-- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0010] occurrences matches {0..1} matches {	-- Requester order identifier
						value matches {
							DV_TEXT matches {*}
							DV_IDENTIFIER matches {*}
						}
					}
					allow_archetype CLUSTER[at0141] occurrences matches {0..1} matches {	-- Requester
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0011] occurrences matches {0..1} matches {	-- Receiver order identifier
						value matches {
							DV_TEXT matches {*}
							DV_IDENTIFIER matches {*}
						}
					}
					allow_archetype CLUSTER[at0142] occurrences matches {0..1} matches {	-- Receiver
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0127] occurrences matches {0..1} matches {	-- Request status
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0128] occurrences matches {0..*} matches {	-- Distribution list
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.distribution\.v1/}
					}
					allow_archetype CLUSTER[at0112] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Service request">
					description = <"Request for a health-related service or activity to be delivered by a clinician, organisation or agency.">
				>
				["at0001"] = <
					text = <"Current Activity">
					description = <"Current Activity.">
				>
				["at0008"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0010"] = <
					text = <"Requester order identifier">
					description = <"The local identifier assigned by the requesting clinical system.">
					comment = <"Usually equivalent to the HL7 Placer Order Identifier.">
				>
				["at0011"] = <
					text = <"Receiver order identifier">
					description = <"The local identifier assigned to the request by the clinician or organisation receiving the request for service.">
					comment = <"Usually equivalent to the HL7 Filler Order Identifier.">
				>
				["at0040"] = <
					text = <"Service due">
					description = <"The date/time, or acceptable interval of date/time, for provision of the service.">
					comment = <"This data element allows for recording of the timing for a single service, either as a date and time, a date ranges or a text descriptor which can allow for 'next available. In practice, clinicians will often think in terms of ordering services as approximate timing, for example: review in 3 months, 6 months or 12 months. As clinical systems need more exact parameters to operate on, this '3 months' will usually be converted to an exact date 3 months from the date of recording and stored using this data element. If complex timing or sequences of timings are required, use the CLUSTER.service_direction archetype within the 'Complex timing' SLOT and this data element becomes redundant.">
				>
				["at0062"] = <
					text = <"Reason for request">
					description = <"A short phrase describing the reason for the request.">
					comment = <"Coding of the 'Reason for request' with a coding system is desirable, if available. This data element allows multiple occurrences to enable the user to record a multiple responses, if required.">
				>
				["at0064"] = <
					text = <"Reason description">
					description = <"Narrative description about the reason for request.">
				>
				["at0065"] = <
					text = <"Intent">
					description = <"Description of the intent for the request.">
					comment = <"For example: a referral to a specialist may have the intent of the specialist taking over responsibility for care of the patient, or it may be to provide a second opinion on treatment options. Coding of the 'Intent' with a coding system is desirable, if available. This data element allows multiple occurrences to enable the user to record a multiple responses, if required.">
				>
				["at0068"] = <
					text = <"Urgency">
					description = <"Urgency of the request for service.">
					comment = <"Specific definitions of emergency and urgent will vary between clinical contexts, clinical systems and the nature of the request itself, so have not been defined in this archetype. If explicit timing is required then the Service period should be clearly stated.">
				>
				["at0076"] = <
					text = <"Supplementary information">
					description = <"Supplementary information will be following request.">
					comment = <"Record as TRUE if additional information has been identified and will be forwarded when available. For example: pending test results.">
				>
				["at0078"] = <
					text = <"Information description">
					description = <"Description of the supplementary information.">
				>
				["at0112"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0116"] = <
					text = <"Patient requirements">
					description = <"Language, transport or other personal requirements to support the patient's attendance or participation in provision of the service.">
				>
				["at0121"] = <
					text = <"Service name">
					description = <"Identification of the single service or activity requested, by name.">
					comment = <"Coding of the 'Service name' with a coding system is desirable, if available.">
				>
				["at0127"] = <
					text = <"Request status">
					description = <"The status of the request for service as indicated by the requester.">
					comment = <"Status is used to denote whether this is the initial request, or a follow-up request to change or provide supplementary information.">
				>
				["at0128"] = <
					text = <"Distribution list">
					description = <"Details of additional clinicians, organisations or agencies who require copies of any communication.">
				>
				["at0132"] = <
					text = <"Specific details">
					description = <"Additional detail about the service requested.">
					comment = <"Example: CLUSTER archetype specifying complex timing requirements.">
				>
				["at0135"] = <
					text = <"Description">
					description = <"Narrative description about the service requested.">
					comment = <"This data point should be used to describe the named service in more detail, including how it should be delivered, patient concerns and issues that might be encountered in delivering the service.">
				>
				["at0136"] = <
					text = <"Emergency">
					description = <"The request requires immediate attention.">
				>
				["at0137"] = <
					text = <"Urgent">
					description = <"The request requires prioritised attention.">
				>
				["at0138"] = <
					text = <"Routine">
					description = <"The request does not require prioritised scheduling.">
				>
				["at0141"] = <
					text = <"Requester">
					description = <"Details about the clinician or organisation requesting the service.">
				>
				["at0142"] = <
					text = <"Receiver">
					description = <"Details about the clinician or organisation receiving the request for service.">
				>
				["at0144"] = <
					text = <"Service period expiry">
					description = <"The date/time that marks the conclusion of the clinically valid period of time for delivery of this service.">
					comment = <"This date/time is the equivalent to the latest possible date for service delivery or to the date of expiry for this request. For example: a service may be required to be completed before another event, such as scheduled surgery.">
				>
				["at0145"] = <
					text = <"Service period start">
					description = <"The date/time that marks the beginning of the valid period of time for delivery of this service.">
					comment = <"This date/time is the equivalent to the earliest possible date for service delivery. For example: sometimes a certain amount of time must pass before a service can be performed, for example some procedures can only be performed once the patient has stopped taking medications for a specific amount of time.">
				>
				["at0147"] = <
					text = <"Indefinite?">
					description = <"The valid period for this request is open ended and has no date of expiry.">
					comment = <"Record as TRUE to record explicity that the request has no expiry date.">
				>
				["at0148"] = <
					text = <"Service type">
					description = <"Category of service requested.">
					comment = <"For example: hospital or home care delivery. Coding of the 'Service type' with a coding system is desirable, if available. If the 'Service name' was coded, it is possible for this data point to be derived from the code.">
				>
				["at0149"] = <
					text = <"Supporting information">
					description = <"Digital document, image, video or diagram supplied as additional information to support or inform the request.">
				>
				["at0150"] = <
					text = <"Comment">
					description = <"Additional narrative about the service request not captured in other fields.">
				>
				["at0151"] = <
					text = <"Complex timing">
					description = <"Details about a complex service request requiring a sequence of timings.">
					comment = <"For example: 'hourly vital signs observations for 4 hours, then 4 hourly for 20 hours' or 'every third Wednesday for 3 visits' or .">
				>
				["at0152"] = <
					text = <"Clinical indication">
					description = <"The clinical reason for the ordered service.">
					comment = <"For example: 'Angina' or 'Type 1 Diabetes mellitus'. Coding of the clinical indication with a terminology is preferred, where possible. This data element allows multiple occurrences.">
				>
				["at0153"] = <
					text = <"Initial request">
					description = <"First ever request for the specified service.">
					comment = <"Record as TRUE if this is the first ever request for this service. This data element may be used by some systems to support automatic scheduling of appointments.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"*Service request(en)">
					description = <"*Request for a health-related service to be delivered by a healthcare provider or agency.(en)">
				>
				["at0001"] = <
					text = <"*Current Activity(en)">
					description = <"*Current Activity(en)">
				>
				["at0008"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0010"] = <
					text = <"Rekvisisjonsidentifikator">
					description = <"Rekvisisjonens identifikator, tilordnet av den som rekvirererer tjenesten. Det kan være helsepersonell, organisasjon eller applikasjonen.">
				>
				["at0011"] = <
					text = <"Mottakers rekvisisjonsidentifikator">
					description = <"Rekvisisjonens identifikator, tilordnet av den som mottar rekvisisjonen. Mottaker kan være helsepersonell, organisasjon eller applikasjonen.">
				>
				["at0040"] = <
					text = <"*Service due(en)">
					description = <"*The date/time, or acceptable interval of date/time, for provision of the service.(en)">
					comment = <"*In practice, clinicians will often think in terms of ordering services as approximate timing, for example: review in 3 months, 6 months or 12 months. As clinical systems need more exact parameters to operate on, this '3 months' will usually be converted to an exact date 3 months from the date of recording and stored using this data element.(en)">
				>
				["at0062"] = <
					text = <"*Reason for request(en)">
					description = <"*A short phrase describing the reason for the request.(en)">
					comment = <"*Coding of the 'Reason for request' with a coding system is desirable, if available. This data element allows multiple occurrences to enable the user to record a multiple responses, if required.(en)">
				>
				["at0064"] = <
					text = <"*Reason description(en)">
					description = <"*Narrative description about the reason for request.(en)">
				>
				["at0065"] = <
					text = <"*Intent(en)">
					description = <"*Description of the intent for the request.(en)">
					comment = <"*For example: a referral to a specialist may have the intent of the specialist taking over responsibility for care of the patient, or it may be to provide a second opinion on treatment options. Coding of the 'Intent' with a coding system is desirable, if available. This data element allows multiple occurrences to enable the user to record a multiple responses, if required.(en)">
				>
				["at0068"] = <
					text = <"*Urgency(en)">
					description = <"*Urgency of the request for service.(en)">
					comment = <"*Specific definitions of emergency and urgent will vary between clinical contexts, clinical systems and the nature of the request itself, so have not been defined in this archetype. If explicit timing is required then the Service period should be clearly stated.(en)">
				>
				["at0076"] = <
					text = <"*Information to follow?(en)">
					description = <"*Will supplementary information be following this request?(en)">
					comment = <"*Record as TRUE if additional information has been identified and will be forwarded when available. For example: pending test results.(en)">
				>
				["at0078"] = <
					text = <"*Information description(en)">
					description = <"*Description of the supplementary information.(en)">
				>
				["at0112"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som er nødvendig for å registrere lokalt innhold/kontekst, eller for å sammenstille med andre referansemodeller/formalismer.">
					comment = <"F.eks lokale informasjonskrav eller ekstra metadata for å tilpasse med FHIR eller CIMI ekvivalenter.">
				>
				["at0116"] = <
					text = <"Pasientens behov">
					description = <"Språk, transport eller andre personlige behov som er nødvendige for å sikre pasientens oppmøte eller deltakelse i utførelsen av den rekvirerte tjeneste.">
				>
				["at0121"] = <
					text = <"*Service name(en)">
					description = <"*Identification of the single service requested, by name.(en)">
					comment = <"*Coding of the 'Service name' with a coding system is desirable, if available.(en)">
				>
				["at0127"] = <
					text = <"Rekvisisjonsstatus">
					description = <"Status for rekvisisjonen oppgitt av rekvirenten. Status brukes for å vise om dette er den primære rekvisisjonen, en endring eller supplerende informasjon.">
				>
				["at0128"] = <
					text = <"Svarmottakere">
					description = <"En liste over personer eller organisasjoner som bør motta svar på rekvisisjonen.">
				>
				["at0132"] = <
					text = <"*Specific details(en)">
					description = <"*Additional detail about the service requested.(en)">
					comment = <"*Example: CLUSTER archetype specifying complex timing requirements.(en)">
				>
				["at0135"] = <
					text = <"*Description(en)">
					description = <"*Narrative description about the service requested.(en)">
					comment = <"*This data point should be used to describe the named service in more detail, including how it should be delivered, patient concerns and issues that might be encountered in delivering the service.(en)">
				>
				["at0136"] = <
					text = <"*Emergency(en)">
					description = <"*The request requires immediate attention.(en)">
				>
				["at0137"] = <
					text = <"*Urgent(en)">
					description = <"*The request requires prioritised attention.(en)">
				>
				["at0138"] = <
					text = <"*Routine(en)">
					description = <"*The request does not require prioritised scheduling.(en)">
				>
				["at0141"] = <
					text = <"Rekvirent">
					description = <"Detaljer om helsepersonellet eller organisasjonen som har rekvirert prosedyren.">
				>
				["at0142"] = <
					text = <"Mottaker">
					description = <"Detaljer om helsepersonellet eller organisasjonen som mottar prosedyrerekvisisjonen.">
				>
				["at0144"] = <
					text = <"*Service period expiry(en)">
					description = <"*The date/time that marks the conclusion of the valid period of time for delivery of this service.(en)">
					comment = <"*This date/time is the equivalent to the latest possible date for service delivery or to the date of expiry for this request. For example: a service may be required to be completed before another event, such as scheduled surgery.(en)">
				>
				["at0145"] = <
					text = <"*Service period start(en)">
					description = <"*The date/time that marks the beginning of the valid period of time for delivery of this service.(en)">
					comment = <"*This date/time is the equivalent to the earliest possible date for service delivery. For example: sometimes a certain amount of time must pass before a service can be performed, for example some procedures can only be performed once the patient has stopped taking medications for a specific amount of time.(en)">
				>
				["at0147"] = <
					text = <"*Indefinite?(en)">
					description = <"*The valid period for this request is open ended and has no date of expiry.(en)">
					comment = <"*Record as TRUE to record explicity that the request has no expiry date.(en)">
				>
				["at0148"] = <
					text = <"*Type(en)">
					description = <"*Category of service requested.(en)">
					comment = <"*For example: hospital or home care delivery. Coding of the 'Service type' with a coding system is desirable, if available. If the 'Service name' was coded, it is possible for this data point to be derived from the code.(en)">
				>
				["at0149"] = <
					text = <"*Attachment(en)">
					description = <"*Digital document, image, video or diagram as supporting information for the request.(en)">
				>
				["at0150"] = <
					text = <"*Comment(en)">
					description = <"*Additional narrative about the service request not captured in other fields.(en)">
				>
				["at0151"] = <
					text = <"*Service direction(en)">
					description = <"**(en)">
				>
				["at0152"] = <
					text = <"*New element(en)">
					description = <"**(en)">
				>
				["at0153"] = <
					text = <"*New element(en)">
					description = <"**(en)">
				>
			>
		>
	>

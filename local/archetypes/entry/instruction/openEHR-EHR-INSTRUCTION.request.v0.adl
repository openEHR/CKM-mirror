archetype (adl_version=1.4; uid=a14624d5-2176-47b7-a923-34c016c2e53d)
	openEHR-EHR-INSTRUCTION.request.v0

concept
	[at0000]	-- Healthcare service request
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Lars Bitsch-Larsen">
				["organisation"] = <"Haukeland University Hospital of Bergen, Norway">
			>
			accreditation = <"MD, DEAA, MBA, spec in anesthesia, spec in tropical medicine.">
		>
	>
description
	original_author = <
		["name"] = <"Dr Ian McNicoll">
		["organisation"] = <"Ocean Informatics, United Kingdom">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"2009-12-08">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Generic pattern for a request for any or all healthcare services.">
			use = <"Use to record a request for any or all healthcare services. Commonly used requests may be specialised and based on this archetype, for example lab requests or referrals.">
			keywords = <"request", "refer", "order", "service", "provide">
			misuse = <"Not to be used for requests for which an explicit specialisation exists - for example:
- Laboratory test request - use the INSTRUCTION.request-laboratory_test archetype;
- Imaging examination request - use the INSTRUCTION.request-imaging_exam archetype;
- Procedure request - use the INSTRUCTION.request-procedure archetype; and 
- Referral request - use the INSTRUCTION.request-referral archetype.">
			copyright = <"© Nasjonal IKT HF">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"Generisk mønster for rekvirering av en hvilken som helst helsetjeneste.">
			use = <"Brukes for å registrere en generell rekvirering/bestilling av helsetjenester. Det lages spesialiserte arketyper for vanlige rekvirerbare tjenester basert på denne arketypen, f.eks. laboratorierekvisisjon eller henvisning.">
			keywords = <"rekvisisjon", "bestilling", "foreskriving", "tjeneste", "tjenesteyter", "rekvirere", "bestille", "anmodning", "forespørre", "forespørsel", "anmode", "tilsyn">
			misuse = <"Skal ikke brukes for rekvisisjoner der det eksisterer eksplisitte spesialiseringer, f.eks:
- Laboratorierekvisisjon - bruk arketypen INSTRUCTION.request-laboratory_test
- Rekvisisjon av billeddiagnostikk - bruk arketypen INSTRUCTION.request-imaging_exam
- Prosedyrerekvisisjon - bruk arketypen INSTRUCTION.request-procedure
- Henvisning - bruk arketypen INSTRUCTION.request-referral.">
			copyright = <"© Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Tomas Alme, DIPS ASA, Norway", "Anne Pauline Anderssen, Helse Nord RHF, Norway", "Vebjørn Arntzen, Oslo universitetssykehus HF, Norway", "Silje Ljosland Bakke, Helse Bergen HF, Norway (Editor)", "Lars Bitsch-Larsen, Haukeland University Hospital, Bergen, Norway", "Anita Bjørnnes, Helse Bergen, Norway", "Lisbeth Dahlhaug, Helse Midt - Norge IT, Norway", "Einar Fosse, UNN HF, Norwegian Centre for Integrated Care and Telemedicine, Norway", "Knut Harboe, Stavanger Universitetssjukehus, Norway", "Ingrid Heitmann, Oslo universitetssykehus HF, Norway", "Andreas Hering, Helse Bergen HF, Haukeland universitetssjukehus, Norway", "Anca Heyd, DIPS ASA, Norway", "Hilde Hollås, Norway", "Lars Morgan Karlsen, DIPS ASA, Norway", "Heather Leslie, Ocean Informatics, Australia", "Hallvard Lærum, Oslo Universitetssykehus HF, Norway", "Lars Ivar Mehlum, Helse Bergen HF, Norway", "Bjørn Næss, DIPS ASA, Norway", "Rune Pedersen, Universitetssykehuset i Nord Norge, Norway", "Line Sæle, Nasjonal IKT HF, Norway", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["revision"] = <"0.0.1-alpha">
		["build_uid"] = <"b5dd31cd-b8ba-4568-9a26-a6e740fbfe00">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["MD5-CAM-1.0.1"] = <"B132E0B61C1C9DDF5CA9E712845ECE34">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	INSTRUCTION[at0000] matches {	-- Helsetjenesterekvirering
		activities cardinality matches {0..*; unordered} matches {
			ACTIVITY[at0001] occurrences matches {1..*} matches {	-- Rekvirering
				description matches {
					ITEM_TREE[at0009] matches {	-- Tree
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0121] matches {	-- Tjenestenavn
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0135] occurrences matches {0..1} matches {	-- Beskrivelse
								value matches {
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0132] occurrences matches {0..*} matches {	-- Spesifikke detaljer
								include
									archetype_id/value matches {/.*/}
							}
							ELEMENT[at0062] occurrences matches {0..1} matches {	-- Rekvireringsårsak
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0064] occurrences matches {0..1} matches {	-- Årsaksbeskrivelse
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0065] occurrences matches {0..1} matches {	-- Hensikt
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0068] occurrences matches {0..1} matches {	-- Hastegrad
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0136, 	-- Akutt
											at0137, 	-- Haster
											at0138]	-- Rutine
										}
									}
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0040] occurrences matches {0..1} matches {	-- Dato/tid for fullført tjeneste
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0146] occurrences matches {0..1} matches {	-- Dato/tid for påbegynt tjeneste
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0144] occurrences matches {0..1} matches {	-- Seneste dato for rekvirert tjeneste
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0145] occurrences matches {0..1} matches {	-- Tidligste dato for rekvirert tjeneste
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0076] occurrences matches {0..1} matches {	-- Supplerende informasjon følger
								value matches {
									DV_BOOLEAN matches {
										value matches {True}
									}
								}
							}
							ELEMENT[at0078] occurrences matches {0..1} matches {	-- Detaljer om følgende supplerende informasjon
								value matches {
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0116] occurrences matches {0..*} matches {	-- Pasientens behov
								include
									archetype_id/value matches {/.*/}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0008] matches {	-- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0010] occurrences matches {0..1} matches {	-- Rekvisisjonsidentifikator
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0141] occurrences matches {0..*} matches {	-- Rekvirent
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0011] occurrences matches {0..1} matches {	-- Mottakers rekvisisjonsidentifikator
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0142] occurrences matches {0..*} matches {	-- Mottaker
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0127] occurrences matches {0..1} matches {	-- Rekvisisjonsstatus
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0128] occurrences matches {0..*} matches {	-- Svarmottakere
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.distribution\.v1/}
					}
					allow_archetype CLUSTER[at0112] occurrences matches {0..*} matches {	-- Tilleggsinformasjon
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Healthcare service request">
					description = <"Request for a healthcare service to be supplied by another healthcare provider or support agency.">
					comment = <"For example equipment request.">
				>
				["at0001"] = <
					text = <"Request">
					description = <"Description of the requested service.">
				>
				["at0008"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0010"] = <
					text = <"Requestor Identifier">
					description = <"The local ID assigned to the order by the healthcare provider or organisation requesting the service. This is also referred to as Placer Order Identifier.">
				>
				["at0011"] = <
					text = <"Receiver identifier">
					description = <"The ID assigned to the order by the healthcare provider or organisation receiving the request for service. This is also referred to as Filler Order Identifier.">
				>
				["at0040"] = <
					text = <"Date &/or time for completed service">
					description = <"The date and time that the service should be performed or completed.">
					comment = <"In practice, clinicians will often think in terms of ordering services in inexact terms, for example: review in 3 months, 6 months or 12 months. As clinical systems need more exact parameters to operate on, this '3 month' will be converted to an exact date 3 months from the date of recording and stored using this data element.">
				>
				["at0062"] = <
					text = <"Reason for request">
					description = <"A short description of the reason for the request.">
					comment = <"Coding of the 'Reason for request' with a coding system is desirable, if available.">
				>
				["at0064"] = <
					text = <"Reason description">
					description = <"A narrative description explaining the reason for request.">
				>
				["at0065"] = <
					text = <"Intent">
					description = <"The requestor's intention as to what they wish to achieve by the request.">
					comment = <"For example a referral with the intent of having specialist care take over the care of the patient, or advice on how to proceed with an investigation or treatment.">
				>
				["at0068"] = <
					text = <"Urgency">
					description = <"Urgency of the request.">
				>
				["at0076"] = <
					text = <"Supplementary information to follow">
					description = <"True indicates that additional information has been identified and will be forwarded when available eg incomplete pathology test results.">
				>
				["at0078"] = <
					text = <"Details of expected supplementary information">
					description = <"Details of the nature of supplementary information that is to follow e.g name of laboratory results.">
				>
				["at0112"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0116"] = <
					text = <"Patient requirements">
					description = <"Language, transport or other personal requirements to support the patient's attendance or participation in provision of the service.">
				>
				["at0121"] = <
					text = <"Service name">
					description = <"Identification of the service requested, by name.">
					comment = <"Coding of the 'Service name' with a coding system is desirable, if available.">
				>
				["at0127"] = <
					text = <"Request status">
					description = <"The status of the request for service as indicated by the requester. 
Status is used to denote whether this is the initial request, or a follow-up request to change or provide supplementary information.">
				>
				["at0128"] = <
					text = <"Distribution list for response">
					description = <"A list of person's or organisation who should receive copies of any communication.">
				>
				["at0132"] = <
					text = <"Specific details">
					description = <"Structured detail defining the service requested.">
					comment = <"Example: CLUSTER archetype specifying the specific catheter that is to be inserted.">
				>
				["at0135"] = <
					text = <"Description">
					description = <"Narrative description of the service requested.">
				>
				["at0136"] = <
					text = <"Emergency">
					description = <"The request is an emergency.">
				>
				["at0137"] = <
					text = <"Urgent">
					description = <"The request is urgent.">
				>
				["at0138"] = <
					text = <"Routine">
					description = <"The request is routine.">
				>
				["at0141"] = <
					text = <"Requestor">
					description = <"Details about the healthcare provider or organisation requesting the service.">
				>
				["at0142"] = <
					text = <"Receiver">
					description = <"Details about the healthcare provider or organisation receiving the request for service.">
				>
				["at0144"] = <
					text = <"Latest date service required">
					description = <"The latest date that is acceptable for the service to be completed.">
					comment = <"For example: a service may be required to be completed before another event, such as scheduled surgery.">
				>
				["at0145"] = <
					text = <"Earliest date service required">
					description = <"The earliest date that is acceptable for the service to be started.">
					comment = <"Somtimes a certain amount of time must pass before a service can be performed, for example some gastroscopies can only be performed once the patient has stopped taking certain medications for a set amount of time.">
				>
				["at0146"] = <
					text = <"Date &/or time for started service">
					description = <"The date and time that the service should be started.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Helsetjenesterekvirering">
					description = <"Forespørsel om utførelse av en helsetjeneste, til annet helsepersonell eller andre organisasjoner.">
					comment = <"For eksempel en forespørsel om postoperative observasjoner, eller en forespørsel om hjemmetjenester etter utskriving.">
				>
				["at0001"] = <
					text = <"Rekvirering">
					description = <"Beskrivelse av tjenesten det rekvireres til.">
				>
				["at0008"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0010"] = <
					text = <"Rekvisisjonsidentifikator">
					description = <"Rekvisisjonens identifikator, tilordnet av den som rekvirererer tjenesten. Det kan være helsepersonell, organisasjon eller applikasjonen.">
				>
				["at0011"] = <
					text = <"Mottakers rekvisisjonsidentifikator">
					description = <"Rekvisisjonens identifikator, tilordnet av den som mottar rekvisisjonen. Mottaker kan være helsepersonell, organisasjon eller applikasjonen.">
				>
				["at0040"] = <
					text = <"Dato/tid for fullført tjeneste">
					description = <"Dato/tid for når tjenesten bør være utført eller avsluttet.">
					comment = <"I praksis vil klinikere ofte rekvirere tjenester med ikke-eksakte tidsangivelser, f.eks. kontroll om 3 måneder, 6 måneder eller 12 måneder. Kliniske systemer krever mer eksakte parametre å jobbe med, og \"3 mnd\" vil konverteres til en eksakt dato 3 måneder fra registreringsdatoen, og lagres i dette dataelementet.">
				>
				["at0062"] = <
					text = <"Rekvireringsårsak">
					description = <"En kort beskrivelse av årsaken eller indikasjon for rekvireringen.">
					comment = <"Koding av rekvireringsårsaken med et kodeverk er ønskelig, dersom tilgjengelig.">
				>
				["at0064"] = <
					text = <"Årsaksbeskrivelse">
					description = <"En fritekstlig beskrivelse som forklarer årsaken til rekvireringen.">
				>
				["at0065"] = <
					text = <"Hensikt">
					description = <"Rekvirentens hensikt, det vil si hva de ønsker å oppnå ved rekvireringen.">
					comment = <"For eksempel en henvisning med den hensikt at spesialisthelsetjenesten skal ta over omsorgen for pasienten, eller for å få råd om hvordan fortsette med utredning eller behandling.">
				>
				["at0068"] = <
					text = <"Hastegrad">
					description = <"Rekvirentens hastegrad.">
				>
				["at0076"] = <
					text = <"Supplerende informasjon følger">
					description = <"Verdien \"Sann\" indikerer at supplerende informasjon er blitt identifisert og vil bli ettersendt når den er tilgjengelig, for eksempel ufullstendige laboratoriesvar.">
				>
				["at0078"] = <
					text = <"Detaljer om følgende supplerende informasjon">
					description = <"Detaljer om hva slags supplerende informasjon som ettersendes, for eksempel navnet på laboratorieundersøkelse.">
				>
				["at0112"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som er nødvendig for å registrere lokalt innhold/kontekst, eller for å sammenstille med andre referansemodeller/formalismer.">
					comment = <"F.eks lokale informasjonskrav eller ekstra metadata for å tilpasse med FHIR eller CIMI ekvivalenter.">
				>
				["at0116"] = <
					text = <"Pasientens behov">
					description = <"Språk, transport eller andre personlige behov som er nødvendige for å sikre pasientens oppmøte eller deltakelse i utførelsen av den rekvirerte tjeneste.">
				>
				["at0121"] = <
					text = <"Tjenestenavn">
					description = <"Navn på forespurt tjeneste.">
					comment = <"Koding av tjenestenavnet med et kodeverk er ønskelig, dersom tilgjengelig.">
				>
				["at0127"] = <
					text = <"Rekvisisjonsstatus">
					description = <"Status for rekvisisjonen oppgitt av rekvirenten. Status brukes for å vise om dette er den primære rekvisisjonen, en endring eller supplerende informasjon.">
				>
				["at0128"] = <
					text = <"Svarmottakere">
					description = <"En liste over personer eller organisasjoner som bør motta svar på rekvisisjonen.">
				>
				["at0132"] = <
					text = <"Spesifikke detaljer">
					description = <"Strukturerte detaljer om den rekvirerte tjenesten.">
					comment = <"Eksempel: En CLUSTER-arketype som spesifiserer det spesifikke kateteret som skal legges inn.">
				>
				["at0135"] = <
					text = <"Beskrivelse">
					description = <"Fritekstlig beskrivelse av tjenesten som er rekvirert.">
				>
				["at0136"] = <
					text = <"Akutt">
					description = <"Rekvisisjonen er akutt.">
				>
				["at0137"] = <
					text = <"Haster">
					description = <"Rekvisisjonen haster.">
				>
				["at0138"] = <
					text = <"Rutine">
					description = <"Rekvisisjonen er rutine.">
				>
				["at0141"] = <
					text = <"Rekvirent">
					description = <"Detaljer om helsepersonellet eller organisasjonen som har rekvirert prosedyren.">
				>
				["at0142"] = <
					text = <"Mottaker">
					description = <"Detaljer om helsepersonellet eller organisasjonen som mottar prosedyrerekvisisjonen.">
				>
				["at0144"] = <
					text = <"Seneste dato for rekvirert tjeneste">
					description = <"Seneste akseptable dato for å fullføre tjenesten.">
					comment = <"Eksempel: I noen tilfeller må en tjeneste være utført før en annen hendelse, f.eks. planlagt kirurgi.">
				>
				["at0145"] = <
					text = <"Tidligste dato for rekvirert tjeneste">
					description = <"Tidligste akseptable dato for å påbegynne tjenesten.">
					comment = <"I noen tilfeller må en viss tid løpe før en tjeneste kan utføres, f.eks. ved noen gastroskopier er en avhengig av at pasienten har seponert legemidler i en periode før prosedyren.">
				>
				["at0146"] = <
					text = <"Dato/tid for påbegynt tjeneste">
					description = <"Dato/tid for når tjenesten bør være påbegynt.">
					comment = <"I praksis vil klinikere ofte rekvirere tjenester med ikke-eksakte tidsangivelser, f.eks. kontroll om 3 måneder, 6 måneder eller 12 måneder. Kliniske systemer krever mer eksakte parametre å jobbe med, og \"3 mnd\" vil konverteres til en eksakt dato 3 måneder fra registreringsdatoen, og lagres i dette dataelementet.">
				>
			>
		>
	>

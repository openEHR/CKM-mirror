archetype (adl_version=1.4)
	openEHR-EHR-INSTRUCTION.request-procedure.v1
specialise
	openEHR-EHR-INSTRUCTION.request.v1

concept
	[at0000.1]	-- Procedure request
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["ar-sy"] = <
			language = <[ISO_639-1::ar-sy]>
			author = <
				["name"] = <"Mona Saleh">
			>
		>
		["es-ar"] = <
			language = <[ISO_639-1::es-ar]>
			author = <
				["name"] = <"Alan March">
				["organisation"] = <"Hospital Universitario Austral, Buenos Aires, Argentina">
			>
			accreditation = <"MD">
		>
	>
description
	original_author = <
		["name"] = <"Dr Ian McNicoll">
		["organisation"] = <"Ocean Informatics, United Kingdom">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"08/12/2009">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Provide simple and generic details about the request for a procedure to be performed.">
			use = <"Use to share details about the order or request for a procedure to be performed. This archetype has been designed to provide the core information required for a simple request for most procedures.  If more specific detail is required about the procedure it can be added via CLUSTER archetypes inserted into the Specific Details slot.">
			keywords = <"request", "refer", "order", "service", "provide">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
		["ar-sy"] = <
			language = <[ISO_639-1::ar-sy]>
			purpose = <"يمثل هذا النموذج تفاصيل جنيسة (غير محدودة الملكية) و بسيطة عن طلب إجراءات طبية ينبغي إجراؤها.">
			use = <"يستخدم لمشاركة معلومات حول الأمر أو الطلب بإجراء طبي. 
تم تصميم هذا النموذج ليقوم بالإمداد بالمعلومات الجوهرية المطلوبة لطلب بسيط لغالبية الإجراءات الطبية. 
إذا كان من المطلوب المزيد من التفاصيل عن الإجراءات فيمكن إضافة ذلك باستخدام عناقيد مناسبة يتم إدراجها في شرفات التفاصيل الخاصة.">
			keywords = <"الطلب", "يُحيل", "الأمر", "الخدمة", "المُزَوِّد">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
		["es-ar"] = <
			language = <[ISO_639-1::es-ar]>
			purpose = <"Proveer detalles simples y generales acerca de la solicitud para un procedimiento a ser realizado.">
			use = <"Utilizar para compartir detalles acerca de una orden o solicitud para un procedimiento a a ser realizado. Este arquetipo ha sido diseñado para proveer la información central requerida para una solicitus simple de la mayor parte de los procedimientos de salud. Si se requiere un detalles específico, el mismo puede ser agregado mediante los arquetipos CLUSTER introducidos en el slot de Detalles Específicos.">
			keywords = <"solicitud", "derivar", "referir", "orden", "servicio", "proveer">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Heather Leslie, Ocean Informatics, Australia", ...>
	other_details = <
		["references"] = <"">
		["MD5-CAM-1.0.1"] = <"A385C7315DE46E986F96E419774E3AB5">
	>

definition
	INSTRUCTION[at0000.1] matches {	-- Procedure request
		activities cardinality matches {0..*; unordered} matches {
			ACTIVITY[at0001] occurrences matches {1..*} matches {	-- Request
				action_archetype_id matches {/openEHR-EHR-ACTION\.procedure\.v1/}
				description matches {
					ITEM_TREE[at0009] matches {	-- Tree
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0121] matches {	-- Procedure requested
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0.146] occurrences matches {0..1} matches {	-- Type of procedure
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0135] occurrences matches {0..1} matches {	-- Description of Procedure
								value matches {
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0132] occurrences matches {0..1} matches {	-- Specific details  
								include
									archetype_id/value matches {/.*/}
							}
							ELEMENT[at0062] occurrences matches {0..1} matches {	-- Reason for request
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0064] occurrences matches {0..1} matches {	-- Reason description
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0.145] occurrences matches {0..1} matches {	-- Objective
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0065] occurrences matches {0..1} matches {	-- Intent
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0068] occurrences matches {0..1} matches {	-- Urgency
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0136, 	-- Emergency
											at0137, 	-- Urgent
											at0138]	-- Routine
										}
									}
								}
							}
							ELEMENT[at0040] occurrences matches {0..1} matches {	-- Date &/or time service required
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0144] occurrences matches {0..1} matches {	-- Latest date service required
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0076] occurrences matches {0..1} matches {	-- Supplementary information to follow
								value matches {
									DV_BOOLEAN matches {
										value matches {True}
									}
								}
							}
							ELEMENT[at0078] occurrences matches {0..1} matches {	-- Supplementary information expected
								value matches {
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0116] occurrences matches {0..1} matches {	-- Patient requirements
								include
									archetype_id/value matches {/.*/}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0008] matches {	-- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0010] occurrences matches {0..1} matches {	-- Requestor Identifier
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0141] occurrences matches {0..1} matches {	-- Requestor 
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0011] occurrences matches {0..1} matches {	-- Receiver identifier
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0142] occurrences matches {0..1} matches {	-- Receiver 
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0127] occurrences matches {0..1} matches {	-- Request status
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0128] occurrences matches {0..*} matches {	-- Distribution list for response 
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.distribution\.v1/}
					}
					allow_archetype CLUSTER[at0112] occurrences matches {0..*} matches {	-- Localisation
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0.145"] = <
					text = <"Objective">
					description = <"Details about specific objective or goal to be achieved by procedure.">
				>
				["at0.146"] = <
					text = <"Type of procedure">
					description = <"*">
				>
				["at0000"] = <
					text = <"Healthcare service request">
					description = <"Generic request for a range of different healthcare services e.g referral, lab request, equipment request.">
				>
				["at0000.1"] = <
					text = <"Procedure request">
					description = <"Request for a procedure to be performed.">
				>
				["at0001"] = <
					text = <"Request">
					description = <"Current Activity">
				>
				["at0008"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0010"] = <
					text = <"Requestor Identifier">
					description = <"The local ID assigned to the order by the healthcare provider or organisation requesting the service. This is also referred to as Placer Order Identifier.">
				>
				["at0011"] = <
					text = <"Receiver identifier">
					description = <"The ID assigned to the order by the healthcare provider or organisation receiving the request for service. This is also referred to as Filler Order Identifier.">
				>
				["at0040"] = <
					text = <"Date &/or time service required">
					description = <"The date and time that the service should be performed or completed.">
				>
				["at0062"] = <
					text = <"Reason for request">
					description = <"A short description of the reason for the request or indication.  This is often coded with an external terminology.">
				>
				["at0064"] = <
					text = <"Reason description">
					description = <"A narrative description explaining the reason for request.">
				>
				["at0065"] = <
					text = <"Intent">
					description = <"Intended impact on underlying health condition or issue eg preventive, curative, palliative, adjunct, adjuvant or supportive.">
				>
				["at0068"] = <
					text = <"Urgency">
					description = <"Urgency of the request.">
				>
				["at0076"] = <
					text = <"Supplementary information to follow">
					description = <"True indicates that additional information has been identified and will be forwarded when available eg incomplete pathology test results.">
				>
				["at0078"] = <
					text = <"Supplementary information expected">
					description = <"Details of the nature of supplementary information that is to follow e.g name of laboratory results.">
				>
				["at0112"] = <
					text = <"Localisation">
					description = <"Local detailed information such as billing requirements.">
				>
				["at0116"] = <
					text = <"Patient requirements">
					description = <"Language, transport or other personal requirements to support the patient's attendance or participation in provision of the service.">
				>
				["at0121"] = <
					text = <"Procedure requested">
					description = <"Identification of the service requested. This is often coded with an external terminology.">
				>
				["at0127"] = <
					text = <"Request status">
					description = <"The status of the request for service as indicated by the requester. 
Status is used to denote whether this is the initial request, or a follow-up request to change or provide supplementary information.">
				>
				["at0128"] = <
					text = <"Distribution list for response ">
					description = <"A list of person's or organisation who should receive copies of any communication.">
				>
				["at0132"] = <
					text = <"Specific details  ">
					description = <"Structured detail defining the service requested eg CLUSTER archetype specifying the specific details of the procedure, +/- anaesthetic requirements.">
				>
				["at0135"] = <
					text = <"Description of Procedure">
					description = <"A detailed narrative description of the service requested.">
				>
				["at0136"] = <
					text = <"Emergency">
					description = <"The request is an emergency">
				>
				["at0137"] = <
					text = <"Urgent">
					description = <"The request is urgent.">
				>
				["at0138"] = <
					text = <"Routine">
					description = <"The request is routine.">
				>
				["at0141"] = <
					text = <"Requestor ">
					description = <"Details about the healthcare provider or organisation requesting the service.">
				>
				["at0142"] = <
					text = <"Receiver ">
					description = <"Details about the healthcare provider or organisation receiving the request for service.">
				>
				["at0144"] = <
					text = <"Latest date service required">
					description = <"The latest date that is acceptable for the service to be completed.">
				>
			>
		>
		["ar-sy"] = <
			items = <
				["at0.145"] = <
					text = <"المقصد">
					description = <"تفاصيل حول الهدف أو المقصد المحدَّد الذي ينبغي تحقيقه بواسطة هذا الإجراء الطبي.">
				>
				["at0.146"] = <
					text = <"نوع الإجراء الطبي">
					description = <"*">
				>
				["at0000"] = <
					text = <"*Healthcare service request(en)">
					description = <"*Generic request for a range of different healthcare services e.g referral, lab request, equipment request.(en)">
				>
				["at0000.1"] = <
					text = <"طلب الإجراء الطبي">
					description = <"طلب لتنفيذ إجراء طبي">
				>
				["at0001"] = <
					text = <"الطلب">
					description = <"النشاط الحالي">
				>
				["at0008"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0009"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0010"] = <
					text = <"العنصر التعريفي الخاص بطالب الخدمة">
					description = <"العنصر التعريفي المحلي الذي تم تعيينه للأمر بواسطة مُزَوِّد الخدمة الصحية أو المؤسسة الصحية التي قامت بطلب الخدمة. و يتم الإشارة إليه أيضا بأنه العنصر التعريفي لمن قام بالأمر.">
				>
				["at0011"] = <
					text = <"العتصر التعريفي الخاص بمُستقبِل الطلب">
					description = <"العنصر التعريفي الذي تم تعيينه للأمر بواسطة مُزَوِّد الخدمة الصحية أو المؤسسة الصحية التي تستقبل الأمر لإجراء الخدمة. و يتم الإشارة إليه أيضا بالعنصر التعريفي لمُنَفِّذ الأمر.">
				>
				["at0040"] = <
					text = <"تاريخ و توقيت الخدمة المطلوبة">
					description = <"التاريخ و التوقيت الذي ينبغي إجراء أو إتمام الخدمة فيه">
				>
				["at0062"] = <
					text = <"سبب الطلب">
					description = <"وصف قصير لسبب الطلب. و عادةً ما يكون ذلك مُرمَزًا بواسطة نظام مصطلحات خارجي.">
				>
				["at0064"] = <
					text = <"وصف السبب">
					description = <"وصف برواية تصف سبب الطلب.">
				>
				["at0065"] = <
					text = <"القصد">
					description = <"التأثير المقصود أو الظرف/القضية الصحية وراء هذا الإجراء, مثلا: وقائي, علاجي, مُساعِد/إضافي, مُسانِد أو داعم">
				>
				["at0068"] = <
					text = <"الإلحاح">
					description = <"إلحاح الطلب">
				>
				["at0076"] = <
					text = <"معلومات تكميلية ينبغي اتباعها">
					description = <"إذا كانت هذه المعلومة صحيحة فذلك يشير إلى أنه تم التعرف على معلومات إضافية و سيتم إحالتها حينما تكون متاحة, مثلا: نتائج غير مكتملة لاختبار خاص بالباثولوجيا/المَرَضية.">
				>
				["at0078"] = <
					text = <"المعلومات التكميلية المُتَوَقَّعة">
					description = <"تفاصيل حول طبيعة المعلومات التكميلية التي ينبغي اتباعها, مثل اسم الاختبارات المعملية.">
				>
				["at0112"] = <
					text = <"التوطين">
					description = <"المعلومات الخاصة بالتفاصيل المحلية مثل متطلبات إجراءات الفوترة.">
				>
				["at0116"] = <
					text = <"مُتطلبات المريض">
					description = <"اللغة, وسيلة الانتقال, أو المتطلبات الشخصية الأخرى لدعم حضور و مشاركة المريض في أثناء تقديم الخدمة.">
				>
				["at0121"] = <
					text = <"الإجراء الطبي الذي تم طلبه">
					description = <"تعريف الخدمة التي تم طلبها. و عادة ما يكون مُرْمَزًا بواسطة نظام مصطلحات خارجي.">
				>
				["at0127"] = <
					text = <"حالة الطلب">
					description = <"حالة طلب الخدمة كما تتم الإشارة إليه بواسطة من طلب الأمر. 
تستخدم الحالة للإشارة إلى ما إذا كان هذا هو الطلب المبدئي أم أنه طلب متابعة لتغيير معلومات أو التزويد بمعلومات تكميلية.
">
				>
				["at0128"] = <
					text = <"قائمة التوزيع للاستجابة">
					description = <"قائمة من الأشخاص أو المؤسسات الذين ينبغي أن يستقبلوا نُسَخًا من أي نوع من الاتصال.">
				>
				["at0132"] = <
					text = <"التفاصيل المُخصصة">
					description = <"تفاصيل مُرَكَّبة لتعريف الخدمة التي تم طلبها, مثل عنقود في إحدى النماذج يصف تفاصيل الإجراء الطبي مع وجود أو عدم وجود متطلبات الاختبار.">
				>
				["at0135"] = <
					text = <"وصف الإجراء الطبي">
					description = <"وصف برواية مفصلة عن الخدمة التي تم طلبها.">
				>
				["at0136"] = <
					text = <"طارئة">
					description = <"هذا الطلب هو حالة طارئة">
				>
				["at0137"] = <
					text = <"مُلِحّ">
					description = <"هذا الطلب مُلِحّ">
				>
				["at0138"] = <
					text = <"روتيني">
					description = <"هذا الطلب روتيني">
				>
				["at0141"] = <
					text = <"الطالب">
					description = <"تفاصيل حول مُزَوِّد الخدمة الصحية أو المؤسسة الصحية التي تطلب الخدمة.">
				>
				["at0142"] = <
					text = <"المُستقبِل">
					description = <"تفاصيل حول مُزَوِّد الخدمة الصحية أو المؤسسة الصحية التي تستقبل طلب تقديم الخدمة.">
				>
				["at0144"] = <
					text = <"آخر تاريخ لاتزال الخدمة مطلوبة في أثناءه">
					description = <"آخر تاريخ لا يزال فيه مقبولا أن يتم إكمال/إتمام الخدمة.">
				>
			>
		>
		["es-ar"] = <
			items = <
				["at0.145"] = <
					text = <"Objetivo">
					description = <"Detalles acerca del objetivo específico o meta que se busca lograr mediante el procedimiento.">
				>
				["at0.146"] = <
					text = <"Tipo de procedimiento">
					description = <"*">
				>
				["at0000"] = <
					text = <"*Healthcare service request(en)">
					description = <"*Generic request for a range of different healthcare services e.g referral, lab request, equipment request.(en)">
				>
				["at0000.1"] = <
					text = <"Solicitud de procedimiento">
					description = <"Solicitud para un procedimiento a ser realizado.">
				>
				["at0001"] = <
					text = <"Solicitud">
					description = <"Actividad actual">
				>
				["at0008"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0009"] = <
					text = <"*Tree(en)">
					description = <"*@ internal @(en)">
				>
				["at0010"] = <
					text = <"Identificador del Solicitante">
					description = <"El ID local asignado a la solicitud por parte del prestador u organización proveedora de servicios de salud que solicita el servicio. También conocido como Identificador del Colocador de la Orden.">
				>
				["at0011"] = <
					text = <"Identificador del Receptor">
					description = <"El ID asignado a la solicitud por parte del prestador u organización proveedora de servicios de salud que recibe la solicitud del servicio. También conocido como Identificador del Receptor de la Orden.">
				>
				["at0040"] = <
					text = <"Fecha y/o hora para el servicio solicitado">
					description = <"La fecha y hora en la que el servicio debería ser realizado o cumplido.">
				>
				["at0062"] = <
					text = <"Motivo de la solicitud">
					description = <"Breve descripción del motivo de la solicitud. A menudo codificado mediante una terminología externa.">
				>
				["at0064"] = <
					text = <"Descripción del motivo">
					description = <"Descripción narrativa que explica el motivo de la solicitud.">
				>
				["at0065"] = <
					text = <"Propósito">
					description = <"Propósito declarado de la solicitud por parte del solicitante.">
				>
				["at0068"] = <
					text = <"Urgencia">
					description = <"Urgencia de la solicitud.">
				>
				["at0076"] = <
					text = <"Información suplementaria a proveer">
					description = <"\"Verdadero\" indica que se ha identificado información adicional y que será remitida cuando se encuentre disponible. Ej.: resultados de una prueba de patología incompleta.">
				>
				["at0078"] = <
					text = <"Información suplementaria prevista">
					description = <"Detalle de la naturaleza de la información suplementaria que se prevé. Ej.: nombre del resultado de laboratorio.">
				>
				["at0112"] = <
					text = <"Localización">
					description = <"Información local detallada, tal como la requerida para facturación.">
				>
				["at0116"] = <
					text = <"Requerimientos para el paciente">
					description = <"Requerimientos de idioma, transporte, u otros, que asistan al paciente para concurrir o participar en la provisión del servicio.">
				>
				["at0121"] = <
					text = <"Procedimiento solicitado">
					description = <"Identificación del procedimiento solicitado. A menudo se lo codifica mediante una terminología externa.">
				>
				["at0127"] = <
					text = <"Estado de la solicitud">
					description = <"El estado de la solicitud de servicio según el solicitante. 
El \"estado\" se utiliza para denotar si se trata de un solicitud inicial, a una solicitud de cambio o provisión de información de la solicitud en curso.">
				>
				["at0128"] = <
					text = <"Lista de distribución para la respuesta">
					description = <"Lista de personas o la organización que debería recibir copias de cualquier comunicación.">
				>
				["at0132"] = <
					text = <"Detalles específicos">
					description = <"Información estructurada que detalla el servicio solicitado. Ej.: Arquetipo CLUSTER que especifica detalles del procedimiento: si requiere anestesia, o no.">
				>
				["at0135"] = <
					text = <"Descripción del procedimiento">
					description = <"Descripción narrativa del servicio solicitado.">
				>
				["at0136"] = <
					text = <"Emergencia">
					description = <"La solicitud es de emergencia.">
				>
				["at0137"] = <
					text = <"Urgente">
					description = <"La solicitud es urgente.">
				>
				["at0138"] = <
					text = <"Rutina">
					description = <"La solicitud es de rutina.">
				>
				["at0141"] = <
					text = <"Solicitante">
					description = <"Detalles del prestador u organización proveedora de servicios de atención de la salud que solicita el servicio.">
				>
				["at0142"] = <
					text = <"Receptor">
					description = <"Detalles del prestador u organización proveedora de servicios de atención de la salud que recibe la solicitud del servicio.">
				>
				["at0144"] = <
					text = <"Fecha límite del servicio solicitado">
					description = <"La fecha mas tardía aceptable para el cumplimiento del servicio.">
				>
			>
		>
	>

archetype (adl_version=1.4; uid=4941b066-57d4-4eaa-9908-000b861b8e7a)
	openEHR-EHR-EVALUATION.death_summary.v0

concept
	[at0000]	-- Death summary
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Mikkel Johan Gaup Grønmo">
				["organisation"] = <"Regional forvaltning EPJ, Helse Nord RHF">
				["email"] = <"mikkel.johan.gaup.gronmo@helse-nord.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Mikkel Johan Gaup Grønmo">
		["organisation"] = <"Forvaltningssenter EPJ, Helse-Nord RHF">
		["email"] = <"mikkel.johan.gaup.gronmo@helse-nord.no">
		["date"] = <"2021-11-19">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"*To record summary information about the death of an individual, excluding details about the cause(s) of death. (en)">
			use = <"*Use to record summary information about the death of an individual, excluding details about the cause(s) of death.

While the content of the EVALUATION.cause_of death is explicitly constrained to record the formal conclusions about the cause of death of an individual, this archetype is designed to carry other details related to the death, including but not limited to the timing and location of death. (en)">
			keywords = <"sertifikat", "medisinsk sertifikat", "dødsårsak", "død", "mccd", "dato for død", "dødstidspunkt", "rapport", "embetsmann">
			misuse = <"*Not to be used to record information about the formal cause(s) of death - use EVALUATION.cause_of_death for this purpose. (en)">
			copyright = <"© openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record summary information about the death of an individual, excluding details about the cause(s) of death.">
			use = <"Use to record summary information about the death of an individual, excluding details about the cause(s) of death.

While the content of the EVALUATION.cause_of death is explicitly constrained to record the formal conclusions about the cause of death of an individual, this archetype is designed to carry other details related to the death, including but not limited to the timing and location of death.">
			keywords = <"certificate", "Medical Certificate", "Cause of Death", "Death", "MCCD", "date of death", "time of death", "medical examiner", "report", "coroner">
			misuse = <"Not to be used to record information about the formal cause(s) of death - use EVALUATION.cause_of_death for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Astrid Askeland, DIPS AS", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "SB Bhattacharyya, Bhattacharyyas Clinical Records Research & Informatics LLP, India", "Yexuan Cheng, 浙江大学, China", "Candice de Lisser, Ministry of Health and Wellness, Jamaica", "Stefan Dubois, Dep't of Pathology and Genetics, University Hospitals of Lund and Malmö, Sweden", "Grant Forrest, Lunaria Ltd, United Kingdom", "Rosane Gotardo, Systema Ltda., Brazil", "Heather Grain, Llewelyn Grain Informatics, Australia", "Mikkel Johan Gaup Grønmo, Regional forvaltning EPJ, Helse Nord, Norway (openEHR Editor)", "Finn Harald Stokland, DIPS AS", "Anca Heyd, DIPS AS", "Joost Holslag, Nedap, Netherlands", "Evelyn Hovenga, EJSH Consulting, Australia", "Kjetil Jørgensen, DIPS AS", "Anjali Kulkarni, Karkinos, India", "Kanika Kuwelker, Helse Vest IKT, Norway (openEHR Editor)", "Jörgen Kuylenstierna, eWeave AB, Sweden", "Liv Laugen, ​Oslo University Hospital, Norway, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Michael Lutz, BITsoft, Germany", "Manisha Mantri, C-DAC, India", "James McClay, University of Nebraska Medical Center, United States", "Mikael Nyström, Cambio Healthcare Systems AB, Sweden", "Bjørn Næss, DIPS AS", "Azra Resulbegovic, DIPS AS", "Benjamin Senst, Germany", "Natalia Strauch, Medizinische Hochschule Hannover, Germany", "Norwegian Review Summary, Norwegian Public Hospitals, Norway", "Anders Thurin, VGR, Sweden", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)", "Martijn van Eenennaam, Nedap Healthcare, Netherlands", "Marit Alice Venheim, Helse Vest IKT, Norway (openEHR Editor)", "Lin Zhang, Taikang Insurance Group, China">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Cause of Death and the Death Certificate [Internet]. College of American Pathologists (CAP); 2006 [cited 2022 Jun 05]. Available from: https://www.health.state.mn.us/people/vitalrecords/physician-me/docs/capcodbook.pdf

International statistical classification of diseases and related health problems [Internet]. World Health Organisation; 2011, pp 211 & 214 [cited 2022 Jun 05]. Available from: https://icd.who.int/browse10/Content/statichtml/ICD10Volume2_en_2016.pdf.

Physicians' Manual on Medical Certification of Cause of Death [Internet]. New Delhi: Office of the Registrar General, India; 2012 [cited 2022 Jun 05]. Available from: ttps://ncdirindia.org/e-mor/Download/Physician's_Manual_MCCD.pdf.

Slik skal dødsmeldingen fylles ut [Internet]. Folkehelseinstituttets; 2019 Sep 13 [updated 2021 Jan 21; cited 2022 Jun 05]. Available from: https://www.fhi.no/hn/helseregistre-og-registre/dodsarsaksregisteret/slik-skal-elektronisk-dodsmelding-fylles-ut/.

US Standard Certificate of Death [Internet]. Atlanta; Centre for Disease Control; revised 2003 Nov (cited 2022 Jun 05]. Available from: https://www.cdc.gov/nchs/data/dvs/DEATH11-03final-ACC.pdf.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"CBBA2D2B74D0F2805869CB95C54CCBA3">
		["build_uid"] = <"69fdfd2d-cb8c-44d2-9a4c-fdf6484cc193">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Death summary
		data matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0092] occurrences matches {0..1} matches {    -- Date/time of death
						value matches {
							DV_DATE_TIME matches {*}
							DV_INTERVAL<DV_DATE_TIME> matches {*}
						}
					}
					allow_archetype CLUSTER[at0104] occurrences matches {0..*} matches {    -- DOD alternatives
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.dod_alternative\.v0/}
					}
					ELEMENT[at0010] occurrences matches {0..1} matches {    -- Manner of death
						value matches {
							DV_TEXT matches {*}
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0011,    -- Disease
									at0012,    -- Assault
									at0013,    -- Intentional self harm
									at0014,    -- Pending investigation
									at0015,    -- War
									at0016,    -- Legal intervention
									at0018]    -- Indeterminate
								}
							}
						}
					}
					ELEMENT[at0093] occurrences matches {0..1} matches {    -- Manner description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0021] occurrences matches {0..*} matches {    -- Place category
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0109] occurrences matches {0..*} matches {    -- Place of death
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0100] occurrences matches {0..*} matches {    -- Structured place of death
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.address(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.organisation(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0054] occurrences matches {0..1} matches {    -- Age at death
						value matches {
							DV_DURATION matches {
								value matches {|>=PT0S|}
							}
						}
					}
					ELEMENT[at0044] occurrences matches {0..1} matches {    -- Gestation at death
						value matches {
							DV_DURATION matches {
								value matches {PWD/|P0D..P50W|}
							}
						}
					}
					ELEMENT[at0106] occurrences matches {0..1} matches {    -- Days post partum
						value matches {
							DV_DURATION matches {
								value matches {PD/|>=P0D|}
							}
						}
					}
					allow_archetype CLUSTER[at0042] occurrences matches {0..*} matches {    -- Additional details
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0105] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0009] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0103] occurrences matches {0..*} matches {    -- Information source
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0101] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0102] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Death summary">
					description = <"Summary information about the death of an individual, excluding details about the cause(s) of death.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0010"] = <
					text = <"Manner of death">
					description = <"Type of context or setting of the death.">
					comment = <"Suggested valueset is from the WHO ICD-10 reference.">
				>
				["at0011"] = <
					text = <"Disease">
					description = <"">
				>
				["at0012"] = <
					text = <"Assault">
					description = <"">
				>
				["at0013"] = <
					text = <"Intentional self harm">
					description = <"">
				>
				["at0014"] = <
					text = <"Pending investigation">
					description = <"">
				>
				["at0015"] = <
					text = <"War">
					description = <"">
				>
				["at0016"] = <
					text = <"Legal intervention">
					description = <"">
				>
				["at0018"] = <
					text = <"Indeterminate">
					description = <"">
				>
				["at0021"] = <
					text = <"Place category">
					description = <"The category of the place where the individual died.">
					comment = <"This data element has multiple occurrences to allow more than one level of category, including subcategories about the place of death. It is recommended that 'Place category' be coded with an external terminology. For example: hospital or home; or, within the context of a hospital: ICU; ward; or operating theatre.">
				>
				["at0042"] = <
					text = <"Additional details">
					description = <"Additional structured details related to the death.">
				>
				["at0044"] = <
					text = <"Gestation at death">
					description = <"Number of completed weeks of gestation of the fetus at the time of fetal death.">
				>
				["at0054"] = <
					text = <"Age at death">
					description = <"The age of the individual at the time of death.">
				>
				["at0092"] = <
					text = <"Date/time of death">
					description = <"Date and or time of death.">
					comment = <"For example: based on findings pertaining to examination of the corpse and the pathologist's reconstruction of time of death based on post-mortem changes, temperature, etc.">
				>
				["at0093"] = <
					text = <"Manner description">
					description = <"Narrative description about the context or setting of the death.">
				>
				["at0100"] = <
					text = <"Structured place of death">
					description = <"Structured detail about a named institution or address where the individual died.">
					comment = <"The 'Address line' in the CLUSTER.address archetype can be used to represent a landmark or a road intersection, if necessary.">
				>
				["at0101"] = <
					text = <"Last updated">
					description = <"The date when the death summary was last updated.">
				>
				["at0102"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0103"] = <
					text = <"Information source">
					description = <"Source of information.">
					comment = <"For example: Where there were multiple sources or word of mouth (considering credibility/reliability).">
				>
				["at0104"] = <
					text = <"DOD alternatives">
					description = <"Additional details about possible alternative dates of death.">
					comment = <"For example: Capturing information about conflicting dates and/or times of death from multiple sources with varying degree of certainty.">
				>
				["at0105"] = <
					text = <"Comment">
					description = <"Additional narrative about the death, not captured in other fields.">
					comment = <"For example: Recording the degree of certainty for place of death.">
				>
				["at0106"] = <
					text = <"Days post partum">
					description = <"The number of days from the end of a pregnancy to death.">
				>
				["at0109"] = <
					text = <"Place of death">
					description = <"Simple representation about the named place where the individual died.">
					comment = <"This data element has multiple occurrences to allow more than one level of detail about the place of death. For example: the name of the hospital where the patient died and the specific place within the hospital, such as 'Ward 6 North'; or 'Eiffel tower'.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"*Death summary (en)">
					description = <"*Summary information about the death of an individual, excluding details about the cause(s) of death. (en)">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0010"] = <
					text = <"Klassifisering av dødsfall">
					description = <"Type kontekst eller setting rundt dødsfallet.">
					comment = <"Foreslått verdisett er fra WHO ICD-10 referansen.">
				>
				["at0011"] = <
					text = <"Sykdom">
					description = <"">
				>
				["at0012"] = <
					text = <"Overfall">
					description = <"">
				>
				["at0013"] = <
					text = <"Selvskading">
					description = <"">
				>
				["at0014"] = <
					text = <"Venter på undersøkelse">
					description = <"">
				>
				["at0015"] = <
					text = <"Krig">
					description = <"">
				>
				["at0016"] = <
					text = <"Rettslig inngripen">
					description = <"">
				>
				["at0018"] = <
					text = <"Ubestemmelig">
					description = <"">
				>
				["at0021"] = <
					text = <"Stedskategori">
					description = <"*The category of the place where the individual died. (en)">
					comment = <"*This data element has multiple occurrences to allow more than one level of category, including subcategories about the place of death. It is recommended that 'Place category' be coded with an external terminology. For example: hospital or home; or, within the context of a hospital: ICU; ward; or operating theatre. (en)">
				>
				["at0042"] = <
					text = <"*Additional details (en)">
					description = <"*Additional structured details related to the death. (en)">
				>
				["at0044"] = <
					text = <"Gestasjon ved død">
					description = <"*Number of completed weeks of gestation of the fetus at the time of fetal death. (en)">
				>
				["at0054"] = <
					text = <"Alder ved død">
					description = <"Individets alder ved dødstidspunktet.">
				>
				["at0092"] = <
					text = <"*Date/time of death (en)">
					description = <"Dato og eventuelt tidspunkt for dødsfall.">
					comment = <"For eksempel: basert på funn ved undersøkelse av levning og patologens rekonstruksjon av dødsfalltidspunkt basert på endringer etter død, temperatur, etc.">
				>
				["at0093"] = <
					text = <"Klassifiserings beskrivelse">
					description = <"Fritekstlig beskrivelse om konteksten eller settingen rundt dødsfallet.">
				>
				["at0100"] = <
					text = <"*Structured place of death (en)">
					description = <"*Structured detail about a named institution or address where the individual died. (en)">
					comment = <"*The 'Address line' in the CLUSTER.address archetype can be used to represent a landmark or a road intersection, if necessary. (en)">
				>
				["at0101"] = <
					text = <"Sist oppdatert">
					description = <"*The date when the death summary was last updated. (en)">
				>
				["at0102"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
				["at0103"] = <
					text = <"*Information source (en)">
					description = <"Informasjonskilde.">
					comment = <"For eksempel: Ved multiple kilder eller annenhåndsinformasjon (med tanke på troverdighet/pålitelighet)">
				>
				["at0104"] = <
					text = <"*DOD alternatives (en)">
					description = <"*Additional details about possible alternative dates of death. (en)">
					comment = <"*For example: Capturing information about conflicting dates and/or times of death from multiple sources with varying degree of certainty. (en)">
				>
				["at0105"] = <
					text = <"Kommentar">
					description = <"*Additional narrative about the death, not captured in other fields. (en)">
					comment = <"For eksempel: Registrering av grad av visshet for dødssted.">
				>
				["at0106"] = <
					text = <"*Days post partum (en)">
					description = <"*The number of days from the end of a pregnancy to death. (en)">
				>
				["at0109"] = <
					text = <"*Place of death (en)">
					description = <"*Simple representation about the named place where the individual died. (en)">
					comment = <"*This data element has multiple occurrences to allow more than one level of detail about the place of death. For example: the name of the hospital where the patient died and the specific place within the hospital, such as 'Ward 6 North'; or 'Eiffel tower'. (en)">
				>
			>
		>
	>

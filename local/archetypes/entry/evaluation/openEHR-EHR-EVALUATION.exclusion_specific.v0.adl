archetype (adl_version=1.4; uid=45c5693a-9bbc-4ce4-bbcd-1a309b104e50)
	openEHR-EHR-EVALUATION.exclusion_specific.v0

concept
	[at0000]	-- Exclusion - specific
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"John Tore Valand">
				["organisation"] = <"Helse Bergen HF">
			>
		>
	>
description
	original_author = <
		["name"] = <"Silje Ljosland Bakke">
		["organisation"] = <"Nasjonal IKT HF">
		["email"] = <"silje.ljosland.bakke@nasjonalikt.no">
		["date"] = <"2017-02-17">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record a statement of exclusion about a specific Problem/diagnosis, Family history, Medication, Procedure, Adverse reaction or other clinical item that is either not currently present, or have not been present in the past.">
			use = <"Use to record a statement of exclusion about a specific Problem/diagnosis, Family history, Medication, Procedure, Adverse reaction or other clinical item that is either not currently present, or have not been present in the past.

This archetype has been specifically designed to make a clear and unambiguous statement of a specific exclusion of a type of clinical item from the health record. This approach is used in preference to relying on flags or terminology to express negation.

The data element 'Specific concept' allows for recording of a single specific statement. The different Specific consepts listed in the \"Specific consept' run-time name constraint identifies the the different specific exclusions. This name constraint can be applied during template modelling or at run-time within a software application.

Each specific statement should be recorded in a separate instance of this archetype. For example: record 'no past history of adverse reaction to penicillin V', 'no past history of adverse reaction to cephalosporins' and 'no known family history of heart disease' in 3 separately constrained instances of this archetype.

Please note that exclusion statements can only be considered to be current and accurate at the point-in-time of recording. It is possible for a record to be able to state that an individual has NO KNOWN history of a specific problem or diagnosis (using an exclusion statement) at the same consultation as recording the evidence of their first experience of the same problem or diagnosis (using the EVALUATION.problem_diagnosis archetype). In future record statements, the individual may have a KNOWN history of the problem or diagnosis recorded in their problem list.">
			keywords = <"exclusion, negation, rule out, rule-out, r/o", ...>
			misuse = <"Not to be used to record the exclusion of all problems or diagnoses, medications, procedures, family history, adverse reactions or other clinical items - use the EVALUATION.exclusion_global archetype for this purpose.

Not to be used to record the exclusion of any component of a physical examination - use the CLUSTER.exclusion_exam archetype within an appropriate OBSERVATION or CLUSTER archetype.

Not to be used to record the exclusion of symptoms use the CLUSTER.exclusion_symptom archetype within an appropriate OBSERVATION or CLUSTER archetype.

Not to be used to record the absence of information - use the EVALUATION.absense archetype for this purpose.">
			copyright = <"© Nasjonal IKT HF, openEHR Foundation">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere et ekslusjonsutsagn om en spesifikk problem/diagnose, familiær sykdom, legemiddel, prosedyre, overfølsomhet eller andre kliniske konsepter som ikke er tilstede hos et individ, enten i nåtid eller fortid.">
			use = <"Brukes til å registrere et ekslusjonsutsagn om en spesifikk problem/diagnose, familiær sykdom, legemiddel, prosedyre, overfølsomhet eller andre kliniske konsepter som ikke er tilstede hos et individ, enten i nåtid eller fortid.

Arketypen er laget spesifikt for kunne uttrykke klart og tydelig en spesifikk utelukkelse av en type klinisk konsept fra journalen. Denne tilnærmingen er valgt i stedet for å basere seg på flagg eller terminologi for å uttrykke negasjon.

Dataelementet \"Spesifikt konsept\" brukes til å registrere et enkelt spesifikt utsagn. De forskjellige typene utsagn som er listet opp i elementets runtime name constraint navngir de forskjellige spesifikke eksklusjonene. En runtime name constraint kan brukes enten under templatmodellering eller i en applikasjon.

Hvert enkelt utsagn registreres i en egen instans av denne arketypen. Man registrerer for eksempel \"Ingen tidligere overfølsomhetsreaksjon til penicillin V\", \"Ingen tidligere overfølsomhetsreaksjoner til cefalosporiner\", \"Ingen kjent hjertesykdom i familien\" i tre ulike instanser av denne arketypen.

Merk at eksklusjonsutsagn kun kan regnes som aktuelle og pålitelige på det tidspunktet de registreres. Et journalnotat kan gjerne uttrykke at et individ ikke har for eksempel diabetes i sin sykehistorie (ved hjelp av et eksklusjonsutsagn) i den samme konsultasjonen der det for første gang registreres en faktisk diabetes (ved hjelp av arketypen EVALUATION.problem_diagnosis). I senere journalnotater kan individet ha diabetes som del av sin kjente historikk.">
			keywords = <"eksklusjon, negasjon, utelukke, utelukkelse", ...>
			misuse = <"Brukes ikke for å registrere et ekslusjonsutsagn alle problemer eller diagnoser, tilstander i familien, legemidler, prosedyrer, overfølsomhetsreaksjoner eller andre kliniske elementer bruk arketypen EVALUATION.exclusion_global for dette formålet.

Brukes ikke for å registrere eksklusjon av noen komponent av en fysisk undersøkelse - bruk arketypen CLUSTER.exclusion_exam i en passende OBSERVATION- eller CLUSTER-arketype.

Brukes ikke for å registrere eksklusjon av symptomer, bruke arketypen CLUSTER.exclusion_symptom i en passende OBSERVATION eller CLUSTER arketype.

Brukes ikke denne arketypen for registrering av fravær av infomasjon - bruk arketypen EVALUATION.absence for dette formålet.">
			copyright = <"© Nasjonal IKT HF, openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo universitetssykehus HF, Norway (Nasjonal IKT redaktør)", "Kristian Berg, Universitetssykehuset Nord Norge, Norway", "Heather Leslie, Ocean Informatics, Australia", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Nasjonal IKT redaktør)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Derived from: Eksklusjonsutsagn - spesifikt, Draft archetype [Internet]. Nasjonal IKT, Nasjonal IKT Clinical Knowledge Manager [sitert: 2017-03-17]. Hentet fra: http://arketyper.no/ckm/#showArchetype_1078.36.1653
Exclusion statement, Published Archetype [Internet]. openEHR Foundation, openEHR Clinical Knowledge Manager [cited: 2017-02-17]. Available from: http://openehr.org/ckm/#showArchetype_1013.1.617">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"04F78FDDBD7A5881A5E05B5D789BD699">
		["build_uid"] = <"1f0b5da6-7a66-4178-8efb-b9551077c768">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {	-- Eksklusjonsutsagn - spesifikt
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0002] matches {	-- Eksklusjonsutsagn
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0003] occurrences matches {0..1} matches {	-- Spesifikt konsept
						name matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0008, 	-- Substans for overfølsomhetsreaksjon
									at0005, 	-- Familiær sykdom
									at0006, 	-- Legemiddel
									at0004, 	-- Problem/diagnose
									at0007]	-- Prosedyre
								}
							}
						}
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0012] occurrences matches {0..1} matches {	-- Kommentar
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0009] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0011] occurrences matches {0..*} matches {	-- Tilleggsinformasjon
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Exclusion - specific">
					description = <"A statement of exclusion about a specific Problem/diagnosis, Family history, Medication, Procedure, Adverse reaction or other clinical item that is either not currently present, or have not been present in the past.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Exclusion statement">
					description = <"A specific statement of exclusion about a Problem/diagnosis, Family history, Medication, Procedure, Adverse reaction or other clinical item.">
					comment = <"Use this statement in one of two ways. Firstly, this statement can be used in conjunction with the 'Specific concept' data element. For example: this data element can support recording general statements such as \"No known history of ...\" where the 'Specific concept' identifies the specific problem, diagnosis, substance, procedure or medication. Alternatively, this statement can be used to record a precoordinated term such as 'No family history of diabetes', and in this case the 'Specific concept' element will be redundant. 
">
				>
				["at0003"] = <
					text = <"Specific concept">
					description = <"Identification of the 'Specific concept' to which the 'Exclusion statement' applies.">
					comment = <"This data element is used to enable exact statements like: 'No past history of diabetes'. In this example, 'diabetes' would be an example of the specific concept 'Problem/Diagnosis' and the rest of the example phrase would be carried in the 'Exclusion statement'. Another possible use case might be 'No past family history of diabetes'. In this example, 'diabetes' would be an example of the specific concept 'Family problem/diagnosis' and the rest of the example phrase would be carried in the 'Exclusion statement'.">
				>
				["at0004"] = <
					text = <"Problem/diagnosis">
					description = <"The problem or diagnosis to which the 'Exclusion statement' applies. For example: 'Diabetes', 'COPD' or 'Asthma'.">
				>
				["at0005"] = <
					text = <"Family problem/diagnosis">
					description = <"The Family history item to which the 'Exclusion statement' applies. For example: 'Heart desease', 'Diabetes' or 'Alzheimer'.">
				>
				["at0006"] = <
					text = <"Medication">
					description = <"The Medication to which the 'Exclusion statement' applies. For example: 'Paracetamol', 'Codeine' or 'Antidepressants'.">
				>
				["at0007"] = <
					text = <"Procedure">
					description = <"The Procedure to which the 'Exclusion statement' applies. For example: 'Heart surgery' or 'Appendectomy' or 'Hip replacement'.">
				>
				["at0008"] = <
					text = <"Adverse reaction substance">
					description = <"The Adverse reaction substance/agent to which the 'Exclusion statement' applies. For example: 'Penicillin', 'Peanuts' or 'Latex'.">
				>
				["at0009"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0011"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: Local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0012"] = <
					text = <"Comment">
					description = <"Additional narrative about the Specific Exclusion not captured in other fields.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Eksklusjonsutsagn - spesifikt">
					description = <"Et eksklusjonsutsagn om en spesifikk problem/diagnose, familiær sykdom, legemiddel, prosedyre, overfølsomhet eller andre kliniske konsepter som ikke er tilstede hos et individ, enten i nåtid eller fortid.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Eksklusjonsutsagn">
					description = <"Et spesifikt eksklusjonsutsagn om en problem/diagnose, familiær sykdom, legemiddel, prosedyre, overfølsomhet eller andre kliniske konsepter.">
					comment = <"Utsagnet kan benyttes på to måter:
- Utsagnet kan brukes sammen med dataelementet \"Spesifikt konsept\". For eksempel: Dataelementet legger til rette for registrering av utsagn som: \"Ingen tidligere...\" hvor det \"Spesifikke konseptet\" identifiserer det spesifikke problemet, diagnosen, substansen, prosedyren eller legemiddelet.
- Utsagnet kan benyttes for å registrere prekoordinerte termer som \"Ingen kjent diabetes i familien\", i dette eksempelet er dataelementet \"Spesifikt konsept\" overflødig.">
				>
				["at0003"] = <
					text = <"Spesifikt konsept">
					description = <"Det \"Spesifikke konseptet\" \"Eksklusjonsutsagn\" omhandler.">
					comment = <"Dette dataelementet brukes for å gi presise utsagn som: \"Ingen tidligere forekomst av diabetes\". I dette eksempelet er \"Diabetes\" et eksempel på det spesifikke konseptet \"Problem/Diagnose\" og resten av utsagnet registreres i \"Eksklusjonsutsagn\". Et annet mulig brukscase kan være \"Ingen diabetes i familien\". I dette eksempelet er \"Diabetes\" et eksempel på det spesifikke konseptet \"Familiære sykdommer\" og resten av utsagnet registreres i \"Eksklusjonsutsagn\".">
				>
				["at0004"] = <
					text = <"Problem/diagnose">
					description = <"Problemet eller diagnoses som \"Eksklusjonsutsagn\" omhandler. For eksempel: \"Diabetes\", \"KOLS\" eller \"Astma\".">
				>
				["at0005"] = <
					text = <"Familiær sykdom">
					description = <"Den familiære sykdommen som \"Eksklusjonsutsagn\" omhandler. For eksempel \"hjertesykdom\", \"diabetes\" eller \"Alzheimer\".">
				>
				["at0006"] = <
					text = <"Legemiddel">
					description = <"Legemiddelet som \"Eksklusjonsutsagn\" omhandler. For eksempel: \"Paracetamol\", \"Kodein\" eller \"Antidepressiver\".">
				>
				["at0007"] = <
					text = <"Prosedyre">
					description = <"Prosedyren som \"Eksklusjonsutsagn\" omhandler. For eksempel: \"Hjertekirurgi\", \"appendektomi\" eller \"hofteprotese\".">
				>
				["at0008"] = <
					text = <"Substans for overfølsomhetsreaksjon">
					description = <"Substansen \"Eksklusjonsutsagn\" omhandler. For eksempel \"pencillin\", \"peanøtter\" eller \"latex\".">
				>
				["at0009"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0011"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR eller CIMI.">
				>
				["at0012"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om \"Spesifikk eksklusjon\" som ikke er registrert i andre felt.">
				>
			>
		>
	>

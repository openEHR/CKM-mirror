archetype (adl_version=1.4; uid=ae6ecf6a-c51b-4bd1-b948-fe32e16637e5)
	openEHR-EHR-EVALUATION.communication_capability.v0

concept
	[at0000]	-- Communication capability
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"John Tore Valand">
				["organisation"] = <"Helse Bergen HF, Norway">
				["email"] = <"john.tore.valand@helse-bergen.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics, Australia">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2018-03-26">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere metoden(e) et individ bruker for å formidle eller motta informasjon.">
			use = <"Brukes for å registrere evne og metode for utveksling av informasjon med et individ.

Hensikten med arketypen er å registrere hvordan individet kommuniserer med andre og hvordan klinikere best kan kommunisere med individet.

Arketypen er laget for bruk i arketypen EVALUATION.social_summary (Sosialanamnese). Arketypen CLUSTER.language (Språk) kan nøstes i SLOTene \"Foretrukket kommunikasjon\" og \"Annen kommunikasjon\".">
			keywords = <"språk", "tolk", "oversettelse", "oversetter">
			misuse = <"Brukes ikke for å registrere detaljer om et språk, bruk arketypen CLUSTER.language (Språk) for dette formålet.

Brukes ikke for å registrere detaljer om en forespørsel om tolketjenester. Bruk arketypen CLUSTER.interpreter_requirements (Tolkebehov) i en egnet INSTRUCTION-arketype, for eksempel INSTRUCTION.service_request (Helsetjenesteforespørsel).

Brukes ikke for å registrere detaljer om tolking som er gjennomført. Bruk arketypen ACTION.interpretation for dette formålet.">
			copyright = <"© openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record detail about the practical ability of the individual to communicate, including impairments and need for communication aids.">
			use = <"Use to record detail about the practical ability of the individual to communicate, including impairments and need for communication aids.

This archetype is intended to capture how the individual communicates with others and how healthcare providers can best communicate with the individual.

This archetype is designed to contain the CLUSTER.language archetype in one or both SLOTS for 'Preferred communication' and 'Other communication'.">
			keywords = <"language", "interpreter", "translation", "translater", "translator">
			misuse = <"Not to be used to record details about a language - use CLUSTER.language for this purpose.

Not to be used to record details about a request for interpreter services - use CLUSTER.Interpreter_requirements with a suitable INSTRUCTION archetype, for example INSTRUCTION.service_request.

Not to be used to record details about an interpretation that was performed - use the proposed ACTION.interpretation.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Nasjonal IKT HF, Norway", "John Tore Valand, Helse Bergen HF, Norway", "Vebjørn Arntzen, Oslo University Hospital, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Heather Leslie, Atomica Informatics, Australia<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"8E6D10CE9F2321A56A6225554B4D1526">
		["build_uid"] = <"b902b345-f68f-494d-937f-875197092138">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Communication capability
		data matches {
			ITEM_TREE[at0001] matches {    -- ItemTree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0002] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0003] occurrences matches {0..*} matches {    -- Preferred communication
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.language(-[a-zA-Z0-9_]+)*\.v0/}
					}
					allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {    -- Other communication
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.language(-[a-zA-Z0-9_]+)*\.v0/}
					}
					allow_archetype CLUSTER[at0005] occurrences matches {0..1} matches {    -- Additional details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device\.v1/}
					}
					CLUSTER[at0006] occurrences matches {0..*} matches {    -- Per language
						items cardinality matches {2..*; unordered} matches {
							allow_archetype CLUSTER[at0007] occurrences matches {0..1} matches {    -- Language details
								include
									archetype_id/value matches {/openEHR-EHR-CLUSTER\.language\.v0/}
							}
							ELEMENT[at0008] matches {    -- Translator required
								value matches {
									DV_BOOLEAN matches {*}
								}
							}
						}
					}
					ELEMENT[at0009] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0011] matches {    -- ItemTree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0012] occurrences matches {0..1} matches {    -- Extension
					}
					ELEMENT[at0013] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Communication capability">
					description = <"The practical ability of the individual to communicate.">
				>
				["at0002"] = <
					text = <"Description">
					description = <"Narrative description about the overall capability of the individual to communicate, including impairments and need for communication aids.">
				>
				["at0006"] = <
					text = <"Per language">
					description = <"Details about the capabilities of the individual for a specific language.">
					comment = <"In clinical systems it is common to record whether a interpreter is required as a simple, stand-alone boolean data point - the language for translation required is implied, rather than specified. However if this single boolean is exchanged to a location where the assumed language is different, the recorded boolean may be incorrect. In order for this data to be correctly exchanged, both the language and requirement for translation needs to be exchanged as a pair. For each language that is proposed for a clinical communication it will be necessary to do a separate assessment about whether a translator is required for the individual. For example, an English speaker in Quebec will not require an English interpreter but may require one if the consultation is being conducted in French. ">
				>
				["at0008"] = <
					text = <"Translator required">
					description = <"Indicator that the services of a translator are required for the identified language.">
				>
				["at0003"] = <
					text = <"Preferred communication">
					description = <"The preferred language and method of communication for an individual.">
					comment = <"More than one instances of the CLUSTER.language may be required here if the individual uses a variety modes of communication, for example speaking in one language and signing in another.">
				>
				["at0004"] = <
					text = <"Other communication">
					description = <"Other languages, and/or methods of communication for an individual.">
				>
				["at0009"] = <
					text = <"Comment">
					description = <"Additional narrative about the communication capability of an individual not captured in other fields.">
				>
				["at0007"] = <
					text = <"Language details">
					description = <"Details about a language.">
				>
				["at0005"] = <
					text = <"Additional details">
					description = <"Additional structured details about the individual's communication capability.">
				>
				["at0011"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0001"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0012"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
				>
				["at0013"] = <
					text = <"Last updated">
					description = <"Date when the Communication capability was last updated.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Kommunikasjon">
					description = <"Metoder for informasjonsutveksling med et individ.">
				>
				["at0002"] = <
					text = <"Beskrivelse">
					description = <"En fritekstbeskrivelse om individets overordnede evne til kommunikasjon.">
				>
				["at0006"] = <
					text = <"Tolkebehov">
					description = <"Detaljer om tolking er nødvendig, eller ikke for språket som foreslås for kommunikasjonen.">
					comment = <"I kliniske system er det vanlig å registrere om det er behov for tolk med en registrering i et enkelt boolsk dataelement. Oversettelsesspråket er underforstått og spesifiseres ikke. Utveksles denne verdien til en lokalisering hvor oversettelsesspråket er et annet kan den registrerte boolske verdien være feil. For å sikre en riktig utveksling av dette dataelementet, må både språket og behov for tolk utveksles sammen. Hvert språk som individet snakker vil trenge en en egen vurdering av om det er et tolkebehov. For eksempel vil ikke en engelsktalende i Quebec trenge engelsk tolk, men det kan være behov for tolk om konsultasjonen utføres på fransk.">
				>
				["at0008"] = <
					text = <"Trenger tolk">
					description = <"Indikererer at det er et tolkebehov på det spesifiserte språket.">
				>
				["at0003"] = <
					text = <"Foretrukket kommunikasjon">
					description = <"Individets foretrukne språk og kommunikasjonsmetode.">
					comment = <"Det kan være behov for flere instanser av CLUSTER.language (Språk) om individet benytter flere kommunikasjonsmetoder. For eksempel: snakker ett språk og benytter et annet for tegnspråk.">
				>
				["at0004"] = <
					text = <"Annen kommunikasjon">
					description = <"Individets andre språk og kommunikasjonsmetoder.">
				>
				["at0009"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekstbeskrivelse om individets evne til kommunikasjon som ikke er registrert i andre felt.">
				>
				["at0007"] = <
					text = <"*Language details (en)">
					description = <"*Details about a language. (en)">
				>
				["at0005"] = <
					text = <"*Additional details (en)">
					description = <"*Additional structured details about the individual's communication capability. (en)">
				>
				["at0011"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0001"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0012"] = <
					text = <"*Extension (en)">
					description = <"*">
				>
				["at0013"] = <
					text = <"*Last updated (en)">
					description = <"*">
				>
			>
		>
	>

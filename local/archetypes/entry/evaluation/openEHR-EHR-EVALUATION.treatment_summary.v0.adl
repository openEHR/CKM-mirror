archetype (adl_version=1.4; uid=b41dfb70-62e2-4382-a7fb-7e894850f8cf)
	openEHR-EHR-EVALUATION.treatment_summary.v0

concept
	[at0000]	-- Treatment summary
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2022-10-28">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record summary or persistent details about the outcome of a treatment course or protocol for an identified condition, such as an infectious disease or cancer, or injury.">
			use = <"Use to record summary or persistent details about the outcome of a treatment course or protocol for an identified condition, such as an infectious disease or cancer, or injury.">
			misuse = <"">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"40EAD2C6ADDDED57F41BF133CF4D9779">
		["build_uid"] = <"29f0c1ca-26a0-4da1-8f60-e14d31b85ea0">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Treatment summary
		data matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0003] occurrences matches {0..1} matches {    -- Treatment name
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {    -- Index problem
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0028] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0031] occurrences matches {0..*} matches {    -- Reason for treatment
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {    -- Treatment status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0021,    -- Completed
									at0022,    -- Not completed
									at0029]    -- Indeterminate
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {    -- Date treatment completed
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0007] occurrences matches {0..*} matches {    -- Reason for non-completion
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0008] occurrences matches {0..1} matches {    -- Treatment outcome
						value matches {
							DV_TEXT matches {*}
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0023,    -- Treatment successful
									at0024,    -- Treatment failed
									at0025]    -- Indeterminate
								}
							}
						}
					}
					ELEMENT[at0027] occurrences matches {0..1} matches {    -- Reason for failed outcome
						value matches {
							DV_CODED_TEXT matches {*}
						}
					}
					ELEMENT[at0026] occurrences matches {0..1} matches {    -- Reason for indeterminate outcome
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0010] occurrences matches {0..1} matches {    -- Date last seen
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0019] occurrences matches {0..*} matches {    -- Additional details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.clinical_evidence(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.drug_resistance_profile(-[a-zA-Z0-9_]+)*\.v0/}
					}
					CLUSTER[at0013] occurrences matches {0..*} matches {    -- Issue
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0012] occurrences matches {0..1} matches {    -- Issue name
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0014] occurrences matches {0..1} matches {    -- Date identified
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							allow_archetype CLUSTER[at0030] occurrences matches {0..*} matches {    -- Issue details
								include
									archetype_id/value matches {/openEHR-EHR-CLUSTER\.drug_resistance_profile(-[a-zA-Z0-9_]+)*\.v0/}
							}
							ELEMENT[at0015] occurrences matches {0..1} matches {    -- Comment
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
					ELEMENT[at0016] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0002] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0017] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0018] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Treatment summary">
					description = <"Summary or persistent details about the outcome of a treatment course or protocol for an identified condition.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Treatment name">
					description = <"The name of the treatment or course of treatment.">
				>
				["at0004"] = <
					text = <"Date treatment completed">
					description = <"The date when the treatment or course of treatment was completed.">
				>
				["at0005"] = <
					text = <"Treatment status">
					description = <"Description about the status of the treatment course.">
					comment = <"For example: completed or not completed.">
				>
				["at0006"] = <
					text = <"Index problem">
					description = <"The name of the index disease, condition or injury.">
				>
				["at0007"] = <
					text = <"Reason for non-completion">
					description = <"Description of the reason why the course of treatment was not completed.">
				>
				["at0008"] = <
					text = <"Treatment outcome">
					description = <"Category of the treatment outcome for the individual.">
				>
				["at0010"] = <
					text = <"Date last seen">
					description = <"The date when the individual last attended for treatment.">
					comment = <"May be useful, for example, when the individual is lost to follow-up.">
				>
				["at0012"] = <
					text = <"Issue name">
					description = <"Name of the issue.">
				>
				["at0013"] = <
					text = <"Issue">
					description = <"Details about an issue that impacted the treatment outcome.">
					comment = <"For example: drug resistance">
				>
				["at0014"] = <
					text = <"Date identified">
					description = <"Date when the issue was identified.">
				>
				["at0015"] = <
					text = <"Comment">
					description = <"Narrative description about the issue, not captured in other data elements.">
				>
				["at0016"] = <
					text = <"Comment">
					description = <"Narrative description about the overall treatment, not captured in other data elements.">
				>
				["at0017"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0018"] = <
					text = <"Last updated">
					description = <"The date and/or time when the Treatment outcome summary was last updated.">
				>
				["at0019"] = <
					text = <"Additional details">
					description = <"Additional structured details about the treatment outcome.">
					comment = <"For example: drug resistance profile; clinical evidence.">
				>
				["at0021"] = <
					text = <"Completed">
					description = <"The treatment course was completed.">
				>
				["at0022"] = <
					text = <"Not completed">
					description = <"The treatment course was not completed.">
				>
				["at0023"] = <
					text = <"Treatment successful">
					description = <"Individual is considered cured; there is no ongoing evidence of the index problem.">
				>
				["at0024"] = <
					text = <"Treatment failed">
					description = <"Treatment was not successful; there is ongoing evidence of the index problem.">
				>
				["at0025"] = <
					text = <"Indeterminate">
					description = <"It is not known if the treatment was successful or unsuccessful.">
				>
				["at0026"] = <
					text = <"Reason for indeterminate outcome">
					description = <"Reason for an indeterminate outcome.">
					comment = <"For example: lost to follow-up; or not followed up.">
				>
				["at0027"] = <
					text = <"Reason for failed outcome">
					description = <"Reason for a failed treatment outcome.">
					comment = <"For example: drug resistance.">
				>
				["at0028"] = <
					text = <"Description">
					description = <"Narrative description about the treatment outcome.">
				>
				["at0029"] = <
					text = <"Indeterminate">
					description = <"It is not known if the treatment was completed or not.">
				>
				["at0030"] = <
					text = <"Issue details">
					description = <"Structured details about the issue.">
				>
				["at0031"] = <
					text = <"Reason for treatment">
					description = <"The reason for treatment.">
					comment = <"For example: palliation; relapse; second line treatment after initial failure.">
				>
			>
		>
	>

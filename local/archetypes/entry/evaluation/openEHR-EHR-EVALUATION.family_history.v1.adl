archetype (adl_version=1.4; uid=6f5ae91b-487e-482f-bcb6-d4152f018d33)
	openEHR-EHR-EVALUATION.family_history.v1

concept
	[at0000]	-- Family history
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			author = <
				["name"] = <"Vladimir Pizzo">
				["organisation"] = <"Hospital Sirio Libanes - Brazil">
				["email"] = <"vladimir.pizzo@hsl.org.br">
			>
		>
	>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"2010-12-15">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record information about the occurrence of significant health-related problems in genetic and non-genetic family members - both alive and deceased. 

The intended scope of this archetype is deliberately kept loose to include the broadest range of problems or issues that might be found within families. It specifically includes known problems and diagnoses, identified biological markers, plus any relevant psychosocial factors and environmental factors.">
			use = <"Use to record a summary of information about problems or diagnoses found in family members. This information may be used to contribute to an assessment of risk to the individual from potentially inheritable conditions or initiation of preventive health activities.

Traditionally the scope of family history has been focused on genetic factors or biomarkers as indicators of risk or potential risk. However the scope of this archetype explicitly includes the recording of information about problems or diagnoses that may be influenced by familial exposure of non-genetic family members to psychosocial or environmental factors as well. Examples that are relevant for non-genetic family members include exposure to toxins in the family environment, domestic violence, sexual abuse, alcoholism and other addictions.

Non-genetic family members can include adopted or long term fostered children, those related by marriage, or other unrelated individuals who participate in the regular life and influence of the family.

This archetype has been designed to include:
- a narrative overview as free text. This will allow family history details from existing systems to be incorporated as non-structured text; and
- a detailed area focusing on relevant health details about specific family members, inlcuding their medical history and biomarkers.

This archetype can be used within many contexts. For example, recording a family history entry within a clinical consultation; populating a Family History List; or to provide a summary statement within a Discharge Summary document.

Additional detail about a family member's specific problem or diagnosis can be captured using the EVALUATION.problem_diagnosis archetype and specifying the 'Subject of Care' as the family member, rather than the subject of the health record.

This archetype can be used as the basis for a Family Pedigree chart of health problems/diagnoses or to support estimations of risk of a condition based on prevalence in the family history or known biomarkers.

It may be necessary to identify each family member specifically and not just by the relationship to the patient. For example, while there will be only one maternal grandmother, there may be many female maternal cousins. This may be required to ensure that a pedigree chart is accurate. It will also enable accurate amendments to the record for each identified family member. If the record is private and will not be shared, for reasons of clarity it may be preferable to record the relative's actual name. If the record, or part of the record, is to be shared, it may be more appropriate for the family member to be identified by a unique label or alias.">
			keywords = <"family", "history", "health", "condition", "problem", "diagnosis", "genetic", "pedigree", "genealogy", "family history", "relative", "hereditary", "family history">
			misuse = <"Not to be used to record information about the relative or absolute risk of developing a condition due to family history - use the EVALUATION.health_risk archetype, including the CLUSTER.family_prevalence for details about the affected ratio of family members.

Not to be used for contact tracing for infectious diseases requiring immediate action.

Not to be used to record an exclusion of Family History - use the EVALUATION.exclusion-family_history archetype for this purpose.">
			copyright = <"© openEHR Foundation">
		>
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Para registrar informações sobre a ocorrência de problemas significativos relacionados com a saúde de membros da família (relacionados ou não relacionados) tanto vivos quanto falecidos.

O âmbito de aplicação deste arquétipo é deliberadamente mantido amplo para incluir a maior gama de problemas ou questões que possam ser encontrados dentro das famílias. Ele inclui problemas e diagnósticos, marcadores biológicos identificados, além de todos os fatores psicossociais e ambientais relevantes conhecidos.">
			use = <"Use para gravar um resumo das informações sobre problemas ou diagnósticos encontrados nos membros da família. Esta informação pode ser utilizada para contribuir numa avaliação de risco de condições potencialmente herdadas ou início de atividades preventivas de saúde para o indivíduo.

Tradicionalmente, o escopo da história da família tem sido focado em fatores genéticos ou biomarcadores como indicadores de risco ou risco potencial. No entanto, o escopo deste arquétipo inclui explicitamente o registro da informação sobre problemas ou diagnósticos que possam ser influenciados pela exposição familiar de membros não aparentados a fatores psicossociais ou ambientais. Exemplos que são relevantes para os membros não aparentados da família incluem a exposição a toxinas no ambiente familiar, violência doméstica, abuso sexual, alcoolismo e outros vícios.

Membros da família não aparentados podem incluir crianças adotadas, os relacionados pelo casamento, ou outros indivíduos não aparentados que participam na vida regular e influência da família.

Este arquétipo foi projetado para incluir:
- Uma visão narrativa como texto livre. Isso permitirá que detalhes de história da família sejam incorporados como texto não-estruturado; e
- Uma exposição detalhada com foco em detalhes de saúde relevantes sobre os membros da família específicos, incluindo a sua história clínica e marcadores biológicos.

Este arquétipo pode ser usado em muitos contextos. Por exemplo, a gravação da história da família dentro de uma consulta clínica; preencher uma lista de História da Família; ou para fornecer uma declaração resumida num sumário de alta.

Detalhes adicionais sobre o problema ou diagnóstico específico de um membro da família podem ser capturados usando o arquétipo EVALUATION.problem_diagnosis e especificando o 'Sujeito do Cuidado' como o membro da família, em vez do sujeito do registro de saúde.

Este arquétipo pode ser usado como a base para uma genealogia dos problemas / ou diagnósticos de saúde de uma família para permitir as estimativas de risco de uma patologia com base em prevalência na história da família ou marcadores biológicos conhecidos.

Pode ser necessário identificar cada membro da família especificamente e não apenas pela relação com o paciente. Por exemplo, só há uma avó materna mas podem haver muitas primas maternas. Isso pode ser necessário para assegurar que um gráfico de linhagem seja preciso. Ele também permite alterações no registro para cada membro da família identificado. Se o registro for privado e não puder ser compartilhado, pode ser preferível gravar o nome real do parente. Se o registro, ou parte do registro, puder ser compartilhado, pode ser mais apropriado que o membro da família seja identificado por um rótulo exclusivo ou alias.
">
			keywords = <"família", "história", "estória", "saúde", "condição", "problema", "diagnóstico", "genético", "pedigree", "genealogia", "história familiar", "parente", "hereditário">
			misuse = <"Não deve ser usado para registrar informações sobre o risco relativo ou absoluto de desenvolver uma condição devido à história familiar - usar o arquétipo EVALUATION.health_risk , incluindo o CLUSTER.family_prevalence para detalhes sobre a relação dos membros da família afetada.

Não deve ser usado para busca de contactantes de doenças infecciosas que requeiram ações imediatas.

Não deve ser usado para registrar uma exclusão de História Familiar - utilizar o arquétipo EVALUATION.exclusion-family_history para esta finalidade.">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <"Tomas Alme, DIPS, Norway", "Rita Apelt, Department of Health,NT, Australia", "Vebjoern Arntzen, Oslo university hospital, Norway", "Koray Atalag, University of Auckland, New Zealand", "Silje Ljosland Bakke, Bergen Hospital Trust, Norway", "John Bennett, NEHTA, Australia", "Lars Bitsch-Larsen, Haukeland University hospital, Norway", "Diego Bosca, IBIME group, Spain", "Rong Chen, Cambio Healthcare Systems, Sweden", "Stephen Chu, Queensland Health, Australia", "David Evans, Queensland Health, Australia", "Shahla Foozonkhah, Ocean Informatics, Australia", "Einar Fosse, National Centre for Integrated Care and Telemedicine, Norway", "Samuel Frade, Marand, Portugal", "Tim Garden, NTG Department of Health, Australia", "Sebastian Garde, Ocean Informatics, Germany", "Jacquie Garton-Smith, Royal Perth Hospital and DoHWA, Australia", "Andrew Goodchild, NEHTA, Australia", "Sam Heard, Ocean Informatics, Australia (Editor)", "Lars Karlsen, DIPS ASA, Norway", "Mary Kelaher, NEHTA, Australia", "Shinji Kobayashi, Kyoto University, Japan", "Robert L'egan, NEHTA, Australia", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Hugh Leslie, Ocean Informatics, Australia", "Hallvard Lærum, Oslo University Hospital, Norway", "Mike Martyn, The Hobart Anaesthetic Group, Australia", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (Editor)", "Chris Mitchell, RACGP, Australia", "Stewart Morrison, NEHTA, Australia", "Bjoern Naess, DIPS ASA, Norway", "Jeremy Oats, NT Health, Australia", "Lynne Parsons, Primary and Community Health Services, Australia", "Jodie Pycroft, Adelaide Northern Division of General Practice Ltd, Australia", "Robyn Richards, NEHTA - Clinical Terminology, Australia", "Jussara Rotzsch, UNB, Brazil", "Anoop Shah, University College London, United Kingdom", "Elizabeth Stanick, Hobart Anaesthetic Group, Australia", "John Taylor, NEHTA, Australia", "Gordon Tomes, Australian Institute of Health and Welfare, Australia", "Richard Townley-O'Neill, NEHTA, Australia", "Donna Truran, ACCTI-UoW, Australia", "Jo Wright, NT Dept of Health, Australia (Editor)">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["revision"] = <"1.0.0">
		["references"] = <"Family History, draft archetype [Internet]. Australia, National eHealth Transition Authority, NEHTA Clinical Knowledge Manager. Authored: 2010 12 15. Available at: http://dcm.nehta.org.au/ckm/#showArchetype_1013.1.927 (last accessed 2015 03 05).

Risk of condition based on family history, rejected archetype, openEHR Clinical Knowledge Manager [Internet]. openEHR Foundation. Authored: 2006 04 23. Available at: http://www.openehr.org/ckm/#showArchetype_1013.1.125 (last accessed 2015 03 05).

HL7 Version 3 Standard: Clinical Genomics; Pedigree, Release 1. ANSI/HL7 V3 CGPED, R1-2007. Published 2007 05 07. Available at: http://www.hl7.org/implement/standards/product_brief.cfm?product_id=8 (last accessed 2015 03 05).

Family History FHIR resource [Internet]. Health Level Seven International. Available at: http://www.hl7.org/implement/standards/fhir/familyhistory.html (last accessed 2015 03 24).">
		["build_uid"] = <"d863e05d-de96-4a87-8219-0d2193fdebd3">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["MD5-CAM-1.0.1"] = <"64E3016F6A863461798A14EB090812AE">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	EVALUATION[at0000] matches {	-- Family history
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0002] occurrences matches {0..1} matches {	-- Summary
						value matches {
							DV_TEXT matches {*}
						}
					}
					CLUSTER[at0003] occurrences matches {0..*} matches {	-- Per family member
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0004] occurrences matches {0..1} matches {	-- Family member name
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0020] occurrences matches {0..1} matches {	-- Alias
								value matches {
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0048] occurrences matches {0..*} matches {	-- Family member
								include
									archetype_id/value matches {/openEHR-EHR-CLUSTER\.person_name(-[a-zA-Z0-9_]+)*\.v1/}
							}
							ELEMENT[at0016] occurrences matches {0..1} matches {	-- Relationship
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0005] occurrences matches {0..1} matches {	-- Date of birth
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0023] occurrences matches {0..1} matches {	-- Deceased?
								value matches {
									DV_BOOLEAN matches {
										value matches {True}
									}
								}
							}
							ELEMENT[at0011] occurrences matches {0..1} matches {	-- Age at death
								value matches {
									DV_DURATION matches {*}
								}
							}
							ELEMENT[at0058] occurrences matches {0..1} matches {	-- Date of death
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							CLUSTER[at0008] occurrences matches {0..*} matches {	-- Medical history
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0009] occurrences matches {0..1} matches {	-- Problem/diagnosis
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0012] occurrences matches {0..1} matches {	-- Clinical description
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0010] occurrences matches {0..1} matches {	-- Age at onset
										value matches {
											DV_DURATION matches {*}
										}
									}
									ELEMENT[at0014] occurrences matches {0..1} matches {	-- Cause of death?
										value matches {
											DV_BOOLEAN matches {
												value matches {True, False}
											}
										}
									}
								}
							}
							ELEMENT[at0046] occurrences matches {0..1} matches {	-- Comment
								value matches {
									DV_TEXT matches {*}
								}
							}
							CLUSTER[at0024] occurrences matches {0..1} matches {	-- Biomarkers
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0022] occurrences matches {0..1} matches {	-- Biomarker description
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype ITEM[at0027] occurrences matches {0..*} matches {	-- Biomarker details
										include
											archetype_id/value matches {/.*/}
									}
								}
							}
						}
					}
					allow_archetype CLUSTER[at0053] occurrences matches {0..*} matches {	-- Multimedia
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0025] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0026] occurrences matches {0..1} matches {	-- Last Updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0045] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Family history">
					description = <"Summary information about the significant health-related problems found in family members.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Summary">
					description = <"Narrative overview about problems, diagnoses, psychosocial, environmental and genetic markers that have been identified in family members.">
					comment = <"Use this field to capture unstructured family history information recorded in clinical records or to import textual data from existing/legacy clinical systems.">
				>
				["at0003"] = <
					text = <"Per family member">
					description = <"Details about a specific family member.">
					comment = <"The data elements in this cluster will relate to the individual identified either by name or by alias. Repeat the use of the cluster for other family members.">
				>
				["at0004"] = <
					text = <"Family member name">
					description = <"Name of family member.">
					comment = <"For example: 'Aunt Susan' or 'Susan Smith'. However, for privacy reasons this may not be appropriate for recording, sharing or public display and in this situation the 'Alias' should be used.">
				>
				["at0005"] = <
					text = <"Date of birth">
					description = <"Full or partial date of birth of the family member.">
				>
				["at0008"] = <
					text = <"Medical history">
					description = <"Detail about problems or diagnoses for the family member.">
				>
				["at0009"] = <
					text = <"Problem/diagnosis">
					description = <"Identification of the significant problem or diagnosis in the identified family member.">
					comment = <"Coding of the family member's problem or diagnosis with a terminology is preferred, where possible. May link from this data element to a detailed record of a Problem/Diagnosis using the EVALUATION.problem_diagnosis archetype with the Subject of Care set to the family member, not to the patient.">
				>
				["at0010"] = <
					text = <"Age at onset">
					description = <"Estimated or actual age of the family member when the problem/diagnosis was clinically recognised.">
					comment = <"For health problems with multiple occurrences, this describes the first nown occurrence.">
				>
				["at0011"] = <
					text = <"Age at death">
					description = <"Exact or estimated age of the family member at death.">
					comment = <"Age of death can be useful if the problem/diagnosis which caused their death is being considered as a risk factor for the subject of the health record. For example: death of mother at young age increases the risk of ">
				>
				["at0012"] = <
					text = <"Clinical description">
					description = <"Narrative description or comments about clinical aspects of the family member's problem/diagnosis.">
				>
				["at0014"] = <
					text = <"Cause of death?">
					description = <"True if the problem/diagnosis was a direct cause of death in this family member.">
				>
				["at0016"] = <
					text = <"Relationship">
					description = <"The relationship of the family member to the subject of care.">
					comment = <"For example: mother, father, maternal grandmother, or paternal uncle. Coding of the relationship with a terminology is preferred, where possible and including specification of maternal and paternal as required.">
				>
				["at0020"] = <
					text = <"Alias">
					description = <"An alternative name or label to uniquely identify a family member, without using a personal name which might publicly identify the individual.">
					comment = <"To be used to assist in distinguishing one individual from multiple family members with identical relationships. For example, the label to distinguish one specific sister from three known sisters might be 'eldest sister' 'sister with the red hair' or 'sister #1'.">
				>
				["at0022"] = <
					text = <"Biomarker description">
					description = <"Description of risk-related biological markers identified in this family member.">
				>
				["at0023"] = <
					text = <"Deceased?">
					description = <"Is the family member is deceased?">
					comment = <"Record as 'True' if family member is deceased.">
				>
				["at0024"] = <
					text = <"Biomarkers">
					description = <"Detailed information about measurable indicators of a biological state or condition of the subject of record.">
					comment = <"For example: detailed information on BRCA mutations in family members. 
Note: More data elements will be needed in future to record detailed genetic marker information.">
				>
				["at0025"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0026"] = <
					text = <"Last Updated">
					description = <"The date this Family History Summary was last updated.">
				>
				["at0027"] = <
					text = <"Biomarker details">
					description = <"Structured details about biological markers.">
				>
				["at0045"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0046"] = <
					text = <"Comment">
					description = <"Additional narrative about the family member not captured in other fields.">
				>
				["at0048"] = <
					text = <"Family member">
					description = <"Structured detail about the identified family member.">
				>
				["at0053"] = <
					text = <"Multimedia">
					description = <"Mulitimedia representation of the family history.">
					comment = <"For example: a pedigree chart.">
				>
				["at0058"] = <
					text = <"Date of death">
					description = <"Full or partial date of death of the family member.">
					comment = <"Date of death may be useful in some situations in which the month of death may trigger decision support or identify groupings of disease. For example: environmental allergens triggering respiratory exaccerbations; or events such as Christmas.">
				>
			>
		>
		["pt-br"] = <
			items = <
				["at0000"] = <
					text = <"História familiar">
					description = <"Resumo dos problemas significativos, relacionados à saúde, encontrados nos membros da família.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Resumo">
					description = <"Relato dos problemas, diagnósticos, marcadores biológicos, ambientais e psicossociais que tenham sido identificados em membros da família">
					comment = <"Use este campo para capturar informações não-estruturadas de história familiar registrada em relatos clínicos ou para importar dados textuais de sistema clínicos existentes ou legados.">
				>
				["at0003"] = <
					text = <"Por membro da família">
					description = <"Detalhes sobre um membro específico da família.">
					comment = <"Os elementos deste cluster se relacionam com o indivíduo identificado quer pelo seu nome ou alias. Repita o uso do cluster para outros membros da família.">
				>
				["at0004"] = <
					text = <"Nome do membro da família">
					description = <"Nome do membro da família.">
					comment = <"Por exemplo: 'Tia Susan' ou 'Susan Smith'. Entretanto, por questões de privacidade pode não ser apropriado para registro, por exposição pública; nesta situação o 'Alias' deve ser utilizado.">
				>
				["at0005"] = <
					text = <"Data de nascimento">
					description = <"Data de nascimento (completa ou parcial) do membro da família.">
				>
				["at0008"] = <
					text = <"História clínica">
					description = <"Detalhes sobre problemas ou diagnósticos do membro da família.">
				>
				["at0009"] = <
					text = <"Problemas/diagnósticos">
					description = <"Identificação de problema ou diagnóstico significativo do membro identificado da família.">
					comment = <"Quando possível, dar preferência a codificação do problema ou diagnóstico do membro da família com uma terminologia. Pode fazer a ligação deste dado com um relato detalhado de um Problema/Diagnóstico utilizando o arquétipo EVALUATION.problem_diagnosis com o Sujeito do Cuidado como o membro da família, não o paciente.">
				>
				["at0010"] = <
					text = <"Idade da detecção do problema/diagnóstico">
					description = <"Idade (real ou estimada) do membro da família quando o problema/diagnóstico foi reconhecido clinicamente.">
					comment = <"Para problemas de saúde com múltiplas ocorrências, este descreve a primeira ocorrência conhecida.">
				>
				["at0011"] = <
					text = <"Idade do falecimento">
					description = <"Idade exata ou estimada do membro da família quando da sua morte.">
					comment = <"Idade da morte pode ser útil e o problema/diagnóstico que causou a morte sendo considerado como um fator de risco para o sujeito do registro de saúde. Por exemplo: morte materna em idade jovem aumenta o risco de...">
				>
				["at0012"] = <
					text = <"Descrição clínica">
					description = <"Descrição narrativa ou comentários sobre aspectos clínicos dos problemas/diagnósticos do membro da família.">
				>
				["at0014"] = <
					text = <"Causou a morte?">
					description = <"Verdadeiro se o problema/diagnóstico teve causa direta com a morte deste membro da família.">
				>
				["at0016"] = <
					text = <"Grau de parentesco">
					description = <"A relação do membro da família com o sujeito do cuidado.">
					comment = <"Por exemplo: mãe, pai, avó materna ou tio paterno. Dar preferência por codificação do relacionamento utilizando uma terminologia quando possível e incluir a especificação 'materno' ou 'paterno' quando cabível.">
				>
				["at0020"] = <
					text = <"Alias">
					description = <"Um nome ou rótulo alternativo que identifica unicamente um membro da família sem utilizar uma denominação pessoal que possa identificar publicamente o indivíduo.">
					comment = <"Para seu usado para auxiliar na distinção de um indivíduo de múltiplos membros da família com relacionamentos idênticos. Por exemplo, um rótulo para distinguir uma irmã específica das outras três irmãs pode ser: 'irmã mai velha', 'irmã ruiva' ou 'irmã # 1'.">
				>
				["at0022"] = <
					text = <"Descrição do marcador biológico">
					description = <"Descrição de marcadores biológicos relacionados a riscos identificados neste membro da família.">
				>
				["at0023"] = <
					text = <"Falecido?">
					description = <"Este membro da família está morto?">
					comment = <"Registre como 'Verdadeiro' se o membro da família estiver morto.">
				>
				["at0024"] = <
					text = <"Marcadores biológicos">
					description = <"Informações detalhadas sobre indicadores mensuráveis de um estado ou condição biológica do sujeito do registro.">
					comment = <"Por exemplo: informação detalhada sobre mutações em membros da família do BRCA. Nota: mais elementos serão necessários no futuro para registrar informações de marcadores genéticos detalhadas.">
				>
				["at0025"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0026"] = <
					text = <"Última atualização">
					description = <"Data da última atualização deste Sumário de história familiar.">
				>
				["at0027"] = <
					text = <"Detalhes do marcador biológico">
					description = <"Detalhes estruturados sobre marcadores biológicos.">
				>
				["at0045"] = <
					text = <"Extensão">
					description = <"Informações adicionais necessárias para capturar conteúdo local ou para alinhar outros modelos/formalismos de referência.">
					comment = <"Por exemplo: requerimentos de informações locais ou metadados adicionais para alinhar com equivalentes a FHIR ou CIMI.">
				>
				["at0046"] = <
					text = <"Comentários">
					description = <"Narrativa adicional sobre membro da família não contemplado em outro campo.">
				>
				["at0048"] = <
					text = <"Membro da família">
					description = <"Detalhe estruturado sobre o membro da família identificado.">
				>
				["at0053"] = <
					text = <"Multimedia">
					description = <"Representação multimedia da história da família.">
					comment = <"Por exemplo: árvore genealógica.">
				>
				["at0058"] = <
					text = <"Data de falecimento">
					description = <"Data completa ou parcial do falecimento do membro da família.">
					comment = <"Data do falecimento pode ser útil em algumas situações em que o mês da morte pode disparar necessidade de decisões ou identificar grupos de doenças. Por exemplo:alérgenos ambientais disparando exacerbações respiratórias; ou eventos como o Natal.">
				>
			>
		>
	>

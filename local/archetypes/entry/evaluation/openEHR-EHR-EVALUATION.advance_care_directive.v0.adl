archetype (adl_version=1.4; uid=3f089dbd-3de2-4a73-9614-a6e95c3d253b)
	openEHR-EHR-EVALUATION.advance_care_directive.v0

concept
	[at0000]	-- Advance care directive
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heidi Koikkalainen">
		["organisation"] = <"NES Digital Service, Edinburgh Napier University">
		["email"] = <"hk.koikkalainen@gmail.com">
		["date"] = <"2019-10-23">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"A framework to communicate the preferences of an individual for future medical treatment or end-of-life care.">
			use = <"A framework to communicate the preferences of an individual for future medical treatment or end-of-life care.

Advance care directive may also be known as living will, advance directive, advance decision, advance decision to refuse treatment, personal directive, advance healthcare directive, or medical directive. 

An individual with capacity may create an advance care directive to record their preferences for medical care and treatment in advance, which is intended to guide decision-making in future situations in which the individual is unable to make or communicate decisions. 

An advance care directive is commonly used to refuse life-sustaining treatment which may include, but is not limited to, cardiopulmonary resuscitation (CPR), clinically assisted nutrition and hydration, artificial or mechanical ventilation, and antibiotics for life-threatening infections. In addition, it could include preferences and instructions for future health care, living arrangements and personal matters.

This archetype has been specifically designed to hold a nested advance care directive details CLUSTER archetype, which will contain specific details as per national or other local requirements. In some countries, an advance care directive is legally persuasive without having an official legal status. In others it is a legally-binding document, and it MUST be ensured that the Advance Care Directive archetype and any nested archetypes adhere to relevant legal requirements.">
			keywords = <"living, will, advance, advanced, directive, decision, legal, preference, EoL, DNR, DNACPR", ...>
			misuse = <"Not to be used to record organ donation preferences. Use specific archetypes for this purpose.

Not to be used to record details of a Power of Attorney or other legal representative/proxy. Use specific archetypes for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "steve baguley, NDS, United Kingdom", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "Marcos Barreto, Universidade Federal da Bahia (UFBA), Brazil", "SB Bhattacharyya, Sudisa Consultancy Services, India", "Greg Burch, Tiny Medical Apps, United Kingdom", "Santiago Cammi, Argentina", "Ady Angelica Castro Acosta, CIBERES-Hospital 12 de Octubre, Spain", "Grant Forrest, NHS Scotland, United Kingdom", "Joost Holslag, Nedap, Netherlands", "Evelyn Hovenga, EJSH Consulting, Australia", "Keltie Jamieson, NSHA, Canada", "Gorazd Kalan, University Medical Centre Ljubljana, Slovenia", "Lars Morgan Karlsen, DIPS ASA, Norway", "Heidi Koikkalainen, United Kingdom (openEHR Editor)", "Tomi Laptoš, Marand, Slovenia", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Neranga Liyanaarachchi, Ministry of Health, Postgraduate Institute of Medicine, Sri Lanka", "Manisha Mantri, C-DAC, India", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Paul Miller, SCIMP NHS Scotland, United Kingdom", "Bjoern Naess, DIPS ASA, Norway", "Arunakiry Natarajan, management4health, Germany", "Andrej Orel, Marand d.o.o., Slovenia", "Jayashree Panickar, Karolinska Institute, Sweden", "Vanessa Pereira, FMUP, Portugal", "SARA PRETE, Abinsula, Italy", "clovis puttini, Core Consulting, Brazil", "Rowan Thomas, St. Vincent's Hospital Melbourne, Australia", "Katrin Troeltzsch, Nationales Centrum für Tumorerkrankungen (NCT); Universitätsklinikum Heidelberg, Germany", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)", "Marit Alice Venheim, Helse Vest IKT, Norway", "Wouter Zanen, Eurotranplant, Netherlands", "Lin Zhang, Taikang Insurance Group, China">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"AdvanceDirective-v3.1(2019EN) - Zorginformatiebouwstenen [Internet]. Zibs.nl. 2020 [cited 2020 Feb 17]. Available from: https://zibs.nl/wiki/AdvanceDirective-v3.1(2019EN)

Advance Decision (Living Will) pack | Compassion in Dying [Internet]. Compassion in Dying. 2019 [cited 2019 Nov 28]. Available from: https://compassionindying.org.uk/library/advance-decision-pack/

Goals of Care Designation Order | Alberta Health Services [Internet]. Albertahealthservices.ca. 2020 [cited 2020 Feb 17]. Available from: https://www.albertahealthservices.ca/frm-103547.pdf

UpToDate [Internet]. Uptodate.com. 2020 [cited 2020 Feb 17]. Available from: https://www.uptodate.com/contents/discussing-goals-of-care">
		["current_contact"] = <"Heidi Koikkalainen, hk.koikkalainen@gmail.com">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"36EF7FB18A7413DC4AB9A080AB7E25BB">
		["build_uid"] = <"050a0719-8a75-4fc0-a625-8f9de80ace03">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Advance care directive
		data matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0005] occurrences matches {0..1} matches {    -- Type of directive
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {    -- Status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0044,    -- Present
									at0045,    -- Absent
									at0047]    -- Unknown
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0007] occurrences matches {0..*} matches {    -- Condition
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0052] occurrences matches {0..*} matches {    -- Directive details
						include
							archetype_id/value matches {/.*/}
					}
					CLUSTER[at0058] occurrences matches {0..*} matches {    -- Location details
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0030] occurrences matches {0..1} matches {    -- Location
								value matches {
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0059] occurrences matches {0..1} matches {    -- Person
								include
									archetype_id/value matches {/.*/}
							}
							allow_archetype CLUSTER[at0060] occurrences matches {0..1} matches {    -- Multimedia representation
								include
									archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia\.v0/}
							}
						}
					}
					ELEMENT[at0038] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0010] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0053] occurrences matches {0..1} matches {    -- Valid period start
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0054] occurrences matches {0..1} matches {    -- Valid period end
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0056] occurrences matches {0..1} matches {    -- Review due date
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0055] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0025] occurrences matches {0..*} matches {    -- Witness
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0027] occurrences matches {0..*} matches {    -- Mandate
						value matches {
							DV_TEXT matches {*}
							DV_URI matches {*}
						}
					}
					allow_archetype CLUSTER[at0061] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Advance care directive">
					description = <"A framework to communicate the preferences of an individual for future medical treatment or end-of-life care.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Status">
					description = <"The status of the advance care directive.">
					comment = <"Coding of the advance care directive status with a terminology is preferred, where possible.">
				>
				["at0005"] = <
					text = <"Type of directive">
					description = <"The type of advance care directive.">
					comment = <"A short text description of the nature of the advance care directive. Coding of the type of directive with a terminology is preferred, where possible. It is expected that this is largely localised to reflect local policy and legislation.

For example, in the Netherlands, advance care directive types include, but are not limited to, 'Treatment prohibition', 'Treatment prohibition with completion of Completed Life', 'Euthanasia request' and 'Declaration of life'.

In the UK, advance care directive types include 'Advance Decision', 'Advance Directive' and 'Advance Statement'.">
				>
				["at0006"] = <
					text = <"Description">
					description = <"Narrative description of the overall advance care directive.">
					comment = <"May be used to record a narrative overview of the complete advance care directive, which may or may not be supported by structured data. Details of specific structured findings can be included using CLUSTER archetypes in the 'Directive details' slot. This data element may be used to capture legacy data that is not available in a structured format.">
				>
				["at0007"] = <
					text = <"Condition">
					description = <"The conditions or situations in which the individual wishes the advance care directive to apply.">
					comment = <"For example: dementia, brain injury, diseases of the central nervous system, and terminal illness. Coding with a terminology is preferred, where possible.

The advance care directive applies to all specified conditions if the individual can no longer make or communicate decisions about their medical treatment and is unlikely to regain the ability to make such decisions. Details of specific decisions that apply to different conditions or situations can be included using CLUSTER archetypes in the 'Directive details' slot.
">
				>
				["at0010"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0025"] = <
					text = <"Witness">
					description = <"Personal details of a person who witnesses the completion of the advance care directive.">
					comment = <"For example, 'John Smith, Lawyer'.">
				>
				["at0027"] = <
					text = <"Mandate">
					description = <"Description of any legislation or other authoritative guidance that apply.">
					comment = <"For example, 'In England and Wales, advance decisions are covered by the Mental Capacity Act.  Mandate: https://www.bma.org.uk/advice/employment/ethics/consent/consent-tool-kit/9-advance-decisions'. 

Or 'Jehovah's Witnesses believe that the Bible prohibits Christians from accepting blood transfusions. Mandate: https://en.wikipedia.org/wiki/Jehovah%27s_Witnesses_and_blood_transfusions'.">
				>
				["at0030"] = <
					text = <"Location">
					description = <"Information regarding where the advance care directive is stored and how to gain access to it.">
					comment = <"For example, 'In the top drawer of the bedside table'.">
				>
				["at0038"] = <
					text = <"Comment">
					description = <"Additional narrative about the advance care directive not captured in other fields.">
				>
				["at0044"] = <
					text = <"Present">
					description = <"The individual has an advance care directive.">
				>
				["at0045"] = <
					text = <"Absent">
					description = <"The individual does not have an advance care directive.">
				>
				["at0047"] = <
					text = <"Unknown">
					description = <"It is not known whether the individual has an advance care directive.">
				>
				["at0052"] = <
					text = <"Directive details">
					description = <"Slot to include CLUSTER archetypes containing specific details about the advance care directive decisions as per national or other local requirements.">
					comment = <"For example, in the UK, there may be a specific question about whether to actively prolong life but only during pregnancy.">
				>
				["at0053"] = <
					text = <"Valid period start">
					description = <"The date/time that marks the beginning of the valid period of time for this advance care directive.">
				>
				["at0054"] = <
					text = <"Valid period end">
					description = <"The date/time that marks the conclusion of the valid period of time for this advance care directive.">
					comment = <"'Valid period end' may often overlap with 'Review due date'. However, they may need to be recorded separately in circumstances where a document has an extended period of validity but requires an interim review. That may be due to changed personal circumstances/events or local policy.">
				>
				["at0055"] = <
					text = <"Last updated">
					description = <"The date when this advance directive record was last updated. This may not be a formal review but e.g. a typo correction.">
				>
				["at0056"] = <
					text = <"Review due date">
					description = <"The date at which the advance care directive is due to be reviewed.">
					comment = <"'Valid period end' may often overlap with 'Review due date'. However, they may need to be recorded separately in circumstances where a document has an extended period of validity but requires an interim review. That may be due to changed personal circumstances/events or local policy.">
				>
				["at0058"] = <
					text = <"Location details">
					description = <"Information regarding where the advance care directive is stored and who has a copy of it.">
				>
				["at0059"] = <
					text = <"Person">
					description = <"Details of a person who has a copy of the advance care directive.">
					comment = <"For example, 'John Smith, Lawyer'.">
				>
				["at0060"] = <
					text = <"Multimedia representation">
					description = <"Digital image, video or diagram representing the advance care directive.">
					comment = <"For example, 'www.dropbox.com/myadvancecaredirective'.">
				>
				["at0061"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
			>
		>
	>

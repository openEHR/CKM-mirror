archetype (adl_version=1.4; uid=4a19faa1-44c6-4426-89e5-2e312f18d513)
	openEHR-EHR-EVALUATION.treatment_directive.v0

concept
	[at0000]	-- Ceilings of treatment directive
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Vebjørn Arntzen, John Tore Valand">
				["organisation"] = <"Oslo University Hospital, Helse Bergen">
				["email"] = <"varntzen@ous-hf.no, john.tore.valand@helse-bergen.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics, Australia">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2020-07-31">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere en forhåndsbeslutning om den overordnede intensjonen med omsorg/behandling, samt formålstjenlig medisinsk behandling eller intervensjoner for et individ. Denne bestemmelsen er vanligvis tatt av en eller flere klinikere som er autorisert til det.">
			use = <"For å registrere en forhåndsbeslutning om den overordnede intensjonen med omsorg/behandling, samt formålstjenlig medisinsk behandling eller intervensjoner for et individ. Slike beslutninger er individuelt tilpasset individets helse og situasjon. Beslutningene blir vanligvis tatt av en eller flere klinikere som er autorisert til det, etter også å ha tatt hensyn til individets ønsker i form av livstestamente eller andre oppgitte preferanser.

Behandlingsbegrensning er ment for å registrere gjennomtenkte kliniske beslutninger om mulig fremtidige behandlinger og intervensjoner. De er vanligvis tatt av en autorisert kliniker eller gruppe av klinikere som er i stand til å nøye vurdere individets helsetilstand og situasjon på et overordnet nivå, samt individets preferanser knyttet til fremtidig behandling i form av livstestamente eller andre utsagn. Instruksene hjelper klinikere med behandlingsansvar i beslutninger om det overordnede målet med behandling, og hvorvidt det er passende at Hjerte-Lungeredning og annen potensielt livsforlengende eller invaderende intervensjoner skal gjøres. Ideelt sett skal beslutningene være en integrert del av behandlingsplan eller plan for omsorg i livets sluttfase, og ikke gjort under tidspress i en akutt eller nødsituasjon.">
			keywords = <"HLR-minus, R minus, Resucitering, DNR, Do not resucitate, EoL, end of life, direktiv, behandlingsmål, intervensjon, beslutning", ...>
			misuse = <"Skal ikke brukes for å registrere individets livstestament eller andre personlige ønsker, bruk i stedet arketypen \"Fremtidig behandlingsvalg\" (EVALUATION.advance_care_directive).">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record an anticipatory directive about the overall intent of care and appropriate medical treatments or interventions for an individual, usually asserted by an authorised clinician.">
			use = <"Use to record an anticipatory directive about the overall intent of care and appropriate medical treatments or interventions for an individual. These directives are personalised in response to the individual's overall health and circumstances, usually asserted by an authorised clinician after consideration of the individual's advance care directives or stated preferences.

The ceilings of treatment directive is intended to be asserted and recorded as a deliberate group of clinical decisions about possible future treatments and interventions. They are usually made by an authorised clinician who is able to carefully consider the individual's overall health condition and broader circumstances and the individual's advance care directive or stated preferences for care. The directives assist treating clinicians in future clinical decision-making by providing guidance about the overall intent of care and appropriateness of CPR and other potentially life-prolonging or invasive interventions. Ideally, the directives are established as part of integrated care, or end-of-life, planning and not under duress in response to an urgent or emergency situation.">
			keywords = <"DNR", "DNAR", "DNACPR", "NFR", "resuscitation", "resuscitate", "EoL", "end of life", "directive", "preference", "goal", "care", "intervention", "ceiling", "limit", "limitation", "treatment">
			misuse = <"Not to be used to record an 'Advance care directive' or the individual's preferences for care - use EVALUATION.advance_care_directive for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Dag Aarhus, Vestre Viken HF, Norway", "Tor Aksel Aasmundstad, Oslo University Hospital, Norway", "Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Kari-Ann Baarlid, Nasjonalforeningen for folkehelsen, Norway", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "Terje Bektesevic Holmlund, UiT Norges arktiske universitet, Norway", "SB Bhattacharyya, Sudisa Consultancy Services, India", "Greg Burch, Tiny Medical Apps, United Kingdom", "Gry Caroline Aarnes, Nasjonalforeningen for folkehelsen, Norway", "Fatemeh Chalabianloo, Helse Bergen, Norway", "Oona Dunlop, Oslo University Hospital, Norway", "Vibeche Fashing, Norwegian Cancer Society, Norway", "Grant Forrest, Lunaria Ltd, United Kingdom", "Reidun Førde, Prof emirata, University of Oslo, Norway", "Maria G, Norway", "Anne Grimstvedt Kvalvik, Haraldsplass Diakonale sykehus, Norway", "Mikkel Johan Gaup Grønmo, Forvaltningssenter EPJ, Helse-Nord, Norway (openEHR Editor)", "Joost Holslag, Nedap, Netherlands", "Evelyn Hovenga, EJSH Consulting, Australia", "Keltie Jamieson, NSHA, Canada", "Heidi Koikkalainen, United Kingdom", "Tomi Laptoš, Marand, Slovenia", "Liv Laugen, ​Oslo University Hospital, Norway, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "Per Meinich, Helse Sør-Øst RHF, Norway", "Bjoern Naess, DIPS ASA, Norway", "Arunakiry Natarajan, management4health, Germany", "Svenne Naumann, Finnmarkssykehuset, Norway", "Andrej Orel, Marand d.o.o., Slovenia", "Vanessa Pereira, Better - Pathfinder, Portugal", "Anne Rita Øksengård, Nasjonalforeningen for folkehelsen, Norway", "Nina Saastad, Norwegian Cancer Society, Norway", "Joran Slaaen, Norwegian Cancer Society, Norway", "Norwegian Review Summary, Nasjonal IKT HF, Norway", "Nyree Taylor, Ocean Informatics, Australia", "Rowan Thomas, St. Vincent's Hospital Melbourne, Australia", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)", "Marit Alice Venheim, Helse Vest IKT, Norway (openEHR Editor)", "Lin Zhang, Taikang Insurance Group, China">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Breault JL. DNR, DNAR, or AND? Is Language Important? Ochsner J. 2011 Winter;11(4):302-6. PMID: 22190879; PMCID: PMC3241061.

Salins N, Gursahani R, Mathur R, Iyer S, Macaden S, Simha N, Mani RK, Rajagopal MR. Definition of Terms Used in Limitation of Treatment and Providing Palliative Care at the End of Life: The Indian Council of Medical Research Commission Report. Indian J Crit Care Med. 2018 Apr;22(4):249-262. doi: 10.4103/ijccm.IJCCM_165_18. PMID: 29743764; PMCID: PMC5930529.

Thomas RL, Zubair MY, Hayes B, Ashby MA. Goals of care: a clinical framework for limitation of medical treatment. Med J Aust. 2014 Oct 20;201(8):452-5. doi: 10.5694/mja14.00623. PMID: 25332031.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"B15B9C2BEDBFA1DEB2D51D99345D0EAF">
		["build_uid"] = <"00551ab5-29e9-4aba-805d-bc59623faa03">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Ceilings of treatment directive
		data matches {
			ITEM_TREE[at0001] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0002] occurrences matches {0..1} matches {    -- Intent of care
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0003] occurrences matches {0..1} matches {    -- Rationale
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {    -- CPR recommendation
						value matches {
							DV_TEXT matches {*}
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0005,    -- Attempt CPR
									at0006,    -- Attempt modified CPR
									at0007,    -- Do not attempt CPR
									at0008]    -- Unknown
								}
							}
						}
					}
					ELEMENT[at0009] occurrences matches {0..1} matches {    -- CPR modification
						value matches {
							DV_TEXT matches {*}
						}
					}
					CLUSTER[at0010] occurrences matches {0..*} matches {    -- Per intervention
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0011] matches {    -- Intervention
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0012,    -- Intubation
											at0013,    -- Mechanical ventilation
											at0014,    -- Non-invasive ventilation
											at0015,    -- Circulatory regulation
											at0016,    -- Dialysis
											at0017,    -- Blood products
											at0018,    -- Antibiotics, antiviral or antifungal agents
											at0019,    -- Parenteral or artificial nutrition
											at0020,    -- Transfer to Intensive Care
											at0021,    -- Transfer to hospital
											at0022,    -- Parenteral fluids
											at0023]    -- IV access
										}
									}
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0024] occurrences matches {0..1} matches {    -- Decision
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0025,    -- Recommended
											at0026,    -- Recommended, with caveat
											at0027,    -- Not recommended
											at0028]    -- Unknown
										}
									}
								}
							}
							ELEMENT[at0029] occurrences matches {0..1} matches {    -- Caveat
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0030] occurrences matches {0..1} matches {    -- Intervention comment
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
					allow_archetype CLUSTER[at0031] occurrences matches {0..*} matches {    -- Additional details
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0032] occurrences matches {0..1} matches {    -- Patient awareness
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0033] occurrences matches {0..1} matches {    -- Carer awareness
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0034] occurrences matches {0..*} matches {    -- Digital representation
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia_source\.v0/}
					}
					ELEMENT[at0035] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0036] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0037] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0038] occurrences matches {0..1} matches {    -- Valid period start
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0039] occurrences matches {0..1} matches {    -- Valid period end
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0040] occurrences matches {0..1} matches {    -- Review due
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0041] occurrences matches {0..1} matches {    -- Mandate
						value matches {
							DV_TEXT matches {*}
							DV_EHR_URI matches {*}
						}
					}
					CLUSTER[at0042] occurrences matches {0..1} matches {    -- Directive location
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0043] occurrences matches {0..1} matches {    -- Location
								value matches {
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0044] occurrences matches {0..*} matches {    -- Copy holder
								include
									archetype_id/value matches {/.*/}
							}
						}
					}
					allow_archetype CLUSTER[at0045] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Ceilings of treatment directive">
					description = <"Anticipatory directive about appropriate medical treatments, usually asserted by an authorised clinician.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Intent of care">
					description = <"Description of the overall intent for future care and treatment for the individual.">
					comment = <"For example: the values suggested in the Thomas et al MJA reference - 'Curative', 'Restorative', 'Supportive', 'Terminal'; and 'Unknown'.">
				>
				["at0003"] = <
					text = <"Rationale">
					description = <"Narrative description about the logic and justification for the 'Intent of care' statement.">
				>
				["at0004"] = <
					text = <"CPR recommendation">
					description = <"Directive about future cardiopulmonary resuscitation (CPR) intervention.">
				>
				["at0005"] = <
					text = <"Attempt CPR">
					description = <"All appropriate cardiopulmonary resuscitation treatments should be attempted to save or prolong life.">
				>
				["at0006"] = <
					text = <"Attempt modified CPR">
					description = <"Some cardiopulmonary resuscitation treatments should be attempted. See 'CPR modification' for details.">
				>
				["at0007"] = <
					text = <"Do not attempt CPR">
					description = <"No cardiopulmonary resuscitation treatments should be attempted to save or prolong life. All treatments to be focused on symptom relief and quality of life. Also known as 'Do not resuscitate (DNR)'; 'Do not attempt resuscitation (DNAR)'; 'Not for resuscitation (NFR)' or similar.">
				>
				["at0008"] = <
					text = <"Unknown">
					description = <"CPR guidance decision is not known.">
				>
				["at0009"] = <
					text = <"CPR modification">
					description = <"Description of the recommended CPR modification.">
					comment = <"This data element is only relevant if 'Attempt modified CPR' is selected in the 'CPR recommendation' data element.">
				>
				["at0010"] = <
					text = <"Per intervention">
					description = <"Details of the decision about a treatment or intervention.">
				>
				["at0011"] = <
					text = <"Intervention">
					description = <"Name of the intervention.">
					comment = <"The proposed DV_CODED_TEXT value set for the 'Intervention' data element is not intended to be exhaustive but to include many of the common life-saving treatments. The alternate choice of the DV_TEXT option can be used to add a local value set or bind to an external value set within a template.">
				>
				["at0012"] = <
					text = <"Intubation">
					description = <"Placement of a flexible plastic tube into the trachea to maintain an open airway.">
				>
				["at0013"] = <
					text = <"Mechanical ventilation">
					description = <"Artificial ventilation where mechanical means are used to assist or replace spontaneous breathing.">
				>
				["at0014"] = <
					text = <"Non-invasive ventilation">
					description = <"Artificial ventilation where non-invasive means are used to assist or replace spontaneous breathing.">
				>
				["at0015"] = <
					text = <"Circulatory regulation">
					description = <"Medication that supports heart function, including but not limited to: vasopressors, inotropes and chronotropes.">
				>
				["at0016"] = <
					text = <"Dialysis">
					description = <"Process of removing excess water, solutes, and toxins from the blood.">
				>
				["at0017"] = <
					text = <"Blood products">
					description = <"Therapeutic substance prepared from human blood including, but not limited to: whole blood; blood components; and plasma derivatives.">
				>
				["at0018"] = <
					text = <"Antibiotics, antiviral or antifungal agents">
					description = <"Medications to treat infections.">
				>
				["at0019"] = <
					text = <"Parenteral or artificial nutrition">
					description = <"Provision of nutrients by means other than eating or drinking.">
				>
				["at0020"] = <
					text = <"Transfer to Intensive Care">
					description = <"Transfer and admission to an intensive care or high dependency unit.">
				>
				["at0021"] = <
					text = <"Transfer to hospital">
					description = <"Transfer to hospital for assessment or admission.">
				>
				["at0022"] = <
					text = <"Parenteral fluids">
					description = <"Maintenance of hydration by means other than eating or drinking.">
				>
				["at0023"] = <
					text = <"IV access">
					description = <"Insertion of an intravenous line.">
				>
				["at0024"] = <
					text = <"Decision">
					description = <"Decision about the identified intervention.">
				>
				["at0025"] = <
					text = <"Recommended">
					description = <"The treatment is recommended, if clinically appropriate.">
				>
				["at0026"] = <
					text = <"Recommended, with caveat">
					description = <"The treatment is recommended, although modified by the conditions of the caveat.">
				>
				["at0027"] = <
					text = <"Not recommended">
					description = <"The treatment is not recommended.">
				>
				["at0028"] = <
					text = <"Unknown">
					description = <"The treatment decision is not known.">
				>
				["at0029"] = <
					text = <"Caveat">
					description = <"Description of any criteria, qualification, exceptions or further clarification about the intervention.">
					comment = <"This data element is only relevant if 'Recommended, with caveat' option is selected in the 'Decision' data element. For example: the types of antibiotics that would be acceptable.">
				>
				["at0030"] = <
					text = <"Intervention comment">
					description = <"Additional narrative about the intervention decision, not captured in other fields.">
					comment = <"For example: the justification or rationale for the intervention decision.">
				>
				["at0031"] = <
					text = <"Additional details">
					description = <"Additional structured details about emergency care and treatment.">
					comment = <"For example: people involved in the decision-making process, if it is not possible to use the reference model participations; or additional locally relevant considerations.">
				>
				["at0032"] = <
					text = <"Patient awareness">
					description = <"Description about awareness of the ceilings of care directives by the individual.">
					comment = <"Coding with an external terminology is optional.">
				>
				["at0033"] = <
					text = <"Carer awareness">
					description = <"Description about awareness of the ceilings of care directives by family, carers or legal proxy.">
					comment = <"Coding with an external terminology is optional.">
				>
				["at0034"] = <
					text = <"Digital representation">
					description = <"Digital document, image or video representing the Ceilings of care directive.">
				>
				["at0035"] = <
					text = <"Comment">
					description = <"Additional narrative about the ceilings of care directives, not captured in other fields.">
				>
				["at0036"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0037"] = <
					text = <"Last updated">
					description = <"The date and/or time when this Ceilings of care directive was last updated.">
				>
				["at0038"] = <
					text = <"Valid period start">
					description = <"The date/time that marks the beginning of the valid period of time for this Ceilings of care directive.">
				>
				["at0039"] = <
					text = <"Valid period end">
					description = <"The date/time that marks the conclusion of the valid period of time for this Ceilings of care directive.">
					comment = <"'Valid period end' may often overlap with 'Review due date'. However, they may need to be recorded separately in circumstances where a document has an extended period of validity but requires an interim review. That may be due to changed personal circumstances/events or local policy.">
				>
				["at0040"] = <
					text = <"Review due">
					description = <"The date when review of this Ceilings of care directive is due.">
				>
				["at0041"] = <
					text = <"Mandate">
					description = <"Description of any legislation or other authoritative guidance that apply.">
				>
				["at0042"] = <
					text = <"Directive location">
					description = <"Information regarding where the ceilings of care directive is stored and who has a copy of it.">
				>
				["at0043"] = <
					text = <"Location">
					description = <"Information regarding where the Ceilings of care directive is stored.">
				>
				["at0044"] = <
					text = <"Copy holder">
					description = <"Details of a person who has a copy of the Ceilings of care directive.">
					comment = <"Comment: For example, 'John Smith, Lawyer'.">
				>
				["at0045"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Behandlingbegrensning">
					description = <"Forhåndsbeslutning om fremtidig formålstjenlig behandling, vanligvis bestemt av en autorisert kliniker.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Intensjon med behandling">
					description = <"Beskrivelse av den overordnede intensjonen med fremtidig behandling og omsorg for individet.">
					comment = <"For eksempel en norsk oversettelse av verdisett som foreslått i den refererte artikkelen i Medical Journal of Australia av Thomas et al: \"Curative\", \"Restorative\", \"Supportive\", \"Terminal\"; og \"Unknown\". Om det etableres en omforent slik liste på norsk, kan den tas inn i templat for deretter brukes i et elektronisk journalsystem.">
				>
				["at0003"] = <
					text = <"Begrunnelse">
					description = <"Fritekstlig begrunnelse om rasjonale og berettigelse for utsagnet om \"Intensjon med behandling\".">
				>
				["at0004"] = <
					text = <"HLR-anbefaling">
					description = <"Bestemmelse om fremtidig Hjerte- og Lungeredning (HLR).">
				>
				["at0005"] = <
					text = <"Forsøk HLR">
					description = <"Alle relevante tiltak innen hjerte-lunge-redning bør forsøkes for å redde eller forlenge livet.">
				>
				["at0006"] = <
					text = <"Forsøk begrenset HLR">
					description = <"Noen tiltak innen hjerte-lunge-redning bør forsøkes. Se detaljer i \"Modifisert HLR\".">
				>
				["at0007"] = <
					text = <"HLR minus">
					description = <"Ingen tiltak innen hjerte-lunge-redning bør utføres for å redde eller forlenge livet. All behandling skal være fokusert på symptomlindring og livskvalitet. Også kjent som \"Res minus\", \"Do not resuscitate (DNR)\"; 'Do not attempt resuscitation (DNAR)' eller lignende.">
				>
				["at0008"] = <
					text = <"Ukjent">
					description = <"Ingen kjent beslutning om hjerte- lungeredning (HLR).">
				>
				["at0009"] = <
					text = <"Modifisert HLR">
					description = <"Beskrivelse av hvilke modifikasjoner om HLR som er anbefalt.">
					comment = <"Dette dataelementet er bare relevant dersom \"Forsøk begrenset HLR\" er valgt i elementet \"HLR anbefaling\".">
				>
				["at0010"] = <
					text = <"Per intervensjon">
					description = <"Bestemmelse om en medisinsk intervensjon som kan redde eller forlenge livet.">
				>
				["at0011"] = <
					text = <"Intervensjon">
					description = <"Navn på intervensjonen.">
					comment = <"Verdisettet i det foreslåtte DV_CODED_TEXT for \"Intervensjon\" er ikke ment å være uttømmende, men å ta med seg mange av de vanlige livreddende behandlingene. Som alternativ kan man bruke DV_TEXT for å lage et lokalt verdisett som er passende, eller å binde til et eksternt verdisett (f.eks oppslag i Volven) i templat.">
				>
				["at0012"] = <
					text = <"Intubering">
					description = <"Et fleksibelt plastikkrør føres ned i luftrøret for å opprettholde åpne luftveier.">
				>
				["at0013"] = <
					text = <"Mekanisk ventilasjon">
					description = <"Kunstig åndedrett, der mekaniske tiltak er brukt for å assistere eller erstatte spontant åndedrett.">
				>
				["at0014"] = <
					text = <"Non-invasiv ventilasjon">
					description = <"Kunstig ventilasjon der ikke-invasive tiltak brukes for å assistere eller erstatte spontant åndedrett.">
				>
				["at0015"] = <
					text = <"Sirkulasjonsregulerende">
					description = <"Medikamenter som støtter hjertets funksjon, inkludert men er ikke begrenset til vasopressorer, inotrope- og chronotrope medikamenter.">
				>
				["at0016"] = <
					text = <"Dialyse">
					description = <"Prosess som fjerne overskuddsvæske, avfallsstoffer og toksiner fra blodet.">
				>
				["at0017"] = <
					text = <"Blodprodukter">
					description = <"Terapeutisk substans framstilt av humant blod. Inkluderer, men er ikke begrenset til: fullblod, blodkomponenter og plasmaderivater.">
				>
				["at0018"] = <
					text = <"Antibiotika, antiviralia og soppdrepende middel.">
					description = <"Medisiner for å behandle infeksjoner.">
				>
				["at0019"] = <
					text = <"Parenteral eller kunstig ernæring">
					description = <"Tilførsel av næringsstoffer via andre metoder enn spising og drikking.">
				>
				["at0020"] = <
					text = <"Overflytting til intensiv">
					description = <"Overflytting og innleggelse på en intensiv- eller overvåkingsenhet.">
				>
				["at0021"] = <
					text = <"Overflytting til sykehus">
					description = <"Overflytting til sykehus for vurdering eller innleggelse.">
				>
				["at0022"] = <
					text = <"Væskebehandling">
					description = <"Opprettholdelse av væskebalanse via andre metoder enn spising og drikking.">
				>
				["at0023"] = <
					text = <"IV tilgang">
					description = <"Innleggelse av intravenøst kateter.">
				>
				["at0024"] = <
					text = <"Beslutning">
					description = <"Beslutning om den valgte intervensjonen.">
				>
				["at0025"] = <
					text = <"Anbefalt">
					description = <"Behandlingen er anbefalt, dersom den er klinisk hensiktsmessig.">
				>
				["at0026"] = <
					text = <"Anbefalt med forbehold">
					description = <"Behandlingen er anbefalt, men med modifikasjoner som beskrevet i \"Forbehold\".">
				>
				["at0027"] = <
					text = <"Ikke anbefalt">
					description = <"Behandlingen er ikke anbefalt.">
				>
				["at0028"] = <
					text = <"Ukjent">
					description = <"Beslutning om behandling er ikke kjent.">
				>
				["at0029"] = <
					text = <"Forbehold">
					description = <"Beskrivelse av hvilket som helst kriterium, kvalifisering, unntak eller annen klargjøring om intervensjonen.">
					comment = <"Dette dataelementet er bare relevant dersom det er valgt \"Anbefalt med forbehold\" i dataelementet \"Beslutning\". For eksempel hvilke typer antibiotika som er akseptabelt.">
				>
				["at0030"] = <
					text = <"Kommentar til intervensjon">
					description = <"Ytterligere fritekst om beslutning om intervensjon, som ikke er fanget opp i andre felt.">
					comment = <"For eksempel rasjonale eller berettigelse for beslutning som er tatt om intervensjon.">
				>
				["at0031"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere strukturerte detaljer om øyeblikkelig hjelp og behandling.">
					comment = <"For eksempel om mennesker som var involvert i beslutningsprossesen dersom det er ikke er mulig å bruke elementet \"Participations\" i refransemodellen (RM), eller til andre lokale dokumentasjonsbehov.">
				>
				["at0032"] = <
					text = <"Pasient informert">
					description = <"Beskrivelse om individet er klar over beslutninger om behandlingsbegrensning.">
					comment = <"Det er mulig å kode dette med en ekstern terminologi eller lage et internt verdisett.">
				>
				["at0033"] = <
					text = <"Pårørende informert">
					description = <"Beskrivelse om individets pårørende, omsorgsgivere eller verge er klar over beslutninger om behandlingsbegrensning.">
					comment = <"Det er mulig å kode dette med en ekstern terminologi eller lage et internt verdisett.">
				>
				["at0034"] = <
					text = <"Digital representasjon">
					description = <"Digitalt dokument, bilde eller video som representerer det behandlingsbegrensningen.">
				>
				["at0035"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekstlig beskrivelse om avgjørelser om begrensning av behandling, og som ikke er fanget opp i andre felter.">
				>
				["at0036"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0037"] = <
					text = <"Sist oppdatert">
					description = <"Dato og/eller tid da oppføringer i behandlingsbegrensningen sist ble oppdatert.">
				>
				["at0038"] = <
					text = <"Gyldighetsperiode start">
					description = <"Dato/Tid som markerer når gyldighetsperioden av behandlingsbegrensningen starter.">
				>
				["at0039"] = <
					text = <"Gyldighetsperiode slutt">
					description = <"Dato/Tid som markerer når gyldighetsperioden av behandlingsbegrensningen opphører.">
					comment = <"\"Gyldighetsperiode slutt\" vil ofte overlappe med \"Dato for fornyelse\". Det kan imidlertid være nødvendig å registrere disse separat i tilfelle en behandlingsbegrensning har en forlenget gyldighetsperiode, men trenger en midlertidig gjennomgang. Dette kan skyldes endringer i personlige forhold/hendelser eller lokale retningslinjer.">
				>
				["at0040"] = <
					text = <"Dato for fornyelse">
					description = <"Dato når behandlingsbegrensningen må fornyes.">
				>
				["at0041"] = <
					text = <"Grunnlag">
					description = <"Beskrivelse av hvilken lovgivning eller annen autorativ retningslinje som gjelder for det behandlingsbegrensningen.">
				>
				["at0042"] = <
					text = <"Behandlingsbegrensningens plassering">
					description = <"Informasjon om hvor behandlingsbegrensningen er lagret (dersom det er et separat dokument eller i et annet system), samt hvem som har en kopi av det.">
				>
				["at0043"] = <
					text = <"Plassering">
					description = <"Informasjon om hvor behandlingsbegrensning er lagret.">
				>
				["at0044"] = <
					text = <"Kopiholder">
					description = <"Detaljer om person som har en kopi av behandlingsbegrensningen.">
					comment = <"For eksempel \"Advokat John Smith\".">
				>
				["at0045"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
			>
		>
	>

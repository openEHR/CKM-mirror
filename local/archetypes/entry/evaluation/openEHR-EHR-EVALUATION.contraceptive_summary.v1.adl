archetype (adl_version=1.4; uid=c00537a4-52ae-4ac0-b27e-81659a213233)
	openEHR-EHR-EVALUATION.contraceptive_summary.v1

concept
	[at0000]	-- Contraceptive use summary
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Marit Alice Venheim, John Tore Valand">
				["organisation"] = <"Helse Vest IKT, Helse Bergen">
				["email"] = <"marit.alice.venheim@helse-vest-ikt.no, john.tore.valand@helse-bergen.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2009-06-21">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere gjeldende status og detaljer om episoder hvor individet har brukt antikonsepsjon, og hvilke typer.">
			use = <"Brukes for å registrere gjeldende status og detaljer om episoder hvor individet har brukt antikonsepsjon, og hvilke typer.

Denne arketypen skal brukes til å registrere informasjon om både nåværende og tidligere bruk av antikonsepsjon.

Arketypen fokuserer spesifikt på dokumentasjon om bruken av alle typer antikonsepsjon, inkludert episoder der mer enn en type antikonsepsjon kan ha blitt brukt samtidig.

Clusteret 'Per type' gjør det mulig å registrere spesifikke detaljer og episoder om hver type antikonsepsjon som brukes, og kan bare gjentas en gang per type.

I mange situasjoner vil den enkelte bare bruke én type antikonsepsjon om gangen. Imidlertid, hvis andre typer antikonsepsjon brukes samtidig, registreres detaljene i en annen forekomst av clusteret 'Per type'.

For hver type antikonsepsjon kan brukshistorikken over tid registreres ved hjelp av det repeterbare clusteret \"Per episode\". Dette clusteret gjør det mulig å registrere et veldig detaljert bruksmønster for antikonsepsjon, om nødvendig.

Dataelementet \"Overordnet beskrivelse\" kan brukes til å overføre fritekstbeskrivelser fra eksisterende eller tidligere kliniske systemer til arketypeformat.">
			keywords = <"prevensjonsmiddel, hormon, prevensjon, fødsel, kontroll, svangerskapsprevensjon, familieplanlegging, prevensjonstiltak, kontrasepsjon, spiral, p-pille, hormonspiral, kondom", ...>
			misuse = <"Brukes ikke til å registrere detaljert informasjon om reseptbelagte medisiner. Lenker fra denne arketypen til den aktuelle \"Legemiddelordinering\" (INSTRUCTION.medication_order) eller \"Legemiddelhåndtering\" (ACTION.medication) bør brukes til å registrere detaljene.

Brukes ikke til å registrere detaljert informasjon om prosedyrene som er utført. Lenker fra denne arketypen til den aktuelle \"Prosedyre\" (ACTION.procedure) skal brukes til å registrere detaljene.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the current status and episodic details about specific types of contraception used by an individual.">
			use = <"Use to record the current status and episodic details about specific types of contraception used by an individual.

This archetype is to be used to record information about both current and previous use of contraception.

The specific scope of this archetype is on overall documentation about the use of all types of contraception, including episodes where more than one type of contraception may have been used simultaneously.

The 'Per type' cluster of data elements allows for recording of specific details and episodes about each type of contraception used and can be repeated only once per type. 

In many situations the individual will use only one type of contraception at a time. However, if other types of contraception are used at the same time, the details will be recorded in another instance of the 'Per type' cluster.

For each type of contraception the history of use over time can be captured using the repeatable 'Per episode' cluster. This cluster of data elements allows for a very detailed pattern of contraception use to be recorded, if necessary.

Use to incorporate the narrative descriptions of contraceptive history within existing or legacy clinical systems into an archetyped format, using the 'Overall description' data element.">
			keywords = <"contraceptive", "hormone", "contraception", "control", "family planning", "birth control measures">
			misuse = <"Not to be used to record detailed information about the medication prescription. Links from this archetype to the relevant INSTRUCTION.medication_order or ACTION.medication should be used to record the detail.

Not to be used to record detailed information about the procedures performed, such as an IUD insertion or a vasectomy. Links from this archetype to the relevant ACTION.procedure should be used to record the detail.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <"Miguel Aguiar Netto, INLAGS, Brazil", "Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "Terje Bektesevic Holmlund, UiT Norges arktiske universitet, Norway", "SB Bhattacharyya, Sudisa Consultancy Services, India", "Peter Fedorcsak, Oslo universitetssykehus, Norway", "Mikkel Gaup Grønmo, Helse-Nord, FSE, Norway (openEHR Editor)", "James Goddard, NHS Wales Informatics Service, United Kingdom", "Heather Grain, Llewelyn Grain Informatics, Australia", "Anne Grimstvedt Kvalvik, Haraldsplass Diakonale sykehus, Norway", "Mikkel Johan Gaup Grønmo, Forvaltningssenter EPJ, Helse-Nord, Norway (openEHR Editor)", "Anca Heyd, DIPS ASA, Norway", "Evelyn Hovenga, EJSH Consulting, Australia", "Nils Kolstrup, Skansen Legekontor og Nasjonalt Senter for samhandling og telemedisin, Norway", "Jörgen Kuylenstierna, eWeave AB, Sweden", "Liv Laugen, Oslo universitetssykehus, Norway (Nasjonal IKT redaktør)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "Johannes Michael Strehle, Helse-Nord, Norway", "Svenne Naumann, Finnmarkssykehuset, Norway", "Bjørn Næss, DIPS ASA, Norway", "sam philip, NHS Grampian, United Kingdom", "Jussara Rotzsch, Hospital Alemão Oswaldo Cruz, Brazil", "Danielle Santos Alves, Federal University of Pernambuco, Brazil", "Line Silsand, Universitetssykehuset i Nord-Norge, Norway", "Norwegian Review Summary, Nasjonal IKT HF, Norway", "Nyree Taylor, Ocean Informatics, Australia", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)", "Marit Alice Venheim, Helse Vest IKT, Norway (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"0ED359CA677901E7435742AC45DAF7CF">
		["build_uid"] = <"f4c30a3d-7cf7-44de-945d-feff45de7b0a">
		["revision"] = <"1.0.0">
	>

definition
	EVALUATION[at0000] matches {    -- Contraceptive use summary
		data matches {
			ITEM_TREE[at0001] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0089] occurrences matches {0..1} matches {    -- Overall status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0003,    -- Current user
									at0005,    -- Not current user
									at0006]    -- Never used
								}
							}
						}
					}
					ELEMENT[at0043] occurrences matches {0..1} matches {    -- Overall description
						value matches {
							DV_TEXT matches {*}
						}
					}
					CLUSTER[at0029] occurrences matches {0..*} matches {    -- Per type
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0151] occurrences matches {0..1} matches {    -- Type
								value matches {
									DV_TEXT matches {*}
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0155,    -- Combination pill
											at0157,    -- Progestogen-only pill
											at0156,    -- Combination skin patch
											at0158,    -- Depot progestogen injection
											at0159,    -- Hormone implant
											at0160,    -- Vaginal ring
											at0169,    -- IUD with hormon
											at0163,    -- IUD without hormon
											at0161,    -- Female sterilisation
											at0162,    -- Male sterilisation
											at0154,    -- Diaphragm
											at0167,    -- Vaginal douching
											at0153,    -- Female condom
											at0152,    -- Male condom
											at0164,    -- Withdrawal
											at0165,    -- Fertility awareness method
											at0166]    -- Abstinence
										}
									}
								}
							}
							ELEMENT[at0144] occurrences matches {0..1} matches {    -- Status
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0145,    -- Current user
											at0146,    -- Not current user
											at0147]    -- Never used
										}
									}
								}
							}
							ELEMENT[at0053] occurrences matches {0..1} matches {    -- Description
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0148] occurrences matches {0..1} matches {    -- Start date
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							CLUSTER[at0064] occurrences matches {0..*} matches {    -- Per episode
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0081] occurrences matches {0..1} matches {    -- Episode label
										value matches {
											DV_COUNT matches {
												magnitude matches {|>=1|}
											}
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0023] occurrences matches {0..1} matches {    -- Specific name
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0030] occurrences matches {0..1} matches {    -- Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0168] occurrences matches {0..1} matches {    -- Clinical indication
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0025] occurrences matches {0..1} matches {    -- Goal
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0013] occurrences matches {0..1} matches {    -- Episode start date
										value matches {
											DV_DATE matches {*}
										}
									}
									ELEMENT[at0082] occurrences matches {0..1} matches {    -- Episode end date
										value matches {
											DV_DATE matches {*}
										}
									}
									ELEMENT[at0074] occurrences matches {0..1} matches {    -- Reason for cessation
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at0026] occurrences matches {0..*} matches {    -- Episode details
										include
											archetype_id/value matches {/.*/}
									}
									ELEMENT[at0087] occurrences matches {0..1} matches {    -- Episode comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
							ELEMENT[at0149] occurrences matches {0..1} matches {    -- End date
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							allow_archetype CLUSTER[at0077] occurrences matches {0..*} matches {    -- Type details
								include
									archetype_id/value matches {/.*/}
							}
							ELEMENT[at0069] occurrences matches {0..1} matches {    -- Comment
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
					allow_archetype CLUSTER[at0150] occurrences matches {0..*} matches {    -- Overall details
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0019] occurrences matches {0..1} matches {    -- Overall comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0021] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0073] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0022] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Contraceptive use summary">
					description = <"Summary and persistent information about the use of methods to prevent pregnancy.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Current user">
					description = <"Individual currently uses contraception.">
				>
				["at0005"] = <
					text = <"Not current user">
					description = <"Individual has previously used contraception but is not a current user.">
				>
				["at0006"] = <
					text = <"Never used">
					description = <"Individual has never used any type of contraception.">
				>
				["at0013"] = <
					text = <"Episode start date">
					description = <"Date when this episode commenced.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0019"] = <
					text = <"Overall comment">
					description = <"Additional narrative about the use of contraception that has not been captured in other fields.">
				>
				["at0021"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0022"] = <
					text = <"Last updated">
					description = <"The date this contraceptive use summary was last updated.">
				>
				["at0023"] = <
					text = <"Specific name">
					description = <"Identification of the specific contraception used in this episode, by name.">
					comment = <"For example: the brand name of the oral contraceptive pill. This data element is most likely redundant in the context of use of some contraceptives such as condoms where specification of the brand or type is usually not relevant.">
				>
				["at0025"] = <
					text = <"Goal">
					description = <"Intended outcome as a result of use of the specified contraception.">
					comment = <"For example: pregnancy prevention, regular cycle or relief of pain. Coding with a terminology is recommended, where possible.">
				>
				["at0026"] = <
					text = <"Episode details">
					description = <"Additional structured details about the specified episode of contraception use.">
				>
				["at0029"] = <
					text = <"Per type">
					description = <"Details about use of a specified type of contraception.">
				>
				["at0030"] = <
					text = <"Description">
					description = <"Narrative description about the overall use of contraception during this episode.">
					comment = <"This data element may be used to describe the pattern of use, including regularity of use. For example, the oral contraceptive pill was missed frequently, condoms used occasionally or to describe minor side effects.">
				>
				["at0043"] = <
					text = <"Overall description">
					description = <"Narrative summary about the individual's overall use of contraception.">
					comment = <"Use this data element to record a narrative description where the structured data does not adequately reflect the use of contraception for this individual or to incorporate unstructured information from existing or legacy clinical systems into an archetyped format.">
				>
				["at0053"] = <
					text = <"Description">
					description = <"Narrative summary about the use of the specified type of contraception.">
				>
				["at0064"] = <
					text = <"Per episode">
					description = <"Details about contraceptive use during an identified period of time.">
				>
				["at0069"] = <
					text = <"Comment">
					description = <"Additional narrative about use of the specified type of contraception, not captured in other fields.">
				>
				["at0073"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR.">
				>
				["at0074"] = <
					text = <"Reason for cessation">
					description = <"Narrative description for the reason for stopping use of the specified type of contraception during this episode.">
					comment = <"For example: Major side effects or the wish to become pregnant. Coding with a terminology is recommended, where possible.">
				>
				["at0077"] = <
					text = <"Type details">
					description = <"Additional structured details about the specified type of contraception used.">
				>
				["at0081"] = <
					text = <"Episode label">
					description = <"Identification of an episode of contraception use - either as a number in a sequence and/or a named event.">
					comment = <"For example: '2' as the second episode within a sequence of episodes; or 'Breastfeeding'.">
				>
				["at0082"] = <
					text = <"Episode end date">
					description = <"Date when this episode ceased.">
					comment = <"Can be a partial date, for example, only a year. This data field will be empty if the episode is current and ongoing.">
				>
				["at0087"] = <
					text = <"Episode comment">
					description = <"Additional narrative about the use of the specified contraception during the specified episode, not captured in other fields.">
				>
				["at0089"] = <
					text = <"Overall status">
					description = <"Statement about regular use of any type of contraception.">
				>
				["at0144"] = <
					text = <"Status">
					description = <"Statement about current use of the specified type of contraception.">
				>
				["at0145"] = <
					text = <"Current user">
					description = <"Individual is a current user of the specified type of contraception.">
				>
				["at0146"] = <
					text = <"Not current user">
					description = <"Individual has previously used the specified type of contraception but is not a current user.">
				>
				["at0147"] = <
					text = <"Never used">
					description = <"Individual has never used the specified type of contraception.">
				>
				["at0148"] = <
					text = <"Start date">
					description = <"Date when the individual first used the specified type of contraception.">
					comment = <"Comment: Can be a partial date, for example, only a year.">
				>
				["at0149"] = <
					text = <"End date">
					description = <"Date when the individual last used the specified type of contraception.">
					comment = <"Comment: Can be a partial date, for example, only a year.">
				>
				["at0150"] = <
					text = <"Overall details">
					description = <"Additional structured details about the overall use of contraception.">
				>
				["at0151"] = <
					text = <"Type">
					description = <"The type of contraception used by the individual.">
					comment = <"Coding the contraceptive type with a terminology is recommended, where possible.">
				>
				["at0152"] = <
					text = <"Male condom">
					description = <"A barrier covering the penis to physically block ejaculated sperm from entering the vagina.">
				>
				["at0153"] = <
					text = <"Female condom">
					description = <"A barrier worn inside the vagina to physically block ejaculated sperm from entering the cervix.">
				>
				["at0154"] = <
					text = <"Diaphragm">
					description = <"A shallow, dome shaped barrier placed in the vagina to cover the cervix.">
				>
				["at0155"] = <
					text = <"Combination pill">
					description = <"A combination oral contraceptive pill containing both an oestrogen and progestin.">
				>
				["at0156"] = <
					text = <"Combination skin patch">
					description = <"A combination skin patch containing both an oestrogen and progestin.">
				>
				["at0157"] = <
					text = <"Progestogen-only pill">
					description = <"Progestogen-only form of oral hormonal contraception.">
				>
				["at0158"] = <
					text = <"Depot progestogen injection">
					description = <"Progestogen-only injectable of hormonal contraception.">
				>
				["at0159"] = <
					text = <"Hormone implant">
					description = <"Hormonal contraception inserted under the skin.">
				>
				["at0160"] = <
					text = <"Vaginal ring">
					description = <"Hormone impregnated ring inserted into the vagina.">
				>
				["at0161"] = <
					text = <"Female sterilisation">
					description = <"Procedure intended to permanently prevent pregnancy, usually by tubal ligitation.">
				>
				["at0162"] = <
					text = <"Male sterilisation">
					description = <"Procedure intended to permanently prevent pregnancy, usually by vasectomy.">
				>
				["at0163"] = <
					text = <"IUD without hormone">
					description = <"Device inserted into the uterus to prevent implantation.">
				>
				["at0164"] = <
					text = <"Withdrawal">
					description = <"Withdrawing the penis from the vagina and away from a woman's external genitals before ejaculation.">
				>
				["at0165"] = <
					text = <"Fertility awareness method">
					description = <"Cyclical use of abstinence or barrier methods during likely fertile periods.">
				>
				["at0166"] = <
					text = <"Abstinence">
					description = <"Avoidance of vaginal intercourse or intimate activities which may result in pregnancy.">
				>
				["at0167"] = <
					text = <"Vaginal douching">
					description = <"Intravaginal cleansing with a liquid solution.">
				>
				["at0168"] = <
					text = <"Clinical indication">
					description = <"Clinical reason for using the specified type of contraception during this episode.">
					comment = <"For example: dysmenorrhoea; or irregular menses. Coding with an external terminology is preferred, where possible.">
				>
				["at0169"] = <
					text = <"IUD with hormone">
					description = <"Device inserted into the uterus to prevent implantation.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Antikonsepsjonssammendrag">
					description = <"Sammendrag og varig (persistent) informasjon om bruk av metoder for å forhindre graviditet.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Bruker antikonsepsjon">
					description = <"Individet bruker antikonsepsjon.">
				>
				["at0005"] = <
					text = <"Har brukt antikonsepsjon">
					description = <"Individet har tidligere brukt antikonsepsjon, men bruker det for tiden ikke.">
				>
				["at0006"] = <
					text = <"Har aldri brukt antikonsepsjon">
					description = <"Individet har aldri brukt noen form for antikonsepsjon.">
				>
				["at0013"] = <
					text = <"Episodens startdato">
					description = <"Dato da episoden startet.">
					comment = <"Kan være en deldato, for eksempel kun årstall.">
				>
				["at0019"] = <
					text = <"Samlet kommentar">
					description = <"Ytterligere fritekst om antikonsepsjonsbruken som ikke er omfattet av andre felt.">
				>
				["at0021"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0022"] = <
					text = <"Sist oppdatert">
					description = <"Datoen da dette antikonsepsjonssammendraget sist ble oppdatert.">
				>
				["at0023"] = <
					text = <"Spesifikt navn">
					description = <"Identifisering av den spesifikke antikonsepsjonen som brukes i denne episoden, med navn.">
					comment = <"For eksempel: Merkenavnet på den p-pillen som brukes. Dette dataelementet er sannsynligvis overflødig for noen antikonsepsjonsmidler der spesifikasjon av merke eller type vanligvis ikke er relevant, som kondom.">
				>
				["at0025"] = <
					text = <"Hensikt">
					description = <"Forventet utfall som et resultat av bruk av den spesifiserte antikonsepsjonen.">
					comment = <"For eksempel: Regelmessig syklus, lindring av smerter eller forhindre graviditet. Koding med ekstern terminologi er å foretrekke, der det er mulig.">
				>
				["at0026"] = <
					text = <"Episodedetaljer">
					description = <"Ytterligere strukturerte detaljer om den spesifiserte episoden med antikonsepsjonsbruk.">
				>
				["at0029"] = <
					text = <"Per type">
					description = <"Detaljer om bruken for en spesifikk type antikonsepsjon.">
				>
				["at0030"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse om den samlede bruken av antikonsepsjon i løpet av denne episoden.">
					comment = <"Dette dataelementet kan brukes til å beskrive bruksmønsteret, inkludert regulariteten. For eksempel \"p-pillen ble ofte glemt\" eller \"kondomer brukt noen ganger, eller for å beskrive mindre bivirkninger.">
				>
				["at0043"] = <
					text = <"Overordnet beskrivelse">
					description = <"Fritekstsammendrag om individets overordnede bruk av antikonsepsjon.">
					comment = <"Bruk dette dataelementet for å registrere en fritekstbeskrivelse om individets bruk av antikonsepsjon dersom man ikke kan eller trenger de strukturerte elementene. Kan også brukes for å overføre ustrukturert informasjon fra eksisterende eller gamle systemer til arketypeformat.">
				>
				["at0053"] = <
					text = <"Beskrivelse">
					description = <"Fritekstsammendrag om bruken av den spesifiserte antikonsepsjonen.">
				>
				["at0064"] = <
					text = <"Per episode">
					description = <"Detaljer om bruk av antikonsepsjon i løpet av en identifisert tidsperiode.">
				>
				["at0069"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om den spesifiserte antikonsepsjonstypen som ikke er omfattet av andre felt.">
				>
				["at0073"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
				["at0074"] = <
					text = <"Årsak til opphør">
					description = <"Fritekstbeskrivelse av årsaken til at en sluttet å bruke den spesifiserte antikonsepsjonstypen i denne episoden.">
					comment = <"For eksempel: Alvorlige bivirkninger eller ønsker å bli gravid. Koding med ekstern terminologi er å foretrekke der det er mulig.">
				>
				["at0077"] = <
					text = <"Detaljer om antikonsepsjonstypen">
					description = <"Ytterligere strukturerte detaljer om den spesifiserte typen antikonsepsjon.">
				>
				["at0081"] = <
					text = <"Episodens navn">
					description = <"Identifisering av en episode med bruk av antikonsepsjon - enten som et nummer i en sekvens og / eller en navngitt hendelse.">
					comment = <"For eksempel: \"2\" som den andre episoden i en sekvens av episoder eller \"Amming\".">
				>
				["at0082"] = <
					text = <"Episodens sluttdato">
					description = <"Datoen episoden opphørte.">
					comment = <"Kan være en deldato, for eksempel kun årstall. Dette datafeltet vil være tomt når gjeldene episode pågår.">
				>
				["at0087"] = <
					text = <"Kommentar til episoden">
					description = <"Ytterligere fritekst om bruken av antikonsepsjon innenfor den spesifiserte episoden som ikke er omfattet av andre felt.">
				>
				["at0089"] = <
					text = <"Overordnet status">
					description = <"Utsagn om regelmessig bruk av alle typer antikonsepsjon.">
				>
				["at0144"] = <
					text = <"Status">
					description = <"Utsagn om nåværende bruk for den spesifiserte antikonsepsjonen.">
				>
				["at0145"] = <
					text = <"Bruker antikonsepsjonstypen">
					description = <"Individet bruker den angitte typen antikonsepsjon.">
				>
				["at0146"] = <
					text = <"Har brukt antikonsepsjonstypen">
					description = <"Individet har tidligere brukt den angitte typen antikonsepsjon, men bruker det ikke nå.">
				>
				["at0147"] = <
					text = <"Aldri brukt antikonsepsjonstypen">
					description = <"Individet har aldri brukt den angitte typen antikonsepsjon.">
				>
				["at0148"] = <
					text = <"Startdato">
					description = <"Datoen når individet første gang brukte den spesifiserte antikonsepsjonen.">
					comment = <"Kan være en deldato, for eksempel årstall.">
				>
				["at0149"] = <
					text = <"Sluttdato">
					description = <"Dato da individet sist brukte den angitte typen antikonsepsjon.">
					comment = <"Kan være en deldato, for eksempel årstall.">
				>
				["at0150"] = <
					text = <"Samlede detaljer">
					description = <"Ytterligere strukturerte detaljer om den overordnede bruken av prevensjon.">
				>
				["at0151"] = <
					text = <"Type">
					description = <"Typen antikonsepsjon individet bruker eller brukte.">
					comment = <"Koding med ekstern terminologi er å foretrekke, der det er mulig.">
				>
				["at0152"] = <
					text = <"Kondom">
					description = <"En barriere som dekker penis for fysisk å blokkere ejakulert sæd fra å komme inn i skjeden.">
				>
				["at0153"] = <
					text = <"Femidom">
					description = <"En barriere som plasseres inne i vagina for fysisk å blokkere ejakulert sæd fra å komme inn i livmorhalsen.">
				>
				["at0154"] = <
					text = <"Pessar">
					description = <"En grunn, kuppelformet barriere plassert i skjeden for å dekke livmorhalsen.">
				>
				["at0155"] = <
					text = <"Kombinasjonspille">
					description = <"En kombinasjons p-pille som inneholder både østrogen og gestagen.">
				>
				["at0156"] = <
					text = <"Kombinasjonsplaster">
					description = <"Et p-plaster som inneholder både østrogen og progestin.">
				>
				["at0157"] = <
					text = <"Progestagen pille">
					description = <"Progestagen som oral hormonell antikonsepsjon.">
				>
				["at0158"] = <
					text = <"P-sprøyte">
					description = <"Progestagen som injiserbar hormonell antikonsepsjon.">
				>
				["at0159"] = <
					text = <"Hormonimplantat">
					description = <"Hormonell antikonsepsjon implantert under huden.">
				>
				["at0160"] = <
					text = <"Vaginal ring">
					description = <"Hormonimpregnert ring satt inn i skjeden.">
				>
				["at0161"] = <
					text = <"Kvinnelig sterilisering">
					description = <"Fremgangsmåte ment for å permanent forhindre graviditet, vanligvis ved å blokkere egglederne.">
				>
				["at0162"] = <
					text = <"Mannlig sterilisering">
					description = <"Fremgangsmåte ment for å permanent forhindre graviditet, vanligvis ved å kutte sædlederne.">
				>
				["at0163"] = <
					text = <"Intrauterin antikonsepsjon uten hormon">
					description = <"Spiral satt inn i livmoren for å forhindre implantasjon.">
				>
				["at0164"] = <
					text = <"Avbrutt samleie">
					description = <"Å trekke penis fra skjeden og bort fra kvinnens ytre kjønnsorganer før utløsning.">
				>
				["at0165"] = <
					text = <"Fertilitetsbevissthet">
					description = <"Periodisk bruk av avholdenhet eller barrieremetoder i sannsynlig fruktbare perioder.">
				>
				["at0166"] = <
					text = <"Avhold">
					description = <"Unngåelse av vaginalt samleie eller intime aktiviteter som kan føre til graviditet.">
				>
				["at0167"] = <
					text = <"Vaginal skylling">
					description = <"Intravaginal skylling med en flytende oppløsning.">
				>
				["at0168"] = <
					text = <"Klinisk indikasjon">
					description = <"Den kliniske årsaken for å bruke spesifisert antikonsepsjon i denne episoden.">
					comment = <"For eksempel: dysmenoré; eller uregelmessige menstruasjoner. Koding med ekstern terminologi er å foretrekke der det er mulig.">
				>
				["at0169"] = <
					text = <"Intrauterin antikonsepsjon med hormon">
					description = <"Spiral satt inn i livmoren for å forhindre implantasjon.">
				>
			>
		>
	>

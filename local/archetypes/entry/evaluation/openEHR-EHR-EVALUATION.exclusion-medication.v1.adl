archetype (adl_version=1.4)
	openEHR-EHR-EVALUATION.exclusion-medication.v1
specialise
	openEHR-EHR-EVALUATION.exclusion.v1

concept
	[at0000.1]	-- Exclusion of a Medication
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2010-03-02">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To positively record statement/s about the use of medications that need to be clinically excluded from the health record at a specific point in time.">
			use = <"Use to record positive statement/s about the absence or exclusion of use of medication within the health record at a specific point in time. For example: to record that the subject is not currently taking any medications; or that the subject has never taken corticosteroids.

This archetype has specifically been designed to avoid the need to use flags or terminology to express negation about any medication use within the health record. 

Exclusion statements can only be considered to be current and accurate at the time of recording. For example: exclusion of use of coumarin on admission to hospital is only relevant at the time of recording, and is effectively obsolete if the subsequent diagnosis is deep venous thrombosis and the subject is commenced on heparin or coumarin.">
			keywords = <"exclusion", "medicine", "medication">
			misuse = <"Not to be used to record exclusion statements about adverse reactions, procedures, problems or diagnoses - use other specific specialisations of the EVALUATION.exclusion archetype for these purposes.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Sam Heard, Ocean Informatics, Australia", "Ian McNicoll, Ocean Informatics, Australia", "Stephen Chu, NEHTA, Australia", "Stephen Royce, NEHTA, Australia">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["references"] = <"Exclusion of a Medication, draft archetype, NEHTA Clinical Knowledge Manager [Internet]. NEHTA. Authored: 02 Mar 2010. Available at: 
http://dcm.nehta.org.au/ckm/#showArchetype_1013.1.1303_2 (accessed 3 Sep 2013)">
		["MD5-CAM-1.0.1"] = <"20AE44E213800D8CCFE164F6B7515D8F">
	>

definition
	EVALUATION[at0000.1] matches {	-- Exclusion of a Medication
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0002.1] occurrences matches {1..*} matches {	-- Exclusion Statement
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0003.1] occurrences matches {0..*} matches {	-- Medication
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {[ac0.1]}		-- Medications
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0006] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0004] occurrences matches {0..1} matches {	-- Date Last Updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Exclusion Statement">
					description = <"Generic, positive statement/s that need to be recorded as clinically excluded from the health record at a specific point in time.">
				>
				["at0000.1"] = <
					text = <"Exclusion of a Medication">
					description = <"Statement/s about use of medication that needs to be positively recorded as clinically excluded from the health record ata a specific point in time.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Exclusion Statement">
					description = <"The statement about an exclusion in the health record.">
					comment = <"Use to capture any information that is needed to be positively recorded as being excluded within the record. The statement can support recording global statements or statements about a specified object. See specialisations for Medications, Adverse Reactions, Problem/Diagnosis and Procedures as common examples that have been explicitly modelled.">
				>
				["at0002.1"] = <
					text = <"Exclusion Statement">
					description = <"A statement about exclusion of use of medication in the health record.">
					comment = <"The statement can support recording that no medications are being taken or that one or more specified medications are not being taken. For example: \"Not currently taking any medications\"; \"Never taken any medications\" or \"Not currently taking corticosteroids\".">
				>
				["at0003"] = <
					text = <"Specific Object">
					description = <"Identification of the specific object/s to which the Exclusion Statement applies.">
				>
				["at0003.1"] = <
					text = <"Medication">
					description = <"Identification of the specific medication/s to which the Exclusion Statement applies.">
					comment = <"Coding of the Medication with a terminology is desirable, where possible.">
				>
				["at0004"] = <
					text = <"Date Last Updated">
					description = <"The date at which the exclusion was last clinically asserted, affirmed or confirmed.">
				>
				["at0006"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the Exclusion not captured in other fields.">
				>
			>
		>
	>
	constraint_definitions = <
		["en"] = <
			items = <
				["ac0.1"] = <
					text = <"Medications">
					description = <"All medications that may be taken or administered.">
				>
			>
		>
	>

archetype (adl_version=1.4; uid=8e02deb2-7500-4703-81a3-e2bfb22c4bc2)
	openEHR-EHR-EVALUATION.precaution.v1

concept
	[at0000]	-- Precaution
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Silje Ljosland Bakke og John Tore Valand">
				["organisation"] = <"Nasjonal IKT HF, Helse Bergen HF">
				["email"] = <"silje.ljosland.bakke@nasjonalikt.no, john.tore.valand@helse-bergen.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Silje Ljosland Bakke / John Tore Valand">
		["organisation"] = <"Nasjonal IKT HF / Helse Bergen HF">
		["email"] = <"silje.ljosland.bakke@nasjonalikt.no/ john.tore.valand@helse-bergen.no">
		["date"] = <"2016-02-29">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere en klinisk tilstand eller status som er unik eller særegen for et individ, og som regnes som viktig informasjon i forbindelse med beslutninger om behandling.">
			use = <"Brukes for å registrere en klinisk tilstand eller status som er unik eller særegen for et individ, og som regnes som viktig informasjon i forbindelse med beslutning om behandling.

Denne arketypen bør regnes som en viktig arketype for beslutningsstøttesystemer i forbindelse med forordning av nye kliniske intervensjoner.

Eksempler på tilstander som kan være grunnlag for å registrere forholdsregler kan være:
- Immunhemmende/-modulerende behandling - med en referanse til en ordinasjon av kjemoterapi eller steroider, eller en leukemidiagnose
- Nyresvikt - lenket til nyrefunksjonstester eller en formell nyresviktdiagnose.
- Står i transplantasjonskø
- Pågående regelmessig behandling som strålebehandling eller dialyse
- Har fått implantert et organ eller metall i kroppen.
- Pågående utredning/oppfølging av mistenkt eller
verifisert malign lidelse.
- Deltagelse i klinisk farmakologiske studie.
- Viktig blodfortynnende behandling.">
			keywords = <"forsiktighetshensyn", "forhindre", "unngå", "uønsket hendelse", "forebygging", "forsiktighet", "advarsel", "varsel">
			misuse = <"Skal ikke brukes for å registrere en klinisk intervensjon (f.eks. en undersøkelse eller behandling) som ikke må utføres på grunn av sannsynligheten eller muligheten for skade på individet. Bruk EVALUATION.contraindication (Norsk: Kontraindikasjon) for dette formålet.

Skal ikke brukes for avgrensing av behandling eller for å registrere individets personlige preferanser. Bruk spesifikke arketyper for dette formålet.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record a condition or state of the individual that is clinically significant and unique or idiosyncratic for this individual, and is considered vital information when making treatment decisions.">
			use = <"Use to record a condition or state of the individual that is clinically significant and unique or idiosyncratic for this individual, and which are considered vital information when making treatment decisions.

This archetype should be regarded as a critical archetype by any clinical decision support system testing for any relevant therapeutic precautions as a clinician commences a new clinical intervention for an individual. 

Examples of conditions that warrant creation of a precaution include, but are not limited to:
- Immunosuppressed/on immunosuppressive therapy – linked to medication order for chemo or steroids; or diagnosis of leukaemia;
- Renal failure – linked to renal function tests and/or a formal diagnosis of renal failure;
- Waiting for organ transplant;
- Undergoing a regular treatment such as radiation therapy or dialysis;
- Transplanted organ or metal implant in situ;
- Ongoing investigation or follow up of suspected or verified malignant disease;
- Participation in a pharmacological trial; or
- Using anticoagulant treatment.">
			keywords = <"precaution", "prevent", "avoid", "adverse event", "prevention", "caution", "alert", "warning">
			misuse = <"Not to be used to record a clinical intervention (including, but not limited to, use of a treatment or performance of a test or procedure) that should not be carried out due to the likelihood, or possibility, of harm being caused to an individual. Use the EVALUATION.contraindication for this purpose.

Not to be used to record advanced directives for care or personal preferences of the individual. Use specific archetypes for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <"Morten Aas, Oslo Universitetssykehus, Norway", "Tomas Alme, DIPS, Norway", "Ole Andreas Bjordal, Webmed, Norway", "Vebjørn Arntzen, Oslo universitetssykehus HF, Norway (Nasjonal IKT redaktør)", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (openEHR Editor)", "Lisbeth Dahlhaug, Helse Midt - Norge IT, Norway", "Hildegard Franke, freshEHR Clinical Informatics Ltd., United Kingdom", "Heather Grain, Llewelyn Grain Informatics, Australia", "Cecilie Graver, Oslo universitetssykehus HF, Norway", "Daniel Habashi, PasientSky AS, Norway", "Annette Hole Sjøborg, DIPS ASA, Norway", "Hilde Hollås, DIPS ASA, Norway", "Evelyn Hovenga, EJSH Consulting, Australia", "Tom Jarl Jakobsen, Helse Bergen, Norway", "Lars Morgan Karlsen, DIPS ASA, Norway", "Nils Kolstrup, Skansen Legekontor og Nasjonalt Senter for samhandling og telemedisin, Norway", "Heather Leslie, Ocean Health Systems, Australia (openEHR Editor)", "Siv Marie Lien, DIPS ASA, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Bjørn Næss, DIPS ASA, Norway", "Andrej Orel, Marand d.o.o., Slovenia", "Rune Pedersen, Universitetssykehuset i Nord Norge, Norway", "Vladimir Pizzo, Hospital Sírio Libanês, Brazil", "Tanja Riise, Nasjonal IKT HF, Norway", "Anoop Shah, University College London, United Kingdom", "Arild Stangeland, Helse Bergen, Norway", "Norwegian Review Summary, Nasjonal IKT HF, Norway", "Nyree Taylor, Ocean Informatics, Australia", "Tesfay Teame, OUS, Norway", "John Tore Valand, Helse Bergen, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"D4DFFB927EC3A0C91C3A5FA6210FC6EB">
		["build_uid"] = <"8648d06c-7a7a-44e3-871c-5a8b66ecf9dd">
		["revision"] = <"1.0.1">
	>

definition
	EVALUATION[at0000] matches {	-- Precaution
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0002] matches {	-- Condition
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0014] occurrences matches {0..1} matches {	-- Status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0015, 	-- Active
									at0018, 	-- Resolved
									at0019]	-- Refuted
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0003] occurrences matches {0..*} matches {	-- Evidence
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0013] occurrences matches {0..1} matches {	-- Category
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0008] occurrences matches {0..1} matches {	-- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0006] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0020] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0022] occurrences matches {0..1} matches {	-- Valid period start
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0024] occurrences matches {0..1} matches {	-- Valid period end
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0009] occurrences matches {0..1} matches {	-- Review date
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {	-- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Precaution">
					description = <"A condition or state of the individual that is clinically significant and unique or idiosyncratic for this individual, and is considered vital information when making treatment decisions.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Condition">
					description = <"Identification, by name, of a condition or state.">
					comment = <"Coding of the identified 'Condition' with a terminology is desirable, where possible.">
				>
				["at0003"] = <
					text = <"Evidence">
					description = <"Description of the evidence identified to support the precaution.">
					comment = <"This data element can be optionally linked to a diagnosis, test result, medication order etc via a URI (as per the Reference Model) in order to provide the rationale or evidence for the precaution assertion. Please note: as this URI link may not be accessible from a message or by receiving clinical system it is desirable that a narrative description of the evidence should also be explicitly recorded.">
				>
				["at0004"] = <
					text = <"Last updated">
					description = <"Date when the precaution information was last updated.">
				>
				["at0006"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0008"] = <
					text = <"Comment">
					description = <"Additional narrative about the precaution, not captured in other fields.">
				>
				["at0009"] = <
					text = <"Review date">
					description = <"Date when next due for review by a clinician.">
					comment = <"In circumstances where precautions are not indefinite or life-long, use this data element to record the date/time when this precaution should be reassessed in the context of the clinical circumstances at the time.">
				>
				["at0013"] = <
					text = <"Category">
					description = <"Type of the 'Condition' identified.">
					comment = <"This data element has been included because it is currently being captured in some clinical systems. This information could be derived when terminology codes are used to represent the condition in clinical systems, and is effectively redundant in that situation.">
				>
				["at0014"] = <
					text = <"Status">
					description = <"Assertion about the current state of the identified 'Precaution'.">
					comment = <"Decision support would typically raise alerts for 'Active' and ignore a 'Resolved' or 'Refuted' precaution. Clinical systems may choose not to display Precaution entries with a 'Refuted' status in the Precaution list. However, 'Refuted' may be useful for reconciliation of the Precaution list or when communicating between systems. Some implementations may choose to make this field mandatory. 'Resolved' may be used variably across systems, depending on clinical use and context. The free text data type will allow for local variation by enabling other value sets to be applied to this data element in a template - in this situation it is recommended that values should be coded using a terminology.">
				>
				["at0015"] = <
					text = <"Active">
					description = <"The precaution is currently active.">
				>
				["at0018"] = <
					text = <"Resolved">
					description = <"The previously asserted precaution has been clinically reassessed and considered no longer to be an active risk.">
				>
				["at0019"] = <
					text = <"Refuted">
					description = <"The precaution has been clinically reassessed or has been disproved with a high level of clinical certainty by testing.">
				>
				["at0020"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0022"] = <
					text = <"Valid period start">
					description = <"Date/time after which the 'Condtion' is regarded as active.">
					comment = <"This data element is intended for use when a precaution is identified to start at some time in the future. For example: the commencement date for a pharmaceutical trial.">
				>
				["at0024"] = <
					text = <"Valid period end">
					description = <"Date/time after which the 'Condtion' is regarded as inactive.">
					comment = <"This data element is intended for use when a precaution is known to have a definitive end date/time in the future. For example: the cessation date for a pharmaceutical trial.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Forholdsregel">
					description = <"En klinisk tilstand eller status som er unik eller særegen for et individ, og som regnes som viktig informasjon i forbindelse med beslutninger om behandling.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Tilstand">
					description = <"Navnet på tilstanden eller statusen.">
					comment = <"Koding av tilstanden med en terminologi er ønskelig, dersom mulig.">
				>
				["at0003"] = <
					text = <"Underlag">
					description = <"Beskrivelse av underlaget som understøtter forholdsregelen.">
					comment = <"Dette dataelementet kan dersom ønskelig lenkes til en diagnose, testresultat eller legemiddelordinering ved hjelp av en URI (spesifisert i referansemodellen) for å oppgi rasjonalet eller evidensen for forholdsregelen. NB: Siden URI-lenken kan være utilgjengelige fra en melding eller et mottakende system, er det ønskelig at en fritekstbeskrivelse av underlaget registreres eksplisitt.">
				>
				["at0004"] = <
					text = <"Sist oppdatert">
					description = <"Dato da forholdsregelinformasjonen sist ble oppdatert.">
				>
				["at0006"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0008"] = <
					text = <"Kommentar">
					description = <"Ytterligere informasjon om forholdsregelen, som ikke kan registreres i andre felter.">
				>
				["at0009"] = <
					text = <"Evalueringsdato">
					description = <"Dato for neste kliniske evaluering.">
					comment = <"Ved omstendigheter der forholdsregler ikke er på ubestemt tid eller livslange, kan dette dataelementet brukes til å registrere når forholdsregelen bør evalueres.">
				>
				["at0013"] = <
					text = <"Kategori">
					description = <"Type \"Tilstand\".">
					comment = <"Dette dataelementet er inkludert fordi det registreres i flere eksisterende kliniske systemer. Informasjonen kan avledes fra Tilstand dersom hierarkiske kodesystemer er brukt, og vil i slike tilfeller være redundant.">
				>
				["at0014"] = <
					text = <"Status">
					description = <"Forholdsregelens nåværende status.">
					comment = <"Beslutningsstøtte vil typisk utløse advarsler for \"Aktiv\", og ignorere \"Opphørt\" eller \"Avkreftet\". Kliniske systemer kan velge å ikke vise registreringer med status \"Avkreftet\" i en liste. Likevel kan \"Avkreftet\" være nyttig for sammenstilling av ulike lister eller ved kommunikasjon mellom systemer. Noen systemer kan velge å gjøre dette elementet obligatorisk. \"Opphørt\" kan brukes ulikt i ulike systemer avhengig av klinisk bruk og kontekst. Fritekst-datatypen tillater lokal variasjon ved å gjøre det mulig å bruke andre verdisett for dette dataelementet i en templat. Det er i denne situasjonen anbefalt å kode verdiene med en terminologi.">
				>
				["at0015"] = <
					text = <"Aktiv">
					description = <"Forholdsregelen er aktiv.">
				>
				["at0018"] = <
					text = <"Opphørt">
					description = <"Den tidligere kjente forholdsregelen er klinisk revurdert ved testing, og regnes ikke lenger for å være en aktiv risiko.">
				>
				["at0019"] = <
					text = <"Avkreftet">
					description = <"Forholdsregelen har blitt klinisk revurdert eller er avkreftet med høy grad av sikkerhet ved testing.">
				>
				["at0020"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR eller CIMI.">
				>
				["at0022"] = <
					text = <"Gyldighetsperiode start">
					description = <"Forholdsregelen regnes som aktiv etter denne datoen.">
					comment = <"Dette dataelementet er ment for å brukes når en forholdregel er ment å starte på et tidspunkt i fremtiden, for eksempel startdatoen for en farmakologisk studie.">
				>
				["at0024"] = <
					text = <"Gyldighetsperiode slutt">
					description = <"Forholdsregelen regnes som inaktiv etter denne datoen.">
					comment = <"Dette dataelementet er ment for å brukes når en forholdsregel er ment å avsluttes på et tidspunkt i fremtiden, for eksempel sluttdatoen for en farmakologisk studie.">
				>
			>
		>
	>

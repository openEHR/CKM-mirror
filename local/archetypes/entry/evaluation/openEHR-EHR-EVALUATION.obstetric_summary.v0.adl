archetype (adl_version=1.4; uid=73666d7d-d7d0-460d-87a3-ed2e68f87fad)
	openEHR-EHR-EVALUATION.obstetric_summary.v0

concept
	[at0000]	-- Obstetric summary
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["es-co"] = <
			language = <[ISO_639-1::es-co]>
			author = <
				["name"] = <"Francisco Ramirez ante">
				["organisation"] = <"SITIS SAS">
				["email"] = <"framirez74@gmail.com">
			>
			accreditation = <"Ingeniero de Sistemas">
		>
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			author = <
				["name"] = <"Marivan Abrahão, Gabriela Alves, Adriana Kitajima e Maria Ângela Scatena">
				["organisation"] = <"Core Consulting">
				["email"] = <"contato@coreconsulting.com.br">
			>
			accreditation = <"Hospital Alemão Oswaldo Cruz (HAOC)">
		>
	>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"2011-02-24">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record an overview about the obstetric history of a woman for purposes of: providing a snapshot of her obstetric history, reporting to statutory bodies or supporting clinical decision support.">
			use = <"Use to record an overview of the obstetric history of a woman, including a summary of all pregnancies and the associated outcomes or interventions. 

Use this summary as the basis of statutory reporting regarding births.

Use to record the TPAL overview of an obstetric history, if required - the numbers of Term Births (T), Preterm Births (P), Abortions (A) and Living Children (L). Some variations add Gravidity (G) and number of Multiple Births (M).

Some data may be calculated or derived from individual pregnancy records if these are available.">
			keywords = <"obstetric", "pregnancy", "parity", "gravida", "para", "terminations", "miscarriages", "abortions", "spontaneous", "live", "births", "stillbirths", "caesarean", "ectopic", "neonatal", "death", "tubal", "living", "multiple">
			misuse = <"Not for recording summary information about a single pregnancy - use EVALUATION. pregnancy_summary for this purpose.

Not for recording detailed information about each of the individual pregnancies, miscarriages, terminations, or deliveries. This information will be recorded using other specific archetypes as event-based data.">
			copyright = <"© openEHR Foundation">
		>
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Para gravar uma visão geral sobre a história obstétrica de uma mulher para fins de: fornecimento de um informações de sua história obstétrica, para reportar a órgãos oficiais ou para apoio à decisão clínica.">
			use = <"Usar para registrar o panorama da história obstétrica de uma mulher, incluindo um resumo de todas as gestações e os resultados ou intervenções associadas.

Utilizar, se adequado, como a base de relatórios legais sobre nascimentos.

Usar para registrar a visão geral de uma história obstétrica, se necessário - o número de nascimentos a termo, nascimento prematuro, abortos e nascidos vivos. Algumas variações adicionam o número de gestações e o número de nascimentos múltiplos.

Alguns dados podem ser calculados ou derivados de registros de gravidez individuais, se disponíveis.
">
			keywords = <"Obstétrico", "Gravidez", "Paridade", "Gestante", "Gravida", "Abortamento induzido", "Aborto espontâneo", "Espontâneo", "Vivo", "Nascimentos", "Natimorto", "Cesariana", "Ectópica", "Neonatal", "Morte", "Tubária", "Vivo", "Múltiplo">
			misuse = <"Não utilizar para registrar informações de resumo sobre uma única gravidez – neste caso utilize EVALUATION.pregnancy_summary.

Não utilizar para registrar informações detalhadas sobre episódios individuais de gravidez, abortos ou partos. Estas informação deverão ser registradas através de arquétipos específicos, baseados nos dados do evento.
">
			copyright = <"© openEHR Foundation">
		>
		["es-co"] = <
			language = <[ISO_639-1::es-co]>
			purpose = <"Sirve para registrar una visión general sobre la historia obstétrica de una mujer con fines de: proporcionar un resumen de su historia obstétrica, la presentación de informes a los órganos estatutarios o el apoyo de soporte de decisiones clínicas">
			use = <"Sirve para registrar una visión general sobre la historia obstétrica de una mujer, incluyendo un resumen de todos los embarazos.

Utilice este resmen como base para informes legales con respecto a los nacimientos .

Se utiliza para registrar la información general de una historia obstétrica, si es necesario - el número de nacimientos a termino (T) , los nacimientos prematuros (P) , Abortos (A) y niños que viven (L) . Algunas variaciones añaden Gravidez (G) y el número de nacimientos múltiples (M) .

Algunos datos pueden ser calculados o derivados de los registros individuales de embarazo, si están disponibles.">
			keywords = <"Obstétrica", "Eembarazo", "Paridad", "Gesta", "Parto", "terminacion", "abortos involuntarios", "abortos", "Espontánea", "Vivo", "nacimientos", "mortinatos", "Cesárea", "Ectópico", "Neonatal", "muerte", "Múltiple">
			misuse = <"No es para registrar la información del embarazo actual - utilice el arquetipo EVALUACIÓN. pregnancy_summary para este propósito.

No es para registrar la información detallada sobre cada uno de los individuales embarazos, abortos involuntarios , Embarazos interrumpidos, o nacimientos. Esta información se registra utilizando otros arquetipos específicos como datos basados ​​en eventos .">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Sheryl Alexander, NT Department of Health, Australia", "Margaret Cotter, AMSANT, Australia", "Michelle Dowden, Miwatj Health Ngalkanbuy Health, Australia", "Tim Garden, NTG Department of Health, Australia", "Sam Heard, Ocean Informatics, Australia (Editor)", "Michelle Kealy, Australia", "Bernadette Lack, Deptartment of Health, NT, Australia", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Ian McNicoll, Ocean Informatics UK, United Kingdom", "Jeremy Oats, NT Health, Australia", "Gary Sinclair, NT DoH, Australia", "Cherie Whitbread, Royal Darwin Hospital, Australia", "Jo Wright, NT Dept of Health, Australia">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["revision"] = <"0.0.1-alpha">
		["references"] = <"NEHTA Clinical Knowledge Manager [Internet]. Australia: National eHealth Transition Authority. [Draft EVALUATION archetype] Obstetric Summary; [authored 2011 Feb 24, cited 2011 Aug 17]. Available from http://dcm.nehta.org.au/ckm/OKM.html#showArchetype_1013.1.971_4.">
		["build_uid"] = <"ad221c86-22d6-4119-9148-02e3594df431">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["MD5-CAM-1.0.1"] = <"5E5629536630883CC4D249A870E97041">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	EVALUATION[at0000] matches {	-- Obstetric summary
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0014] occurrences matches {0..1} matches {	-- Ever Pregnant?
						value matches {
							DV_BOOLEAN matches {
								value matches {True}
							}
						}
					}
					ELEMENT[at0002] occurrences matches {0..1} matches {	-- Gravidity (G)
						value matches {
							DV_COUNT matches {
								magnitude matches {|0..<100|}
							}
						}
					}
					ELEMENT[at0003] occurrences matches {0..1} matches {	-- Parity
						value matches {
							DV_COUNT matches {
								magnitude matches {|>=0|}
							}
						}
					}
					ELEMENT[at0015] occurrences matches {0..1} matches {	-- Term Births (T)
						value matches {
							DV_COUNT matches {*}
						}
					}
					ELEMENT[at0016] occurrences matches {0..1} matches {	-- Preterm Births (P)
						value matches {
							DV_COUNT matches {*}
						}
					}
					ELEMENT[at0017] occurrences matches {0..1} matches {	-- Abortions (A)
						value matches {
							DV_COUNT matches {*}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {	-- Miscarriages
						value matches {
							DV_COUNT matches {
								magnitude matches {|>=0|}
							}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {	-- Terminations
						value matches {
							DV_COUNT matches {
								magnitude matches {|>=0|}
							}
						}
					}
					ELEMENT[at0011] occurrences matches {0..1} matches {	-- Ectopic Pregnancies
						value matches {
							DV_COUNT matches {*}
						}
					}
					ELEMENT[at0012] occurrences matches {0..1} matches {	-- Stillbirths
						value matches {
							DV_COUNT matches {*}
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {	-- Live Births
						value matches {
							DV_COUNT matches {
								magnitude matches {|>=0|}
							}
						}
					}
					ELEMENT[at0010] occurrences matches {0..1} matches {	-- Caesarean Sections
						value matches {
							DV_COUNT matches {
								magnitude matches {|>=0|}
							}
						}
					}
					ELEMENT[at0018] occurrences matches {0..1} matches {	-- Multiple Births (M)
						value matches {
							DV_COUNT matches {*}
						}
					}
					ELEMENT[at0007] occurrences matches {0..1} matches {	-- Living Children (L)
						value matches {
							DV_COUNT matches {
								magnitude matches {|>=0|}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0008] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0009] occurrences matches {0..1} matches {	-- Date Updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0013] occurrences matches {0..1} matches {	-- Stillbirth Definition
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Obstetric summary">
					description = <"An overview of the obstetric history of a woman, including a summary of all pregnancies and the associated outcomes or interventions.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Gravidity (G)">
					description = <"Number of times a woman has been pregnant, including the current pregnancy, if appropriate.">
				>
				["at0003"] = <
					text = <"Parity">
					description = <"Number of times a woman has given birth after 20 weeks and 0 days gestation.">
					comment = <"Late terminations of pregnancy (after 20 weeks of gestation) are not included. When both 'Term Births (T)' and 'Preterm Births (P)' are recorded, this number should be equal to their sum. Note: Parity is not regarded as the same as the number of infants born - for example, twins would be recorded as one birth event.">
				>
				["at0004"] = <
					text = <"Miscarriages">
					description = <"Number of times a woman has had a miscarriage at less than 20 weeks of gestation.">
					comment = <"Including missed abortions.">
				>
				["at0005"] = <
					text = <"Terminations">
					description = <"Number of times a woman has had a pregnancy terminated, regardless of gestation.">
				>
				["at0006"] = <
					text = <"Live Births">
					description = <"Number of babies born alive.">
				>
				["at0007"] = <
					text = <"Living Children (L)">
					description = <"Number of children who remain alive.">
				>
				["at0008"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Date Updated">
					description = <"The date this summary was last updated.">
				>
				["at0010"] = <
					text = <"Caesarean Sections">
					description = <"Number of Caesarean sections performed.">
				>
				["at0011"] = <
					text = <"Ectopic Pregnancies">
					description = <"Number of ectopic pregnancies.">
				>
				["at0012"] = <
					text = <"Stillbirths">
					description = <"Number of stillbirths.">
					comment = <"Note: Stillbirth definition may be optionally captured in specific data element in Protocol.">
				>
				["at0013"] = <
					text = <"Stillbirth Definition">
					description = <"Definition of stillbirth.">
					comment = <"Maybe useful to record when comparing data from different regions as, as definitions of stillbirth may vary. For example, in Australia a stillborn baby will have a gestation greater than 20 weeks or, if gestation is unknown, weigh more than 400 grams.">
				>
				["at0014"] = <
					text = <"Ever Pregnant?">
					description = <"Has the woman ever been pregnant?">
					comment = <"Record as TRUE if a woman has ever been pregnant. Only useful to record if there is no other detailed data about pregnancies available, or where it may be used to support further knowledge-based activities such as clinical decision support.">
				>
				["at0015"] = <
					text = <"Term Births (T)">
					description = <"Number of infants born on or after 37 weeks of gestation.">
				>
				["at0016"] = <
					text = <"Preterm Births (P)">
					description = <"Number of infants born at less than 37 weeks of gestation.">
				>
				["at0017"] = <
					text = <"Abortions (A)">
					description = <"Number of non-viable pregnancies from all causes and at any gestation, including miscarriages, induced terminations and ectopic pregnancies.">
					comment = <"When all of 'Miscarriages', 'Terminations' and 'Ectopic Pregnancies' are recorded, this number should be equal to their sum.">
				>
				["at0018"] = <
					text = <"Multiple Births (M)">
					description = <"Number of birth events in which more than one fetus has been born.">
				>
			>
		>
		["pt-br"] = <
			items = <
				["at0000"] = <
					text = <"Sumário obstétrico">
					description = <"Uma visão geral da história obstétrica de uma mulher, incluindo um resumo de todas as gestações seus resultados ou as intervenções associadas.

">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Gestas prévias">
					description = <"Número de vezes que uma mulher esteve grávida, incluindo a gravidez atual, se for o caso.">
				>
				["at0003"] = <
					text = <"Paridade">
					description = <"Número de vezes que uma mulher deu à luz após 20 semanas e 0 dias de gestação.">
					comment = <"Abortos tardios da gravidez (após 20 semanas de gestação) não estão incluídos. Quando ambos “Nascimento a termo” (T) \"e\" Nascimentos prematuro (P) são gravadas , este número(paridade) deve ser igual a sua soma dos dois. Nota: paridade não é considerado como o mesmo que o número de bebés nascidos - por exemplo , gêmeos seriam registados como um evento nascimento.">
				>
				["at0004"] = <
					text = <"Aborto espontâneo">
					description = <"Número de vezes que uma mulher teve um aborto espontâneo, com menos de 20 semanas de gestação.">
					comment = <"Incluindo abortos retidos.">
				>
				["at0005"] = <
					text = <"Aborto induzido">
					description = <"Número de vezes que uma mulher teve uma gravidez terminada, desconsiderando a gestação.">
				>
				["at0006"] = <
					text = <"Nascidos vivos">
					description = <"Número de bebes nascidos vivos.">
				>
				["at0007"] = <
					text = <"Filhos vivos">
					description = <"Número de crianças que permanecem vivas.">
				>
				["at0008"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Data da atualização">
					description = <"Data da última atualização do sumário.">
				>
				["at0010"] = <
					text = <"Cesarianas">
					description = <"Número de cesarianas realizadas.">
				>
				["at0011"] = <
					text = <"Gravidez ectópica">
					description = <"Número de gravidezes ectópicas.">
				>
				["at0012"] = <
					text = <"Natimorto">
					description = <"Número de natimortos">
					comment = <"Nota: definição de natimorto pode ser opcionalmente capturada no elemento de dados específicos no protocolo.">
				>
				["at0013"] = <
					text = <"Definição de natimorto">
					description = <"Definição de natimorto.">
					comment = <"Talvez útil para gravar quando se comparam dados de diferentes regiões como, como definições de morte fetal pode variar. Por exemplo , na Austrália um feto morto terá uma gestação superior a 20 semanas ou, no caso de idade gestacional desconhecida, pesar mais de 400 gramas .">
				>
				["at0014"] = <
					text = <"Já esteve grávida?">
					description = <"A mulher já esteve grávida?">
					comment = <"Registre como VERDADEIRO se a mulher já está grávida. Só é útil para gravar se não há outros dados detalhados sobre as gravidezes disponíveis, ou onde pode ser utilizado para apoiar atividades baseadas no conhecimento, tais como apoio à decisão clínica.">
				>
				["at0015"] = <
					text = <"Nascimentos a termo">
					description = <"Número de bebês nascidos em ou após 37 semanas de gestação.">
				>
				["at0016"] = <
					text = <"Nascimento prematuro">
					description = <"Número de crianças nascidas com menos de 37 semanas de gestação.">
				>
				["at0017"] = <
					text = <"Abortos">
					description = <"Número de gravidezes não viáveis por todas as causas e em qualquer gestação, incluindo abortos espontâneos, abortos induzidos e gravidezes ectópicas.">
					comment = <"Quando todos “abortos espontâneos\",\" abortos induzidos\" e \"gravidez ectópicas” são registrados, este número deve ser igual a sua soma.">
				>
				["at0018"] = <
					text = <"Nascimentos múltiplos">
					description = <"Número de eventos de nascimento (partos) nos quais nasceu mais de um feto.">
				>
			>
		>
		["es-co"] = <
			items = <
				["at0000"] = <
					text = <"Antecedentes Obstétricos">
					description = <"Una visión general de la historia obstétrica de una mujer, incluyendo un resumen de todos los embarazos.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Gravidez">
					description = <"Número de veces que la mujer ha estado en embarazo, incluida la gestacion actual, si es apropiado">
				>
				["at0003"] = <
					text = <"Paridad">
					description = <"Número de veces que una mujer ha dado a luz después de 20 semanas y 0 días de gestación">
					comment = <"las terminaciones tardías del embarazo (después de 20 semanas de gestación) no están incluidos. 

Cuando se registran los Nacimientos a termino (T) y los nacimientos prematuros (P)' , este número debe ser igual a su suma . Nota : La paridad no es considerado como el mismo que el número de niños nacidos - por ejemplo, los gemelos se registraría como un evento de nacimiento.">
				>
				["at0004"] = <
					text = <"abortos involuntarios">
					description = <"numero de veces que una mujer ha tenido un aborto involuntario antes de las 20 semanas de gestación">
					comment = <"Incluyendo abortos perdidos">
				>
				["at0005"] = <
					text = <"Embarazos interrumpidos">
					description = <"Número de veces que una mujer ha tenido un embarazo interrumpidos, independientemente de las semanas de gestación">
				>
				["at0006"] = <
					text = <"Nacidos Vivos">
					description = <"Número de nacidos Vivos">
				>
				["at0007"] = <
					text = <"vivos">
					description = <"Número de Nacidos que continuan vivos">
				>
				["at0008"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Fecha de Actualización">
					description = <"Fecha de ultima actualización del resumen">
				>
				["at0010"] = <
					text = <"Cesárea">
					description = <"Número de Cesáreas realizadas">
				>
				["at0011"] = <
					text = <"embarazos ectópicos">
					description = <"Número de embarazos ectópicos">
				>
				["at0012"] = <
					text = <"Nacidos Nuertos">
					description = <"Numero de Nacidos Muertos">
					comment = <"Nota : definición muerte fetal puede estar opcionalmente registrado en el apartado específico en el Protocolo">
				>
				["at0013"] = <
					text = <"Definición muerte fetal">
					description = <"Definición muerte fetal">
					comment = <"útil para comparar datos de diferentes regiones, como las definiciones de muerte fetal pueden variar. Por ejemplo, en Australia un bebé nacido muerto tendrá un embarazo de más de 20 semanas o, si la gestación es desconocida, y pesan más de 400 gramos.">
				>
				["at0014"] = <
					text = <"Nunca ha estado embarazada?">
					description = <"ha estado alguna vez embarazada?">
					comment = <"Registrar como verdadero si nunca ha estado embarazada. Sólo es útil para registrar si no hay otros datos detallados sobre los embarazos anteriores, o donde pueda ser utilizado para apoyar otras actividades basadas en el conocimiento , tales como apoyo a la decisión clínica.">
				>
				["at0015"] = <
					text = <"Nacimientos a Termino">
					description = <"Numero de recién nacidos de 37 o mas semanas de gestación">
				>
				["at0016"] = <
					text = <"Nacimientos pretermino">
					description = <"Numero de recien nacidos antes de las 37 semanas de gestación">
				>
				["at0017"] = <
					text = <"Abortos">
					description = <"Número de embarazos no viables por todas las causas y en cualquier edad gestacional , incluyendo abortos involuntarios , Embarazos interrumpidos y embarazos ectópicos">
					comment = <"Cuando todos los Abortos Involuntarios, Terminaciones y Los embarazos ectópicos son registrados , este número debe ser igual a su suma.">
				>
				["at0018"] = <
					text = <"Nacimientos Múltiples (M)">
					description = <"Número de nacimientos en los cuales ha nacido mas de un producto vivo">
				>
			>
		>
	>

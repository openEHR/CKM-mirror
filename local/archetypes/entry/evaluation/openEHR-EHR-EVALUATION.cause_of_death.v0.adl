archetype (adl_version=1.4; uid=ce91d191-2ab5-478e-a349-6c4ce8b0cbea)
	openEHR-EHR-EVALUATION.cause_of_death.v0

concept
	[at0000]	-- Cause of death
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Mikkel Johan Gaup Grønmo">
				["organisation"] = <"Regional forvaltning EPJ, Helse Nord">
				["email"] = <"mikkel.johan.gaup.gronmo@helse-nord.no">
			>
			accreditation = <"Nasjonal IKT HF">
		>
	>
description
	original_author = <
		["name"] = <"Mikkel Johan Gaup Grønmo">
		["organisation"] = <"Forvaltningssenter EPJ, Helse-Nord RHF">
		["email"] = <"mikkel.johan.gaup.gronmo@helse-nord.no">
		["date"] = <"2020-08-03">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"*To record details about specific diseases, injuries or conditions that directly caused or contributed to the death of an individual, as required on a death notification or certificate. (en)">
			use = <"*Use to record details about specific diseases, injuries or conditions that directly caused or contributed to the death of an individual, as required on a death notification or certificate. 

This archetype has been designed to be used within COMPOSITION.certificate. It may also be used within:
- the COMPOSITION.report-post_mortem archetype, as one of a number of components of a post mortem report; or
- the COMPOSITION.report-case_investigation archetype, as one of a number of components of a case investigation, such as a maternal mortality investigation.

A suggested example of use:
Cerebral haemorrhage (as 'Direct cause', A) 
- 'due to' Metastasis of the brain (as 'Intermediate cause', B);
- 'due to' Breast cancer (as the earliest 'Underlying cause'). (en)">
			keywords = <"sertifikat", "årsak", "grunn">
			misuse = <"*Not to be used record details about diseased, injuries or conditions. Use the EVALUATION.problem_diagnosis or ACTION.procedure archetypes for this purpose. (en)">
			copyright = <"© openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about specific diseases, injuries or conditions that directly caused or contributed to the death of an individual, as required on a death notification or certificate.">
			use = <"Use to record details about specific diseases, injuries or conditions that directly caused or contributed to the death of an individual, as required on a death notification or certificate. 

This archetype has been designed to be used within COMPOSITION.certificate. It may also be used within:
- the COMPOSITION.report-post_mortem archetype, as one of a number of components of a post mortem report; or
- the COMPOSITION.report-case_investigation archetype, as one of a number of components of a case investigation, such as a maternal mortality investigation.

A suggested example of use:
Cerebral haemorrhage (as 'Direct cause', A) 
- 'due to' Metastasis of the brain (as 'Intermediate cause', B);
- 'due to' Breast cancer (as the earliest 'Underlying cause').">
			keywords = <"certificate", "cause", "reason">
			misuse = <"Not to be used record details about diseased, injuries or conditions. Use the EVALUATION.problem_diagnosis or ACTION.procedure archetypes for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "SB Bhattacharyya, Bhattacharyyas Clinical Records Research & Informatics LLP, India", "Yexuan Cheng, 浙江大学, China", "Candice de Lisser, Ministry of Health and Wellness, Jamaica", "Stefan Dubois, Dep't of Pathology and Genetics, University Hospitals of Lund and Malmö, Sweden", "Grant Forrest, Lunaria Ltd, United Kingdom", "Rosane Gotardo, Systema Ltda., Brazil", "Heather Grain, Llewelyn Grain Informatics, Australia", "Martin Grundberg, Cambio Healthcare Systems, Sweden", "Mikkel Johan Gaup Grønmo, Regional forvaltning EPJ, Helse Nord, Norway (openEHR Editor)", "Joost Holslag, Nedap, Netherlands", "Evelyn Hovenga, EJSH Consulting, Australia", "Kanika Kuwelker, Helse Vest IKT, Norway (openEHR Editor)", "Jörgen Kuylenstierna, eWeave AB, Sweden", "Liv Laugen, ​Oslo University Hospital, Norway, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Michael Lutz, BITsoft, Germany", "Manisha Mantri, C-DAC, India", "James McClay, University of Nebraska Medical Center, United States", "Paul Miller, NES Digital Service, NHS Scotland, United Kingdom", "Mikael Nyström, Cambio Healthcare Systems AB, Sweden", "Alan Smith, OXLEAS NHS Mental Health Foundation Trust, United Kingdom", "Natalia Strauch, Medizinische Hochschule Hannover, Germany", "Norwegian Review Summary, Norwegian Public Hospitals, Norway", "Anders Thurin, VGR, Sweden", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)", "Martijn van Eenennaam, Nedap Healthcare, Netherlands", "Marit Alice Venheim, Helse Vest IKT, Norway (openEHR Editor)", "Lin Zhang, Taikang Insurance Group, China">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Cause of Death and the Death Certificate [Internet]. College of American Pathologists (CAP); 2006 [cited 2022 Jun 05]. Available from: https://www.health.state.mn.us/people/vitalrecords/physician-me/docs/capcodbook.pdf

International statistical classification of diseases and related health problems [Internet]. World Health Organisation; 2011, pp 211 & 214 [cited 2022 Jun 05]. Available from: https://icd.who.int/browse10/Content/statichtml/ICD10Volume2_en_2016.pdf.

Physicians' Manual on Medical Certification of Cause of Death [Internet]. New Delhi: Office of the Registrar General, India; 2012 [cited 2022 Jun 05]. Available from: ttps://ncdirindia.org/e-mor/Download/Physician's_Manual_MCCD.pdf.

Slik skal dødsmeldingen fylles ut [Internet]. Folkehelseinstituttets; 2019 Sep 13 [updated 2021 Jan 21; cited 2022 Jun 05]. Available from: https://www.fhi.no/hn/helseregistre-og-registre/dodsarsaksregisteret/slik-skal-elektronisk-dodsmelding-fylles-ut/.

US Standard Certificate of Death [Internet]. Atlanta; Centre for Disease Control; revised 2003 Nov (cited 2022 Jun 05]. Available from: https://www.cdc.gov/nchs/data/dvs/DEATH11-03final-ACC.pdf.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"55134B6546AAE1BB8257FA7FA8395585">
		["build_uid"] = <"f88a43b9-f2a0-456e-af98-0a6167775663">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Cause of death
		data matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0019] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0002] occurrences matches {0..1} matches {    -- Direct cause
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0017] occurrences matches {0..1} matches {    -- Direct time interval
						value matches {
							DV_DURATION matches {
								value matches {|>=PT0S|}
							}
							DV_INTERVAL<DV_DURATION> matches {
								lower matches {
									DV_DURATION matches {
										value matches {|>=PT0S|}
									}
								}
								upper matches {
									DV_DURATION matches {
										value matches {|>=PT0S|}
									}
								}
							}
							DV_TEXT matches {*}
						}
					}
					CLUSTER[at0003] occurrences matches {0..*} matches {    -- Antecedent cause(s)
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0004] matches {    -- Cause
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0005] occurrences matches {0..1} matches {    -- Order
								value matches {
									DV_TEXT matches {*}
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0006,    -- B
											at0007,    -- C
											at0008]    -- D
										}
									}
								}
							}
							ELEMENT[at0009] occurrences matches {0..1} matches {    -- Time interval
								value matches {
									DV_TEXT matches {*}
									DV_INTERVAL<DV_DURATION> matches {
										lower matches {
											DV_DURATION matches {
												value matches {|>=PT0S|}
											}
										}
										upper matches {
											DV_DURATION matches {
												value matches {|>=PT0S|}
											}
										}
									}
									DV_DURATION matches {
										value matches {|>=PT0S|}
									}
									DV_DATE_TIME matches {*}
								}
							}
						}
					}
					CLUSTER[at0020] occurrences matches {0..*} matches {    -- Contributing conditions
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0018] matches {    -- Condition name
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0021] occurrences matches {0..1} matches {    -- Time interval
								value matches {
									DV_TEXT matches {*}
									DV_INTERVAL<DV_DURATION> matches {
										lower matches {
											DV_DURATION matches {
												value matches {|>=PT0S|}
											}
										}
										upper matches {
											DV_DURATION matches {
												value matches {|>=PT0S|}
											}
										}
									}
									DV_DURATION matches {
										value matches {|>=PT0S|}
									}
									DV_DATE_TIME matches {*}
								}
							}
						}
					}
					allow_archetype CLUSTER[at0012] occurrences matches {0..*} matches {    -- Additional details
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0013] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0014] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0015] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0016] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Cause of death">
					description = <"Details about specific diseases, injuries or conditions that directly caused or contributed to the death of an individual.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0014"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Direct cause">
					description = <"The disease, injury or condition that directly led to, or occurred closest to the time of death.">
					comment = <"It is strongly recommended that 'Direct cause' be coded with an external terminology. 

This data element may also be considered as the Cause labelled as 'A' within a death certificate recording a chain of events leading to death. For example:
Cerebral haemorrhage (as 'Direct cause', A) 
- 'due to' Metastasis of the brain ('Antecedent cause', B);
- 'due to' Breast cancer (as the earliest 'Antecedent cause', C; or underlying cause).">
				>
				["at0003"] = <
					text = <"Antecedent cause(s)">
					description = <"Details about one or more diseases, injuries or conditions in the chain of events preceding death, recorded in order from most recent onset to the earliest onset.">
					comment = <"In the following example, there would only need to be two instances of this 'Antecedent cause(s)' CLUSTER represented in a template:
- Cerebral haemorrhage (as 'Direct cause', A) 
- 'due to' Metastasis of the brain ('Antecedent cause', B);
- 'due to' Breast cancer (as the earliest 'Antecedent cause', C; or underlying cause).
If there is more than one 'Antecedent cause', each is represented as a separate instance of this CLUSTER.">
				>
				["at0004"] = <
					text = <"Cause">
					description = <"Identification of an antecedent disease, injury or condition that directly contributed to the 'Direct cause'.">
					comment = <"It is strongly recommended that 'Cause' be coded with an external terminology.">
				>
				["at0005"] = <
					text = <"Order">
					description = <"Order of onset of the identified 'Antecedent cause' in the chain of events leading to death.">
					comment = <"The Cause labelled as 'B' is considered the cause that most closely preceded the 'Direct cause' and the Cause labelled as 'C' precedes 'B'. The Cause labelled as 'D' is considered the earliest Cause in the chain of events leading to death, and may also be known as the 'Underlying cause'.">
				>
				["at0006"] = <
					text = <"B">
					description = <"">
				>
				["at0007"] = <
					text = <"C">
					description = <"">
				>
				["at0008"] = <
					text = <"D">
					description = <"">
				>
				["at0009"] = <
					text = <"Time interval">
					description = <"The time interval from onset of the identified 'Cause' to death.">
				>
				["at0015"] = <
					text = <"Last updated">
					description = <"The date when the cause of death summary was last updated.">
				>
				["at0013"] = <
					text = <"Comment">
					description = <"Additional narrative about the diseases, injuries and conditions leading to death, not captured in other fields.">
				>
				["at0012"] = <
					text = <"Additional details">
					description = <"Additional details about the cause of death.">
				>
				["at0016"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0017"] = <
					text = <"Direct time interval">
					description = <"The time interval from onset of the identified 'Direct cause' to death.">
				>
				["at0018"] = <
					text = <"Condition name">
					description = <"Other significant condition(s) contributing, but not resulting in the underlying cause of death.">
					comment = <"It is strongly recommended that 'Other condition' be coded with an external terminology.">
				>
				["at0019"] = <
					text = <"Description">
					description = <"Narrative description about the contributing diseases, injuries, conditions and events leading to death.">
				>
				["at0020"] = <
					text = <"Contributing conditions">
					description = <"Details about other significant conditions that may have contributed to, but not had a caused in the underlying cause of death.">
				>
				["at0021"] = <
					text = <"Time interval">
					description = <"The time interval from onset of the identified disease, injury or condition to death.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Dødsårsak">
					description = <"*Details about specific diseases, injuries or conditions that directly caused or contributed to the death of an individual. (en)">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0014"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"*Direct cause (en)">
					description = <"*The disease, injury or condition that directly led to, or occurred closest to the time of death. (en)">
					comment = <"*It is strongly recommended that 'Direct cause' be coded with an external terminology. 

This data element may also be considered as the Cause labelled as 'A' within a death certificate recording a chain of events leading to death. For example:
Cerebral haemorrhage (as 'Direct cause', A) 
- 'due to' Metastasis of the brain ('Antecedent cause', B);
- 'due to' Breast cancer (as the earliest 'Antecedent cause', C; or underlying cause). (en)">
				>
				["at0003"] = <
					text = <"*Antecedent cause(s) (en)">
					description = <"*Details about one or more diseases, injuries or conditions in the chain of events preceding death, recorded in order from most recent onset to the earliest onset. (en)">
					comment = <"*In the following example, there would only need to be two instances of this 'Antecedent cause(s)' CLUSTER represented in a template:
- Cerebral haemorrhage (as 'Direct cause', A) 
- 'due to' Metastasis of the brain ('Antecedent cause', B);
- 'due to' Breast cancer (as the earliest 'Antecedent cause', C; or underlying cause).
If there is more than one 'Antecedent cause', each is represented as a separate instance of this CLUSTER. (en)">
				>
				["at0004"] = <
					text = <"*Cause (en)">
					description = <"*Identification of an antecedent disease, injury or condition that directly contributed to the 'Direct cause'. (en)">
					comment = <"*It is strongly recommended that 'Cause' be coded with an external terminology. (en)">
				>
				["at0005"] = <
					text = <"Rekkefølge">
					description = <"*Order of onset of the identified 'Antecedent cause' in the chain of events leading to death. (en)">
					comment = <"*The Cause labelled as 'B' is considered the cause that most closely preceded the 'Direct cause' and the Cause labelled as 'C' precedes 'B'. The Cause labelled as 'D' is considered the earliest Cause in the chain of events leading to death, and may also be known as the 'Underlying cause'. (en)">
				>
				["at0006"] = <
					text = <"*B (en)">
					description = <"">
				>
				["at0007"] = <
					text = <"*C (en)">
					description = <"">
				>
				["at0008"] = <
					text = <"*D (en)">
					description = <"">
				>
				["at0009"] = <
					text = <"*Time interval (en)">
					description = <"*The time interval from onset of the identified 'Cause' to death. (en)">
				>
				["at0015"] = <
					text = <"Sist oppdatert">
					description = <"*The date when the cause of death summary was last updated. (en)">
				>
				["at0013"] = <
					text = <"Kommentar">
					description = <"*Additional narrative about the diseases, injuries and conditions leading to death, not captured in other fields. (en)">
				>
				["at0012"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere detaljer om dødsårsak.">
				>
				["at0016"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
				["at0017"] = <
					text = <"*Direct time interval (en)">
					description = <"*The time interval from onset of the identified 'Direct cause' to death. (en)">
				>
				["at0018"] = <
					text = <"*Condition name (en)">
					description = <"*Other significant condition(s) contributing, but not resulting in the underlying cause of death. (en)">
					comment = <"*It is strongly recommended that 'Other condition' be coded with an external terminology. (en)">
				>
				["at0019"] = <
					text = <"Beskrivelse">
					description = <"*Narrative description about the contributing diseases, injuries, conditions and events leading to death. (en)">
				>
				["at0020"] = <
					text = <"*Contributing conditions (en)">
					description = <"*Details about other significant conditions that may have contributed to, but not had a caused in the underlying cause of death. (en)">
				>
				["at0021"] = <
					text = <"*Time interval (en)">
					description = <"*The time interval from onset of the identified disease, injury or condition to death. (en)">
				>
			>
		>
	>

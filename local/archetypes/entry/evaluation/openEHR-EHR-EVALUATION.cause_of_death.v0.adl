archetype (adl_version=1.4; uid=ce91d191-2ab5-478e-a349-6c4ce8b0cbea)
	openEHR-EHR-EVALUATION.cause_of_death.v0

concept
	[at0000]	-- Cause of death
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Hannes Bornhorst, Natalia Strauch">
				["organisation"] = <"Medizinische Hochschule Hannover">
				["email"] = <"Strauch.Natalia@mh-hannover.de">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Mikkel Johan Gaup Grønmo, John Tore Valand">
				["organisation"] = <"Regional forvaltning EPJ, Helse Nord, Helse Bergen">
				["email"] = <"mikkel.johan.gaup.gronmo@helse-nord.no, john.tore.valand@helse-vest-ikt.no">
			>
			accreditation = <"Nasjonal IKT HF">
		>
	>
description
	original_author = <
		["name"] = <"Mikkel Johan Gaup Grønmo">
		["organisation"] = <"Forvaltningssenter EPJ, Helse-Nord RHF">
		["email"] = <"mikkel.johan.gaup.gronmo@helse-nord.no">
		["date"] = <"2020-08-03">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Zur Darstellung von Details zu bestimmten Krankheiten, Zuständen oder Verletzungen, die den Tod einer Person hervorgerufen oder dazu beigetragen haben.">
			use = <"Benutzen Sie den Archetyp zur Darstellung von Details zu bestimmten Krankheiten, Zuständen oder Verletzungen, die den Tod einer Person hervorgerufen oder dazu beigetragen haben.

Ausschnitt an Beispielen für Anwendungsfälle:
- Eine Sterbeurkunde oder Todesbescheinigung verschachtelt in dem COMPOSITION.certificate-Archetyp;
- Ein Post-Mortem-Bericht, verschachtelt in dem COMPOSITION.report-post_mortem-Archetyp, als eine mehrerer Komponenten eines Post-Mortem-Berichts;
- Krankheitsregister und epidemiologische Überwachung, verschachtelt in dem COMPOSITION.report-case_investigation-Archetyp als eine mehrerer Komponenten einer Fall-Untersuchung, wie eine Müttersterblichkeits-Untersuchung.

Beispiel 1:
- Gehirnblutung (als \"Direkte Ursache\", A)
- \"durch\" Metastase im Gehirn (als \"Vorhergehende Ursache B\")
- \"durch\" Brustkrebs (als früheste \"Vorhergehende Ursache C\"). Da Brustkrebs als früheste vorhergehende Ursache aufgeführt ist, könnte sie auch als \"Zugrundeliegende Ursache\" gekennzeichnet sein.

Beispiel 2:
- Pulmonaler Embolus (als \"Direkte Ursache\", A)
- \"durch\" gebrochener Oberschenkelknochen (als \"Vorhergehende Ursache B\")
- \"durch\" unbeabsichtigter Sturz (als \"Vorhergehende / Zugrundeliegende Ursache C\").">
			keywords = <"*certificate (en)", "*cause (en)", "*reason (en)", "*death (en)", "*condition (en)", "*antecedent (en)", "*post mortem (en)", "*surveillance (en)", "*notification (en)", "*injury (en)", "*disease (en)", "*summary (en)", "*autopsy (en)">
			misuse = <"Nicht zur Darstellung von Details zu Krankheiten, Zuständen oder Verletzungen, die keine Beteiligung am Tod der Person hatten. Zu diesem Zweck die Archetypen EVALUATION.problem_diagnosis oder ACTION.procedure benutzen.">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere detaljer om spesifikke sykdommer, tilstander eller skader som forårsaket eller bidro til individets dødsfall.">
			use = <"Brukes for å registrere detaljer om spesifikke sykdommer, tilstander eller skader som forårsaket eller bidro til individets dødsfall.

Brukseksempler inkluderer, men er ikke begrenset til:
- dødsattest eller -melding, nøstet inn i COMPOSITION.certificate arketypen;
- obduksjonsrapport, nøstet inn i COMPOSITION.report-post_mortem arketypen, som en av flere komponenter i en obduksjonsrapport; eller
- Helseregistre eller MSIS, nøstet inn i COMPOSITION.report-case_investigation arketypen, som en av flere komponenter i en undersøkelse, så som smittesporing.

Eksempel 1:
- Hjerneblødning (som 'Umiddelbar dødsårsak', A)
-- 'som følge av' Hjernemetastase (som 'Forutgående årsak B')
-- 'som følge av' Brystkreft (som første 'Forutgående årsak C'). Da brystkreft er den tidligste registrerte forutgående årsaken, så kan tilstanden også være kjent som 'Underliggende årsak'.

Eksempel 2:
- Lungeemboli (som 'Umiddelbar dødsårsak', A)
-- 'som følge av' Lårbensbrudd (som 'Forutgående årsak B')
-- 'som følge av' Fallulykke (som 'Forutgående årsak C').">
			keywords = <"Sertifikat", "årsak", "død", "tilstand", "obduksjon", "overvåking", "melding", "forutgående", "skade", "sykdom", "oppsummering", "mord", "drap", "MORS", "umiddelbar", "mellomliggende", "underliggende", "medvirkende dødsårsak", "dødsårsak", "ytre årsak", "dødsmelding", "selvmord">
			misuse = <"Brukes ikke for å registrere detaljer om sykdommer, tilstander eller skader som ikke har bidratt til individets død. Bruk EVALUATION.problem_diagnosis eller ACTION.procedure arketypene til det formålet.">
			copyright = <"© openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about specific diseases, conditions or injuries that caused or contributed to the death of an individual.">
			use = <"Use to record details about specific diseases, conditions or injuries that caused or contributed to the death of an individual.

Examples of use cases include, but are not limited to:
- a death certificate or notification, nested within the COMPOSITION.certificate archetype;
- a post mortem report, nested within the COMPOSITION.report-post_mortem archetype, as one of a number of components of a post mortem report; or
- disease registries and epidemiological surveillance, nested within the COMPOSITION.report-case_investigation archetype, as one of a number of components of a case investigation, such as a maternal mortality investigation.

Example 1:
- Cerebral haemorrhage (as 'Direct cause', A) 
-- 'due to' Metastasis of the brain (as 'Antecedent cause B');
-- 'due to' Breast cancer (as the earliest 'Antecedent cause C'). As Breast cancer is the earliest recorded antecedent cause, this condition may also be known as the 'Underlying cause'.

Example 2: 
- Pulmonary embolus (as 'Direct cause', A) 
-- 'due to' Fractured femur (as 'Antecedent cause B')
-- 'due to' Accidental fall (as 'Antecedent/Underlying cause C').">
			keywords = <"certificate", "cause", "reason", "death", "condition", "antecedent", "post mortem", "surveillance", "notification", "injury", "disease", "summary", "autopsy">
			misuse = <"Not to be used to record details about diseases, conditions or injuries that have not contributed to the death of an individual. Use the EVALUATION.problem_diagnosis or ACTION.procedure archetypes for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Marit Alice Venheim, Helse Vest IKT, Norway (openEHR Editor)", "Vebjørn Arntzen, Oslo universitetssykehus HF, Norway", "Astrid Askeland, Dips AS, Norway", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (Nasjonal IKT redaktør)", "Aleocidio Balzanelo, DASA, Brazil", "Malin Berg, DIPS ASA, Norway", "SB Bhattacharyya, Bhattacharyyas Clinical Records Research & Informatics LLP, India", "Marte Rime Bø, Direktoratet for e-helse, Norway", "Yexuan Cheng, 浙江大学, China", "Candice de Lisser, Ministry of Health and Wellness, Jamaica", "Stefan Dubois, Dep't of Pathology and Genetics, University Hospitals of Lund and Malmö, Sweden", "Are Edvardsen, SKDE, Helse Nord RHF, Norway", "Kåre Flø, DIPS ASA, Norway", "Grant Forrest, Lunaria Ltd, United Kingdom", "Rosane Gotardo, Systema Ltda., Brazil", "Heather Grain, Llewelyn Grain Informatics, Australia", "Martin Grundberg, Cambio Healthcare Systems, Sweden", "Maria Grønmo, Norway", "Mikkel Johan Gaup Grønmo, Regional forvaltning EPJ, Helse Nord, Norway (Nasjonal IKT redaktør)", "Amanda Herbrand, University Hospital Basel, Switzerland", "Joost Holslag, Nedap, Netherlands", "Evelyn Hovenga, EJSH Consulting, Australia", "Jörgen Kuylenstierna, eWeave AB, Sweden", "Siri Laronningen, Kreftregisteret, Norway", "Liv Laugen, Oslo universitetssykehus, Norway", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Michael Lutz, BITsoft, Germany", "Manisha Mantri, C-DAC, India", "James McClay, University of Nebraska Medical Center, United States", "Paul Miller, NES Digital Service, NHS Scotland, United Kingdom", "Hilde Mjærfoss, Norsk Helsenett SF, Norway", "Svenne Naumann, Finnmarkssykehuset, Norway", "Mikael Nyström, Cambio Healthcare Systems AB, Sweden", "Bjørn Næss, DIPS ASA, Norway", "Norwegian Review Summary, Norwegian Public Hospitals, Norway", "FREDERICO RIBEIRO, Hospital das Clínicas da UFPE, Brazil", "Terje Sagmyr, Helse Vest IKT, Norway", "Alan Smith, OXLEAS NHS Mental Health Foundation Trust, United Kingdom", "Frode Stenvik, Helse Sør-Øst, Norway", "Natalia Strauch, Medizinische Hochschule Hannover, Germany", "Olav Thorsen, Stavanger University Hospital, Norway", "Anders Thurin, VGR, Sweden", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)", "Lars Uhlin Hansen, UNN, Norway", "Martijn van Eenennaam, Nedap Healthcare, Netherlands", "Ina Wille, Helse-Vest RHF, Norway", "Lin Zhang, Taikang Insurance Group, China">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Cause of Death and the Death Certificate [Internet]. College of American Pathologists (CAP); 2006 [cited 2022 Jun 05]. Available from: https://www.health.state.mn.us/people/vitalrecords/physician-me/docs/capcodbook.pdf

International statistical classification of diseases and related health problems [Internet]. World Health Organisation; 2011, pp 211 & 214 [cited 2022 Jun 05]. Available from: https://icd.who.int/browse10/Content/statichtml/ICD10Volume2_en_2016.pdf.

Physicians' Manual on Medical Certification of Cause of Death [Internet]. New Delhi: Office of the Registrar General, India; 2012 [cited 2022 Jun 05]. Available from: ttps://ncdirindia.org/e-mor/Download/Physician's_Manual_MCCD.pdf.

Slik skal dødsmeldingen fylles ut [Internet]. Folkehelseinstituttets; 2019 Sep 13 [updated 2021 Jan 21; cited 2022 Jun 05]. Available from: https://www.fhi.no/hn/helseregistre-og-registre/dodsarsaksregisteret/slik-skal-elektronisk-dodsmelding-fylles-ut/.

US Standard Certificate of Death [Internet]. Atlanta; Centre for Disease Control; revised 2003 Nov (cited 2022 Jun 05]. Available from: https://www.cdc.gov/nchs/data/dvs/DEATH11-03final-ACC.pdf.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"4DECA6BBFDB1A61CBEB6B03B9F9C0208">
		["build_uid"] = <"9d761955-0753-4e7c-9f12-8b2bf211db46">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Cause of death
		data matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0019] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0002] occurrences matches {0..1} matches {    -- Direct cause
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0017] occurrences matches {0..1} matches {    -- Direct cause duration
						value matches {
							DV_DURATION matches {
								value matches {|>=PT0S|}
							}
							DV_INTERVAL<DV_DURATION> matches {
								lower matches {
									DV_DURATION matches {
										value matches {|>=PT0S|}
									}
								}
								upper matches {
									DV_DURATION matches {
										value matches {|>=PT0S|}
									}
								}
							}
							DV_TEXT matches {*}
						}
					}
					CLUSTER[at0003] occurrences matches {0..*} matches {    -- Antecedent cause(s)
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0004] matches {    -- Cause
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0005] occurrences matches {0..1} matches {    -- Order
								value matches {
									DV_TEXT matches {*}
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0006,    -- B
											at0007,    -- C
											at0008]    -- D
										}
									}
								}
							}
							ELEMENT[at0009] occurrences matches {0..1} matches {    -- Duration
								value matches {
									DV_TEXT matches {*}
									DV_INTERVAL<DV_DURATION> matches {
										lower matches {
											DV_DURATION matches {
												value matches {|>=PT0S|}
											}
										}
										upper matches {
											DV_DURATION matches {
												value matches {|>=PT0S|}
											}
										}
									}
									DV_DURATION matches {
										value matches {|>=PT0S|}
									}
									DV_DATE_TIME matches {*}
								}
							}
						}
					}
					CLUSTER[at0020] occurrences matches {0..*} matches {    -- Contributing conditions
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0018] matches {    -- Condition
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0021] occurrences matches {0..1} matches {    -- Duration
								value matches {
									DV_TEXT matches {*}
									DV_INTERVAL<DV_DURATION> matches {
										lower matches {
											DV_DURATION matches {
												value matches {|>=PT0S|}
											}
										}
										upper matches {
											DV_DURATION matches {
												value matches {|>=PT0S|}
											}
										}
									}
									DV_DURATION matches {
										value matches {|>=PT0S|}
									}
									DV_DATE_TIME matches {*}
								}
							}
						}
					}
					allow_archetype CLUSTER[at0012] occurrences matches {0..*} matches {    -- Additional details
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0013] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0014] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0015] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0016] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}






ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Cause of death">
					description = <"Details about specific diseases, conditions or injuries that caused or contributed to the death of an individual.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0014"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Direct cause">
					description = <"The disease, condition or injury that directly led to, or occurred closest to, the time of death.">
					comment = <"It is recommended that 'Direct cause' be coded with an external terminology. 

This data element may also be considered as the Cause labelled as 'A' within a death certificate recording a sequence of events leading to death. For example:
Cerebral haemorrhage (as 'Direct cause', A) 
- 'due to' Metastasis of the brain ('Antecedent cause B');
- 'due to' Breast cancer (as the earliest 'Antecedent cause C'; or 'Underlying cause').">
				>
				["at0003"] = <
					text = <"Antecedent cause(s)">
					description = <"Details about one or more diseases, conditions or injuries in the sequence of events preceding death, recorded in order from most recent onset to the earliest onset.">
					comment = <"In the following example, there would only need to be two instances of this 'Antecedent cause(s)' CLUSTER represented in a template:
- Cerebral haemorrhage (as 'Direct cause', A) 
- 'due to' Metastasis of the brain ('Antecedent cause B');
- 'due to' Breast cancer (as the earliest 'Antecedent cause C'; or 'Underlying cause').
If there is more than one 'Antecedent cause', each is represented as a separate instance of this CLUSTER.">
				>
				["at0004"] = <
					text = <"Cause">
					description = <"Identification of an antecedent disease, condition or injury that directly contributed to the 'Direct cause'.">
					comment = <"It is recommended that 'Cause' be coded with an external terminology.">
				>
				["at0005"] = <
					text = <"Order">
					description = <"Order of onset of the identified 'Antecedent cause' in the sequence of events leading to death.">
					comment = <"The Cause labelled as 'B' is considered the cause that most closely preceded the 'Direct cause' and the Cause labelled as 'C' precedes 'B'. The Cause labelled as 'D' is considered the earliest Cause in the sequence of events leading to death, and may also be known as the 'Underlying cause'.">
				>
				["at0006"] = <
					text = <"B">
					description = <"The disease, condition or injury most closely preceding the 'Direct cause (A)'.">
				>
				["at0007"] = <
					text = <"C">
					description = <"The disease, condition or injury most closely preceding the 'Cause' order 'B'.">
				>
				["at0008"] = <
					text = <"D">
					description = <"The disease, condition or injury considered the earliest in the sequence of events leading to death.">
					comment = <"Also known as the 'Underlying cause'.">
				>
				["at0009"] = <
					text = <"Duration">
					description = <"The approximate interval from onset of the identified 'Cause' to death.">
				>
				["at0015"] = <
					text = <"Last updated">
					description = <"The date when the cause of death was last updated.">
				>
				["at0013"] = <
					text = <"Comment">
					description = <"Additional narrative about the cause of death, not captured in other fields.">
				>
				["at0012"] = <
					text = <"Additional details">
					description = <"Additional details about the cause of death.">
				>
				["at0016"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0017"] = <
					text = <"Direct cause duration">
					description = <"The approximate interval from onset of the identified 'Direct cause' to death.">
				>
				["at0018"] = <
					text = <"Condition">
					description = <"Name of a significant condition that may have contributed to the death, but without a causal association.">
					comment = <"It is recommended that 'Condition' be coded with an external terminology.">
				>
				["at0019"] = <
					text = <"Description">
					description = <"Narrative description about the contributing diseases, injuries, conditions and events leading to death.">
				>
				["at0020"] = <
					text = <"Contributing conditions">
					description = <"Details about other significant conditions that may have contributed to the death, but without a causal association.">
				>
				["at0021"] = <
					text = <"Duration">
					description = <"The approximate interval from onset of the identified 'Condition' to death.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Dødsårsak">
					description = <"Detaljer om spesifikke sykdommer, tilstander eller skader som forårsaket eller bidro til individets dødsfall.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Umiddelbar dødsårsak">
					description = <"Sykdom, tilstand eller skade som direkte førte til, eller skjedde i tilknytning til dødsfallet.">
					comment = <"Det anbefales å kode \"Umiddelbar dødsårsak\" med en ekstern terminologi. 

Dette dataelementet kan også brukes som Årsak merket 'A' i en dødsattest hvor årsaksekvensen som førte til død registreres. For eksempel: 
Hjerneblødning (som 'Umiddelbar dødsårsak', A)
- 'som følge av' Hjernemetastase (som 'Forutgående årsak B')
- 'som følge av' Brystkreft (som første 'Forutgående årsak C'; eller 'Underliggende årsak').">
				>
				["at0003"] = <
					text = <"Forutgående årsak(er)">
					description = <"Detaljer om en eller flere sykdommer, tilstander eller skader i en hendelsessekvens forut for dødsfallet, rekkefølgen registreres fra ferskeste til eldste debut.">
					comment = <"I følgende eksempel vil det kun være behov for to forekomster av dette 'Forutgående årsak(er)' CLUSTERet representert i et templat:
- Hjerneblødning (som 'Umiddelbar dødsårsak', A)
- 'som følge av' Hjernemetastase (som 'Forutgående årsak B')
- 'som følge av' Brystkreft (som første 'Forutgående årsak C'; eller 'Underliggende årsak'). 
Hvis det finnes flere enn en 'Forutgående årsak', skal de registreres som separate forekomster av dette CLUSTERet.">
				>
				["at0004"] = <
					text = <"Årsak">
					description = <"Identifisering av en forutgående sykdom, tilstand eller skade som førte til 'Umiddelbar dødsårsak'.">
					comment = <"Det anbefales å kode \"Årsak\" med en ekstern terminologi.">
				>
				["at0005"] = <
					text = <"Rekkefølge">
					description = <"Rekkefølgen for identifiserte 'Forutgående årsaker' i en hendelsessekvens som førte til død.">
					comment = <"Årsak merket som 'B' vil være årsaken som ledet til 'Umiddelbar dødsårsak' og Årsak merket som 'C' leder til årsak 'B'. Årsak merket som 'D' er å anse som den tidligste registrerte forutgående årsak, og kan også være kjent som 'Underliggende årsak'.">
				>
				["at0006"] = <
					text = <"B">
					description = <"Sykdom, tilstand eller skade som ledet til 'Umiddelbar dødsårsak (A)'.">
				>
				["at0007"] = <
					text = <"C">
					description = <"Sykdom, tilstand eller skade som ledet til 'Årsak' rekkefølge 'B'.">
				>
				["at0008"] = <
					text = <"D">
					description = <"Sykdom, tilstand eller skade ansett som den tidligste registrerte forutgående årsak.">
					comment = <"Også kjent som 'Underliggende årsak'.">
				>
				["at0009"] = <
					text = <"Varighet">
					description = <"Det omtrentlige intervallet fra debut av identifisert 'Årsak' til død.">
				>
				["at0012"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere detaljer om dødsårsak.">
				>
				["at0013"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekstbeskrivelse av dødsårsak som ikke er registrert i andre felt.">
				>
				["at0014"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0015"] = <
					text = <"Sist oppdatert">
					description = <"Dato for når dødsårsak sist ble oppdatert.">
				>
				["at0016"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
				["at0017"] = <
					text = <"Varighet Umiddelbar dødsårsak">
					description = <"Omtrentlig intervall fra debut av den identifiserte 'Umiddelbar dødsårsak' til død.">
				>
				["at0018"] = <
					text = <"Tilstand">
					description = <"Navn på relevant tilstand som kan ha bidratt til død, men uavhengig av årsaken til 'Forutgående årsak'">
					comment = <"Det anbefales å kode \"Tilstand\" med en ekstern terminologi.">
				>
				["at0019"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse om medvirkende sykdommer, tilstander, skader og hendelser som førte til død.">
				>
				["at0020"] = <
					text = <"Medvirkende årsaker">
					description = <"Detaljer om andre relevante tilstander som kan ha bidratt til død, men uavhengig av årsaken til 'Forutgående årsak'.">
				>
				["at0021"] = <
					text = <"Varighet">
					description = <"Det omtrentlige intervallet fra debut av identifisert 'Tilstand' til død.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Todesursache">
					description = <"Details zu spezifischen Krankheiten, Zuständen oder Verletzungen, die den Tod der Person hervorgerufen oder dazu beigetragen haben.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Direkte Ursache">
					description = <"Die Krankheit, Verletzung oder der Zustand, die/der direkt zum Tod geführt hat oder zeitlich am nächsten am Todeszeitpunkt liegt.">
					comment = <"Es wird empfohlen, die \"Direkte Ursache\" mit einer externen Terminologie zu kodieren.

Dieses Datenelement kann auch als \"A\" innerhalb des Totenscheins gekennzeichnet werden, um eine Abfolge von zum Tode führenden Ereignissen darzustellen. Zum Beispiel:
- Gehirnblutung (als \"Direkte Ursache\", A)
- \"durch\" Metastase im Gehirn (als \"Vorhergehende Ursache B\")
- \"durch\" Brustkrebs (als früheste \"Vorhergehende Ursache C\").">
				>
				["at0003"] = <
					text = <"Vorhergehende Ursache(n)">
					description = <"Details zu einer oder mehreren Krankheiten, Verletzungen oder Zuständen iin der Abfolge von Ereignissen vor dem Tod, aufgezeichnet in der Reihenfolge vom jüngsten Beginn bis zum frühesten Beginn.">
					comment = <"Im folgenden Beispiel müssten nur zwei CLUSTER-Instanzen von \"Vorhergehende Ursache(n)\" im Template verwendet werden:
- Gehirnblutung (als \"Direkte Ursache\", A)
- \"durch\" Metastase im Gehirn (als \"Vorhergehende Ursache B\")
- \"durch\" Brustkrebs (als früheste \"Vorhergehende Ursache C\" oder \"Zugrundeliegende Ursache\").
Bei mehr als einer \"Vorhergehenden Ursache\" wird jede Ursache als eine seperate Instanz dieses CLUSTERs dargestellt.">
				>
				["at0004"] = <
					text = <"Ursache">
					description = <"Identifizierung einer vorhergehenden Ursache, Verletzung oder Zustand, die/der direkt zur \"Direkten Ursache\" beigetragen hat.">
					comment = <"Es wird empfohlen die \"Ursache\" mit einer externen Terminologie zu kodieren.">
				>
				["at0005"] = <
					text = <"Ordnung">
					description = <"Die Reihenfolge der identifizierten \"Vorhergehenden Ursache\" in der Abfolge der Ereignisse unmittelbar vor dem Tod.">
					comment = <"Die zeitlich am nächsten vor der \"Direkten Ursache\" liegenden Ursache wird als \"B\" gekennzeichnet. Die zeitlich vor \"B\" liegende Ursache wird als \"C\" gekennzeichnet. Die mit \"D\" gekennzeichnete Ursache stellt den Beginn der zeitlichen Abfolge an Ereignissen vor dem Tod dar und kann auch als \"Zugrundeliegende Ursache\" gekennzeichnet sein.">
				>
				["at0006"] = <
					text = <"B">
					description = <"Die Krankheit, Verletzung oder der Zustand, die/der der \"Direkten Ursache (A)\" am nächsten vorangeht.">
				>
				["at0007"] = <
					text = <"C">
					description = <"Die Krankheit, Verletzung oder der Zustand, die/der der \"Direkten Ursache (B)\" am nächsten vorangeht.">
				>
				["at0008"] = <
					text = <"D">
					description = <"Die Krankheit, Verletzung oder der Zustand, die/der als früheste in der Abfolge von Ereignissen angesehen wird, die zum Tod führen.">
					comment = <"Auch als die \"Zugrundeliegende Ursache\" bekannt.">
				>
				["at0009"] = <
					text = <"Dauer">
					description = <"Der ungefähre Intervall zwischen Auftreten der identifizierten \"Ursache\" und dem Tod.">
				>
				["at0012"] = <
					text = <"Zusätzliche Informationen">
					description = <"Zusätzliche Informationen zur Ursache des Todes.">
				>
				["at0013"] = <
					text = <"Kommentar">
					description = <"Zusätzliche Beschreibung zur Ursache des Todes, mit Informationen, die in den sonstigen Feldern nicht abgedeckt wurden.">
				>
				["at0014"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0015"] = <
					text = <"Zuletzt aktualisiert">
					description = <"Das Datum, wann die Todesursache zuletzt aktualisiert wurde.">
				>
				["at0016"] = <
					text = <"Erweiterung">
					description = <"Zusätzliche Informationen zur Erfassung lokaler Inhalte oder Anpassung an andere Referenzmodelle/Formalismen.">
					comment = <"Zum Beispiel: Lokaler Informationsbedarf oder zusätzliche Metadaten zur Anpassung an FHIR-Ressourcen.">
				>
				["at0017"] = <
					text = <"Direkte Ursache Dauer">
					description = <"Das ungefähre Intervall zwischen Auftreten der identifizierten \"Direkten Ursache\" bis zum Tod.">
				>
				["at0018"] = <
					text = <"Zustand">
					description = <"Name eines relevanten Zustands, der zum Tod beigetragen haben könnte, jedoch ohne direkten kausalen Zusammenhang.">
					comment = <"Es wird empfohlen, den \"Zustand\" mit einer externen Terminologie zu kodieren.">
				>
				["at0019"] = <
					text = <"Beschreibung">
					description = <"Beschreibung der beteiligten Krankheiten, Verletzungen, Zuständen und Ereignissen, die zum Tod geführt haben.">
				>
				["at0020"] = <
					text = <"Beteiligte Zustände">
					description = <"Details zu anderen relevanten Bedingungen, die möglicherweise zum Tod beigetragen haben, jedoch ohne kausalen Zusammenhang.">
				>
				["at0021"] = <
					text = <"Dauer">
					description = <"Das ungefähre Intervall zwischen Auftreten des identifizierten \"Zustands\" und dem Tod.">
				>
			>
		>
	>

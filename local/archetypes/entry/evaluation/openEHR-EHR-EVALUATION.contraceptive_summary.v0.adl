archetype (adl_version=1.4; uid=1c27309d-b7fe-4038-918c-937702db122b)
	openEHR-EHR-EVALUATION.contraceptive_summary.v0

concept
	[at0000]	-- Contraceptive use summary
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Marit Alice Venheim">
				["organisation"] = <"Helse Vest IKT">
				["email"] = <"marit.alice.venheim@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2009-06-21">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere gjeldende status og episodiske detaljer om bestemte typer antikonsepsjon brukt i løpet av individets levetid.">
			use = <"Brukes for å registrere gjeldende status og episodiske detaljer om bestemte typer antikonsepsjon brukt i løpet av individets levetid.

Det spesifikke omfanget av denne arketypen er å dokumentere bruken av alle typer antikonsepsjon i løpet av individets levetid, inkludert episoder der mer enn en type antikonsepsjon kan ha blitt brukt samtidig. Flere typer antikonsepsjon kan registreres per episode.

De repeterbare \"Per episode\" dataelementene gjør det mulig å registrere detaljer om bruken av antikonsepsjon i bestemte tidsintervaller. Det kreves en ny episode ved hver endring av type antikonsepsjon som brukes. 

Dataelementet \"Overordnet beskrivelse\" brukes til å overføre fritekstbeskrivelser fra eksisterende eller tidligere kliniske systemer til arketypeformat.">
			keywords = <"prevensjonsmiddel, hormon, prevensjon, fødsel, kontroll, svangerskapsprevensjon, familieplanlegging, prevensjonstiltak", ...>
			misuse = <"Brukes ikke til å registrere detaljert informasjon om reseptbelagte medisiner. Lenker fra denne arketypen til den aktuelle INSTRUCTION.medication_order eller ACTION.medication bør brukes til å registrere detaljene.

Brukes ikke til å registrere detaljert informasjon om prosedyrene som er utført. Lenker fra denne arketypen til den aktuelle ACTION.prosedyren skal brukes til å registrere detaljene.

Brukes ikke til å registrere bruk av medisiner som nødprevensjon. Bruk en ACTION.medication for å registrere administrering av angrepille.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the current status and episodic details about specific types of contraception used during an individual's lifetime.">
			use = <"Use to record the current status and episodic details about specific types of contraception used during an individual's lifetime.

The specific scope of this archetype is to document the use of all types of contraception during an individual's lifetime, including episodes where more than one type of contraception may have been used simultaneously. Multiple types of contraception can be recorded per episode.

The 'Per episode' cluster of data elements allows for recording of details about contraception use during specific intervals of time. Each change of contraception used requires a new episode. 

Use to incorporate the narrative descriptions of contraceptive history within existing or legacy clinical systems into an archetyped format, using the 'Overall description' data element.">
			keywords = <"contraceptive", "hormone", "contraception", "birth", "control", "family planning", "birth control measures">
			misuse = <"Not to be used to record detailed information about the medication prescription. Links from this archetype to the relevant INSTRUCTION.medication_order or ACTION.medication should be used to record the detail.

Not to be used to record detailed information about the procedures performed. Links from this archetype to the relevant ACTION.procedure should be used to record the detail.

Not to be used to record use of medication as emergency contraception. Use an ACTION.medication to record the administration of the morning after pill.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "Terje Bektesevic Holmlund, UiT Norges arktiske universitet, Norway", "SB Bhattacharyya, Sudisa Consultancy Services, India", "Heather Grain, Llewelyn Grain Informatics, Australia", "Anne Grimstvedt Kvalvik, Haraldsplass Diakonale sykehus, Norway", "Mikkel Gaup Grønmo, Helse-Nord, FSE, Norway (openEHR Editor)", "Evelyn Hovenga, EJSH Consulting, Australia", "Liv Laugen, Oslo universitetssykehus, Norway (Nasjonal IKT redaktør)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Johannes Michael Strehle, Helse-Nord, Norway", "Svenne Naumann, Finnmarkssykehuset, Norway", "Bjørn Næss, DIPS ASA, Norway", "Line Silsand, Universitetssykehuset i Nord-Norge, Norway", "Norwegian Review Summary, Nasjonal IKT HF, Norway", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)", "Marit Alice Venheim, Helse Vest IKT, Norway (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"3A4F37A471271467EF0B4517618CA858">
		["build_uid"] = <"9fd2b8f5-1a27-4657-b9fa-4f3c59bc18f5">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Contraceptive use summary
		data matches {
			ITEM_TREE[at0001] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0089] occurrences matches {0..1} matches {    -- Overall status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0003,    -- Current user
									at0005,    -- Former user
									at0006]    -- Never used
								}
							}
						}
					}
					ELEMENT[at0043] occurrences matches {0..1} matches {    -- Overall description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0015] occurrences matches {0..1} matches {    -- Regular contraception commenced
						value matches {
							DV_DATE matches {*}
						}
					}
					CLUSTER[at0064] occurrences matches {0..*} matches {    -- Per episode
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0081] occurrences matches {0..1} matches {    -- Episode label
								value matches {
									DV_COUNT matches {
										magnitude matches {|>=1|}
									}
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0013] occurrences matches {0..1} matches {    -- Episode start date
								value matches {
									DV_DATE matches {*}
								}
							}
							ELEMENT[at0030] occurrences matches {0..1} matches {    -- Description
								value matches {
									DV_TEXT matches {*}
								}
							}
							CLUSTER[at0029] occurrences matches {0..*} matches {    -- Per type
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0126] occurrences matches {0..1} matches {    -- Type
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0127,    -- None
													at0128,    -- Male condom
													at0129,    -- Female condom
													at0130,    -- Diaphragm
													at0131,    -- Combination pill
													at0142,    -- Combination skin patch
													at0132,    -- Progestogen-only pill
													at0133,    -- Depot progestogen injection
													at0134,    -- Hormone implant
													at0141,    -- Vaginal ring
													at0135,    -- Female sterilisation
													at0136,    -- Male sterilisation
													at0137,    -- IUD
													at0138,    -- Withdrawal
													at0139,    -- Fertility awareness method
													at0140,    -- Abstinence
													at0143]    -- Vaginal douching
												}
											}
										}
									}
									ELEMENT[at0023] occurrences matches {0..1} matches {    -- Contraception name
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0053] occurrences matches {0..1} matches {    -- Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0025] occurrences matches {0..1} matches {    -- Primary intent
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at0077] occurrences matches {0..*} matches {    -- Type details
										include
											archetype_id/value matches {/.*/}
									}
									ELEMENT[at0074] occurrences matches {0..1} matches {    -- Reason for cessation
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0069] occurrences matches {0..1} matches {    -- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
							allow_archetype CLUSTER[at0026] occurrences matches {0..*} matches {    -- Episode details
								include
									archetype_id/value matches {/.*/}
							}
							ELEMENT[at0082] occurrences matches {0..1} matches {    -- Episode end date
								value matches {
									DV_DATE matches {*}
								}
							}
							ELEMENT[at0087] occurrences matches {0..1} matches {    -- Episode comment
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
					ELEMENT[at0019] occurrences matches {0..1} matches {    -- Overall comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0021] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0073] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0022] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Contraceptive use summary">
					description = <"Summary and persistent information about the deliberate use of methods to prevent pregnancy during the individual's lifetime.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Current user">
					description = <"Individual currently uses contraception.">
				>
				["at0005"] = <
					text = <"Former user">
					description = <"Individual has previously used contraception but is not a current user.">
				>
				["at0006"] = <
					text = <"Never used">
					description = <"Individual has never used any type of contraception.">
				>
				["at0013"] = <
					text = <"Episode start date">
					description = <"Date when this episode commenced.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0015"] = <
					text = <"Regular contraception commenced">
					description = <"The date, or partial date, when the individual first started using contraception on a regular basis.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0019"] = <
					text = <"Overall comment">
					description = <"Additional narrative about the use of contraception that has not been captured in other fields.">
				>
				["at0021"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0022"] = <
					text = <"Last updated">
					description = <"The date this contraceptive use summary was last updated.">
				>
				["at0023"] = <
					text = <"Contraception name">
					description = <"Identification of the specific contraception used in this episode, by name.">
					comment = <"For example: the brand name of the oral contraceptive pill. This data element is most likely redundant in the context of use of some contraceptives such as condoms where specification of the brand or type is usually not relevant.">
				>
				["at0025"] = <
					text = <"Primary intent">
					description = <"Primary reason for using the identified type of contraception.">
					comment = <"For example: pregnancy prevention, regular cycle or relief of pain.">
				>
				["at0026"] = <
					text = <"Episode details">
					description = <"Additional structured details about the specified episode of contraception use.">
				>
				["at0029"] = <
					text = <"Per type">
					description = <"Details about use of a specified type of contraception.">
				>
				["at0030"] = <
					text = <"Description">
					description = <"Narrative description about the overall use of contraception during this episode.">
					comment = <"This data element may be used to describe the pattern of use, including regularity of use. For example, the oral contraceptive pill was missed frequently or condoms used occasionally.">
				>
				["at0043"] = <
					text = <"Overall description">
					description = <"Narrative summary about the individual's overall use of contraception.">
					comment = <"Use this data element to record a narrative description only where the structured data does not adequately reflect the use of contraception for this individual or to incorporate unstructured information from existing or legacy clinical systems into an archetyped format.">
				>
				["at0053"] = <
					text = <"Description">
					description = <"Narrative summary about the use of the specified type of contraception during this episode.">
				>
				["at0064"] = <
					text = <"Per episode">
					description = <"Details about contraceptive use during an identified period of time.">
				>
				["at0069"] = <
					text = <"Comment">
					description = <"Additional narrative about use of the specified type of contraception, not captured in other fields.">
				>
				["at0073"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR.">
				>
				["at0074"] = <
					text = <"Reason for cessation">
					description = <"Narrative description for the reason for stopping use of the specified type of contraception during this episode.">
					comment = <"For example: side effects or becoming pregnant.">
				>
				["at0077"] = <
					text = <"Type details">
					description = <"Additional structured details about the specified type of contraception used during the episode.">
				>
				["at0081"] = <
					text = <"Episode label">
					description = <"Identification of an episode of contraception use - either as a number in a sequence and/or a named event.">
					comment = <"For example: '2' as the second episode within a sequence of episodes; or 'Breastfeeding'.">
				>
				["at0082"] = <
					text = <"Episode end date">
					description = <"Date when this episode ceased.">
					comment = <"Can be a partial date, for example, only a year. This data field will be empty if the episode is current and ongoing.">
				>
				["at0087"] = <
					text = <"Episode comment">
					description = <"Additional narrative about the use of the specified contraception during the specified episode, not captured in other fields.">
				>
				["at0089"] = <
					text = <"Overall status">
					description = <"Statement about current use of any type of contraception.">
				>
				["at0126"] = <
					text = <"Type">
					description = <"The type of contraception used by the individual.">
				>
				["at0127"] = <
					text = <"None">
					description = <"No form of contraception was used.">
				>
				["at0128"] = <
					text = <"Male condom">
					description = <"A barrier covering the penis to physically block ejaculated sperm from entering the vagina.">
				>
				["at0129"] = <
					text = <"Female condom">
					description = <"A barrier worn inside the vagina to physically block ejaculated sperm from entering the cervix.">
				>
				["at0130"] = <
					text = <"Diaphragm">
					description = <"A shallow, dome shaped barrier placed in the vagina to cover the cervix.">
				>
				["at0131"] = <
					text = <"Combination pill">
					description = <"A combination oral contraceptive pill containing both an oestrogen and progestin.">
				>
				["at0132"] = <
					text = <"Progestogen-only pill">
					description = <"Progestogen-only form of oral hormonal contraception.">
				>
				["at0133"] = <
					text = <"Depot progestogen injection">
					description = <"Progestogen-only injectable of hormonal contraception.">
				>
				["at0134"] = <
					text = <"Hormone implant">
					description = <"Hormonal contraception inserted under the skin.">
				>
				["at0135"] = <
					text = <"Female sterilisation">
					description = <"Surgical procedure to block the fallopian tubes.">
				>
				["at0136"] = <
					text = <"Male sterilisation">
					description = <"Surgical procedure to cut the vas deferens in a male sexual partner.">
				>
				["at0137"] = <
					text = <"IUD">
					description = <"Device inserted into the uterus to prevent implantation.">
				>
				["at0138"] = <
					text = <"Withdrawal">
					description = <"Withdrawing the penis from the vagina and away from a woman's external genitals before ejaculation.">
				>
				["at0139"] = <
					text = <"Fertility awareness method">
					description = <"Cyclical use of abstinence or barrier methods during likely fertile periods.">
				>
				["at0140"] = <
					text = <"Abstinence">
					description = <"Avoidance of vaginal intercourse or intimate activities which may result in pregnancy.">
				>
				["at0141"] = <
					text = <"Vaginal ring">
					description = <"Hormone impregnated ring inserted into the vagina.">
				>
				["at0142"] = <
					text = <"Combination skin patch">
					description = <"A combination skin patch containing both an oestrogen and progestin.">
				>
				["at0143"] = <
					text = <"Vaginal douching">
					description = <"Intravaginal cleansing with a liquid solution.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Antikonsepsjonssammendrag">
					description = <"Sammendrag og vedvarende informasjon om bevisst bruk av metoder for å forhindre graviditet i individets levetid.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Bruker antikonsepsjon">
					description = <"Individet bruker antikonsepsjon.">
				>
				["at0005"] = <
					text = <"Tidligere brukt antikonsepsjon">
					description = <"Individet har tidligere bruk antikonsepsjon, men bruker det for tiden ikke.">
				>
				["at0006"] = <
					text = <"Har aldri brukt antikonsepsjon">
					description = <"Individet har aldri brukt noen form for antikonsepsjon.">
				>
				["at0013"] = <
					text = <"Episodens startdato">
					description = <"Dato da episoden startet.">
					comment = <"Kan være en deldato, for eksempel kun årstall.">
				>
				["at0015"] = <
					text = <"Startet regelmessig antikonsepsjonsbruk">
					description = <"Datoen, eller deldato, da individet først begynte å bruke antikonsepsjon regelmessig.">
					comment = <"Kan være en deldato, for eksempel kun årstall.">
				>
				["at0019"] = <
					text = <"Samlet kommentar">
					description = <"Ytterligere fritekst om antikonsepsjonsbruken som ikke er omfattet av andre felt.">
				>
				["at0021"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0022"] = <
					text = <"Sist oppdatert.">
					description = <"Datoen da dette antikonsepsjonssammendraget sist ble oppdatert.">
				>
				["at0023"] = <
					text = <"Navnet på antikonsepsjonmiddelet">
					description = <"Identifisering av den spesifikke antikonsepsjonen som brukes i denne episoden, med navn.">
					comment = <"For eksempel: Merkenavnet på den p-pillen som brukes. Dette dataelementet er sannsynligvis overflødig for noen antikonsepsjonsmidler der spesifikasjon av merke eller type vanligvis ikke er relevant, som kondom.">
				>
				["at0025"] = <
					text = <"Hovedintensjon">
					description = <"Hovedårsaken for bruk av den identifiserte typen antikonsepsjon.">
					comment = <"For eksempel: Regelmessig syklus, lindring av smerter eller forhindre graviditet.">
				>
				["at0026"] = <
					text = <"Episodedetaljer">
					description = <"Ytterligere strukturerte detaljer om den spesifiserte episoden med antikonsepsjonsbruk.">
				>
				["at0029"] = <
					text = <"Per type">
					description = <"Detaljer om bruken for en spesifikk type antikonsepsjon.">
				>
				["at0030"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse om den samlede bruken av antikonsepsjon i løpet av denne episoden.">
					comment = <"Dette dataelementet kan brukes til å beskrive bruksmønsteret, inkludert regulariteten. For eksempel \"ble p-pillen ofte glemt\" eller \"kondomer brukt noen ganger\".">
				>
				["at0043"] = <
					text = <"Overordnet beskrivelse">
					description = <"Fritekstsammendrag om individets overordnede bruk av antikonsepsjon.">
					comment = <"Bruk dette dataelementet for å registrere en fritekstbeskrivelse om individets bruk av antikonsepsjon, dersom man ikke kan eller trenger bruke de strukturerte elementene. Kan også brukes for å overføre ustrukturert informasjon fra eksisterende eller gamle systemer til arketypeformat.">
				>
				["at0053"] = <
					text = <"Beskrivelse">
					description = <"Fritekstsammendrag om bruken av den spesifiserte antikonsepsjonstypen i denne episoden.">
				>
				["at0064"] = <
					text = <"Per episode">
					description = <"Detaljer om bruk av antikonsepsjon i løpet av en identifisert tidsperiode.">
				>
				["at0069"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om den spesifiserte antikonsepsjonstypen som ikke er omfattet av andre felt.">
				>
				["at0073"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
				["at0074"] = <
					text = <"Årsak til opphør">
					description = <"Fritekstbeskrivelse av årsaken til at en sluttet å bruke den spesifiserte antikonsepsjonstypen i denne episoden.">
					comment = <"For eksempel: Bivirkninger eller ble gravid.">
				>
				["at0077"] = <
					text = <"Detaljer om antikonsepsjonstypen">
					description = <"Ytterligere strukturerte detaljer om den spesifiserte typen antikonsepsjon brukt i løpet av episoden.">
				>
				["at0081"] = <
					text = <"Episodens navn">
					description = <"Identifisering av en episode med bruk av antikonsepsjon - enten som et nummer i en sekvens og / eller en navngitt hendelse.">
					comment = <"For eksempel: \"2\" som den andre episoden i en sekvens av episoder eller \"Amming\".">
				>
				["at0082"] = <
					text = <"Episodens sluttdato">
					description = <"Datoen episoden opphørte.">
					comment = <"Kan være en deldato, for eksempel kun årstall. Dette datafeltet vil være tomt når gjeldene episode pågår.">
				>
				["at0087"] = <
					text = <"Kommentar til episoden">
					description = <"Ytterligere fritekst om bruken av antikonsepsjon innenfor den spesifiserte episoden, som ikke er omfattet av andre felt.">
				>
				["at0089"] = <
					text = <"Overordnet status">
					description = <"Utsagn om nåværende bruk av alle typer antikonsepsjon.">
				>
				["at0126"] = <
					text = <"Type">
					description = <"Typen antikonsepsjon individet brukte.">
				>
				["at0127"] = <
					text = <"Ingen">
					description = <"Ingen bruk av antikonsepsjon.">
				>
				["at0128"] = <
					text = <"Kondom">
					description = <"En barriere som dekker penis for fysisk å blokkere ejakulert sæd fra å komme inn i skjeden.">
				>
				["at0129"] = <
					text = <"Femidom">
					description = <"En barriere som plasseres inne i vagina for fysisk å blokkere ejakulert sæd fra å komme inn i livmorhalsen.">
				>
				["at0130"] = <
					text = <"Pessar">
					description = <"En grunn, kuppelformet barriere plassert i skjeden for å dekke livmorhalsen.">
				>
				["at0131"] = <
					text = <"Kombinasjonspille">
					description = <"En kombinasjonsp-pille som inneholder både østrogen og gestagen.">
				>
				["at0132"] = <
					text = <"Progestagen pille">
					description = <"Progestagen som oral hormonell antikonsepsjon.">
				>
				["at0133"] = <
					text = <"P-sprøyte">
					description = <"Progestagen som injiserbar hormonell antikonsepsjon.">
				>
				["at0134"] = <
					text = <"Hormonimplantat">
					description = <"Hormonell antikonsepsjon implantert under huden.">
				>
				["at0135"] = <
					text = <"Kvinnelig sterilisering">
					description = <"Kirurgisk inngrep for å blokkere egglederne.">
				>
				["at0136"] = <
					text = <"Mannlig sterilisering">
					description = <"Kirurgisk inngrep for å kutte sædlederne hos en mannlig seksualpartner.">
				>
				["at0137"] = <
					text = <"Intrauterin antikonsepsjon">
					description = <"Spiral satt inn i livmoren for å forhindre implantasjon.">
				>
				["at0138"] = <
					text = <"Avbrutt samleie">
					description = <"Å trekke penis fra skjeden og bort fra kvinnens ytre kjønnsorganer før utløsning.">
				>
				["at0139"] = <
					text = <"Fertilitetsbevissthet">
					description = <"Periodisk bruk av avholdenhet eller barrieremetoder i sannsynlig fruktbare perioder.">
				>
				["at0140"] = <
					text = <"Avhold">
					description = <"Unngåelse av vaginalt samleie eller intime aktiviteter som kan føre til graviditet.">
				>
				["at0141"] = <
					text = <"Vaginal ring">
					description = <"Hormonimpregnert ring satt inn i skjeden.">
				>
				["at0142"] = <
					text = <"Kombinasjonsplaster">
					description = <"Et p-plaster som inneholder både østrogen og progestin.">
				>
				["at0143"] = <
					text = <"Vaginal skylling">
					description = <"Intravaginal skylling med en flytende oppløsning.">
				>
			>
		>
	>

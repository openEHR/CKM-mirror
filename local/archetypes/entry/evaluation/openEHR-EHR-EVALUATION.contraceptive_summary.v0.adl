archetype (adl_version=1.4; uid=1c27309d-b7fe-4038-918c-937702db122b)
	openEHR-EHR-EVALUATION.contraceptive_summary.v0

concept
	[at0000]	-- Contraceptive summary
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Marit Alice Venheim">
				["organisation"] = <"Helse Vest IKT">
				["email"] = <"marit.alice.venheim@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2009-06-21">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere et sammendrag om et individs bruk av prevensjon i løpet av individets levetid.">
			use = <"	
For å registrere et sammendrag om et individs bruk av prevensjon i løpet av individets levetid.

Denne arketypen skal brukes til å registrere informasjon om både nåværende og tidligere bruk av prevensjon.

Det spesifikke omfanget av denne arketypen gjelder den generelle dokumentasjonen om bruk av alle typer prevensjon gjennom et individs levetid, inkludert episoder der mer enn en type prevensjon kan ha blitt brukt samtidig.

Clusteret \"Per type\" gjør det mulig å registrere spesifikke detaljer og episoder om hver type prevensjon som brukes og kan bare gjentas en gang per type.

I mange situasjoner vil den enkelte bare bruke en type prevensjon om gangen. Imidlertid, hvis andre typer prevensjon brukes samtidig, vil detaljene bli registrert i en annen forekomst av \"Per type\".

For hver type prevensjon kan historien om bruk over tid fanges opp ved å bruke de repeterbare \"Per episode\" dataelementene. Denne samlingen av dataelementer gjør det mulig å registrere et veldig detaljert mønster av prevensjonsbruk om nødvendig.

Dataelementet \"Overordnet beskrivelse\" brukes til å overføre fritekstbeskrivelser fra eksisterende eller tidligere kliniske systemer til arketypeformat.">
			keywords = <"Prevensjons", "Hormonell prevensjon", "Prevensjon">
			misuse = <"Må ikke brukes til å registrere detaljert informasjon om reseptforskrivelsen eller utførte prosedyrer. Koblinger fra denne arketypen til den aktuelle ACTION.medication_order eller ACTION.procedure bør brukes til å registrere disse detaljene.

Skal ikke brukes til å registrere bruk av nødprevensjon som medisiner eller annet etter et enkelt ubeskyttet samleie med håp om å forhindre graviditet.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record summary information about a individ's patterns of use of contraception during the individual's lifetime.">
			use = <"Use to record summary information about a individ's patterns of use of contraception during the individual's lifetime.

This archetype is to be used to record information about both current and previous use of contraception.

The specific scope of this archetype is on overall documentation about the use of all types of contraception throughout a individ's lifetime, including episodes where more than one type of contraception may have been used simultaneously. 

The 'Per type' cluster of data elements allows for recording of specific details and episodes about each type of contraception used and can be repeated only once per type. 

In many situations the individual will use only one type of contraception at a time. However, if other types of contraception are used at the same time, the details will be recorded in another instance of the 'Per type' cluster.

For each type of contraception the history of use over time can be captured using the repeatable 'Per episode' cluster. This cluster of data elements allows for a very detailed pattern of contraception use to be recorded, if necessary. 

Use to incorporate the narrative descriptions of contraceptive history within existing or legacy clinical systems into an archetyped format, using the 'Overall description' data element.">
			keywords = <"Contraceptive", "Hormonal contraceptive", "Contraception">
			misuse = <"Not to be used to record detailed information about the medication prescription or procedures performed. Links from this archetype to the relevant ACTION.medication_order or ACTION.procedure should be used to record the detail.

Not to be used to record use of emergency contraception such as medications or devices after a single unprotected intercourse with the hope of preventing pregnancy.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Patter of types and episodes derived from: Tobacco smoking summary, Published archetype [Internet]. openEHR Foundation, openEHR Clinical Knowledge Manager [cited: 2018-05-19]. Available from: http://www.openehr.org/ckm/#showArchetype_1013.1.2466">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"D0906E1104094C6563B21EB06A7594C0">
		["build_uid"] = <"ddd5d7a0-e5ec-4670-b309-257fd3e8c9ae">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Contraceptive summary
		data matches {
			ITEM_TREE[at0001] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0089] occurrences matches {0..1} matches {    -- Overall status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0003,    -- Current user
									at0005,    -- Former user
									at0006]    -- Never used
								}
							}
						}
					}
					ELEMENT[at0043] occurrences matches {0..1} matches {    -- Overall description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0015] occurrences matches {0..1} matches {    -- Regular contraception commenced
						value matches {
							DV_DATE matches {*}
						}
					}
					CLUSTER[at0029] occurrences matches {0..*} matches {    -- Per type
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0126] occurrences matches {0..1} matches {    -- Type
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0127,    -- None
											at0128,    -- Male condom
											at0129,    -- Female condom
											at0130,    -- Diaphragm
											at0131,    -- Combination pill
											at0142,    -- Combination skin patch
											at0132,    -- Progestogen-only pill
											at0133,    -- Depot progestogen injection
											at0134,    -- Hormone implant
											at0141,    -- Vaginal ring
											at0135,    -- Female sterilisation
											at0136,    -- Male sterilisation
											at0137,    -- IUD
											at0138,    -- Withdrawal
											at0139,    -- Fertility awareness
											at0140]    -- Abstinence
										}
									}
								}
							}
							ELEMENT[at0052] occurrences matches {0..1} matches {    -- Status
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0061,    -- Current user
											at0059,    -- Former user
											at0091]    -- Never used
										}
									}
								}
							}
							ELEMENT[at0053] occurrences matches {0..1} matches {    -- Description
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0017] occurrences matches {0..1} matches {    -- Start date
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							CLUSTER[at0064] occurrences matches {0..*} matches {    -- Per episode
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0081] occurrences matches {0..1} matches {    -- Episode label
										value matches {
											DV_COUNT matches {
												magnitude matches {|>=1|}
											}
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0023] occurrences matches {0..1} matches {    -- Contraception
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0030] occurrences matches {0..1} matches {    -- Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0065] occurrences matches {0..1} matches {    -- Clinical indication
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0025] occurrences matches {0..1} matches {    -- Intent
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0013] occurrences matches {0..1} matches {    -- Episode start date
										value matches {
											DV_DATE matches {*}
										}
									}
									ELEMENT[at0082] occurrences matches {0..1} matches {    -- Episode end date
										value matches {
											DV_DATE matches {*}
										}
									}
									ELEMENT[at0074] occurrences matches {0..1} matches {    -- Reason for cessation
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at0026] occurrences matches {0..*} matches {    -- Episode details
										include
											archetype_id/value matches {/.*/}
									}
									ELEMENT[at0087] occurrences matches {0..1} matches {    -- Episode comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
							ELEMENT[at0014] occurrences matches {0..1} matches {    -- End date
								value matches {
									DV_DATE matches {*}
								}
							}
							allow_archetype CLUSTER[at0077] occurrences matches {0..*} matches {    -- Type details
								include
									archetype_id/value matches {/.*/}
							}
							ELEMENT[at0069] occurrences matches {0..1} matches {    -- Comment
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
					allow_archetype CLUSTER[at0086] occurrences matches {0..*} matches {    -- Overall details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.change(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0016] occurrences matches {0..1} matches {    -- Overall end date
						value matches {
							DV_DATE matches {*}
						}
					}
					ELEMENT[at0019] occurrences matches {0..1} matches {    -- Overall comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0021] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0073] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0022] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Contraceptive summary">
					description = <"Summary information about a individ's use of contraception during the indiviual's lifetime.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Current user">
					description = <"Individual currently uses contraception.">
				>
				["at0005"] = <
					text = <"Former user">
					description = <"Individual has previously used contraception but is not a current user.">
				>
				["at0006"] = <
					text = <"Never used">
					description = <"Individual has never used any type of contraception.">
				>
				["at0013"] = <
					text = <"Episode start date">
					description = <"Date when this episode commenced.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0014"] = <
					text = <"End date">
					description = <"Date when the individ last used the specified type of contraception.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0015"] = <
					text = <"Regular contraception commenced">
					description = <"The date, or partial date, when the individ first started using contraception on a regular basis.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0016"] = <
					text = <"Overall end date">
					description = <"The date when the individ last stopped using any type of contraception.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0017"] = <
					text = <"Start date">
					description = <"Date when the individ first used the specified type of contraception.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0019"] = <
					text = <"Overall comment">
					description = <"Additional narrative about the use of contraception that has not been captured in other fields.">
				>
				["at0021"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0022"] = <
					text = <"Last updated">
					description = <"The date this contraception summary was last updated.">
				>
				["at0023"] = <
					text = <"Contraception">
					description = <"Identification of the specific contraception used in this episode, by name.">
					comment = <"For example: the brand name of the oral contraceptive pill. This data element is most likely redundant in the context of use of some contraceptives such as condoms where specification of the brand or type is usually not relevant.">
				>
				["at0025"] = <
					text = <"Intent">
					description = <"Intended outcome anticipated as a result of use of the specified contraception.">
					comment = <"For example: pregnancy prevention, regular cycle or relief of pain.">
				>
				["at0026"] = <
					text = <"Episode details">
					description = <"Additional structured details about the specified episode of contraception use.">
				>
				["at0029"] = <
					text = <"Per type">
					description = <"Details about use of a specified type of contraception.">
				>
				["at0030"] = <
					text = <"Description">
					description = <"Narrative description of the use for the specified type of contraception during this episode.">
					comment = <"This data element may be used to describe the pattern of use, including regularity of use. For example, the oral contraceptive pill was missed frequently or condoms used occasionally.">
				>
				["at0043"] = <
					text = <"Overall description">
					description = <"Narrative summary about the individ's overall use of contraception.">
					comment = <"Use this data element to record a narrative description only where the structured data does not adequately reflect the use of contraception for this individ or to incorporate unstructured information from existing or legacy clinical systems into an archetyped format.">
				>
				["at0052"] = <
					text = <"Status">
					description = <"Statement about current use of the specified type of contraception.">
				>
				["at0053"] = <
					text = <"Description">
					description = <"Narrative summary about the use of the specified type of contraception.">
				>
				["at0059"] = <
					text = <"Former user">
					description = <"Individual has previously used the specified type of contraception but is not a current user.">
				>
				["at0061"] = <
					text = <"Current user">
					description = <"Individual is a current user of the specified type of contraception.">
				>
				["at0064"] = <
					text = <"Per episode">
					description = <"Details about a discrete period of use for the specified type of contraception.">
				>
				["at0065"] = <
					text = <"Clinical indication">
					description = <"Clinical reason for using the specified type of contraception during this episode.">
					comment = <"For example: dysmennorrhoea; or irregular menses. Coding with an external terminology is preferred, where possible.">
				>
				["at0069"] = <
					text = <"Comment">
					description = <"Additional narrative about use of the specified type of contraception, not captured in other fields.">
				>
				["at0073"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR.">
				>
				["at0074"] = <
					text = <"Reason for cessation">
					description = <"Narrative description for the reason for stopping use of the specified type of contraception during this episode.">
					comment = <"For example: side effects or becoming pregnant.">
				>
				["at0077"] = <
					text = <"Type details">
					description = <"Additional structured details about the specified type of contraception used.">
				>
				["at0081"] = <
					text = <"Episode label">
					description = <"Identification of an episode of contraception use - either as a number in a sequence and/or a named event.">
					comment = <"For example: '2' as the second episode within a sequence of episodes; or 'Pregnancy with twins'.">
				>
				["at0082"] = <
					text = <"Episode end date">
					description = <"Date when this episode ceased.">
					comment = <"Can be a partial date, for example, only a year. This data field will be empty if the episode is current and ongoing.">
				>
				["at0086"] = <
					text = <"Overall details">
					description = <"Additional structured details about the overall use of contraception.">
				>
				["at0087"] = <
					text = <"Episode comment">
					description = <"Additional narrative about the use of the specified contraception during the specified episode, not captured in other fields.">
				>
				["at0089"] = <
					text = <"Overall status">
					description = <"Statement about current use of any type of contraception.">
				>
				["at0091"] = <
					text = <"Never used">
					description = <"Individual has never used the specified type of contraception.">
				>
				["at0126"] = <
					text = <"Type">
					description = <"The type of contraception used by the individual.">
				>
				["at0127"] = <
					text = <"None">
					description = <"No form of contraception was used.">
				>
				["at0128"] = <
					text = <"Male condom">
					description = <"The male partner used a condom.">
				>
				["at0129"] = <
					text = <"Female condom">
					description = <"A female condom was used.">
				>
				["at0130"] = <
					text = <"Diaphragm">
					description = <"A diaphragm was used.">
				>
				["at0131"] = <
					text = <"Combination pill">
					description = <"An oral contraceptive pill containing both an oestrogen and progestin.">
				>
				["at0132"] = <
					text = <"Progestogen-only pill">
					description = <"An oral contraceptive pill containing only a progestin was used.">
				>
				["at0133"] = <
					text = <"Depot progestogen injection">
					description = <"A depot injection of a progestogen was used.">
				>
				["at0134"] = <
					text = <"Hormone implant">
					description = <"A hormonal implant was inserted.">
				>
				["at0135"] = <
					text = <"Female sterilisation">
					description = <"The woman was surgically sterilised.">
				>
				["at0136"] = <
					text = <"Male sterilisation">
					description = <"The woman's regular sexual partner was surgically sterilised.">
				>
				["at0137"] = <
					text = <"IUD">
					description = <"An intrauterine device was in situ.">
				>
				["at0138"] = <
					text = <"Withdrawal">
					description = <"The withdrawal method was used.">
				>
				["at0139"] = <
					text = <"Fertility awareness">
					description = <"Abstinence during estimated periods of fertility.">
				>
				["at0140"] = <
					text = <"Abstinence">
					description = <"Sexual abstinence was used.">
				>
				["at0141"] = <
					text = <"Vaginal ring">
					description = <"A vaginal ring containing etonogestrel/ethinyl estradiol.">
				>
				["at0142"] = <
					text = <"Combination skin patch">
					description = <"A combination skin patch containing both an oestrogen and progestin.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Prevensjonsammendrag">
					description = <"Sammendrag om ett individs bruk av prevensjon i løpet av individets levetid.
">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Bruker prevensjon">
					description = <"Individet bruker prevensjon.">
				>
				["at0005"] = <
					text = <"Tidligere brukt prevensjon">
					description = <"Individet har tidligere bruk prevensjon, men bruker det for tiden ikke.">
				>
				["at0006"] = <
					text = <"Har aldri brukt prevensjon">
					description = <"Individet har aldri brukt noen form for prevensjon.">
				>
				["at0013"] = <
					text = <"Episodens startdato">
					description = <"Dato da episoden startet.">
					comment = <"Kan være en deldato, for eksempel kun årstall.">
				>
				["at0014"] = <
					text = <"Sluttdato">
					description = <"Dato da individet sist brukte den angitte typen prevensjon.">
					comment = <"Kan være en deldato, for eksempel årstall.">
				>
				["at0015"] = <
					text = <"Startet regelmessig prevensjonsbruk">
					description = <"Datoen, eller deldato, da individet først begynte å bruke prevensjon regelmessig.">
					comment = <"Kan være en deldato, for eksempel kun årstall.">
				>
				["at0016"] = <
					text = <"Samlet sluttdato">
					description = <"Datoen da individet sist sluttet å bruke noen form for prevensjon.">
					comment = <"Kan være en deldato, for eksempel årstall.">
				>
				["at0017"] = <
					text = <"Startdato">
					description = <"Datoen når individet første gang brukte den spesifiserte prevensjonstypen.">
					comment = <"Kan være en deldato, for eksempel årstall.">
				>
				["at0019"] = <
					text = <"Samlet kommentar">
					description = <"Ytterligere fritekst om prevensjonsbruken som ikke er omfattet av andre felt.">
				>
				["at0021"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0022"] = <
					text = <"Sist oppdatert.">
					description = <"Datoen da dette prevensjons sammendraget ble sist oppdatert.">
				>
				["at0023"] = <
					text = <"Prevensjon">
					description = <"Identifisering av den spesifikke prevensjonen som brukes i denne episoden, med navn.">
					comment = <"For eksempel: Merkenavnet på den p-pillen som brukes. Dette dataelementet er sannsynligvis overflødig i forbindelse med bruk av noen prevensjonsmidler, for eksempel kondomer, der spesifikasjon av merke eller type vanligvis ikke er relevant.">
				>
				["at0025"] = <
					text = <"Intensjon">
					description = <"Forventet utfall som et resultat av bruk av den spesifiserte prevensjonen.">
					comment = <"For eksempel: Regelmessig syklus, lindring av smerter eller forhindre graviditet,">
				>
				["at0026"] = <
					text = <"Episodedetaljer">
					description = <"Ytterligere strukturerte detaljer om den spesifiserte episoden med prevensjonsbruk.">
				>
				["at0029"] = <
					text = <"Per type">
					description = <"Detaljer om bruken for en spesifikk type prevensjon.">
				>
				["at0030"] = <
					text = <"Beskrivelse">
					description = <"Fritekst beskrivelse av bruken for den spesifiserte prevensjonstypen i løpet av denne episoden.">
					comment = <"Dette dataelementet kan brukes til å beskrive bruksmønsteret, inkludert regulariteten. For eksempel ble p-pillen ofte glemt eller kondomer brukt noen ganger.">
				>
				["at0043"] = <
					text = <"Overordnet beskrivelse">
					description = <"Fritekstsammendrag om individets overordnede bruk av prevensjon.">
					comment = <"Bruk dette dataelementet til å registrere en friteksbeskrivelse av det de strukturerte dataene ikke reflekterer i bruken av prevensjon for dette individet eller til å innlemme ustrukturert informasjon fra eksisterende eller gamle kliniske systemer i et arketypet format.">
				>
				["at0052"] = <
					text = <"Status">
					description = <"Utsagn om nåværende bruk for den spesifiserte prevensjonstypen.">
				>
				["at0053"] = <
					text = <"Beskrivelse">
					description = <"Fritekstsammendrag om bruken av den spesifiserte prevensjonstypen.">
				>
				["at0059"] = <
					text = <"Har brukt prevensjonstypen">
					description = <"Individet har tidligere brukt den angitte typen prevensjon, men bruker det ikke nå.">
				>
				["at0061"] = <
					text = <"Bruker prevensjonstypen">
					description = <"Individet bruker den angitte typen prevensjon.">
				>
				["at0064"] = <
					text = <"Per episode">
					description = <"Detaljer om en diskret brukstid for den angitte typen prevensjon.">
				>
				["at0065"] = <
					text = <"Klinisk indikasjon">
					description = <"Den kliniske årsaken for å bruke spesifisert prevensjonstype i denne episoden.">
					comment = <"For eksempel: dysmennorrhoea; eller uregelmessige menstruasjoner. Koding med ekstern terminologi er å foretrekke, der det er mulig.">
				>
				["at0069"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om røyking av den spesifiserte prevensjonstypen som ikke er omfattet av andre felt.">
				>
				["at0073"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR.">
				>
				["at0074"] = <
					text = <"Årsak til opphør">
					description = <"Fritekstbeskrivelse av årsaken til at en stoppet bruk av spesifisert prevensjonstype i denne episoden.">
					comment = <"For eksempel: Bivirkninger eller ble gravid.">
				>
				["at0077"] = <
					text = <"Detaljer om prevensjonstypen">
					description = <"Ytterligere strukturerte detaljer om den spesifiserte typen prevensjon.">
				>
				["at0081"] = <
					text = <"Episodens navn">
					description = <"Identifisering av en episode med prevensjonsbruk - enten som et nummer i en sekvens og / eller en navngitt hendelse.">
					comment = <"For eksempel: \"2\" som den andre episoden i en sekvens av flere episoder; eller \"Graviditet med tvillinger\".">
				>
				["at0082"] = <
					text = <"Episodens sluttdato">
					description = <"Datoen episoden opphørte.">
					comment = <"Kan være en deldato, for eksempel kun årstall. Dette datafeltet vil være tomt når gjeldene episode pågår.">
				>
				["at0086"] = <
					text = <"Samlede detaljer">
					description = <"Ytterligere strukturerte detaljer om den overordnede bruken av prevensjon.">
				>
				["at0087"] = <
					text = <"Kommentar til episoden">
					description = <"Ytterligere fritekst om bruken av prevensjon innenfor den spesifiserte episoden, som ikke er omfattet av andre felt.">
				>
				["at0089"] = <
					text = <"Overordnet status">
					description = <"Utsagn om nåværende bruk av alle typer prevensjon.">
				>
				["at0091"] = <
					text = <"Aldri brukt prevesjonstypen">
					description = <"Individet har aldri brukt den angitte typen prevensjon">
				>
				["at0126"] = <
					text = <"Type">
					description = <"Typen prevensjon individet brukte.">
				>
				["at0127"] = <
					text = <"Ingen">
					description = <"Ingen bruk av prevensjon.">
				>
				["at0128"] = <
					text = <"Kondom">
					description = <"Den mannlige partner brukte kondom.">
				>
				["at0129"] = <
					text = <"Femidom">
					description = <"Et kvinnekondom ble brukt.">
				>
				["at0130"] = <
					text = <"Pessar">
					description = <"Et pessar ble brukt.">
				>
				["at0131"] = <
					text = <"Kombinasjonspille">
					description = <"En oral p-pille som inneholder både østrogen og progestin.">
				>
				["at0132"] = <
					text = <"Progesteron pille">
					description = <"En p-pille som bare inneholder progestin ble brukt.">
				>
				["at0133"] = <
					text = <"P-sprøyte">
					description = <"En depotinjeksjon av progestron ble brukt.">
				>
				["at0134"] = <
					text = <"Hormonimplantat">
					description = <"Et hormonimplantat ble injesert.">
				>
				["at0135"] = <
					text = <"Kvinnelig sterilisering">
					description = <"Kvinnen var kirurgisk sterilisert.">
				>
				["at0136"] = <
					text = <"Mannlig sterilisering">
					description = <"Individet regelmessige seksualpartner var kirurgisk sterilisert.">
				>
				["at0137"] = <
					text = <"Intrauterin prevensjon">
					description = <"En spiral var in situ.">
				>
				["at0138"] = <
					text = <"Avbrutt samleie">
					description = <"Avbrutt samleie ble praktisert.">
				>
				["at0139"] = <
					text = <"Fertilitetsbevissthet">
					description = <"Avholdenhet i estimerte fruktbarhetsperioder.">
				>
				["at0140"] = <
					text = <"Avhold">
					description = <"Seksuelt avhold ble praktisert.">
				>
				["at0141"] = <
					text = <"Vaginal ring">
					description = <"En vaginal ring som inneholder etonogestrel/ethinyl estradiol.">
				>
				["at0142"] = <
					text = <"Kombinasjons hudplaster">
					description = <"Et p-plaster som inneholder både østrogen og progestin.">
				>
			>
		>
	>

archetype (adl_version=1.4; uid=6f7c406d-8b9d-4609-b6e6-6fa4c6d0d129)
	openEHR-EHR-EVALUATION.education_summary.v0

concept
	[at0000]	-- Education summary
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"John Tore Valand">
				["organisation"] = <"Helse Bergen">
				["email"] = <"john.tore.valand@helse-bergen.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics, Australia">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2010-12-21">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere sammendrag om et individs nåværende og tidligere utdanning eller opplæring, som gir et overblikk over utdanningsbakgrunn og lese- og skriveferdigheter.">
			use = <"Brukes for å registrere sammendrag om et individs nåværende og tidligere utdanning eller opplæring for å få et overblikk over utdanningsbakgrunn og lese- og skriveferdigheter.

Arketypen er laget for å være en frittstående arketype, og kan benyttes som del av individets sosialanamnese i et templat. Arketypen er ment å gi et sammendrag over all utdanning eller opplæring i den videste forstand. 

For hver enkelt utdanning eller opplæring brukes én instans av arketypen CLUSTER.education_record (Utdanning) i SLOTet \"Utdanningsdetaljer\".">
			keywords = <"utdanning", "skole", "universitet", "videregående", "ungdomsskole", "barneskole", "lærling", "læretid", "kurs", "fagbrev", "diplom", "studie", "bachelor", "master", "PhD">
			misuse = <"Brukes ikke for å registrere strukturerte detaljer om en spesifikk utdanning eller opplæring. Bruk arketypen CLUSTER.education_record (Utdanning) i SLOTet \"Utdanningsdetaljer\" for dette formålet.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record	summary information about an individual's current and past education or training, which provides an overview of their educational background and literacy.">
			use = <"Use to record summary information about an individual's current and past education or training, which provides an overview of their educational background and literacy.

This archetype has been designed to be used as a standalone archetype within the context of a Social History (or similar) template. It is intended to provide a summary of all education or training, considered in the broadest sense. For each education or training, use a separate instance of the CLUSTER.education_record within the SLOT for 'Education record'.">
			keywords = <"education", "school", "university", "secondary", "tertiary", "college", "apprenticeship", "training", "needs", "primary">
			misuse = <"Not to be used for recording structured details about a specific education or training. Use the CLUSTER.education_record archetype within 'Education record' SLOT for this purpose.">
			copyright = <"© Australian Digital Health Agency, openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"John Bennett, NEHTA, Australia", "Stephen Chu, NEHTA, Australia", "Matthew Cordell, NEHTA, Australia", "David Evans, Queensland Health, Australia", "Andrew Goodchild, NEHTA, Australia", "Mary Kelaher, NEHTA, Australia", "Robert L'egan, NEHTA, Australia", "Heather Leslie, Atomica Informatics, Australia (Editor)", "Ian McNicoll, Ocean Informatics UK, United Kingdom", "Stewart Morrison, NEHTA, Australia", "Jodie Pycroft, Adelaide Northern Division of General Practice Ltd, Australia", "John Taylor, NEHTA, Australia", "Gordon Tomes, National Disability Insurance Scheme Agency, Australia", "Richard Townley-O'Neill, NEHTA, Australia">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Derived from: Education and Training, Draft archetype [Internet]. Australian Digital Health Agency (NEHTA), ADHA Clinical Knowledge Manager. Authored: 2010 Dec 21. Available at: http://dcm.nehta.org.au/ckm#showArchetype_1013.1.924_5 (discontinued).">
		["current_contact"] = <"Heather Leslie, Atomica Informatics, Australia<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"5D19D49996E116E2876FA53063719F4C">
		["build_uid"] = <"3c79cedb-c2ee-4dba-850b-47e6d3f33ec4">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Education summary
		data matches {
			ITEM_TREE[at0001] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0018] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0003] occurrences matches {0..1} matches {    -- Age commenced
						value matches {
							DV_DURATION matches {
								value matches {PYM/|>=P0Y|}
							}
						}
					}
					ELEMENT[at0031] occurrences matches {0..1} matches {    -- Age ceased
						value matches {
							DV_DURATION matches {
								value matches {PYM/|>=P0Y|}
							}
						}
					}
					ELEMENT[at0002] occurrences matches {0..1} matches {    -- Highest achieved
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0029] occurrences matches {0..*} matches {    -- Education record
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0032] occurrences matches {0..1} matches {    -- Special education needs
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0030] occurrences matches {0..*} matches {    -- Additional details
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0007] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0026] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0027] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0028] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Education summary">
					description = <"Summary or persistent information about an individual's current and past education or training.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Highest achieved">
					description = <"Description of highest level of education or training achieved.">
					comment = <"Coding with a terminology is desirable, where possible. For example: completed secondary school; completed postgraduate degree; completed apprenticeship; or never attended school. ">
				>
				["at0003"] = <
					text = <"Age commenced">
					description = <"Age when an individual first commenced formal education.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about an individual's education, not captured in other fields.">
				>
				["at0018"] = <
					text = <"Description">
					description = <"Narrative description about the overall education or training history of an individual.">
				>
				["at0026"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0027"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0028"] = <
					text = <"Last updated">
					description = <"The date that this education summary was last updated.">
					comment = <"At implementation, it is assumed that if an associated education record is added or updated then this date will also be updated.">
				>
				["at0029"] = <
					text = <"Education record">
					description = <"Structured details about each education or training, both current and past.">
				>
				["at0030"] = <
					text = <"Additional details">
					description = <"Additional details about the education or training, or previous education history of an individual.">
				>
				["at0031"] = <
					text = <"Age ceased">
					description = <"Age when an individual last attended formal education.">
					comment = <"Usually recorded when an individual has not completed a formal education.">
				>
				["at0032"] = <
					text = <"Special education needs">
					description = <"Narrative description of any special education requirements.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Utdanningssammendrag">
					description = <"Sammendrag eller varig (persistent) informasjon om et individs nåværende og tidligere utdanning eller opplæring.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Høyeste fullførte utdanning">
					description = <"Beskrivelse av det høyeste fullførte nivået av utdanning eller opplæring.">
					comment = <"Koding med en terminologi er ønskelig om mulig. For eksempel: \"Ikke avsluttet grunnskole\", \"Grunnskole\", \"Faglig yrkesutdanning\", \"Mer enn treårig høgskole/universitet\" (fra OID 8707), \"Fag/Svennebrev\" (fra OID 7703) eller \"Har aldri gått på skole\".">
				>
				["at0003"] = <
					text = <"Alder skolestart">
					description = <"Alder da individet begynte sin skolegang.">
				>
				["at0007"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekstkommentar til individets skolegang/utdanning som ikke er fanget i andre felt.">
				>
				["at0018"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse om individets overordnede utdannings- og opplæringshistorikk.">
				>
				["at0026"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0027"] = <
					text = <"Tilleggsinformasjon">
					description = <"Ytterligere informasjon som trengs for å kunne registrere lokalt definert innhold eller for å tilpasse til andre referansemodeller/formalismer.">
					comment = <"For eksempel lokale informasjonsbehov eller ytterligere metadata for å kunne tilpasse til tilsvarende konsepter i FHIR eller CIMI.">
				>
				["at0028"] = <
					text = <"Sist oppdatert">
					description = <"Datoen da dette sammendraget om utdanning sist ble oppdatert.">
					comment = <"Ved implementering av arketypen forutsettes det at dersom en utdanning eller opplæring legges til eller oppdateres i CLUSTER.education_record (Utdanning), vil også denne datoen oppdateres.">
				>
				["at0029"] = <
					text = <"Utdanningsdetaljer">
					description = <"Strukturerte detaljer om hver enkelt utdanning eller opplæring, både nåværende og tidligere.">
				>
				["at0030"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere strukturerte detaljer om individets nåværende eller tidligere skolegang/utdanning.">
				>
				["at0031"] = <
					text = <"Alder skoleslutt">
					description = <"Alder da individet avsluttet sin formelle skolegang.">
					comment = <"Registreres vanligvis når et individ ikke fullfører en formell utdanning (eller obligatorisk skole).">
				>
				["at0032"] = <
					text = <"Behov for spesialundervisning">
					description = <"Fritekstbeskrivelse om krav til spesialundervisning.">
				>
			>
		>
	>

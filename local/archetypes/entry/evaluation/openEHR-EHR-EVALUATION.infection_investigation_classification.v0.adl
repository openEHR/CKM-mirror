archetype (adl_version=1.4; uid=f35324e8-4315-4783-8e65-f9f474a165a6)
	openEHR-EHR-EVALUATION.infection_investigation_classification.v0

concept
	[at0000]	-- Infectious disease investigation classification
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2022-06-25">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the disease classification for an identified infectious disease as part of a disease surveillance investigation.">
			use = <"Use to record the disease for an identified infectious disease as part of a disease surveillance investigation, most commonly the final determination at the completion of a case investigation but may also record initial classification as a suspected infection.

Use a separate instance of this archetype to record the conclusion for each suspected 'Index disease'. For example in a fever & rash investigation, considering Measles and Rubella as the potential causes, use one instance of this archetype to record the conclusion, rationale, evidence etc related to Measles, and another to carry the information related to Rubella.">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"EC89F81D31EAFF1F75D6B5ED082C1C89">
		["build_uid"] = <"9796b166-be2c-4641-b5bf-6bb876fb38b5">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Infectious disease investigation classification
		data matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0007] matches {    -- Index disease
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0002] occurrences matches {0..1} matches {    -- Classification
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0035,    -- Suspected
									at0003,    -- Probable
									at0004,    -- Confirmed
									at0005,    -- Excluded
									at0006]    -- Inconclusive
								}
							}
							DV_TEXT matches {*}
						}
						name matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0024,    -- Initial classification
									at0025]    -- Final classification
								}
							}
						}
					}
					ELEMENT[at0008] occurrences matches {0..*} matches {    -- Reason category
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0043,    -- Laboratory confirmed case
									at0044]    -- Clinically confirmed case
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0009] occurrences matches {0..*} matches {    -- Reason description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0011] occurrences matches {0..1} matches {    -- Source category
						value matches {
							DV_TEXT matches {*}
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0045,    -- Imported case
									at0046,    -- Import-related case
									at0047,    -- Endemic case
									at0048,    -- Vaccine-associated case
									at0049,    -- Congenital case
									at0050]    -- Unknown case
								}
							}
						}
					}
					ELEMENT[at0036] occurrences matches {0..1} matches {    -- Source description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0021] occurrences matches {0..*} matches {    -- Source country
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0051] occurrences matches {0..1} matches {    -- Endemic source category
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0052] occurrences matches {0..1} matches {    -- Endemic source description
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0016] occurrences matches {0..*} matches {    -- Additional details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.drug_resistance_profile(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.clinical_evidence\.v1/}
					}
					ELEMENT[at0017] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0013] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0015] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Infectious disease investigation classification">
					description = <"Classification for an identified infectious disease as part of a disease surveillance investigation.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Classification">
					description = <"Classification of the likelihood of the index infectious disease as cause of illness.">
					comment = <"Run-time name constraints - 'Initial classification' is the classification made prior to commencing the investigation; 'Final classification' is the conclusion reached after the investigation.">
				>
				["at0003"] = <
					text = <"Probable">
					description = <"A case is 'Probable' when common, relevant symptoms and/or signs are exhibited and there is an epidemiological link. This classification is only ever temporary, until reclassification with 'Confirmed', 'Excluded/Discarded' or 'Inconclusive' after investigation.">
				>
				["at0004"] = <
					text = <"Confirmed">
					description = <"A case is 'Confirmed' when laboratory testing confirms the evidence of the disease and may fulfill clinical criteria.">
				>
				["at0005"] = <
					text = <"Excluded">
					description = <"A case is 'Excluded' or 'Discarded' after exclusion by adequate investigation or negative laboratory test results.">
				>
				["at0006"] = <
					text = <"Inconclusive">
					description = <"A case is 'Inconclusive' when it is not possible to determine if the index disease is the cause of the infection.">
				>
				["at0007"] = <
					text = <"Index disease">
					description = <"The name of the infectious disease under investigation.">
					comment = <"This data element may be redundant if the index disease has been identified elsewhere within a template.">
				>
				["at0008"] = <
					text = <"Reason category">
					description = <"The broad category of the reason for the classification.">
					comment = <"For example: child of an infected mother, laboratory confirmed, or clinically confirmed.">
				>
				["at0009"] = <
					text = <"Reason description">
					description = <"Narrative description about the reason for the classification.">
					comment = <"For example: description of clinical findings and/or conclusive laboratory test results; or alternate diagnosis made.">
				>
				["at0011"] = <
					text = <"Source category">
					description = <"The category for the source of a confirmed case.">
					comment = <"For example: imported, import-related, endemic, vaccine-associated, congenital or unknown.">
				>
				["at0013"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0014"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0015"] = <
					text = <"Last updated">
					description = <"The date when this infectious disease investigation classification was last updated.">
				>
				["at0016"] = <
					text = <"Additional details">
					description = <"Additional structured details about the classification.">
					comment = <"For example: details about the clinical evidenct confirming or excluding this disease as the cause of the infection">
				>
				["at0017"] = <
					text = <"Comment">
					description = <"Additional narrative about the classification, not captured in other fields.">
				>
				["at0021"] = <
					text = <"Source country">
					description = <"Country identified as the (likely or probable) source of an imported or import-related case.">
				>
				["at0024"] = <
					text = <"Initial classification">
					description = <"The initial classification before an investigation has commenced.">
				>
				["at0025"] = <
					text = <"Final classification">
					description = <"The final classification after an investigation has been completed.">
				>
				["at0035"] = <
					text = <"Suspected">
					description = <"A case may be 'Suspected' when common, relevant symptoms and/or signs are exhibited. This classification is only ever temporary, until reclassification with 'Confirmed', 'Discarded' or 'Inconclusive' after investigation..">
				>
				["at0036"] = <
					text = <"Source description">
					description = <"Narrative description about the source of infection.">
					comment = <"May include information about the degree of certainty.">
				>
				["at0043"] = <
					text = <"Laboratory confirmed case">
					description = <"A case that meets one or more laboratory criteria.">
				>
				["at0044"] = <
					text = <"Clinically confirmed case">
					description = <"A suspected case without adequate laboratory investigation.">
				>
				["at0045"] = <
					text = <"Imported case">
					description = <"Exposure occurred outside of the country.">
				>
				["at0046"] = <
					text = <"Import-related case">
					description = <"Exposure occurred locally but directly associated to an imported case.">
				>
				["at0047"] = <
					text = <"Endemic case">
					description = <"Exposure occurred locally as part of a chain of endemic transmission.">
				>
				["at0048"] = <
					text = <"Vaccine-associated case">
					description = <"Exposure occurred as an adverse event following exposure to a vaccine.">
				>
				["at0049"] = <
					text = <"Congenital case">
					description = <"Exposure occurred during pregnancy.">
				>
				["at0050"] = <
					text = <"Unknown case">
					description = <"Exposure source has not been identified.">
				>
				["at0051"] = <
					text = <"Endemic source category">
					description = <"The category for the source of an endemic case.">
					comment = <"For example: household contact, community, or unknown.">
				>
				["at0052"] = <
					text = <"Endemic source description">
					description = <"Narrative description about the source of an endemic case.">
				>
			>
		>
	>

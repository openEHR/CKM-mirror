archetype (adl_version=1.4; uid=eaa8770d-ab93-4fdd-be2e-4f8ac17502c4)
	openEHR-EHR-EVALUATION.adverse_reaction_fhir.v0

concept
	[at0000]	-- Adverse reaction risk (FHIR/openEHR)
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2010-11-08">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record a clinical assessment of a propensity, or potential risk to an individual, of an adverse reaction upon future exposure to the specified substance, or class of substance.

Where a propensity is identified, to record information or evidence about reaction events that is characterised by any harmful or undesirable physiological response that is unique to the individual, and triggered by exposure of an individual to the identified substance or substance class.">
			use = <"Use to provide a single place within the health record to document a range of clinical statements about adverse reactions, including:
- record a clinical assessment of the individual’s propensity for a potential future reaction upon re-exposure; and
- record cumulative information about the reaction to each exposure.

Use to record information about the positive presence of the risk of an adverse reaction:
- to support direct clinical care of an individual; 
- as part of a managed adverse reaction or allergy/intolerance list; 
- to support exchange of information about the propensity and events related to adverse reactions; 
- to inform adverse reaction reporting; and 
- to assist computerised knowledge-based activities such as clinical decision support and alerts.

Use to record information about the risk of adverse reactions to a broad range of substances, including: incipients and excipients in medicinal preparations; biological products; metal salts; and organic chemical compounds.  

Adverse reaction may be:
- an immune mediated reaction - Types I-VI (including allergic reactions and hypersensitivities); or 
- a non-immune mediated reaction - including pseudo-allergic reactions, side effects, intolerances, drug toxicities (eg to Gentamicin).
In clinical practice distinguishing between immune-mediated and non-immune mediated reactions is difficult and often not practical. Identification of the type of reaction is not a proxy for seriousness or risk of harm to the patient, which is better expressed by the manifestation in clinical practice.

The risk of an adverse reaction event or manifestation should not be recorded without identifying a proposed causative substance or class of substance. If there is uncertainty that a specific substance is the cause, this uncertainty can be recorded using the ‘Status’ data element. If there are multiple possible substances that may have caused a reaction/manifestation, each substance should be recorded using a separate instance of this adverse reaction archetype/FHIR resource with the ‘Status’ set to an initial state of ‘Suspected’ so that adverse reaction checking can be activated in clinical systems. Once the substance, agent or class is later proven not to be the cause for a given reaction then the ‘Status’ can be modified to ‘Refuted’.

This archetype/FHIR resource has been designed to allow recording of information about a specific substance (amoxycillin, oysters, or bee sting venom) or, alternatively, a class of substance (eg Penicillins)). If a class of substance is recorded then identification of the exact substance can be recorded on a per exposure basis.

The scope of this archetype/FHIR resource has deliberately focused on identifying a pragmatic data set that are used in most clinical systems or will be suitable for most common clinical scenarios, however it permits extension of the model when additional detail is required, for example 'Reaction details', 'Exposure details', and 'Reporting details' slots. Examples of clinical situations where the extension may be required include: a detailed allergist/immunologist assessment, for reporting to regulatory bodies or use in a clinical trial.

The act of recording any adverse reaction risk in a health record involves the clinical assessment that a potential hazard exists for an individual if they are exposed to the same substance/agent/class in the future – that is, a relative contraindication - and the default ‘Criticality’ value should be set to ‘Low risk’. If a clinician considers that it is not safe for the individual to be deliberately re-exposed to the substance/agent again, for example, following a manifestation of a life-threatening anaphylaxis, then the 'Criticality' data element should be amended to ‘High’. 

A formal Adverse Event Report to regulatory bodies is a document that will contain a broad range of information in addition to the specific details about the adverse reaction. The report could utilise parts of this Risk of adverse reaction archetype/FHIR resource plus include additional data as required per jurisdiction.

An adverse reaction or allergy/intolerance list is a record of all identified propensities for an adverse reaction for the individual upon future exposure to the substance or class, plus provides potential access to the evidence provided by details about each reaction event, such as manifestation.

Valuable first-level information that could be presented to the clinician when they need to assess propensity for future reactions are:
- statements about previous clinical manifestations following exposure;
- source of the information/reporter; and
- the ‘Criticality’ flag.
Second-level information can be drawn from each exposure event and links to additional detailed information such as history, examination and diagnoses stored elsewhere in the record, if it is available.

openEHR only: Links to other parts of the health record where further details may be located, such as consultation notes, is allowed by the openEHR reference model, but not modelled explicitly in this archetype.">
			keywords = <"reaction", "allergy", "allergic", "adverse", "event", "effect", "sensitivity", "intolerance", "hypersensitivity", "side effect", "toxicity", "drug", "food", "agent", "substance", "immune", "non-immune", "chemical", "anaphylaxis", "allergen", "medication", "supplement", "medicine", "natural remedies", "immunological", "non-immunological">
			misuse = <"Not to be used for recording physiological reactions to physical agents, such as heat, cold, sunlight, vibration, exercise activity, by infectious agents or food contaminants. Use archetypes/FHIR resources for Problem/Diagnosis (openEHR) or Conditions (FHIR).

Not to be used to record adverse events, including failures of clinical process, interventions or products. For example: abnormal use or mistakes/errors made in maladministration of an agent or substance; incorrect dosage; mislabelling; harm or injury caused by an intervention or procedure; overdose/poisoning etc.

Not to be used to record an adverse reaction where the substance is unknown. Use EVALUATION.problem_diagnosis or CLUSTER.symptoms to record as part of the health record until a possible substance is identified.

Not to be used to record reactions to transfusions of blood products. Use a specific archetype for the purpose. 

Not to be used as a proxy for an Adverse Event Report. See above for how it may be used as one component of an Adverse Event Report.

Not to be used for recording alerts.

Not to be used for recording failed therapy.

openEHR only: Not to be used for the explicit recording of an absence (or negative presence) of a reaction to 'any substances' or to identified substances, for example ‘No known allergies or adverse reactions’ or ‘No known allergies to Penicillin’. Use the EVALUATION. exclusion-adverse_reaction archetype to express a positive statement of adverse reaction exclusion.

openEHR only: Not to be used for the explicit recording that no information was able to be obtained about the adverse reaction status of a patient. Use the EVALUATION.absence archetype to record that a positive statement that information was not able to be obtained, for example, if a non-cooperative patient refuses to answer questions.">
			copyright = <"© openEHR Foundation / HL7">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Fatima Almeida, Critical SW, Portugal", "Vebjoern Arntzen, Oslo university hospital, Norway", "Koray Atalag, University of Auckland, New Zealand", "Elaine Ayres, US National Institutes of Health, United States", "Silje Ljosland Bakke, National ICT Norway, Norway", "John Bennett, NEHTA, Australia", "Steve Bentley, Allscripts, United Kingdom", "Sharmila Biswas, Dr Sharmila Biswas GP, Australia", "Lars Bitsch-Larsen, Haukeland University hospital, Norway", "Claire Chalopin, ICCAS, University of Leipzig, Germany", "Rong Chen, Cambio Healthcare Systems, Sweden", "Stephen Chu, Queensland Health, Australia", "Matthew Cordell, NEHTA, Australia", "Howard Edidin, Edidin Group, Inc, United States", "Brett Esler, Oridashi, Australia", "David Evans, Queensland Health, Australia", "Jerry Fahrni, Kaweah Delta Health Care District, United States", "Shahla Foozonkhah, Ocean Informatics, Australia", "Einar Fosse, National Centre for Integrated Care and Telemedicine, Norway", "Joanne Foster, School of Nursing & Midwifery, QLD University of Technology & Nursing Informatics Australia, Australia", "Sebastian Garde, Ocean Informatics, Germany", "Jacquie Garton-Smith, Royal Perth Hospital and DoHWA, Australia", "Sarah Gaunt, NEHTA, Australia", "Andrew Goodchild, NEHTA, Australia", "Heather Grain, Llewelyn Grain Informatics, Australia", "Trina Gregory, cpc, Australia", "Grahame Grieve, Health Intersections, Australia (Editor)", "Sam Heard, Ocean Informatics, Australia", "Andrew James, University of Toronto, Canada", "Julie James, Blue Wave Informatics LLP, United Kingdom", "Ivor Jones, Queensalnd Helath, Australia", "Konstantinos Kalliamvakos, Cambio Healthcare Systems, Sweden", "Lars Karlsen, DIPS ASA, Norway", "Mary Kelaher, NEHTA, Australia", "Diane Kirkham, NEHTA, Australia", "Shinji Kobayashi, Kyoto University, Japan", "Robert L'egan, NEHTA, Australia", "Jobst Landgrebe, ii4sm, Switzerland", "Russell Leftwich, Russell B Leftwich MD, United States (openEHR Editor)", "Heather Leslie, Ocean Informatics, Australia (openEHR Editor)", "Hugh Leslie, Ocean Informatics, Australia", "Rikard Lovstrom, Swedish Medical Association, Sweden", "Sarah Mahoney, Australia", "Luis Marco Ruiz, Norwegian Center for Integrated Care and Telemedicine, Norway", "Mike Martyn, The Hobart Anaesthetic Group, Australia", "Lloyd McKenzie, Gordon Point Informatics, Canada", "David McKillop, NEHTA, Australia", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Chris Mitchell, RACGP, Australia", "Stewart Morrison, NEHTA, Australia", "Jörg Niggemann, Compugroup, Germany", "Tom Oniki, Intermountain Healthcare, United States", "Chris Pearce, Melbourne East GP Network, Australia", "General Practice Computing Group, Australia", "Camilla Preeston, Royal Australian College of General Practitioners, Australia", "Margaret Prichard, NEHTA, Australia", "Jodie Pycroft, Adelaide Northern Division of General Practice Ltd, Australia", "Cathy Richardson, NEHTA, Australia", "Robyn Richards, NEHTA - Clinical Terminology, Australia", "Jussara Rotzsch, UNB, Brazil", "Stefan Sauermann, University of Applied Sciences Technikum Wien, Austria", "Thilo Schuler, Australia", "Peter Scott, Medical Objects, Australia", "Elena Shabanova, UMMSSOft, Russian Federation", "Anoop Shah, University College London, United Kingdom", "Elizabeth Stanick, Hobart Anaesthetic Group, Australia", "Norwegian Review Summary, Nasjonal IKT, Norway", "Hwei-Yee Tai, Tan Tock Seng Hospital, Singapore", "John Taylor, NEHTA, Australia", "Micaela Thierley, Helse Bergen, Norway", "Gordon Tomes, Australian Institute of Health and Welfare, Australia", "Richard Townley-O'Neill, NEHTA, Australia", "Ines Vaz, UFN, Portugal", "Andrew Yap, Australia", "Kylie Young, The Royal Australian College of General Practitioners, Australia", "Lin Zhang, BIPH, China">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, Australia">
		["revision"] = <"0.0.1-alpha">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["references"] = <"Adverse Reaction, draft archetype, National eHealth Transition Authority [Internet]. NEHTA Clinical Knowledge Manager. Authored: 08 Nov 2010. Available at: http://dcm.nehta.org.au/ckm/OKM.html#showarchetype_1013.1.868_7 (accessed Jan 16, 2012).

Allergy and Intolerance Domain Analysis Model, Release 1, HL7 [Internet]. Publication pending, expected August 2014; Available at http://wiki.hl7.org/images/1/1b/Allergy_and_Intolerance_INFORM_2013_MAY.pdf (accessed 06 July 2014). 

Allergy, clinical element model, GE/Intermountain Healthcare. Clinical Element Model Search. Available at: http://intermountainhealthcare.org/cem/Pages/Detail.aspx?NCID=520861661&k=allergy (accessed Jan 16, 2012).

Edwards IR, Aronson JK. Adverse drug reactions: definitions, diagnosis, and management. Lancet. 2000 Oct 7;356(9237):1255-9. PubMed PMID: 11072960. 

FHIR Resource AllergyIntolerance, HL7 [Internet]. Available at: http://www.hl7.org/implement/standards/fhir/allergyintolerance.html (accessed 06 Jul 2014).

Horsfield P, Sibeko S. Representation in Electronic Patient Records of Allergic Reactions, Adverse Reactions, and Intolerance of Pharmaceutical Products [Internet]. London, UK: National Health Service; 2006 Sep 07 [cited 2011 Jun 21]; Available at https://svn.connectingforhealth.nhs.uk/svn/public/nhscontentmodels/TRUNK/ref/NPfIT/Allergy_ADR_Intolerance%20v%201.2Final.doc.

Long R, Bentley S. SCG Guidance on the Representation of Allergies and Adverse Reaction Information Using NHS Message Templates [Internet]. London, UK: National Health Service; 2008 Apr 30 [cited 2011 Jun 21]; Available at http://www.connectingforhealth.nhs.uk/systemsandservices/data/scg/scg0001.pdf.

Microsoft. Design Guidance: Displaying Adverse Drug Reaction Risks [Internet]. 2009 January 28 [cited 2011 Jun 21]; Available at www.mscui.net/DesignGuide/DisplayingAllergies.aspx.

Microsoft. Design Guidance: Recording Adverse Drug Reaction Risks [Internet]. 2009 March 27 [cited 2011 Jun 21]; Available at www.mscui.net/DesignGuide/RecordingAllergies.aspx.

Mosby. Mosby's Pocket Dictionary of Medicine, Nursing and Health Professions. 6th Edition. USA: Mosby Elsevier; 2010

National E-Health Transition Authority. Adverse Reactions (Data Specifications) Version 1.1 [Internet]. Sydney, Australia: NEHTA; 2008 Feb 29 [cited 2011 Jun 21]; Available at http://www.nehta.gov.au/component/docman/doc_download/453-adverse-reaction-data-specification-v11.

Riedl MA, Casillas AM. Adverse drug reactions: types and treatment options. Am Fam Physician. 2003 Nov 1;68(9):1781-90. Review. PubMed PMID: 14620598.

Royal Australian College of General Practitioners. Fact Sheet: Allergies & Adverse Reactions (Draft). 2010.

Thien FC. Drug hypersensitivity. Med J Aust. 2006 Sep 18;185(6):333-8. Review. PubMed PMID: 16999678.

- Uppsala Monitoring Centre (WHO): http://www.who-umc.org/

- European Medicines Agency: http://www.ema.europa.eu/ema/

- DIRECTIVE 2010/84/EU OF THE EUROPEAN PARLIAMENT AND OF THE COUNCIL, of 15 December 2010, amending, as regards pharmacovigilance, Directive 2001/83/EC on the Community code relating to medicinal products for human use: http://ec.europa.eu/health/files/eudralex/vol-1/dir_2010_84/dir_2010_84_en.pdf">
		["build_uid"] = <"bbd19719-3f3b-4a82-a9a5-2709079a3cc7">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["MD5-CAM-1.0.1"] = <"0342914AEF7D75DC88B98C9A889C564F">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	EVALUATION[at0000] matches {	-- Adverse reaction risk (FHIR/openEHR)
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {2..*; unordered} matches {
					ELEMENT[at0002] matches {	-- Substance
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0063] occurrences matches {0..1} matches {	-- Status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0127, 	-- Suspected
									at0064, 	-- Likely
									at0065, 	-- Confirmed
									at0067, 	-- Resolved
									at0066]	-- Refuted
								}
							}
						}
					}
					ELEMENT[at0101] occurrences matches {0..1} matches {	-- Criticality
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0102, 	-- Low
									at0103, 	-- High
									at0124]	-- Indeterminate
								}
							}
						}
					}
					ELEMENT[at0058] occurrences matches {0..1} matches {	-- Reaction mechanism
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0059, 	-- Immune mediated
									at0060, 	-- Non-immune mediated
									at0126]	-- Indeterminate
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0120] occurrences matches {0..1} matches {	-- Substance category
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0121, 	-- Food
									at0122, 	-- Medication
									at0123]	-- Other
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0117] occurrences matches {0..1} matches {	-- Date of last reaction
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {	-- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
					CLUSTER[at0009] occurrences matches {0..*} matches {	-- Reaction event
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0010] occurrences matches {0..1} matches {	-- Specific substance
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0021] occurrences matches {0..1} matches {	-- Certainty
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0095, 	-- Suspected
											at0023, 	-- Likely
											at0118]	-- Confirmed
										}
									}
								}
							}
							ELEMENT[at0011] occurrences matches {0..*} matches {	-- Manifestation
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0012] occurrences matches {0..1} matches {	-- Reaction description
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0027] occurrences matches {0..1} matches {	-- Onset of reaction
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0028] occurrences matches {0..1} matches {	-- Duration of reaction
								value matches {
									DV_DURATION matches {*}
								}
							}
							ELEMENT[at0089] occurrences matches {0..1} matches {	-- Severity of reaction
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0093, 	-- Mild
											at0092, 	-- Moderate
											at0090]	-- Severe
										}
									}
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0029] occurrences matches {0..*} matches {	-- Reaction details
								include
									archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.symptom_sign(-[a-zA-Z0-9_]+)*\.v0/}
							}
							ELEMENT[at0020] occurrences matches {0..1} matches {	-- Initial exposure
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0025] occurrences matches {0..1} matches {	-- Duration of exposure
								value matches {
									DV_DURATION matches {*}
								}
							}
							ELEMENT[at0106] occurrences matches {0..1} matches {	-- Route of exposure
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0018] occurrences matches {0..1} matches {	-- Exposure description
								value matches {
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0096] occurrences matches {0..*} matches {	-- Exposure details
								include
									archetype_id/value matches {/openEHR-EHR-CLUSTER\.citation(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.amount(-[a-zA-Z0-9_]+)*\.v1/}
							}
							ELEMENT[at0040] occurrences matches {0..1} matches {	-- Clinical management description
								value matches {
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0119] occurrences matches {0..*} matches {	-- Clinical management details
								include
									archetype_id/value matches {/.*/}
							}
							allow_archetype CLUSTER[at0041] occurrences matches {0..*} matches {	-- Reporting details
								include
									archetype_id/value matches {/.*/}
							}
							allow_archetype CLUSTER[at0116] occurrences matches {0..*} matches {	-- Information source
								include
									archetype_id/value matches {/.*/}
							}
							ELEMENT[at0032] occurrences matches {0..1} matches {	-- Reaction comment
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0042] matches {	-- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0062] occurrences matches {0..1} matches {	-- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0047] occurrences matches {0..1} matches {	-- Supporting clinical record information
						value matches {
							DV_EHR_URI matches {*}
						}
					}
					ELEMENT[at0044] occurrences matches {0..1} matches {	-- Reaction reported?
						value matches {
							DV_BOOLEAN matches {
								value matches {True, False}
							}
						}
					}
					CLUSTER[at0099] occurrences matches {0..*} matches {	-- Report summary
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0125] occurrences matches {0..1} matches {	-- Date of report
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0048] occurrences matches {0..1} matches {	-- Report comment
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0045] occurrences matches {0..*} matches {	-- Adverse reaction report
								value matches {
									DV_URI matches {*}
								}
							}
						}
					}
					CLUSTER[at0098] occurrences matches {0..1} matches {	-- FHIR record provenance
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0068] matches {	-- Subject
								value matches {
									DV_URI matches {*}
								}
							}
							ELEMENT[at0050] occurrences matches {0..*} matches {	-- Identifier
								value matches {
									DV_IDENTIFIER matches {*}
								}
							}
							ELEMENT[at0069] occurrences matches {0..1} matches {	-- Recorder
								value matches {
									DV_URI matches {*}
								}
							}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Adverse reaction risk (FHIR/openEHR)">
					description = <"Risk of harmful or undesirable physiological response which is unique to an individual and associated with exposure to a substance.">
					comment = <"Substances include, but are not limited to: a therapeutic substance administered correctly at an appropriate dosage for the individual; food; material derived from plants or animals; or venom from insect stings.">
					source = <"openEHR,FHIR">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Substance">
					description = <"Identification of a substance, or substance class, that is considered to put the individual at risk of an adverse reaction event.">
					comment = <"Both an individual substance and a substance class are valid entries in 'Substance'. If the value in 'Substance' is an individual substance, it may be duplicated in 'Specific substance'. It is strongly recommended that both 'Substance' and 'Specific substance' be coded with a terminology capable of triggering decision support, where possible. For example: Snomed CT, DM+D, RxNorm, NDFRT, ATC, New Zealand Universal List of Medicines and Australian Medicines Terminology. Free text entry should only be used if there is no appropriate terminology available.">
					source = <"openEHR,FHIR,DAM">
				>
				["at0006"] = <
					text = <"Comment">
					description = <"Additional narrative about the propensity for the adverse reaction, not captured in other fields.">
					comment = <"For example: including reason for flagging a 'Criticality' of 'High risk'; and instructions related to future exposure or administration of the Substance, such as administration within an Intensive Care Unit or under corticosteroid cover.">
					source = <"openEHR">
				>
				["at0009"] = <
					text = <"Reaction event">
					description = <"Details about each adverse reaction event linked to exposure to the identified 'Substance'.">
					source = <"openEHR,FHIR,DAM">
				>
				["at0010"] = <
					text = <"Specific substance">
					description = <"Identification of the substance considered to be responsible for the specific adverse reaction event.">
					comment = <"For example: 'Amoxycillin'. Only an individual substance is a valid entry in 'Specific substance'. If the value in 'Substance' is an individual substance and not a substance class, then it may be duplicated in this data element. It is strongly recommended that 'Specific substance' be coded with a terminology capable of triggering decision support, where possible.  For example: RxNorm, Snomed CT, DM+D, NDFRT, ICD-9, ICD-10, UNI, ATC and CPT. Free text entry should only be used if there is no appropriate terminology available.">
					source = <"FHIR, openEHR,DAM">
				>
				["at0011"] = <
					text = <"Manifestation">
					description = <"Clinical symptoms and/or signs that are observed or associated with the adverse reaction.">
					comment = <"Manifestation can be expressed as a single word, phrase or brief description. For example: nausea, rash.  'No reaction'may be appropriate where a previous reaction has been noted but the reaction did not re-occur after further exposure. It is preferable that 'Manifestation' should be coded with a terminology, where possible. The values entered here may be used to display on an application screen as part of a list of adverse reactions, as recommended in the UK NHS CUI guidelines.



Terminologies commonly used include, but are not limited to, SNOMED-CT or ICD10.">
					source = <"FHIR, openEHR,DAM">
				>
				["at0012"] = <
					text = <"Reaction description">
					description = <"Narrative description about the adverse reaction as a whole, including details of the manifestation if required.">
					source = <"FHIR, openEHR">
				>
				["at0018"] = <
					text = <"Exposure description">
					description = <"Narrative description about exposure to the identified 'Specific substance'.">
					source = <"openEHR">
				>
				["at0020"] = <
					text = <"Initial exposure">
					description = <"Record of the date and/or time of the first exposure to the Substance for this Reaction Event.">
					comment = <"Exposure can be more complicated by more than one exposure events leading to a reaction. Further details about the nature of the exposure can be provided by use of additional archetypes in the 'Exposure details' SLOT or as text in the 'Exposure description'.">
					source = <"FHIR, openEHR,DAM">
				>
				["at0021"] = <
					text = <"Certainty">
					description = <"Statement about the degree of clinical certainty that the identified 'Specific substance' was the cause of the 'Manifestation' in this reaction event.">
					source = <"FHIR">
				>
				["at0023"] = <
					text = <"Likely">
					description = <"A reasonable level of clinical certainty that the reaction was caused by the identified 'Specific substance'.">
					source = <"openEHR">
				>
				["at0025"] = <
					text = <"Duration of exposure">
					description = <"The total amount of time the individual was exposed to the identified 'Specific substance'.">
					source = <"openEHR">
				>
				["at0027"] = <
					text = <"Onset of reaction">
					description = <"Record of the date and/or time of the onset of the reaction.">
					source = <"openEHR,FHIR, DAM">
				>
				["at0028"] = <
					text = <"Duration of reaction">
					description = <"The total amount of time that the manifestation of the adverse reaction persisted.">
					source = <"openEHR">
				>
				["at0029"] = <
					text = <"Reaction details">
					description = <"Additional details about the adverse reaction, including anatomical location and Common Toxicity Criteria, can be provided by inclusion of specific archetypes in this SLOT.">
					comment = <"May include structured detail about symptoms; the anatomical location of the manifestation; grading, classification or formal severity assessments such as Common Terminology Criteria for Adverse Events; or the Multimedia CLUSTER archetype. [Note: FHIR - These would be extensions as specified in a profile.]">
					source = <"FHIR, openEHR">
				>
				["at0032"] = <
					text = <"Reaction comment">
					description = <"Additional narrative about the adverse reaction event not captured in other fields.">
					source = <"openEHR">
				>
				["at0040"] = <
					text = <"Clinical management description">
					description = <"Narrative description about the clinical management provided.">
					source = <"openEHR">
				>
				["at0041"] = <
					text = <"Reporting details">
					description = <"Additional structured details required for reporting to regulatory bodies can be provided by inclusion of specific archetypes in this SLOT.">
					comment = <"[Note: FHIR - These would be extensions as specified in a profile.]">
					source = <"FHIR, openEHR">
				>
				["at0042"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0044"] = <
					text = <"Reaction reported?">
					description = <"Has the adverse reaction ever been reported to a regulatory body?">
					source = <"openEHR">
				>
				["at0045"] = <
					text = <"Adverse reaction report">
					description = <"Link to an adverse reaction Report sent to a regulatory body.">
					source = <"openEHR">
				>
				["at0047"] = <
					text = <"Supporting clinical record information">
					description = <"Link to further information about the presentation and findings that exist elsewhere in the health record, including allergy test reports.">
					comment = <"For example, presenting symptoms, examination findings, diagnosis etc.
 [Note: FHIR,DAM: Maps to Sensitivity Test.]">
					source = <"FHIR, openEHR, DAM">
				>
				["at0048"] = <
					text = <"Report comment">
					description = <"Narrative about the adverse reaction report or reporting process.">
					comment = <"For example, the reason for non-reporting.">
					source = <"openEHR">
				>
				["at0050"] = <
					text = <"Identifier">
					description = <"External Ids for this item.">
					comment = <"openEHR: implicit in the reference model ENTRY/id.">
					source = <"FHIR">
				>
				["at0058"] = <
					text = <"Reaction mechanism">
					description = <"Identification of the underlying physiological mechanism for the adverse reaction.">
					comment = <"Immune-mediated responses have been traditionally regarded as an indicator for escalation of significant future risk. Contemporary knowledge suggests that some reactions previously thought to be immune are actually non-immune and still carry life threatening risk. 

It is acknowledged that many clinicians may not be in a position to distinguish the mechanism of a particular reaction.  This data element is included nevertheless because many legacy systems have captured this attribute.

 Immunological testing may provide supporting evidence for the basis and causative substance , but no tests are 100% sensitive or specific for a sensitivity.">
					source = <"FHIR,DAM">
				>
				["at0059"] = <
					text = <"Immune mediated">
					description = <"Immune mediated reaction, including allergic reactions and hypersensitivities.">
				>
				["at0060"] = <
					text = <"Non-immune mediated">
					description = <"A non-immune mediated reaction, which can include pseudo-allergic reactions, side effects, intolerances, drug toxicities (for example, to Gentamicin).">
				>
				["at0062"] = <
					text = <"Last updated">
					description = <"Date when the propensity/reaction event was updated.">
					comment = <"[Note: FHIR - maps to recordedDate in FHIR.]">
					source = <"openEHR,FHIR.DAM">
				>
				["at0063"] = <
					text = <"Status">
					description = <"Assertion about the certainty of the propensity, or potential future risk, of the identified 'Substance' to cause a reaction.">
					comment = <"Decision support would typically raise alerts for 'Suspected', 'Likely', 'Confirmed', and ignore a 'Refuted' reaction. Clinical systems may choose not to display Adverse reaction entries with a 


'Refuted' status in the Adverse Reaction List. 

However, 'Refuted' may be useful for reconciliation of the adverse reaction list or when communicating between systems

. Some implementations may choose to make this field mandatory.
 'Resolved' may be used variably across systems, depending on clinical use and context - there appears to be differing opinion whether this should still be used to raise potential alerts or to display in an Adverse Reaction List.">
					source = <"FHIR,DAM">
				>
				["at0064"] = <
					text = <"Likely">
					description = <"A reasonable level of certainty about the propensity for a reaction to the identified 'Substance'.">
				>
				["at0065"] = <
					text = <"Confirmed">
					description = <"A high level of certainty about the propensity for a reaction to the identified 'Substance', which may include clinical evidence by testing or re-challenge.">
				>
				["at0066"] = <
					text = <"Refuted">
					description = <"The propensity for a reaction to the identified 'Substance' has been clinically reassessed or has been disproved with a high level of clinical certainty by re-exposure or deliberate challenge.">
				>
				["at0067"] = <
					text = <"Resolved">
					description = <"The previously known reaction to the identified 'Substance' has been clinically reassessed and considered no longer to be an active risk.">
				>
				["at0068"] = <
					text = <"Subject">
					description = <"The patient who has the allergy or intolerance.">
					comment = <"openEHR: implicit in the reference model ENTRY/subject.">
					source = <"FHIR">
				>
				["at0069"] = <
					text = <"Recorder">
					description = <"Indicates who has responsibility for the record.">
					comment = <"openEHR: implicit in the reference model ENTRY/provider.">
					source = <"FHIR">
				>
				["at0089"] = <
					text = <"Severity of reaction">
					description = <"Clinical assessment of the severity of the reaction event as a whole, potentially considering multiple different manifestations.">
					comment = <"It is acknowledged that this assessment is very subjective. There may be some some specific practice domains where objective scales have been applied.
 Objective scales can be included in this model using the 'Reaction details' Cluster in openEHR or extensions in FHIR.">
					source = <"DAM">
				>
				["at0090"] = <
					text = <"Severe">
					description = <"Causes severe physiological effects.">
					source = <"FHIR">
				>
				["at0092"] = <
					text = <"Moderate">
					description = <"Causes moderate physiological effects.">
					source = <"FHIR">
				>
				["at0093"] = <
					text = <"Mild">
					description = <"Causes mild physiological effects.">
					source = <"FHIR">
				>
				["at0095"] = <
					text = <"Suspected">
					description = <"A low level of clinical certainty that the reaction was caused by the identified 'Specific substance'.">
				>
				["at0096"] = <
					text = <"Exposure details">
					description = <"Additional details about exposure to the 'Specific substance', especially in situations where there may have been multiple or cumulative exposures can be provided by inclusion of specific archetypes in this SLOT.">
					comment = <"[Note: FHIR - These would be extensions as specified in a profile.]">
				>
				["at0098"] = <
					text = <"FHIR record provenance">
					description = <"FHIR elements that are carried implicitly in the openEHR reference model.">
				>
				["at0099"] = <
					text = <"Report summary">
					description = <"Structured details about reports that have been forwarded to regulatory bodies.">
				>
				["at0101"] = <
					text = <"Criticality">
					description = <"An indication of the potential for critical system organ damage or life threatening consequence.">
					comment = <"This can be regarded as a judgement of a 'worst case scenario'.
 In most contexts 'Low' would be regarded as the default value.">
					source = <"DAM, openEHR">
				>
				["at0102"] = <
					text = <"Low">
					description = <"Exposure to substance unlikely to result in critical system organ damage or life threatening consequence. Future exposure to the identified 'Substance' should be considered a relative contra-indication in normal clinical circumstances.">
				>
				["at0103"] = <
					text = <"High">
					description = <"Exposure to substance may result in critical organ system damage or life threatening consequence. Future exposure to the identified 'Substance' should be considered an absolute contra-indication in normal clinical circumstances.">
				>
				["at0106"] = <
					text = <"Route of exposure">
					description = <"Identification of the route by which the subject was exposed to the identified 'Specific substance'.">
					comment = <"Coding of the Route of Exposure with a terminology should be used wherever possible.">
					source = <"FHIR,DAM">
				>
				["at0116"] = <
					text = <"Information source">
					description = <"Details about the provenance of the information can be provided by inclusion of specific archetypes in this SLOT.">
					comment = <"This SLOT is for FHIR purposes only - the data would be defined in a provenance resource.">
				>
				["at0117"] = <
					text = <"Date of last reaction">
					description = <"Represents the date and/or time of the last known occurrence of a reaction event.">
					comment = <"This date may be be a duplicate of the most recent 'Onset of reaction' date.
 Where a textual representation of the date of last occurrence is required e.g 'In Childhood, '10 years ago' the Comment element should be used.">
					source = <"IMH">
				>
				["at0118"] = <
					text = <"Confirmed">
					description = <"A high level of clinical certainty that the reaction was due to the identified 'Substance', which may include clinical evidence by testing or re-challenge.">
				>
				["at0119"] = <
					text = <"Clinical management details">
					description = <"Additional structured details about clinical management for this reaction event can be provided by inclusion of specific archetypes in this SLOT.">
					comment = <"[Note: FHIR - These would be extensions as specified in a profile.]">
				>
				["at0120"] = <
					text = <"Substance category">
					description = <"Category of the identified 'Substance'.">
					comment = <"This data element has been included because it is currently being captured in some clinical systems. This data can be derived from the Substance where coding systems are used, and is effectively redundant in that situation.">
				>
				["at0121"] = <
					text = <"Food">
					description = <"Any substance consumed to provide nutritional support for the body, such as peanut or egg.">
				>
				["at0122"] = <
					text = <"Medication">
					description = <"Any substance administered to achieve a physiological effect.">
				>
				["at0123"] = <
					text = <"Other">
					description = <"Any other substance encountered including venom, latex and other environmental substances.">
				>
				["at0124"] = <
					text = <"Indeterminate">
					description = <"Unable to assess with information available.">
				>
				["at0125"] = <
					text = <"Date of report">
					description = <"Date that the report was sent to the regulatory authority.">
				>
				["at0126"] = <
					text = <"Indeterminate">
					description = <"The physiological mechanism could not be determined.">
				>
				["at0127"] = <
					text = <"Suspected">
					description = <"A low level of clinical certainty about the propensity of a reaction to the identified 'Substance'.">
				>
			>
		>
	>

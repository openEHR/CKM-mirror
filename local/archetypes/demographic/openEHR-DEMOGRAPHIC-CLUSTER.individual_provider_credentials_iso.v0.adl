archetype (adl_version=1.4; uid=b2ac6f71-dc01-4914-8fcc-ef1cdbb7104f)
	openEHR-DEMOGRAPHIC-CLUSTER.individual_provider_credentials_iso.v0

concept
	[at0000]	-- Individual healthcare provider credentials
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			author = <
				["name"] = <"Sergio Miranda Freire">
				["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
				["email"] = <"sergio@lampada.uerj.br">
			>
		>
	>
description
	original_author = <
		["name"] = <"Sergio Miranda Freire & Rigoleta Dutra Mediano Dias">
		["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
		["email"] = <"sergio@lampada.uerj.br">
		["date"] = <"2009-05-22">
	>
	details = <
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Representação dos dados das credenciais de um profissional de saúde">
			use = <"Usado em um serviço demográfico para coletar dados das credenciais de um profissional de saúde">
			keywords = <"serviço demográfico", "credenciais de um profissional de saúde">
			misuse = <"">
			copyright = <"© copyright (c) 2009 openEHR Foundation, openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Representation of data about an individual healthcare provider credentials">
			use = <"Used in demographic services to collect data about an individual healthcare provider credentials">
			keywords = <"demographic service", "individual healthcare provider credentials">
			misuse = <"">
			copyright = <"© copyright (c) 2009 openEHR Foundation, openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Rigoleta Dutra, Ministry of Defense, Brazil (openEHR Editor)", "Sergio Freire, State University of Rio de Janeiro, Brazil (openEHR Editor)", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"ISO/DTS 27527:2007(E) - Provider Identification - Draft Technnical Specification - International Organization for Standardization">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"FC0BC3E72D30564A02DFF68869486562">
		["build_uid"] = <"8ea618b0-defa-4534-b9f3-1250d51a364a">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER [at0000] matches {	-- individual healthcare provider credentials
		items cardinality matches {1..*; ordered} matches {
			ELEMENT [at0001] occurrences matches {1..1} matches {  -- Field of Practice
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {[ac0001]}
					}
				}
			}
			ELEMENT [at0002] occurrences matches {1..1} matches {  -- Primary Field of Practice
				value matches {
					DV_BOOLEAN matches {*}
				}
			}
			CLUSTER [at0003] occurrences matches {1..*} matches { --  Registration
				items cardinality matches {4..5; ordered} matches {
					ELEMENT [at0005] occurrences matches {1..1} matches { -- Registration number
						value matches {
							DV_TEXT matches {*}
						} 	
					}
					ELEMENT[at0006] occurrences matches {1..1} matches {  -- Registering body 
						value matches {
							DV_TEXT matches {*}
						}
					}		
					ELEMENT [at0007] occurrences matches {1..1} matches { -- Registration Status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
								              	      [local::
                                    						        at0021,
                                    							at0022,
                                    							at0023,
                                    							at0024,
                                    							at0025,
                                    							at0026,
											at0027,
											at0028]
								}
							}	
						}
					}
					ELEMENT[at0008] occurrences matches {1..1} matches {  -- Period 
						value matches {
							DV_INTERVAL<DV_DATE> matches {*}
						}
					}		
	            			allow_archetype CLUSTER[at0009] occurrences matches {0..1} matches {
                				include
                    					archetype_id/value matches {/openEHR-DEMOGRAPHIC-CLUSTER\.registration_other_data[a-zA-Z0-9_-]*\.v0/}
            				}
				}
			}
		
			CLUSTER [at0004] occurrences matches {0..*} matches { -- qualifications
	 			items cardinality matches {3..5; ordered} matches {
					ELEMENT [at0010] occurrences matches {1..1} matches { -- Qualification Name
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT [at0011] occurrences matches {0..1} matches { -- Qualification level
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {[ac0002]}
							}
						}
					}
					ELEMENT [at0012] occurrences matches {1..1} matches { -- Issuing Institution
						value matches { 
							DV_TEXT matches  {*}
						}			
					}
					ELEMENT [at0013] occurrences matches {1..1} matches { -- Issuing Institution Country
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {[ac0003]}
							}
						}
					}
					ELEMENT [at0014] occurrences matches {0..1} matches { -- Qualification year
						value matches {
							DV_DATE matches {*}			
						}
					}
				}
			}
		}
	}




ontology
	term_definitions = <
		["pt-br"] = <
			items = <
				["at0000"] = <
					text = <"Credenciais do profissional de saúde">
					description = <"Credenciais do profissional de saúde">
				>
				["at0001"] = <
					text = <"Campo de atuação">
					description = <"O campo que um indivíduo identifica como o seu campo de atuação e possui as qualificações e registro exigidos">
				>
				["at0002"] = <
					text = <"Campo de atuação primário">
					description = <"Indica se este campo de atuação é o campo de atuação principal do indivíduo ">
				>
				["at0003"] = <
					text = <"Registro no Conselho">
					description = <"Registro no Conselho Profissional">
				>
				["at0004"] = <
					text = <"Qualificações">
					description = <"Este grupo de dados indica as qualificações formais de um indivíduo">
				>
				["at0005"] = <
					text = <"Número do registro">
					description = <"O identificador único emitido pelo conselho profissional para uma pessoa que a identifica univocamente no sistema">
				>
				["at0006"] = <
					text = <"Conselho">
					description = <"A organização com a capacidade legal e profissional para registrar ou certificar um profissional neste campo de atuação">
				>
				["at0007"] = <
					text = <"Status">
					description = <"O status do registro do indivíduo para exercer uma determinada profissão">
				>
				["at0008"] = <
					text = <"Período">
					description = <"O período de tempo que vai do início formal do registro até o final deste registro">
				>
				["at0009"] = <
					text = <"Outros dados">
					description = <"Outros dados sobre este registro profissional">
				>
				["at0010"] = <
					text = <"Qualificação">
					description = <"O nome formal dado à qualificação">
				>
				["at0011"] = <
					text = <"Nível">
					description = <"Classificação que indica o nível da qualificação">
				>
				["at0012"] = <
					text = <"Instituição">
					description = <"O nome da instituição educational que emitiu a qualificação">
				>
				["at0013"] = <
					text = <"País">
					description = <"O código internacional do país no qual a instituição está registrada">
				>
				["at0014"] = <
					text = <"Ano">
					description = <"O ano em que o indivíduo obteve esta qualificação">
				>
				["at0021"] = <
					text = <"Ativo/Registro Completo">
					description = <"O indivíduo está plenamente qualificado a prestar serviços no escopo do seu campo de atuação">
				>
				["at0022"] = <
					text = <"Registro limitado">
					description = <"O indivíduo está limitado a prestar serviços dentro de um escopo restrito do seu campo de atuação">
				>
				["at0023"] = <
					text = <"Registro de estudante">
					description = <"O indivíduo é capaz de prestar somente serviços supervisionados">
				>
				["at0024"] = <
					text = <"Registro suspenso">
					description = <"O registro está suspenso">
				>
				["at0025"] = <
					text = <"Registro encerrado">
					description = <"O registro está encerrado">
				>
				["at0026"] = <
					text = <"Anulado">
					description = <"O registro está anulado">
				>
				["at0027"] = <
					text = <"Pendente">
					description = <"O registro do indivíduo está pendente">
				>
				["at0028"] = <
					text = <"Registro inativo">
					description = <"O indivíduo não está mais ativo neste campo">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Individual healthcare provider credentials">
					description = <"Individual healthcare provider credentials">
				>
				["at0001"] = <
					text = <"Field of Practice">
					description = <"The field that an individual Healthcare provider identifies as being his/her field of practice and has the required qualifications and registration to support that claim">
				>
				["at0002"] = <
					text = <"Primary field of practice">
					description = <"A flag that indicates the current primary specialty or field of practice of an individual provider">
				>
				["at0003"] = <
					text = <"Registration">
					description = <"Registration data">
				>
				["at0004"] = <
					text = <"Qualifications">
					description = <"This data group indicates the formal qualifications of an individual">
				>
				["at0005"] = <
					text = <"Registration ID">
					description = <"The unique identifier issued by the registering body to this person that identifies him/her uniquely within the registration system">
				>
				["at0006"] = <
					text = <"Registering body">
					description = <"The organisation with the legal and professional capacity to register or certify a professional in this field of practice">
				>
				["at0007"] = <
					text = <"Registration Status">
					description = <"The status of the individual registration to practice in a given field of practice">
				>
				["at0008"] = <
					text = <"Period">
					description = <"The period of time from the beginning to the end of the formal registration">
				>
				["at0009"] = <
					text = <"Other data">
					description = <"Other data about this registration">
				>
				["at0010"] = <
					text = <"Qualification Name">
					description = <"The full and formal name given to the qualification">
				>
				["at0011"] = <
					text = <"Qualification level">
					description = <"Classification indicating the level of qualification">
				>
				["at0012"] = <
					text = <"Issuing Institution">
					description = <"The name of the educational organisation who issued the qualification">
				>
				["at0013"] = <
					text = <"Issuing Institution Country">
					description = <"The international code for the country within which the education institution is registered">
				>
				["at0014"] = <
					text = <"Qualification year">
					description = <"The year in which the individual obtained this qualification">
				>
				["at0021"] = <
					text = <"Active/Full registration">
					description = <"The registrant is fully qualified to provide services within the scope of the field of practice">
				>
				["at0022"] = <
					text = <"Limited registration">
					description = <"The registrant is limited to provide services within a defined subset of the field of practice">
				>
				["at0023"] = <
					text = <"Student registration">
					description = <"The registrant is able to provide supervised services only">
				>
				["at0024"] = <
					text = <"Suspended registration">
					description = <"The registration is suspended">
				>
				["at0025"] = <
					text = <"Terminated registration">
					description = <"The registration is terminated">
				>
				["at0026"] = <
					text = <"Nullified">
					description = <"The registration is nullified">
				>
				["at0027"] = <
					text = <"Pending">
					description = <"The registration is pending">
				>
				["at0028"] = <
					text = <"Inactive registration">
					description = <"O indivíduo não está mais ativo neste campo">
				>
			>
		>
	>
	constraint_definitions = <
		["pt-br"] = <
			items = <
				["ac0001"] = <
					text = <"Códigos de ocupações">
					description = <"códigos válidos para ocupações">
				>
				["ac0002"] = <
					text = <"Códigos de níveis de qualificação">
					description = <"códigos válidos para nível de qualificação">
				>
				["ac0003"] = <
					text = <"Códigos de países">
					description = <"códigos válidos para países">
				>
			>
		>
		["en"] = <
			items = <
				["ac0001"] = <
					text = <"Field of practice codes">
					description = <"valid codes for field of practice">
				>
				["ac0002"] = <
					text = <"Qualification level codes">
					description = <"valid codes for qualification level">
				>
				["ac0003"] = <
					text = <"Country codes">
					description = <"valid codes for countries">
				>
			>
		>
	>

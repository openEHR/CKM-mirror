archetype (adl_version=1.4)
	openEHR-DEMOGRAPHIC-PERSON.person-patient.v1
specialise
	openEHR-DEMOGRAPHIC-PERSON.person.v1

concept
	[at0000.1]	-- Dados do paciente
language
	original_language = <[ISO_639-1::pt-br]>
	translations = <
		["en"] = <
			language = <[ISO_639-1::en]>
			author = <
				["name"] = <"Sergio Miranda Freire">
				["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
				["email"] = <"sergio@lampada.uerj.br">
			>
		>
	>
description
	original_author = <
		["name"] = <"Sergio Miranda Freire & Rigoleta Dutra Mediano Dias">
		["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
		["email"] = <"sergio@lampada.uerj.br">
		["date"] = <"22/05/2009">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Representation of a patient's demographic data.">
			use = <"Used in demographic service to collect a patient's data.">
			keywords = <"demographic service", "patient's data">
			misuse = <"">
			copyright = <"© 2011 openEHR Foundation">
		>
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Representação dos dados demográficos de um paciente.">
			use = <"Usado em serviço demográficos para coletar os dados de um paciente.">
			keywords = <"serviço demográfico", "dados de um paciente">
			misuse = <"">
			copyright = <"© 2011 openEHR Foundation">
		>
	>
	lifecycle_state = <"Authordraft">
	other_contributors = <>
	other_details = <
		["references"] = <"ISO/TS 22220:2008(E) - Identification of Subject of Care - Technical Specification - International Organization for Standardization.">
	>

definition
    PERSON[at0000.1] matches {  -- patient demographic data
        details matches {
            allow_archetype ITEM_TREE[at0001] occurrences matches {1..1} matches {
                include
                   archetype_id/value matches {/(person_details)[a-zA-Z0-9_-]*\.v1/}
            }
        }
        identities matches {
            allow_archetype PARTY_IDENTITY[at0002.1] occurrences matches {1..1} matches {
                include
                    archetype_id/value matches {/(person_name)[a-zA-Z0-9_-]*\.v1/}
            }
        }
        contacts matches {
            CONTACT[at0003.1] occurrences matches {1..1} matches {  -- Contacts
                addresses matches {
                    allow_archetype ADDRESS[at0030] occurrences matches {1..1} matches {
                        include
                            archetype_id/value matches {/(address)([a-zA-Z0-9_]+)*\.v1/}
                            archetype_id/value matches {/(electronic_communication)[a-zA-Z0-9_-]*\.v1/}
                    }
                }
            }
        }
        relationships matches {
			PARTY_RELATIONSHIP[at0004.1] matches {	-- personal relationships
				details matches {
					ITEM_TREE [at0.40] matches {	
						items matches {
							ELEMENT[at0040] matches {	-- type of relationship
								value matches { 
									DV_TEXT matches {*}
									DV_CODED_TEXT matches {
										defining_code matches {[ac0000]}
									}		
								}
							 }
						}
					}
				}
			}		
			PARTY_RELATIONSHIP[at0.2] matches {	-- relationship between a patient and a third-party payer
				details matches {
					ITEM_TREE[at0.20] matches {
						items matches {
							allow_archetype CLUSTER[at0.21]	matches {
								include
								archetype_id/value matches {/(person_identifier)[a-zA-Z0-9_-]*\.v1/}
							}
						}
					}
				}
			}
			PARTY_RELATIONSHIP[at0.3] matches {	--  relationship between a patient and a healthcare provider organisation/individual provider
				details matches {
					ITEM_TREE[at0.30] matches {
						items  matches {
							allow_archetype CLUSTER[at0.31]	matches {
								include
								archetype_id/value matches {/(person_identifier)[a-zA-Z0-9_-]*\.v1/}
							}
						}	
					}
				}
			}
        }
    }




ontology
	term_definitions = <
		["pt-br"] = <
			items = <
				["at0000.1"] = <
					text = <"Dados do paciente">
					description = <"Dados do paciente.">
				>
				["at0001"] = <
					text = <"Detalhes">
					description = <"Detalhes demográficos do paciente.">
				>
				["at0002"] = <
					text = <"Nome">
					description = <"Conjunto de dados que especificam o nome da pessoa.">
				>
				["at0003"] = <
					text = <"Contatos">
					description = <"Contatos da pessoa.">
				>
				["at0004"] = <
					text = <"Relacionamentos">
					description = <"Relacionamentos de uma pessoa, especialmente laços familiares.">
				>
				["at0002.1"] = <
					text = <"Nome">
					description = <"Conjunto de dados que especificam o nome do paciente.">
				>
				["at0003.1"] = <
					text = <"Contatos">
					description = <"Contatos da pessoa.">
				>
				["at0004.1"] = <
					text = <"Relacionamentos">
					description = <"Relacionamentos de um paciente, especialmente laços familiares.">
				>
				["at0030"] = <
					text = <"Endereço">
					description = <"Endereços vinculados a um único contato, ou seja, com o mesmo período de validade.">
				>
				["at0040"] = <
					text = <"Grau de parentesco">
					description = <"Define o grau de parentesco entre as pessoas envolvidas.">
				>
				["at0.2"] = <
					text = <"Fonte pagadora">
					description = <"Beneficiário: Relacionamento do paciente com uma Fonte Pagadora.">
				>
				["at0.3"] = <
					text = <"Serviço de saúde/Profissional de saúde">
					description = <"Relacionamento do paciente com uma organização prestadora de serviço de saúde/profissional de saúde.">
				>
				["at0.40"] = <
					text = <"Personal relationships">
					description = <"Personal relationship.">
				>
				["at0.20"] = <
					text = <"Identificações do beneficiário">
					description = <"Identificações do beneficiário junto à fonte pagadora.">
				>
				["at0.21"] = <
					text = <"Identificação do beneficiário">
					description = <"Documento de identificação do beneficiário junto à fonte pagadora.">
				>
				["at0.30"] = <
					text = <"Identificações no prestador">
					description = <"Identificações do paciente junto ao prestador.">
				>
				["at0.31"] = <
					text = <"Identificação no prestador">
					description = <"Documento de identificação do paciente junto ao prestador.">
				>
			>
		>
		["en"] = <
			items = <
				["at0000.1"] = <
					text = <"Patient">
					description = <"Patient demographic data.">
				>
				["at0001"] = <
					text = <"Demographic details">
					description = <"A patient's demographic details.">
				>
				["at0002"] = <
					text = <"Name">
					description = <"A person's name.">
				>
				["at0003"] = <
					text = <"Contacts">
					description = <"A person's contacts.">
				>
				["at0004"] = <
					text = <"Relationships">
					description = <"A person's relationships, especially family ties.">
				>
				["at0002.1"] = <
					text = <"Name">
					description = <"A patient's name.">
				>
				["at0003.1"] = <
					text = <"Contacts">
					description = <"A patient's contacts.">
				>
				["at0004.1"] = <
					text = <"Relationships">
					description = <"A patient's relationships, especially family ties.">
				>
				["at0030"] = <
					text = <"Addresses">
					description = <"Addresses linked to a single contact, i.e. with the same time validity.">
				>
				["at0040"] = <
					text = <"Relationship type">
					description = <"Defines the type of relationship between related persons.">
				>
				["at0.2"] = <
					text = <"Third party payer">
					description = <"Relationship between the patient and a third-party payer.">
				>
				["at0.3"] = <
					text = <"Healthcare provider/Health professional">
					description = <"Patient: relationship between the patient and a healthcare provider organisation/health professional.">
				>
				["at0.40"] = <
					text = <"Personal relationship">
					description = <"Personal relationship.">
				>
				["at0.20"] = <
					text = <"Patient identifiers">
					description = <"Identifiers of the patient at the third-party payer.">
				>
				["at0.21"] = <
					text = <"Healthcare consumer identifier.">
					description = <"An identifier of the patient at the third-party payer.">
				>
				["at0.30"] = <
					text = <"Patient identifiers">
					description = <"Patient identifiers at the related healthcare provider.">
				>
				["at0.31"] = <
					text = <"Patient identifier">
					description = <"A patient identifier at the related healthcare provider.">
				>
			>
		>
	>
	constraint_definitions = <
		["pt-br"] = <
			items = <
				["ac0000"] = <
					text = <"Códigos para tipo de parentesco">
					description = <"códigos válidos para tipo de parentesco.">
				>
			>
		>
		["en"] = <
			items = <
				["ac0000"] = <
					text = <"Codes for the type of relationship">
					description = <"Valid codes for the type of relationship.">
				>
			>
		>
	>

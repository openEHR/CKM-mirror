archetype (adl_version=1.4)
	openEHR-DEMOGRAPHIC-ADDRESS.address.v1

concept
	[at0000]	-- Endereço
language
	original_language = <[ISO_639-1::pt-br]>
	translations = <
		["en"] = <
			language = <[ISO_639-1::en]>
			author = <
				["name"] = <"Sergio Miranda Freire">
				["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
				["email"] = <"sergio@lampada.uerj.br">
			>
		>
	>
description
	original_author = <
		["name"] = <"Sergio Miranda Freire & Rigoleta Dutra Mediano Dias">
		["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
		["email"] = <"sergio@lampada.uerj.br">
		["date"] = <"22/05/2009">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Representation of data about a personal/organizational address, based on ISO 22220 standard. This address represents a geographic location which can be used in several ways: residential address, postal address, etc.">
			use = <"Used to communicate with, identify service provision requirements and trends, and to provide direct services.">
			keywords = <"demographic service", "address", "geographic location", "residential address">
			misuse = <"This archetype can not used for email address, IP address, computer address, telephone, fax and pager. For these items use the electronic_communication archetype.">
			copyright = <"© 2011 openEHR Foundation">
		>
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Representação dos detalhes do endereço de uma pessoa/organização com base na norma ISO 22220. Este endereço representa uma localização geográfica que pode ser utilizada para diversos fins: endereço residencial, endereço postal, etc.">
			use = <"Usado para comunicação, identificar requisitos para provimento de serviços e tendências, e proporcionar serviços diretos.">
			keywords = <"serviço demográfico", "endereço", "localização geográfica">
			misuse = <"Este arquétipo não deve ser usado para endereços de correio eletrônico, endereços IP, endereços de computador, telefone, fax e pager. Para estes itens, use o arquétipo electronic_communication.">
			copyright = <"© 2011 openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Rong Chen, Cambio Healthcare Systems, Sweden", "Ricardo Correia, SBIM/FMUP, Portugal", "Rigoleta Dutra, Ministry of Defense, Brazil (Editor)", "Shahla Foozonakah, Ocean Informatics, Australia", "Heath Frankel, Ocean Informatics, Australia", "Sergio Freire, State University of Rio de Janeiro, Brazil (Editor)", "Heather Grain, Llewelyn Grain Informatics, Australia", "Grahame Grieve, Australia", "Ian McNicoll, Ocean Informatics, United Kingdom (Editor)">
	other_details = <
		["references"] = <"ISO/TS 22220:2008(E) - Identification of Subject of Care - Technical Specification - International Organization for Standardization.">
	>

definition
    ADDRESS[at0000] matches { 
        name matches {
            DV_TEXT matches {*} 
            DV_CODED_TEXT matches { 
                defining_code matches {
                    [local::
                    at0461,
                    at0462,
                    at0463,
                    at0464,
                    at0465,
                    at0466]
                }
            }
        }
        details matches {
            ITEM_TREE[at0001] occurrences matches {0..1} matches { 
                items matches {
                    CLUSTER[at0002] occurrences matches {0..*} matches {  -- Address lines
                        items cardinality matches {1..*; ordered} matches {
                            ELEMENT[at0021] occurrences matches {0..1} matches {  -- Building/complex sub-unit type—abbreviation
                                value matches {
				    DV_TEXT matches {*}
                                    DV_CODED_TEXT matches {  
                                        defining_code matches {
                                            [ac0000]
                                        }
                                    }
                                }
                            }
                            ELEMENT[at0022] occurrences matches {0..1} matches {  -- Building/complex sub-unit number
                                value matches {
                                    DV_TEXT matches {*} 
                                }
                            }
                            ELEMENT[at0023] occurrences matches {0..1} matches {  -- Address site name
                                value matches {
                                    DV_TEXT matches {*} 
                                }
                            }
                            ELEMENT[at0024] occurrences matches {0..1} matches {  -- Floor/level number
                                value matches {
                                    DV_TEXT matches {*}
                                }
                            }
                            ELEMENT[at0025] occurrences matches {0..1} matches {  -- Floor/level type
                                value matches {
                                    DV_TEXT matches {*} 
                                }
                            }
                            ELEMENT[at0026] occurrences matches {0..1} matches {  -- Lot number
                                value matches {
                                    DV_TEXT matches {*} 
                                }
                            }
                            ELEMENT[at0027] occurrences matches {0..1} matches {  -- Street type
                                value matches {
 				    DV_TEXT matches {*}
                                    DV_CODED_TEXT matches { 
                                        defining_code matches {
                                            [ac0001]
                                        }
                                    }
                                }
                            }
                            ELEMENT[at0028] occurrences matches {0..1} matches {  -- Street name
                                value matches {
                                    DV_TEXT matches {*}   
                                }
                            }
                            ELEMENT[at0029] occurrences matches {0..1} matches {  -- Number
                                value matches {
                                    DV_TEXT matches {*}  
                                }
                            }
                            ELEMENT[at0030] occurrences matches {0..1} matches {  -- Street suffix
                                value matches {
				    DV_TEXT matches {*}
                                    DV_CODED_TEXT matches { 
                                        defining_code matches {
                                            [ac0002]
                                        }
                                    }
                                }
                            }
                            ELEMENT[at0031] occurrences matches {0..1} matches {  -- address line visualization
                                value matches {
                                    DV_TEXT matches {*} 
                                }
                            }
                        }
                    }
		    ELEMENT[at0003] occurrences matches {0..1} matches {  -- district
			value matches {
			    DV_TEXT matches {*}
			}
		    }
		    ELEMENT[at0004] occurrences matches {0..1} matches {  -- census area
			value matches {
			    DV_TEXT matches {*}
			}
		    }
                    ELEMENT[at0005] occurrences matches {0..1} matches {  -- Delivery Point Identifier
                        value matches {
                            DV_TEXT matches {*}
                        }
                    }
                    ELEMENT[at0006] occurrences matches {0..1} matches {  -- Postal code
                        value matches {
                            DV_TEXT matches {*} 
                        }
                    }
                    ELEMENT[at0007] occurrences matches {0..1} matches {  -- Suburb/town/locality
                        value matches {
                            DV_TEXT matches {*}   
                        }
                    }
                    ELEMENT[at0008] occurrences matches {0..1} matches {  -- State/Province/Territory
                        value matches {
                            DV_CODED_TEXT matches { 
                                defining_code matches {
                                    [ac0003]
                                }
                            }
                        }
                    }
                    ELEMENT[at0009] occurrences matches {0..1} matches {  -- Country
                        value matches {
                            DV_CODED_TEXT matches { 
                                defining_code matches {
                                    [ac0004]
                                }
                            }
                        }
                    }
                    ELEMENT[at0010] occurrences matches {0..1} matches {  -- Address type start date accuracy indicator
                        value matches {
                            DV_TEXT matches { 
                                value matches {/[AEU][AEU][AEU]/}
                            }
                        }
                    }
                    ELEMENT[at0011] occurrences matches {0..1} matches {  -- Address type end date accuracy indicator
                        value matches {
                            DV_TEXT matches { 
                                value matches {/[AEU][AEU][AEU]/}
                            }
                        }
                    }
                 }
            }
        }
    }



ontology
	term_definitions = <
		["pt-br"] = <
			items = <
				["at0000"] = <
					text = <"Endereço">
					description = <"Endereço.">
				>
				["at0001"] = <
					text = <"Itens do endereço">
					description = <"Itens do endereço.">
				>
				["at0002"] = <
					text = <"Linhas de endereço">
					description = <"Uma composição de um ou mais componentes padrões do endereço que fornece uma descrição geográfica/física de uma localização e que, usada em conjunto com outros componentes de nível mais alto do endereço, isto é, 'Subúrbio/cidade/nome da localidade', 'Código Postal', 'Estado/território/província', e 'País', formam um endereço geográfico/físico completo.">
				>
				["at0003"] = <
					text = <"Bairro">
					description = <"Bairro do Logradouro.">
				>
				["at0004"] = <
					text = <"Setor censitário">
					description = <"Código do setor censitário.">
				>
				["at0005"] = <
					text = <"Caixa Postal">
					description = <"Um número único atribuído a um endereço postal conforme designado pelo serviço postal.">
				>
				["at0006"] = <
					text = <"CEP">
					description = <"O código para uma área de entrega postal, alinhada com a localidade, subúrbio ou local para o endereço, conforme definido pelo serviço postal.">
				>
				["at0007"] = <
					text = <"Município">
					description = <"O nome completo da localidade que contém o endereço específico de uma pessoa/organização.">
				>
				["at0008"] = <
					text = <"Estado">
					description = <"Um identificador da Província, Estado ou Território que contém o endereço.">
				>
				["at0009"] = <
					text = <"País">
					description = <"Um código que representa o país componente de um endereço.">
				>
				["at0010"] = <
					text = <"Indicador de acurácia da data de início de validade do endereço">
					description = <"Uma indicação da acurácia da data de início de validade do endereço. Ela é expressa por meio de uma sequência de três caracteres com os valores possíveis A (acurada), E (estimada) ou U (desconhecida) cada um. Os caracteres representam a acurácia para o ano, mês e ano respectivamente. Este elemento foi introduzido para permitir a representação da acurácia de acordo com a norma ISO 22220, mas o modelo de referência do openEHR representa a acurácia da data de modo diferente (veja a especificação dos tipos de dados).">
				>
				["at0011"] = <
					text = <"Indicador de acurácia da data final de validade do endereço">
					description = <"Uma indicação da acurácia da data final de validade do endereço. Ela é expressa por meio de uma sequência de três caracteres com os valores possíveis A (acurada), E (estimada) ou U (desconhecida) cada um. Os caracteres representam a acurácia para o ano, mês e ano respectivamente. Este elemento foi introduzido para permitir a representação da acurácia de acordo com a norma ISO 22220, mas o modelo de referência do openEHR representa a acurácia da data de modo diferente (veja a especificação dos tipos de dados).">
				>
				["at0021"] = <
					text = <"Abreviação do tipo de sub-unidade">
					description = <"A especificação do tipo de uma parte identificada separadamente em um edifício/complexo, marina, etc, para distinguí-la de outras.">
				>
				["at0022"] = <
					text = <"Número da sub-unidade">
					description = <"A especificação do número do identificador de um edifício/complexo, marina, etc, para distinguí-lo de outros.">
				>
				["at0023"] = <
					text = <"Nome do edifício/propriedade">
					description = <"O nome completo usado para identificar a propriedade física ou o edifício como parte de sua localização.">
				>
				["at0024"] = <
					text = <"Número do andar/nível">
					description = <"Descritor usado para identificar o andar ou nível de um complexo/edifício de múltiplos andares.">
				>
				["at0025"] = <
					text = <"Tipo do andar/nível">
					description = <"Descritor usado para classificar o tipo de andar ou nível de um complexo/edifício de múltiplos andares.">
				>
				["at0026"] = <
					text = <"Número do lote">
					description = <"Seção, Número no Loteamento.">
				>
				["at0027"] = <
					text = <"Tipo do Logradouro">
					description = <"Um código que identifica o tipo de logradouro público.">
				>
				["at0028"] = <
					text = <"Logradouro">
					description = <"O nome que identifica um logradouro público e o diferencia de outros no mesmo subúrbio/cidade/localidade.">
				>
				["at0029"] = <
					text = <"Número">
					description = <"O número de referência (numérico ou string) da residência ou propriedade que é único em um nome de logradouro, subúrbio.">
				>
				["at0030"] = <
					text = <"Sufixo do logradouro">
					description = <"Termo usado para qualificar um nome de logradouro adequado para uma referência direcional.">
				>
				["at0031"] = <
					text = <"Visualização da linha de endereço">
					description = <"Como a linha de endereço é visualizada pelo usuário.">
				>
				["at0461"] = <
					text = <"Comercial">
					description = <"Usado para indicar um endereço que é a localização física de um escritório ou de um negócio no qual uma entidade pode ser contactada.">
				>
				["at0462"] = <
					text = <"Correio ou postal">
					description = <"Usado para indicar um endereço que é somente para correspondência e/ou cobrança.">
				>
				["at0463"] = <
					text = <"Acomodação temporária">
					description = <"Acomodação temporária (tal como um endereço de uma pessoa que usualmente reside no exterior ou onde o fornecedor do endereço está em uma acomodação temporária devido a renovação ou tratamento).">
				>
				["at0464"] = <
					text = <"Residencial">
					description = <"Usado para indicar onde a pessoa está morando. Observe que este código não é válido para organizações.">
				>
				["at0465"] = <
					text = <"Sem endereço fixo">
					description = <"Usado onde a pessoa não possui endereço fixo.">
				>
				["at0466"] = <
					text = <"Desconhecido/Não declarado/Descrito inadequadamente">
					description = <"Desconhecido/Não declarado/Descrito inadequadamente pode também ser usado onde a pessoa não possui endereço fixo ou não deseja ter o seu endereço residencial ou para correspondência registrado.">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Address">
					description = <"Address based on ISO22220 standard.">
				>
				["at0001"] = <
					text = <"Items">
					description = <"Address items.">
				>
				["at0002"] = <
					text = <"Address lines">
					description = <"A composite of one or more standard address components that describe a low level of geographical/physical description of a location that, used in conjunction with the other high-level address components i.e. ‘suburb / town / locality name’, ‘postal code’, ‘state / territory / province’, and ‘country’, forms a complete geographical/physical address.">
				>
				["at0003"] = <
					text = <"District">
					description = <"District.">
				>
				["at0004"] = <
					text = <"Census area">
					description = <"Area defined by the Brazilian Geographic and Statistics Bureau according to the spacial distribution of the population.">
				>
				["at0005"] = <
					text = <"Delivery point identifier">
					description = <"A unique number assigned to a postal address as designated by the postal service.">
				>
				["at0006"] = <
					text = <"Postal code (ZIP code)">
					description = <"The code for a postal delivery area, aligned with locality, suburb or place for the address, as defined by the postal service.">
				>
				["at0007"] = <
					text = <"Suburb/town/locality">
					description = <"The full name of the general locality containing the specific address.">
				>
				["at0008"] = <
					text = <"State/territory/province identifier">
					description = <"An identifier of the province, state or territory containing the address.">
				>
				["at0009"] = <
					text = <"Country identifier">
					description = <"A code representing the country component of an address.">
				>
				["at0010"] = <
					text = <"Address type start date accuracy indicator">
					description = <"An indication of the accuracy of the address type start date at the component level for the date. It is expressed by means of a sequence of three characters with possible values A (accurate), E (estimated) or U (unknown) each. The characters represent the accuracy for the year, month and day respectively. This element was introduced in order to be compatible with ISO 22220, but openEHR reference model represents date accuracy in a different way (see the data types specification).">
				>
				["at0011"] = <
					text = <"Address type end date accuracy indicator">
					description = <"An indication of the accuracy of the address type end date at the component level for the date. It is expressed by means of a sequence of three characters with possible values A (accurate), E (estimated) or U (unknown) each. The characters represent the accuracy for the year, month and day respectively. This element was introduced in order to be compatible with ISO 22220, but openEHR reference model represents date accuracy in a different way (see the data types specification).">
				>
				["at0021"] = <
					text = <"Building/complex sub-unit type—abbreviation">
					description = <"The specification of the type of a separately identifiable portion within a building/complex, marina etc. to clearly distinguish it from another.">
				>
				["at0022"] = <
					text = <"Building/complex sub-unit number">
					description = <"The specification of the number of identifier of a building/complex, marina etc. to clearly distinguish it from another.">
				>
				["at0023"] = <
					text = <"Address site name">
					description = <"The full name used to identify the physical building or property as part of its location.">
				>
				["at0024"] = <
					text = <"Floor/level number">
					description = <"Descriptor used to identify the floor or level of a multi-storey building/complex.">
				>
				["at0025"] = <
					text = <"Floor/level type">
					description = <"Descriptor used to classify the type of floor or level of a multi-storey building/complex.">
				>
				["at0026"] = <
					text = <"Lot number">
					description = <"Section, allotment number.">
				>
				["at0027"] = <
					text = <"Street type code">
					description = <"A code that identifies the type of public thoroughfare.">
				>
				["at0028"] = <
					text = <"Street name">
					description = <"The name that identifies a public thoroughfare and differentiates it from others in the same suburb/town/locality.">
				>
				["at0029"] = <
					text = <"Street number">
					description = <"The numeric or string reference number of a house or property that is unique within a street name, suburb.">
				>
				["at0030"] = <
					text = <"Street suffix code">
					description = <"Term used to qualify the street name suitable for directional references.">
				>
				["at0031"] = <
					text = <"Address line visualization">
					description = <"How the address line is presented to the user.">
				>
				["at0461"] = <
					text = <"Business">
					description = <"Used to indicate an address that is the physical location of a business or office at which an entity can be contacted.">
				>
				["at0462"] = <
					text = <"Mailing or postal">
					description = <"Used to indicate an address that is only for correspondence and/or billing purposes.">
				>
				["at0463"] = <
					text = <"Temporary accomodation">
					description = <"Temporary accomodation address (such as for a person who usually resides overseas or where the provider of the address is in temporary accomodation due to renovation or treatment).">
				>
				["at0464"] = <
					text = <"Residential">
					description = <"Used to indicate where a person is living. Note that this code is not valid for organisations.">
				>
				["at0465"] = <
					text = <"No fixed address">
					description = <"Used where a person has no fixed address.">
				>
				["at0466"] = <
					text = <"Unknown/not stated/inadequately described">
					description = <"Unknown/not stated/inadequately described may also be used where the person has no fixed address or does not wish to have their residential or correspondence address recorded.">
				>
			>
		>
	>
	constraint_definitions = <
		["pt-br"] = <
			items = <
				["ac0000"] = <
					text = <"Códigos de tipos de edifício">
					description = <"Códigos válidos para tipo de edifício.">
				>
				["ac0001"] = <
					text = <"Códigos de logradouros">
					description = <"Códigos válidos para tipo de logradouro.">
				>
				["ac0002"] = <
					text = <"Códigos de sufixos de logradouro">
					description = <"Códigos válidos para sufixos de logradouro.">
				>
				["ac0003"] = <
					text = <"Códigos de estados">
					description = <"Códigos válidos para estados.">
				>
				["ac0004"] = <
					text = <"Códigos de países">
					description = <"Códigos válidos para países.">
				>
			>
		>
		["en"] = <
			items = <
				["ac0000"] = <
					text = <"Building type codes">
					description = <"Valid codes for types of buildings.">
				>
				["ac0001"] = <
					text = <"Street type codes">
					description = <"Valid codes for street types.">
				>
				["ac0002"] = <
					text = <"Street suffix codes">
					description = <"Valid codes for street suffixes.">
				>
				["ac0003"] = <
					text = <"State codes">
					description = <"valid codes for state/territory/province.">
				>
				["ac0004"] = <
					text = <"Country codes">
					description = <"valid codes for countries.">
				>
			>
		>
	>

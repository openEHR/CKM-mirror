archetype (adl_version=1.4; uid=4ed59bfe-d2a5-4d4d-9e98-74c0ba6f53ee)
	openEHR-EHR-CLUSTER.person.v0

concept
	[at0000]	-- Person
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Natalia Strauch">
				["organisation"] = <"Medizinische Hochschule Hannover">
				["email"] = <"Strauch.Natalia@mh-hannover.de">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Liv Laugen, John Tore Valand">
				["organisation"] = <"​Oslo University Hospital, Norway, Helse Bergen">
				["email"] = <"liv.laugen@ous-hf.no, john.tore.valand@helse-bergen.no">
			>
		>
		["zh"] = <
			language = <[ISO_639-1::zh]>
			author = <
				["name"] = <"Yexuan Cheng">
				["organisation"] = <"浙江大学">
				["email"] = <"3160100913@zju.edu.cn">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2021-04-14">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Zur Darstellung von Informationen zu einer Person, die in der Gesundheitsakte repräsentiert sein muss.">
			use = <"Der Archetyp wird verwendet, um Informationen zu einer Person darzustellen, die in der Gesundheitsakte dargestellt werden muss. Beispiele beinhalten:
- Name und Kontaktdaten einer Person, die eine Hard-Kopie einer Grundversorgungsakte besitzt;
- Name und Kontaktdaten eines Betreuers bei einer Überweisung;
- Name, Funktion und Kontaktdaten eines Mitglieds eines Pflegeteams;
- die Rolle und Kontaktdaten einer benannten Kontaktperson innerhalb einer Organisation;
- Angaben zu einem Verwandten in einer Familienanamnese; oder
- Zeuge eines epileptischen Anfalls.

Dieser Archetyp könnte auch als Proxy für formale demografische Daten verwendet werden, wenn ein Template mit Domänenexperten überprüft wird – beispielsweise bei einer Bewertung, bei der die Prüfer erwarten würden, dass die Personendaten oben auf dem Bewertungsformular angezeigt werden.

Wenn der Archetyp CLUSTER.structured_name im SLOT \"Name strukturiert\" verschachtelt ist, können beliebige oder alle strukturierten Namenselemente verkettet und im Element \"Namenszeile\" dargestellt werden.">
			keywords = <"Organisation, Anbieter, Versorger", ...>
			misuse = <"Der Archetyp darf nicht verwendet werden, um die formellen demografischen Daten für die formelle Identifizierungsverwaltung oder zum Zwecke der Führung eines demografischen Registers oder Index zu repräsentieren oder zu ersetzen. Verwenden Sie zu diesem Zweck einen formellen Master Patient Index oder Health Provider Index oder Archetypen, die auf dem openEHR Demographic Information Model basieren.">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere detaljer om en person i en pasientjournal.">
			use = <"Brukes for å registrere detaljer om en person i en pasientjournal.

For eksempel:
- navn og kontaktdetaljer til en person som har en kopi av et fremtidige behandlingsvalg.
- navn og kontaktdetaljer til en omsorgsperson eller helsepersonell i en henvisning.
- navn, rolle og kontaktdetaljer til et medlem av et behandlingsteam.
- rolle og kontaktdetaljer til en navngitt kontaktperson i en organisasjon.
- detaljer om en slektning i en familieanamnese.
- detaljer om et vitne til et epileptisk anfall.

Denne arketypen kan også brukes som en midlertidig erstatning for formelle demografiske data når man gjennomgår et templat med domeneeksperter - for eksempel en vurdering der man i templatet forventer å se detaljer om et individ på toppen av vurderingsskjema.

I tilfeller der arketypen CLUSTER.structured_name er lagt i SLOTet \"Strukturert navn\", kan noen eller alle de strukturerte navnelementene kobles sammen og vises i dataelementet \"Navn\".">
			keywords = <"organisasjon, tjenesteyter, helsepersonell, omorgsperson", ...>
			misuse = <"Brukes ikke til å representere eller erstatte de formelle demografiske detaljene for formell identitetsforvaltning eller for å opprettholde et demografisk register eller en indeks. Bruk formelle pasientregister, helsepersonellregister eller arketyper basert på openEHR demografisk informasjonsmodell til dette formålet.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about a person who needs to be represented within the health record.">
			use = <"Use to record details about a person who needs to be represented within the health record. Examples include:
- the name and contact details for a person who holds a physical copy of an advanced care record;
- the name and contact details of a carer in a referral; 
- the name, role and contact details of a member of a care team;
- the role and contact details of a named contact person within an organisation;
- details about a relative in a family history record; or
- a witness to an epileptic fit.

This archetype could also be used as a proxy for formal demographic data when reviewing a template with domain experts - for example, an assessment where reviewers would expect to see the individual's details at the top of the assessment form.

If the CLUSTER.structured_name archetype is nested within the 'Structured name' SLOT, any or all of the structured name elements can be concatenated and represented in the 'Name line' element.">
			keywords = <"organisation, provider, carer", ...>
			misuse = <"Not to be used to represent or replace the formal demographic details for formal identification management or for the purposes of maintaining a demographic register or index. Use a formal Master Patient Index or Healthcare Provider Index for this purpose, or archetypes based on the openEHR Demographic Information Model.">
			copyright = <"© openEHR Foundation">
		>
		["zh"] = <
			language = <[ISO_639-1::zh]>
			purpose = <"记录需要出现在健康记录中的人的详细信息。">
			use = <"用于记录需要出现在健康记录中的人的详细信息。包括：
- 拥有更高级的护理记录的人的姓名和联系信息；
- 转院过程中护士的姓名和联系信息；
- 护理组成员的角色，姓名和联系信息；
- 家族史中某位亲戚的详细信息；或者
- 癫痫发作的目击者。

这个原型也可以在同领域专家一起审查模板时用作正式人口统计学数据的代理。比如，审查者期望在评估表单的表头看到个人信息。

如果原型 CLUSTER.structured_name 嵌套在”Structured name“ SLOT 中，任何或所有结构化的姓名组成部分都可以拼接并在“Name line”中表示。">
			keywords = <"组织机构, 提供者, 护理者(zh)", ...>
			misuse = <"不得代表或替换正式人口统计详细信息进行正式身份管理或维护人口登记或索引。为此目的，请使用正式的主患者索引或医疗保健提供者索引或基于 openEHR Demographic Information Model 的原型。">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"AS 4846-2006 Health Care Provider Identification. Standards Australia.

AS 5017-2006 Health Care Client Identification. Standards Australia.

HL7 FHIR Resource - Person v4.0.1: R4 [Internet]. Health Level Seven International; [accessed 2021 05 04]. Available from: http://hl7.org/fhir/2021May/person.html

ISO/TS 22220:2007 Health informatics — Identification of subjects of health care. International Organization for Standardization.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"88A81FA5B94E7CB0774CA09EA8AA31B2">
		["build_uid"] = <"e6363ebb-5ad1-40fc-a1d9-8692a27c40ac">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {    -- Person
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {    -- Name line
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0002] occurrences matches {0..*} matches {    -- Structured name
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.structured_name\.v0/}
			}
			ELEMENT[at0003] occurrences matches {0..*} matches {    -- Identifier
				value matches {
					DV_IDENTIFIER matches {*}
				}
			}
			ELEMENT[at0004] occurrences matches {0..*} matches {    -- Role
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {    -- Address
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.address\.v0/}
			}
			CLUSTER[at0006] occurrences matches {0..*} matches {    -- Electronic communication
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0007] occurrences matches {0..1} matches {    -- Medium
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0008,    -- Mobile (cellular) telephone
									at0009,    -- Telephone (excluding mobile telephone)
									at0010,    -- Email
									at0011]    -- Pager
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0012] occurrences matches {0..1} matches {    -- Type
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0013,    -- Business use
									at0014,    -- Personal use
									at0015]    -- Both business and personal use
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0016] matches {    -- Value
						value matches {
							DV_TEXT matches {*}
							DV_URI matches {*}
						}
					}
				}
			}
			allow_archetype CLUSTER[at0017] occurrences matches {0..*} matches {    -- Additional details
				include
					archetype_id/value matches {/.*/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Person">
					description = <"A person who needs to be represented within the health record.">
				>
				["at0003"] = <
					text = <"Identifier">
					description = <"Identifier associated with the identified person.">
				>
				["at0005"] = <
					text = <"Address">
					description = <"Details about a specific address for the person.">
				>
				["at0017"] = <
					text = <"Additional details">
					description = <"Additional details about the person.">
				>
				["at0004"] = <
					text = <"Role">
					description = <"The role or capacity in which the identified person contributes to the health or social care of the subject of care.">
				>
				["at0001"] = <
					text = <"Name line">
					description = <"The name by which an individual is known as free text or a text composite of one or more structured name components.">
				>
				["at0002"] = <
					text = <"Structured name">
					description = <"Additional structured details about the name of a person that can be concatenated to form the 'Name line' entry.">
				>
				["at0007"] = <
					text = <"Medium">
					description = <"The type of electronic communication or channel.">
					comment = <"The scope of electronic communication medium includes, but is not limited to use of : a telephone, cellular phone, computer, or pager.">
				>
				["at0008"] = <
					text = <"Mobile (cellular) telephone">
					description = <"">
				>
				["at0009"] = <
					text = <"Telephone (excluding mobile telephone)">
					description = <"">
				>
				["at0010"] = <
					text = <"Email">
					description = <"">
				>
				["at0011"] = <
					text = <"Pager">
					description = <"">
				>
				["at0012"] = <
					text = <"Type">
					description = <"The purpose or use for the identified medium.">
					comment = <"Coding with an external terminology is preferred, where possible.">
				>
				["at0013"] = <
					text = <"Business use">
					description = <"">
				>
				["at0014"] = <
					text = <"Personal use">
					description = <"">
				>
				["at0015"] = <
					text = <"Both business and personal use">
					description = <"">
				>
				["at0016"] = <
					text = <"Value">
					description = <"The unique combination of alphanumeric characters, meaningful for the identified medium, for contacting the person.">
					comment = <"For example: area code + landline phone/pager number; country code + mobile phone number; email address; or skype contact name.">
				>
				["at0006"] = <
					text = <"Electronic communication">
					description = <"Details about a specific type of electronic communication for the person.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Person">
					description = <"En person som trengs å bli representert i en pasientjournal.">
				>
				["at0001"] = <
					text = <"Navn">
					description = <"Navnet til et individ som fritekst eller som en sammenstilling av en eller flere strukturerte navnekomponenter.">
				>
				["at0002"] = <
					text = <"Strukturert navn">
					description = <"Ytterligere strukturerte detaljer om navnet på en person, som kan kobles sammen til ett felt i dataelementet \"Navn\".">
				>
				["at0003"] = <
					text = <"Identifikator">
					description = <"Personidentifikator for den aktuelle personen.">
				>
				["at0004"] = <
					text = <"Rolle">
					description = <"Individets rolle i bidrag til helse eller omsorg for pasienten.">
				>
				["at0005"] = <
					text = <"Adresse">
					description = <"Detaljer om personens adresse.">
				>
				["at0006"] = <
					text = <"Elektronisk kommunikasjon">
					description = <"Detaljer om type elektronisk kommunikasjon.">
				>
				["at0007"] = <
					text = <"Medie">
					description = <"Type elektronisk kommunikasjon eller kommunikasjonskanal.">
					comment = <"For eksempel: telefon, datamaskin eller personsøker.">
				>
				["at0008"] = <
					text = <"Mobiltelefon">
					description = <"">
				>
				["at0009"] = <
					text = <"Telefon (ikke mobiltelefon)">
					description = <"">
				>
				["at0010"] = <
					text = <"E-post">
					description = <"">
				>
				["at0011"] = <
					text = <"Personsøker">
					description = <"">
				>
				["at0012"] = <
					text = <"Type">
					description = <"Det brukte mediets formål eller bruk.">
					comment = <"Koding med ekstern terminologi er ønskelig om mulig.">
				>
				["at0013"] = <
					text = <"Arbeid">
					description = <"">
				>
				["at0014"] = <
					text = <"Privat">
					description = <"">
				>
				["at0015"] = <
					text = <"Både arbeid og privat">
					description = <"">
				>
				["at0016"] = <
					text = <"Verdi">
					description = <"En kombinasjon av alfanumeriske tegn som utgjør en unik adresse for å kontakte personen i det valgte mediet.">
					comment = <"For eksempel fasttelefonnummer, landskode og mobiltelefonnummer, e-postadresse eller skype kontaktnavn.">
				>
				["at0017"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere detaljer om personen.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Person">
					description = <"Eine Person, die in der Gesundheitsakte repräsentiert sein muss.">
				>
				["at0001"] = <
					text = <"Namenszeile">
					description = <"Der Name, unter dem eine Person bekannt ist. Es ist ein freier Text oder eine Textzusammensetzung aus einer oder mehreren strukturierten Namenskomponenten.">
				>
				["at0002"] = <
					text = <"Name strukturiert">
					description = <"Zusätzliche strukturierte Angaben zum Namen einer Person, die zum Eintrag „Namenszeile“ verkettet werden können.">
				>
				["at0003"] = <
					text = <"Identifier">
					description = <"Identifikator, der der identifizierten Person zugeordnet ist.">
				>
				["at0004"] = <
					text = <"Rolle">
					description = <"Die Rolle oder Funktion, in der die identifizierte Person zur Gesundheits- oder Sozialversorgung des Patienten beiträgt.">
				>
				["at0005"] = <
					text = <"Adresse">
					description = <"Details zu einer bestimmten Adresse der Person.">
				>
				["at0006"] = <
					text = <"Elektronische Kommunikation">
					description = <"Angaben zu einer bestimmten Art der elektronischen Kommunikation für die Person.">
				>
				["at0007"] = <
					text = <"Medium">
					description = <"Die Art der elektronischen Kommunikation oder des Kommunikationswegs.">
					comment = <"Der Umfang des elektronischen Kommunikationsmediums umfasst, ist aber nicht beschränkt auf die Verwendung von: einem Telefon, Mobiltelefon, Computer oder Pager.">
				>
				["at0008"] = <
					text = <"Mobiltelefon">
					description = <"">
				>
				["at0009"] = <
					text = <"Telefon (ohne Mobiltelefon)">
					description = <"">
				>
				["at0010"] = <
					text = <"E-Mail">
					description = <"">
				>
				["at0011"] = <
					text = <"Pager">
					description = <"">
				>
				["at0012"] = <
					text = <"Art">
					description = <"Der Zweck oder die Verwendung für das identifizierte Medium.">
					comment = <"Wenn möglich, wird die Codierung mit einer externen Terminologie bevorzugt.">
				>
				["at0013"] = <
					text = <"Geschäftlich">
					description = <"">
				>
				["at0014"] = <
					text = <"Privat">
					description = <"">
				>
				["at0015"] = <
					text = <"Geschäftlich und privat">
					description = <"">
				>
				["at0016"] = <
					text = <"Daten">
					description = <"Die eindeutige Kombination von alphanumerischen Zeichen, die für das identifizierte Medium sinnvoll sind, um die Person zu kontaktieren.">
					comment = <"Zum Beispiel: Vorwahl + Festnetz-Nummer; Ländervorwahl + Mobilnummer; E-Mail-Addresse oder Skype-Kontaktname.">
				>
				["at0017"] = <
					text = <"Zusätzliche Details">
					description = <"Weitere Angaben zur Person.">
				>
			>
		>
		["zh"] = <
			items = <
				["at0000"] = <
					text = <"人">
					description = <"需要出现在健康记录中的人">
				>
				["at0001"] = <
					text = <"姓名行">
					description = <"用于代表个体的自由文本或一个或多个结构化姓名组成部分的文本组合。">
				>
				["at0002"] = <
					text = <"结构化姓名">
					description = <"可用于拼接形成“Name line”的姓名的其他结构化详细信息。">
				>
				["at0003"] = <
					text = <"标识符">
					description = <"如果该人员已被标识，与其相关联的标识符。">
				>
				["at0004"] = <
					text = <"角色">
					description = <"被标识人员在对象健康护理或社会护理活动中的角色或能力。">
				>
				["at0005"] = <
					text = <"地址">
					description = <"有关此人特定地址的详细信息。">
				>
				["at0006"] = <
					text = <"电子通讯">
					description = <"有关此人特定类型电子通讯的详细信息。">
				>
				["at0007"] = <
					text = <"媒介">
					description = <"电子通讯或渠道的类型。">
					comment = <"电子通讯媒介的范围包括但不限于：电话、移动电话、计算机或寻呼机。">
				>
				["at0008"] = <
					text = <"移动（蜂窝）电话">
					description = <"">
				>
				["at0009"] = <
					text = <"电话（不包括移动电话）">
					description = <"">
				>
				["at0010"] = <
					text = <"邮件">
					description = <"">
				>
				["at0011"] = <
					text = <"寻呼机">
					description = <"">
				>
				["at0012"] = <
					text = <"类型">
					description = <"使用该媒介的目的或用途。">
					comment = <"在可能的情况下，最好使用外部术语进行编码。">
				>
				["at0013"] = <
					text = <"商用">
					description = <"">
				>
				["at0014"] = <
					text = <"私人">
					description = <"">
				>
				["at0015"] = <
					text = <"商用和私人">
					description = <"">
				>
				["at0016"] = <
					text = <"值">
					description = <"对识别该媒介有意义的字母数字字符的独特组合，用于联系此人。">
					comment = <"例如：区号+座机电话/寻呼机号码；国家代码+手机号码；电子邮件地址;或 Skype 联系人姓名。">
				>
				["at0017"] = <
					text = <"其他详细信息">
					description = <"有关此人的其他详细信息。">
				>
			>
		>
	>

archetype (adl_version=1.4; uid=6b5517ef-08cb-4e3b-bf0d-7fd40db28f77)
	openEHR-EHR-CLUSTER.exam_wound.v0

concept
	[at0000]	-- Examination of a wound
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2015-06-25">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording a narrative description and clinical interpretation of the findings observed during the physical examination of an identified wound.">
			use = <"Use to record a narrative description of the overall findings observed during the physical examination of an identified wound. This initial draft archetype consists only of the core examination pattern data elements (as per CLUSTER.exam_pattern) and will likely be expanded further over time as specific clinical requirements are identified.

This archetype has been specifically designed to be used in the 'Examination findings' SLOT within many of the CLUSTER archetypes for examination of various regions or anatomical structures - for example, CLUSTER.exam_abdomen or CLUSTER.exam_breast - but can also be used within the OBSERVATION.exam.

Use to provide a framework in which CLUSTER archetypes can be nested in the 'Examination findings' SLOT to record additional structured physical examination findings. 

The CLUSTER.exclusion_exam archetype can be nested within the 'Examination not done' SLOT to optionally record explicit details about the examination not being performed.

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical Description' data element.">
			misuse = <"Not to be used for recording data not related to the physical examination of an identified wound.

Not to be used for recording the clinical history - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom.

Not to be used to record stand-alone clinical observations or measurements or test results - use specific OBSERVATION archetypes. For example OBSERVATION.blood_pressure or OBSERVATION.imaging_exam.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Bergen Hospital Trust, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "John Tore Valand, Haukeland Universitetssjukehus, Norway", "Heather Leslie, Ocean Informatics, Australia">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics<heather.leslie@oceaninformatics.com>">
		["revision"] = <"0.0.1-alpha">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["build_uid"] = <"4982a91c-4c6f-4139-baa0-45cd841fdd22">
		["original_publisher"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"490B3D1B92F202F907479349C9B2EE2E">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	CLUSTER[at0000] matches {	-- Examination of a wound
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- Wound type
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0012] occurrences matches {0..1} matches {	-- Alias
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Body site
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0011] occurrences matches {0..*} matches {	-- Structured body site
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_clock(-[a-zA-Z0-9_]+)*\.v0/}
			}
			allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Examination findings
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.dimensions(-[a-zA-Z0-9_]+)*\.v1/}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Multimedia representation
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {	-- Clinical interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0008] occurrences matches {0..*} matches {	-- Examination not done
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination of a wound">
					description = <"Findings observed during the physical examination of an identified wound.">
				>
				["at0001"] = <
					text = <"Body site">
					description = <"Identification of the body site under examination.">
					comment = <"The body site could be on an area of skin or a mucous membrane, for example the right knee, palate or duodenum. If the body site has previously been identified in another archetype in which this archetype is nested, this data element becomes redundant.">
				>
				["at0002"] = <
					text = <"Wound type">
					description = <"Identification of the type of wound found on physical examination.">
					comment = <"For example, abrasion, laceration, or ulcer. ">
				>
				["at0003"] = <
					text = <"Clinical description">
					description = <"Narrative description of the overall findings observed during the physical examination.">
				>
				["at0004"] = <
					text = <"Examination findings">
					description = <"Structured details about the physical examination findings.">
				>
				["at0005"] = <
					text = <"Multimedia representation">
					description = <"Digital image, video or diagram representing the physical examination findings.">
				>
				["at0006"] = <
					text = <"Clinical interpretation">
					description = <"Single word, phrase or brief description that represents the clinical meaning and significance of the physical examination findings.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the physical examination findings, not captured in other fields.">
				>
				["at0008"] = <
					text = <"Examination not done">
					description = <"Details to explicitly record that this examination was not performed.">
					comment = <"'Wound' should be coded with a terminology, where possible.">
				>
				["at0011"] = <
					text = <"Structured body site">
					description = <"A structured anatomical location of the finding.">
					comment = <"If the body site has previously been identified in another archetype in which this archetype is then nested, this SLOT becomes redundant.">
				>
				["at0012"] = <
					text = <"Alias">
					description = <"Identification, by using a alias name, of the wound to distinguish one wound from similar wounds at the same body site.">
					comment = <"For example, 'Abrasion #1' as marked on a diagram of the body site.">
				>
			>
		>
	>

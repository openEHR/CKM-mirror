archetype (adl_version=1.4; uid=1d983fb8-0828-46e7-989e-9bddddade035)
	openEHR-EHR-CLUSTER.imaging_exam-uterine_body.v0
specialise
	openEHR-EHR-CLUSTER.imaging_exam.v0

concept
	[at0000.1]	-- Imaging examination of the uterine body
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Peter Fedorcsak, Vebjørn Arntzen, Marit Alice Venheim">
				["organisation"] = <"University of Oslo, Oslo University Hospital, Helse Vest IKT">
				["email"] = <"peterfe@uio.no, varntzen@ous-hf.no, marit.alice.venheim@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2021-11-24">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere en fritekstbeskrivelse, strukturerte detaljer og klinisk tolkning av observerte funn ved bildeundersøkelse av livmoren.">
			use = <"Brukes til å registrere fritekstbeskrivelse, strukturerte detaljer og klinisk tolkning av funn ved bildeundersøkelse av livmoren. Denne arketypen omhandler ikke detaljert informasjon om livmorhalsen. Imidlertid kan arketypen CLUSTER.imaging_exam_cervix nøstes inn i denne i SLOT'et \"Strukturerte detaljer\" dersom det er nødvendig.

Denne arketypen ble utviklet til bruk i SLOT'et \"Strukturerte funn\" i arketypen OBSERVATION.imaging_exam_result (Bildediagnostisk svar), men kan også brukes innen andre ENTRY- og CLUSTER-arketyper der det er klinisk passende.

Brukes som et rammeverk hvor andre CLUSTER arketyper kan nøstes i SLOT'et \"Strukturerte detaljer\" for å registrere ytterligere strukturerte bildefunn for delstrukturer av eggstokk eller andre relevante funn.">
			misuse = <"Skal ikke brukes til å registrere funn utenfor det spesifiserte organ eller kroppsdel. Bruk andre arketyper fra gruppen av CLUSTER.imaging_exam-arketyper for dette.

Skal ikke brukes til å registrere funn av livmorhalsen. Bruk CLUSTER.imaging_exam_cervix til dette.

Skal ikke brukes til å registrere overordnede undersøkelsesfunn, kontekst eller tekniske detaljer relatert til bildeundersøkelsen. Bruk arketypen OBSERVATION.imaging_exam_result (Bildediagnostisk svar) for å registrere for eksempel kommentar om undersøkelseskvalitet, differensialdiagnose og overordnet konklusjon.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record a narrative description, structured details and clinical impression of the findings observed in an imaging examination targeting the uterine body.">
			use = <"Use to record a narrative description, structured details and clinical impression of the findings observed in an imaging examination targeting the uterine body. The scope of this archetype does not include detailed information about the uterine cervix, however CLUSTER.imaging_exam-cervix may be nested within the 'Additional details' SLOT in this archetype as necessary.

This archetype has been designed to be used in the 'Structured imaging findings' SLOT within the OBSERVATION.imaging_exam_result archetype, but can also be used within other ENTRY or CLUSTER archetypes, where clinically appropriate. 

Use to provide a framework in which CLUSTER archetypes can be nested in the 'Additional details' SLOT within this archetype to record additional nested levels of structured imaging findings for related body substructures, pregnancy findings, details about structural abnormalities such as a bicornuate uterus, morphological abnormalities such as a myometrial mass or intracavity lesion, or an adnexal mass.">
			misuse = <"Not to be used to record findings outside the specified body structure or region. Use other archetypes from the CLUSTER.imaging_exam family for this purpose.

Not to be used to record findings of the uterine cervix. Use CLUSTER.imaging_exam-cervix for this purpose.

Not to be used to record details related to the overall findings, context or technical details related to a complete imaging examination test result. Use the OBERSERVATION.imaging_exam_result for this purpose. For example, comments on the study quality, differential diagnoses, and overall impression.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital", "Liv Laugen, Oslo University Hospital", "Peter Fedorcsak, Oslo University Hospital">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Abuhamad A, Chaoui R, Jeanty P, Paladini D, Walsh E. Ultrasound in Obstetrics and Gynecology: A Practical Approach [Internet]. The Global Library of Women’s Medicine; 2014 [cited 2021 Dec 21]. 322 p. Available from: https://www.glowm.com/pdf/Ultrasound_in_obstetrics_and_gynecology-chapter11.pdf.

AIUM Practice Parameter for the Performance of an Ultrasound Examination of the Female Pelvis. J Ultrasound Med. 2020 May;39(5):E17-E23. doi: 10.1002/jum.15205. Epub 2020 Mar 9. PMID: 32150295. Available from: https://www.aium.org/resources/guidelines/femalePelvis.pdf.

American Institute of Ultrasound in Medicine; Society for Reproductive Endocrinology and Infertility; American Society of Reproductive Medicine. AIUM practice guideline for ultrasonography in reproductive medicine. J Ultrasound Med. 2009 Jan;28(1):128-37. PMID: 19106372. Available from: https://www.aium.org/resources/guidelines/reproductiveMed.pdf.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"928F9646FC76DB96676AA7CA93EFE41F">
		["build_uid"] = <"e41ec7a7-fa78-4bdd-b0d6-16af779a884c">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact https://www.snomed.org/snomed-ct/get-snomed or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000.1] matches {    -- Imaging examination of the uterine body
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001.1] matches {    -- Body structure
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::at0.1]    -- Uterine body
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {    -- Anatomical location
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0003] occurrences matches {0..*} matches {    -- Structured anatomical location
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v2|openEHR-EHR-CLUSTER\.anatomical_location_circle(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0004] occurrences matches {0..1} matches {    -- Imaging findings
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {    -- Additional details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.imaging_finding(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-cervix(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-gestational_sac(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-fetus(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-lymph_node_group(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-abnormality-adnexal_mass(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-abnormality(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0006] occurrences matches {0..1} matches {    -- Impression
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {    -- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.13] occurrences matches {0..1} matches {    -- Length
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::122]>
						list = <
							["1"] = <
								units = <"mm">
								magnitude = <|>=0.0|>
								precision = <|0|>
							>
							["2"] = <
								units = <"cm">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
						>
					>
				}
			}
			ELEMENT[at0.14] occurrences matches {0..1} matches {    -- Width
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::122]>
						list = <
							["1"] = <
								units = <"mm">
								magnitude = <|>=0.0|>
								precision = <|0|>
							>
							["2"] = <
								units = <"cm">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
						>
					>
				}
			}
			ELEMENT[at0.15] occurrences matches {0..1} matches {    -- Depth
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::122]>
						list = <
							["1"] = <
								units = <"mm">
								magnitude = <|>=0.0|>
								precision = <|0|>
							>
							["2"] = <
								units = <"cm">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
						>
					>
				}
			}
			ELEMENT[at0.16] occurrences matches {0..1} matches {    -- Endometrium description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.17] occurrences matches {0..1} matches {    -- Endometrial thickness
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::122]>
						list = <
							["1"] = <
								units = <"mm">
								magnitude = <|>=0.0|>
								precision = <|0|>
							>
						>
					>
				}
			}
			ELEMENT[at0.18] occurrences matches {0..*} matches {    -- Endometrial texture
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.5] occurrences matches {0..1} matches {    -- Intrauterine pregnancy
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.6,    -- Present, viable
							at0.7,    -- Absent
							at0.8,    -- Indeterminate
							at0.12]    -- Present, non-viable
						}
					}
				}
			}
			ELEMENT[at0.2] occurrences matches {0..1} matches {    -- Number of gestations
				value matches {
					DV_COUNT matches {
						magnitude matches {|>=0|}
					}
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.10,    -- Single
							at0.11]    -- Multiple
						}
					}
				}
			}
			ELEMENT[at0.3] occurrences matches {0..1} matches {    -- Chorionicity
				value matches {
					DV_COUNT matches {
						magnitude matches {|>=0|}
					}
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.41,    -- Monochorionic
							at0.42]    -- Dichorionic
						}
					}
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.4] occurrences matches {0..1} matches {    -- Amnionicity
				value matches {
					DV_COUNT matches {*}
					DV_TEXT matches {*}
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.43,    -- Monoamnionic
							at0.44]    -- Diamnionic
						}
					}
				}
			}
			ELEMENT[at0.19] occurrences matches {0..1} matches {    -- Myometrium description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.20] occurrences matches {0..1} matches {    -- Myometrial walls
				value matches {
					DV_TEXT matches {*}
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.21,    -- Symmetrical
							at0.22]    -- Asymmetrical
						}
					}
				}
			}
			ELEMENT[at0.23] occurrences matches {0..1} matches {    -- Myometrial texture
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.24] occurrences matches {0..1} matches {    -- Junctional zone
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.37] occurrences matches {0..1} matches {    -- Junctional zone thickness
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::122]>
						list = <
							["1"] = <
								units = <"mm">
								magnitude = <|>=0.0|>
								precision = <|0|>
							>
						>
					>
				}
			}
			ELEMENT[at0.25] occurrences matches {0..1} matches {    -- Uterine axis
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.26,    -- Anteverted
							at0.27,    -- Axial
							at0.28]    -- Retroverted
						}
					}
				}
			}
			ELEMENT[at0.29] occurrences matches {0..1} matches {    -- Uterine flexion
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.30,    -- Anteflexed
							at0.31]    -- Retroflexed
						}
					}
				}
			}
			ELEMENT[at0.32] occurrences matches {0..1} matches {    -- IUD presence
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.33,    -- Present
							at0.34,    -- Absent
							at0.35]    -- Indeterminate
						}
					}
				}
			}
			ELEMENT[at0.36] occurrences matches {0..1} matches {    -- IUD description
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0.1"] = <
					text = <"Uterine body">
					description = <"The uterine body was examined.">
				>
				["at0.10"] = <
					text = <"Single">
					description = <"A single gestation is observed.">
				>
				["at0.11"] = <
					text = <"Multiple">
					description = <"More than one gestation is observed.">
				>
				["at0.12"] = <
					text = <"Present, non-viable">
					description = <"Intrauterine pregnancy is observed, with no associated cardiac activity.">
				>
				["at0.13"] = <
					text = <"Length">
					description = <"The length of the uterus.">
					comment = <"Usually measured in the midsagittal plane, from the outer serosal surface of the fundus to the external os of the cervix.">
				>
				["at0.14"] = <
					text = <"Width">
					description = <"The width of the uterus.">
					comment = <"Usually measured at the widest dimension in the transverse or coronal plane, perpendicular to the length.">
				>
				["at0.15"] = <
					text = <"Depth">
					description = <"The depth or height of the uterus.">
					comment = <"Usually measured in the sagittal plane, from the anterior to posterior walls, perpendicular to the length. Also known as 'antero-posterior diameter'.">
				>
				["at0.16"] = <
					text = <"Endometrium description">
					description = <"Narrative description about the endometrium.">
					comment = <"For example: thickness, focal abnormalities, echogenicity, and the presence and characteristics of fluid or masses in the cavity. Record structured details about any morphological abnormality using CLUSTER.imaging_exam-abnormality nested within the 'Additional details' SLOT.">
				>
				["at0.17"] = <
					text = <"Endometrial thickness">
					description = <"Maximal measured thickness of the endometrium.">
					comment = <"Also known as the endometrial stripe.">
				>
				["at0.18"] = <
					text = <"Endometrial texture">
					description = <"Description about the endometrial texture.">
					comment = <"For example: homogeneous or heterogeneous; or describing ultrasound echogenicity as hypoechoic, isoechoic, or hyperechoic.">
				>
				["at0.19"] = <
					text = <"Myometrium description">
					description = <"Narrative description about the myometrium.">
					comment = <"For example: contour changes, texture, masses, and cysts as well as symmetry between the anterior and posterior segments of the myometrium. Record structured details about any morphological abnormality using CLUSTER.imaging_exam-abnormality nested within the 'Additional details' SLOT.">
				>
				["at0.2"] = <
					text = <"Number of gestations">
					description = <"Number of gestations observed.">
				>
				["at0.20"] = <
					text = <"Myometrial walls">
					description = <"Description of the relative thickness of the endometrial walls.">
				>
				["at0.21"] = <
					text = <"Symmetrical">
					description = <"The thickness of the myometrial walls are symmetrical.">
				>
				["at0.22"] = <
					text = <"Asymmetrical">
					description = <"The thickness of the myometrial walls are not symmetrical.">
				>
				["at0.23"] = <
					text = <"Myometrial texture">
					description = <"Description about the myometrial texture.">
					comment = <"For example: homogeneous or heterogeneous.">
				>
				["at0.24"] = <
					text = <"Junctional zone">
					description = <"Description about the junctional zone of the myometrium.">
					comment = <"For example: regular, interrupted, or not visible.">
				>
				["at0.25"] = <
					text = <"Uterine axis">
					description = <"The axis of the cervix relative to the vagina.">
					comment = <"Also known as 'position'.">
				>
				["at0.26"] = <
					text = <"Anteverted">
					description = <"The vagina-cervix angle faces anteriorly.">
				>
				["at0.27"] = <
					text = <"Axial">
					description = <"The cervix is in the same axis as the vagina.">
				>
				["at0.28"] = <
					text = <"Retroverted">
					description = <"The vagina-cervix angle faces posteriorly.">
				>
				["at0.29"] = <
					text = <"Uterine flexion">
					description = <"The axis of the uterine fundus relative to the axis of the cervix.">
				>
				["at0.3"] = <
					text = <"Chorionicity">
					description = <"The number of chorionic (outer) membranes or placentas observed in a multiple pregnancy.">
				>
				["at0.30"] = <
					text = <"Anteflexed">
					description = <"The cervix-body angle faces anteriorly.">
				>
				["at0.31"] = <
					text = <"Retroflexed">
					description = <"The cervix-body angle faces posteriorly.">
				>
				["at0.32"] = <
					text = <"IUD presence">
					description = <"The presence or absence of an intrauterine contraceptive device.">
				>
				["at0.33"] = <
					text = <"Present">
					description = <"An IUD is observed in the uterus.">
				>
				["at0.34"] = <
					text = <"Absent">
					description = <"No IUD is observed in the uterus.">
				>
				["at0.35"] = <
					text = <"Indeterminate">
					description = <"It is not possible to determine if an IUD is present or absent.">
				>
				["at0.36"] = <
					text = <"IUD description">
					description = <"Narrative description about an observed IUD in the uterus.">
				>
				["at0.37"] = <
					text = <"Junctional zone thickness">
					description = <"Measured thickness of the junctional zone.">
				>
				["at0.4"] = <
					text = <"Amnionicity">
					description = <"The number of amnionic sacks (inner membranes) observed in a multiple pregnancy.">
				>
				["at0.41"] = <
					text = <"Monochorionic">
					description = <"One chorion or placenta is observed.">
				>
				["at0.42"] = <
					text = <"Dichorionic">
					description = <"Two chorions or placentae are observed.">
				>
				["at0.43"] = <
					text = <"Monoamnionic">
					description = <"One amniotic sac is observed.">
				>
				["at0.44"] = <
					text = <"Diamnionic">
					description = <"Two amniotic sacs are observed.">
				>
				["at0.5"] = <
					text = <"Intrauterine pregnancy">
					description = <"The presence or absence of an intrauterine pregnancy.">
				>
				["at0.6"] = <
					text = <"Present, viable">
					description = <"Intrauterine pregnancy is observed, with associated cardiac activity.">
				>
				["at0.7"] = <
					text = <"Absent">
					description = <"No intrauterine pregnancy is observed.">
				>
				["at0.8"] = <
					text = <"Indeterminate">
					description = <"It is not possible to determine if an intrauterine pregnancy is present of absent.">
				>
				["at0000.1"] = <
					text = <"Imaging examination of the uterine body">
					description = <"Findings observed in an imaging examination targeting the uterine body, using radiographic techniques.">
				>
				["at0001.1"] = <
					text = <"Body structure">
					description = <"Identification of the body structure or region examined.">
				>
				["at0005"] = <
					text = <"Additional details">
					description = <"Additional structured details about imaging findings for the identified body structure or region, or findings for related substructures.">
				>
				["at0000"] = <
					text = <"Imaging examination">
					description = <"Findings observed in an imaging examination targeting a specified structure or region, using radiographic techniques.">
				>
				["at0004"] = <
					text = <"Imaging findings">
					description = <"Narrative description of the imaging findings observed during this examination.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the imaging findings, not captured in other fields.">
				>
				["at0001"] = <
					text = <"Body structure">
					description = <"Identification of the body structure or region examined.">
				>
				["at0003"] = <
					text = <"Structured anatomical location">
					description = <"Structured details about the anatomical location of the identified body structure.">
					comment = <"If the anatomical location of the identified body structure is known or it has been identified in the 'Anatomical location' data element, this data element is redundant.">
				>
				["at0006"] = <
					text = <"Impression">
					description = <"Narrative concise, clinically relevant impression of all imaging findings for the identified body structure.">
					comment = <"Also referred to as 'Opinion'. May include a comparison with previous studies of the identified body structure, where appropriate.">
				>
				["at0002"] = <
					text = <"Anatomical location">
					description = <"Simple body site for the identified structure.">
					comment = <"If the anatomical location of the identified body structure is known, this data element is redundant.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0.1"] = <
					text = <"Livmoren">
					description = <"Undersøkelse av livmoren.">
				>
				["at0.10"] = <
					text = <"Enkel">
					description = <"En enkelt graviditet er sett.">
				>
				["at0.11"] = <
					text = <"Flerling">
					description = <"Mer enn en graviditet er sett.">
				>
				["at0.12"] = <
					text = <"Tilstede, ikke-levende">
					description = <"Intrauterint svangerskap uten observert hjerteaktivitet.">
				>
				["at0.13"] = <
					text = <"Lengde">
					description = <"Livmorens lengde.">
					comment = <"Som regel måles i midsagittalt, langsgående snitt fra den ytre serosaoverflaten ved fundus til den ytre livmormunnen.">
				>
				["at0.14"] = <
					text = <"Bredde">
					description = <"Livmorens bredde.">
					comment = <"Som regel måles ved den bredeste utstrekningen i koronal eller tverrsnitt, loddrett over lengden.">
				>
				["at0.15"] = <
					text = <"Dybde">
					description = <"Livmorens dybde eller høyde.">
					comment = <"Måle som regel i sagittalt snitt fra fremre vegg til bakre vegg, loddrett over lengden. Kjent også som antero-posterior diameter.">
				>
				["at0.16"] = <
					text = <"Beskrivelse av endometrium">
					description = <"Fritekstbeskrivelse av endometriet.">
					comment = <"Eksempelvis tykkelse, fokale avvik, ekkomønster, tilstedeværelse og egenskaper av væske eller masser i kaviteten. Bruk elementet CLUSTER.imaging_exam-abnormality nøstet i SLOT'et \"Strukturerte detaljer\" for å registrere strukturerte detaljer.">
				>
				["at0.17"] = <
					text = <"Endometrietykkelse">
					description = <"Den største tykkelsen av endometriet.">
					comment = <"Kjent også som endometriestripe.">
				>
				["at0.18"] = <
					text = <"Endometriemønster">
					description = <"Beskrivelse av endometriemønster.">
					comment = <"Eksempelvis homogen eller heterogen, eller ekkomønster som lavekkoisk, isoekkoisk eller hyperekkoisk.">
				>
				["at0.19"] = <
					text = <"Beskrivelse av myometrium">
					description = <"Fritekstbeskrivelse av myometriet.">
					comment = <"Eksempelvis konturendringer, mønster, masser og cyster, samt symmetri av fremre og bakre områder. Bruk elementet CLUSTER.imaging_exam-abnormality nøstet i SLOT'et \"Strukturerte detaljer\" for å registrere dette.">
				>
				["at0.2"] = <
					text = <"Antall graviditeter">
					description = <"Antall graviditeter sett.">
				>
				["at0.20"] = <
					text = <"Myometriumvegg">
					description = <"*Beskrivelse av relativ tykkelse for myometriumvegg.">
				>
				["at0.21"] = <
					text = <"Symmetrisk">
					description = <"Myometriumveggene har symmetrisk tykkelse.">
				>
				["at0.22"] = <
					text = <"Asymmetrisk">
					description = <"Myometriumveggene har ikke symmetrisk tykkelse.">
				>
				["at0.23"] = <
					text = <"*Myometrium mønster Myometrium tekstur">
					description = <"Beskrivelse av signalmønster over myometrium.">
					comment = <"Eksempelvis homogen eller heterogen.">
				>
				["at0.24"] = <
					text = <"Overgangssone">
					description = <"Beskrivelse av overgangssone.">
					comment = <"Eksempelvis regelmessig, avbrutt eller ikke synlig.">
				>
				["at0.25"] = <
					text = <"Livmorens akse">
					description = <"Livmorhalsens akse i forhold til skjedens akse.">
					comment = <"Kjent også som posisjon.">
				>
				["at0.26"] = <
					text = <"Antevertert">
					description = <"Vinkel mellom skjedens og livmorhalsens akse peker fremover.">
				>
				["at0.27"] = <
					text = <"Aksial">
					description = <"Livmorshalsen og skjedens akse er parallelle.">
				>
				["at0.28"] = <
					text = <"Retrovertert">
					description = <"Vinkel mellom skjedens og livmorhalsens akse peker bakover.">
				>
				["at0.29"] = <
					text = <"Livmorens fleksjon">
					description = <"Fundus uteri akse i forhold til livmorhalsens akse.">
				>
				["at0.3"] = <
					text = <"Korionisitet">
					description = <"Antall chorion eller morkaker sett i flerlingsvangerskap.">
				>
				["at0.30"] = <
					text = <"Anteflektert">
					description = <"Livmorkroppens akse peker fremover i forhold til livmorhalsens akse.">
				>
				["at0.31"] = <
					text = <"Retroflektert">
					description = <"Vinkelen mellom livmorkroppens akse og livmorhalsens akse peker bakover.">
				>
				["at0.32"] = <
					text = <"Spiral">
					description = <"Tilstedeværelse eller fravær av intrauterin spiral.">
				>
				["at0.33"] = <
					text = <"Tilstede">
					description = <"Intrauterin spiral er tilstede.">
				>
				["at0.34"] = <
					text = <"Fraværende">
					description = <"Intrauterin spiral er ikke tilstede.">
				>
				["at0.35"] = <
					text = <"Ubestemmelig">
					description = <"Det er ikke mulig å bestemme om intrauterin spiral er tilstede eller ikke.">
				>
				["at0.36"] = <
					text = <"Beskrivelse av spiral">
					description = <"Fritekstbeskrivelse av intrauterin spiral.">
				>
				["at0.37"] = <
					text = <"Tykkelse av overgangssone">
					description = <"Overgangssonens tykkelse.">
				>
				["at0.4"] = <
					text = <"Amnionisitet">
					description = <"Antall amnionsekk sett i flerlingsvangerskap.">
				>
				["at0.41"] = <
					text = <"Monochorion">
					description = <"En chorion eller morkake er sett.">
				>
				["at0.42"] = <
					text = <"Dichorion">
					description = <"To chorion eller morkaker er sett.">
				>
				["at0.43"] = <
					text = <"Monoamnionisk">
					description = <"En amniosekk er sett.">
				>
				["at0.44"] = <
					text = <"Diamnionisk">
					description = <"To amnionsekker er sett.">
				>
				["at0.5"] = <
					text = <"Intrauterint svangerskap">
					description = <"Tilstedeværelse eller fravær av et intrauterint svangerskap.">
				>
				["at0.6"] = <
					text = <"Tilstede, levende">
					description = <"Intrauterint svangerskap med observert hjerteaktivitet.">
				>
				["at0.7"] = <
					text = <"Fraværende">
					description = <"Intrauterint svangerskap er ikke sett.">
				>
				["at0.8"] = <
					text = <"Ubestemmelig">
					description = <"Det er ikke mulig å bestemme om et intrauterint svangerskap er tilstede eller fraværende.">
				>
				["at0000"] = <
					text = <"Bildeundersøkelse">
					description = <"Funn ved bildeundersøkelse rettet mot en spesifikk struktur eller region med bruk av radiologiske metoder.">
				>
				["at0000.1"] = <
					text = <"Bildeundersøkelse av livmoren">
					description = <"Funn ved bildeundersøkelse av livmoren med bruk av radiologiske metoder.">
				>
				["at0001"] = <
					text = <"Struktur">
					description = <"Identifisering av strukturen i kroppen eller regionen som er undersøkt.">
				>
				["at0001.1"] = <
					text = <"Struktur">
					description = <"Navn av undersøkt struktur eller område på eller i kroppen.">
				>
				["at0002"] = <
					text = <"Anatomisk lokalisasjon">
					description = <"Enkel beskrivelse av den anatomiske lokaliseringen av strukturen i kroppen eller regionen.">
					comment = <"Dersom den anatomiske lokaliseringen av den identifiserte strukturen i kroppen er kjent, er dette dataelementet overflødig.">
				>
				["at0003"] = <
					text = <"Strukturert anatomisk lokalisasjon">
					description = <"Strukturerte detaljer om den anatomiske lokaliseringen av strukturen i kroppen eller regionen.">
					comment = <"Dersom den anatomiske lokaliseringen av den identifiserte strukturen i kroppen er kjent, eller er identifisert av dataelementet \"Anatomisk lokalisasjon\" er dette dataelementet overflødig.">
				>
				["at0004"] = <
					text = <"Funn">
					description = <"Fritekstlig beskrivelse av funnene på bilder tatt under denne undersøkelsen.">
				>
				["at0005"] = <
					text = <"Strukturerte detaljer">
					description = <"Ytterligere strukturerte detaljer om funn på bilder for den identifiserte strukturen i kroppen eller regionen, eller som er knyttet til delstrukturer av disse.">
				>
				["at0006"] = <
					text = <"Svar">
					description = <"Fritekstlig konsist, klinisk relevant inntrykk av alle radiologiske bildefunn for den identifiserte strukturen i kroppen.">
					comment = <"Kan inkludere en sammenligning med tidligere undersøkelser av den identifiserte strukturen i kroppen, dersom det er hensiktsmessig.">
				>
				["at0007"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekstlig kommentar om funn ved radiologiske metoder og som ikke er fanget i andre felter.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0.1"] = <[SNOMED-CT::23952005]>
			>
		>
	>

archetype (adl_version=1.4; uid=3ed5eab0-9e4f-4417-a80b-b14f0f655e7b)
	openEHR-EHR-CLUSTER.imaging_exam-ovary.v0
specialise
	openEHR-EHR-CLUSTER.imaging_exam.v0

concept
	[at0000.1]	-- Imaging examination of an ovary
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Peter Fedorcsak, Vebjørn Arntzen">
				["organisation"] = <"University of Oslo, Oslo University Hospital">
				["email"] = <"peterfe@uio.no, varntzen@ous-hf.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2021-11-24">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere en fritekstbeskrivelse, strukturerte detaljer og klinisk tolkning av observerte funn ved bildeundersøkelse av en eggstokk.">
			use = <"Brukes til å registrere en fritekstbeskrivelse, strukturerte detaljer og klinisk tolkning av observerte funn ved bildeundersøkelse av en eggstokk.

Denne arketypen ble utviklet til bruk i SLOT'et \"Strukturerte funn\" i arketypen OBSERVATION.imaging_exam_result (Bildediagnostisk svar), men kan også brukes innen andre ENTRY- og CLUSTER-arketyper der det er klinisk passende.

Brukes som et rammeverk hvor andre CLUSTER arketyper kan nøstes i SLOT'et \"Strukturerte detaljer\" for å registrere ytterligere strukturerte billedfunn for delstrukturer av eggstokk eller andre relevante funn.">
			misuse = <"Skal ikke brukes til å registrere funn utenfor det spesifiserte organ eller kroppsdel. Bruk andre arketyper fra gruppen av CLUSTER.imaging_exam-arketyper for dette.

Skal ikke brukes til å registrere overordnede undersøkelsesfunn, kontekst eller tekniske detaljer relatert til bildeundersøkelsen. Bruk arketypen OBSERVATION.imaging_exam_result (Bildediagnostisk svar) for å registrere for eksempel kommentar om undersøkelseskvalitet, differensialdiagnose og overordnet konklusjon.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record a narrative description, structured details and clinical impression of the findings observed in an imaging examination targeting an ovary.">
			use = <"Use to record a narrative description, structured details and clinical impression of the findings observed in an imaging examination targeting an ovary.

This archetype has been designed to be used in the 'Structured imaging findings' SLOT within the OBSERVATION.imaging_exam_result archetype, but can also be used within other ENTRY or CLUSTER archetypes, where clinically appropriate. 

Use to provide a framework in which CLUSTER archetypes can be nested in the 'Additional details' SLOT within this archetype to record additional nested levels of structured imaging findings for related body substructures or site-specific findings.">
			misuse = <"Not to be used to record findings outside the specified body structure or region. Use other archetypes from the CLUSTER.imaging_exam family for this purpose.

Not to be used to record details related to the overall findings, context or technical details related to a complete imaging examination test result. Use the OBERSERVATION.imaging_exam_result for this purpose. For example, comments on the study quality, differential diagnoses, and overall impression.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital", "Liv Laugen, Oslo University Hospital", "Peter Fedorcsak, Oslo University Hospital">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Timmerman, D., et al., Terms, definitions and measurements to describe the sonographic features of adnexal tumors: a consensus opinion from the International Ovarian Tumor Analysis (IOTA) Group. Ultrasound Obstet Gynecol, 2000. 16(5): p. 500-5.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"05CC498C1057ED6189FE1EDD0C8429CF">
		["build_uid"] = <"8e3eea8d-f9a5-47dc-aad3-a29b383ce366">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact https://www.snomed.org/snomed-ct/get-snomed or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000.1] matches {    -- Imaging examination of an ovary
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001.1] matches {    -- Body structure
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.1,    -- Left ovary
							at0.2]    -- Right ovary
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {    -- Anatomical location
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0003] occurrences matches {0..*} matches {    -- Structured anatomical location
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v2|openEHR-EHR-CLUSTER\.anatomical_location_circle(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0004] occurrences matches {0..1} matches {    -- Imaging findings
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {    -- Additional details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.imaging_finding(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-lymph_node(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-lymph_node_group(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-abnormality(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-abnormality-adnexal_mass(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0006] occurrences matches {0..1} matches {    -- Impression
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {    -- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.3] occurrences matches {0..3} matches {    -- Diameter
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::122]>
						list = <
							["1"] = <
								units = <"mm">
								magnitude = <|>=0.0|>
								precision = <|0|>
							>
							["2"] = <
								units = <"cm">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
						>
					>
				}
			}
			ELEMENT[at0.4] occurrences matches {0..3} matches {    -- Largest follicle diameter
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::122]>
						list = <
							["1"] = <
								units = <"mm">
								magnitude = <|>=0.0|>
								precision = <|0|>
							>
							["2"] = <
								units = <"cm">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
						>
					>
				}
			}
			ELEMENT[at0.5] occurrences matches {0..1} matches {    -- Number of antral follicles
				value matches {
					DV_COUNT matches {
						magnitude matches {|>=0|}
					}
				}
			}
			CLUSTER[at0.6] occurrences matches {0..*} matches {    -- Per follicle
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0.7] occurrences matches {0..3} matches {    -- Diameter
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::122]>
								list = <
									["1"] = <
										units = <"mm">
										magnitude = <|>=0.0|>
										precision = <|0|>
									>
									["2"] = <
										units = <"cm">
										magnitude = <|>=0.0|>
										precision = <|1|>
									>
								>
							>
						}
					}
					ELEMENT[at0.8] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
	}


ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000.1"] = <
					text = <"Imaging examination of an ovary">
					description = <"Findings observed in an imaging examination targeting an ovary, using radiographic techniques.">
				>
				["at0001.1"] = <
					text = <"Body structure">
					description = <"Identification of the body structure or region examined.">
				>
				["at0.1"] = <
					text = <"Left ovary">
					description = <"The left ovary was examined.">
				>
				["at0.2"] = <
					text = <"Right ovary">
					description = <"The right ovary was examined.">
				>
				["at0.3"] = <
					text = <"Diameter">
					description = <"A measured diameter of the ovary.">
					comment = <"This data element has 3 occurrences to allow for measurements in 3 different axes.">
				>
				["at0.4"] = <
					text = <"Largest follicle diameter">
					description = <"A measured diameter of the largest ovarian follicle.">
					comment = <"This data element has 3 occurrences to allow for measurements in 3 different axes.">
				>
				["at0.5"] = <
					text = <"Number of antral follicles">
					description = <"The number of antral follicles.">
				>
				["at0.6"] = <
					text = <"Per follicle">
					description = <"Details about a single ovarian follicle.">
				>
				["at0.7"] = <
					text = <"Diameter">
					description = <"A measured diameter of an ovarian follicle.">
					comment = <"This data element has 3 occurrences to allow for measurements in 3 different axes.">
				>
				["at0.8"] = <
					text = <"Description">
					description = <"Narrative description about a single follicle.">
					comment = <"For example: may be a corpus luteum.">
				>
				["at0005"] = <
					text = <"Additional details">
					description = <"Additional structured details about imaging findings for the identified body structure or region, or findings for related substructures.">
				>
				["at0000"] = <
					text = <"Imaging examination">
					description = <"Findings observed in an imaging examination targeting a specified structure or region, using radiographic techniques.">
				>
				["at0004"] = <
					text = <"Imaging findings">
					description = <"Narrative description of the imaging findings observed during this examination.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the imaging findings, not captured in other fields.">
				>
				["at0001"] = <
					text = <"Body structure">
					description = <"Identification of the body structure or region examined.">
				>
				["at0003"] = <
					text = <"Structured anatomical location">
					description = <"Structured details about the anatomical location of the identified body structure.">
					comment = <"If the anatomical location of the identified body structure is known or it has been identified in the 'Anatomical location' data element, this data element is redundant.">
				>
				["at0006"] = <
					text = <"Impression">
					description = <"Narrative concise, clinically relevant impression of all imaging findings for the identified body structure.">
					comment = <"Also referred to as 'Opinion'. May include a comparison with previous studies of the identified body structure, where appropriate.">
				>
				["at0002"] = <
					text = <"Anatomical location">
					description = <"Simple body site for the identified structure.">
					comment = <"If the anatomical location of the identified body structure is known, this data element is redundant.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0.1"] = <
					text = <"Venstre eggstokk">
					description = <"Undersøkelse av venstre eggstokk.">
				>
				["at0.2"] = <
					text = <"Høyre eggstokk">
					description = <"Undersøkelse av høyre eggstokk.">
				>
				["at0.3"] = <
					text = <"Diameter">
					description = <"Eggstokkens diameter.">
					comment = <"Elementet har 3 forekomster for å tillate registrering av målinger i 3 retninger.">
				>
				["at0.4"] = <
					text = <"Den største follikkelens diameter">
					description = <"Diameter av eggstokkens største follikkel.">
					comment = <"Elementet har 3 forekomster for å tillate registrering av målinger i 3 retninger.">
				>
				["at0.5"] = <
					text = <"Antall antrale follikler">
					description = <"Antall antrale follikler i eggstokken.">
				>
				["at0.6"] = <
					text = <"Per follikkel">
					description = <"Detaljer om en enkel follikkel.">
				>
				["at0.7"] = <
					text = <"Diameter">
					description = <"Follikkelens diameter.">
					comment = <"Elementet har 3 forekomster for å tillate registrering av målinger i 3 retninger.">
				>
				["at0.8"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse av en enkel follikkel.">
					comment = <"For eksempel, sannsynlig gult legeme.">
				>
				["at0000"] = <
					text = <"Bildeundersøkelse">
					description = <"Funn ved bildeundersøkelse rettet mot en spesifikk struktur eller region med bruk av radiologiske metoder.">
				>
				["at0000.1"] = <
					text = <"Bildeundersøkelse av en eggstokk">
					description = <"Funn ved bildeundersøkelse av eggstokk med bruk av radiologiske metoder.">
				>
				["at0001"] = <
					text = <"Struktur">
					description = <"Identifisering av strukturen i kroppen eller regionen som er undersøkt.">
				>
				["at0001.1"] = <
					text = <"Struktur">
					description = <"Navn av undersøkt struktur eller område på eller i kroppen.">
				>
				["at0002"] = <
					text = <"Anatomisk lokalisasjon">
					description = <"Enkel beskrivelse av den anatomiske lokaliseringen av strukturen i kroppen eller regionen.">
					comment = <"Dersom den anatomiske lokaliseringen av den identifiserte strukturen i kroppen er kjent, er dette dataelementet overflødig.">
				>
				["at0003"] = <
					text = <"Strukturert anatomisk lokalisasjon">
					description = <"Strukturerte detaljer om den anatomiske lokaliseringen av strukturen i kroppen eller regionen.">
					comment = <"Dersom den anatomiske lokaliseringen av den identifiserte strukturen i kroppen er kjent, eller er identifisert av dataelementet \"Anatomisk lokalisasjon\" er dette dataelementet overflødig.">
				>
				["at0004"] = <
					text = <"Funn">
					description = <"Fritekstlig beskrivelse av funnene på bilder tatt under denne undersøkelsen.">
				>
				["at0005"] = <
					text = <"Strukturerte detaljer">
					description = <"Ytterligere strukturerte detaljer om funn på bilder for den identifiserte strukturen i kroppen eller regionen, eller som er knyttet til delstrukturer av disse.">
				>
				["at0006"] = <
					text = <"Svar">
					description = <"Fritekstlig konsist, klinisk relevant inntrykk av alle radiologiske bildefunn for den identifiserte strukturen i kroppen.">
					comment = <"Kan inkludere en sammenligning med tidligere undersøkelser av den identifiserte strukturen i kroppen, dersom det er hensiktsmessig.">
				>
				["at0007"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekstlig kommentar om funn ved radiologiske metoder og som ikke er fanget i andre felter.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0.1"] = <[SNOMED-CT::43981004]>
				["at0.2"] = <[SNOMED-CT::20837000]>
			>
		>
	>

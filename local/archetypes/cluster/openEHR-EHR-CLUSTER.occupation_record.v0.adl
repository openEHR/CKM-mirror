archetype (adl_version=1.4; uid=fa46c222-cfc7-4d5d-b670-409d33f01f0a)
	openEHR-EHR-CLUSTER.occupation_record.v0

concept
	[at0000]	-- Occupation record
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Silje Ljosland Bakke and Vebjørn Arntzen">
				["organisation"] = <"Nasjonal IKT HF">
				["email"] = <"silje.ljosland.bakke@nasjonalikt.no / varntzen@ous-hf.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2010-12-17">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere detaljert informasjon om en aktivitet, yrke eller jobb for et individ i en spesifisert periode.">
			use = <"Brukes for å registrere detaljert informasjon om en aktivitet, yrke eller jobb for et individ i en spesifisert periode.

En aktiv eller nåværende sysselsetting kan bli utledet fra \"Dato for oppstart\" hvis det ikke er registrert noe i \"Dato opphør\".

Et individ kan ha mange samtidige sysselsettinger, og de kan hver for seg være betalt eller ubetalt. Hver slik sysselsetting registreres i egne instanser av denne arketypen.

Hvis detaljer om en sysselsetting endrer seg vesentlig, som en forandring av tittel eller stillingsprosent, registreres dette i egne instanser av denne arketypen.

Arketypen er laget for å benyttes i SLOTet \"Sysselsetingsperioder\" i arketypen EVALUATION.occupation_summary (Sysselsettingssammendrag), men kan også brukes innen andre ENTRY- eller CLUSTER-arketyper der det er klinisk relevant.

Det kan være tilsynelatende eller reell overlapp mellom dataelementene i denne arketypen og opplysninger om sysselsetting/arbeidsforhold i demografisk informasjon i kliniske systemer. Dataelementene i denne arketypen er laget spesifikt for å støtte kliniske bruksområder som for eksempel sykemeldinger eller legeerklæringer.">
			keywords = <"arbeid", "arbeidstaker", "arbeidsgiver", "arbeidsforhold", "arbeidshistorikk", "jobb", "ansatt", "yrke", "arbeidsløs", "studerer", "student", "elev", "trygdet", "ufør", "arbeidssituasjon", "erverv", "yrkestilknytning", "pensjon", "pensjonist", "attføring", "bransje", "arbeidsledig", "hjemmeværende", "stilling", "profesjon", "frivillig", "vernepliktig", "sektor", "næring">
			misuse = <"Brukes ikke for å registrere midlertidige endringer eller episoder innen en enkelt sysselsettingsperiode, som i permisjon, sykemeldt, permittert eller overført til annen organisasjon. Dette er ikke innenfor anvendelsesområdet for denne arketypen, og skal registreres i et personal- eller HR-system.

Brukes ikke for å beskrive helserisikoer eller eksponering for farlige substanser i arbeidssituasjonen. Til dette brukes henholdsvis arketypene EVALUATION.health_risk eller EVALUATION.exposure.

Brukes ikke for å registrere informasjon om individets inntektskilder eller detaljer om inntekt. Bruk arketypen EVALUATION.income_summary for dette formålet.

Brukes ikke for å registrere informasjon om sysselsetting for et individ på en bestemt dato (for eksempel 16. juni 2014) eller i løpet av en relativ tidsperiode, som for eksempel \"siste 30 dager\". Dette kan utledes fra \"Dato for oppstart\" hvis det ikke er registrert noe i \"Dato opphør\", og må registreres i en egen OBSERVATION-arketype for dette formålet.">
			copyright = <"© 2010 NEHTA, openEHR Foundation, Nasjonal IKT HF">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about a single activity, service trade or craft carried out by an individual during a specified period of time.">
			use = <"Use to record details about a single activity, service trade or craft carried out by an individual during a specified period of time. 

An active, or current occupation may be implied from a 'Date commenced' but no 'Date ceased'. 

An individual may carry out many simultaneous occupations, each of which may be paid or unpaid. Each occupation should be recorded in a separate instance of this archetype.

If occupation attributes change significantly, such as a change of role/title or number of hours, then this should be recorded as a separate instance of this archetype.

This archetype has been specifically designed to be used in the 'Occupation record' SLOT within the EVALUATION.occupation_summary archetype, but can also be used within any other ENTRY or CLUSTER archetypes, where clinically appropriate.

There may be some apparent or real overlap between the data elements in this archetype and occupation/employment details that may be stored as demographic details in clinical systems. These data elements have been designed specifically to support clinical purposes including generation of a medical certificate to a current employer.">
			keywords = <"employment", "employer", "job", "occupation", "work", "profession", "unemployed", "employee", "unemployment", "studying", "employed", "student", "sector", "profession", "volunteer">
			misuse = <"Not to be used to record temporary changes or episodes within a single occupation record, such as being on leave or seconded to another organisaton. This is out of scope for this archetype and should be part of a human relations system.

Not to be used for detailed descriptions of health risks or exposure to hazardous substances in the workplace. Use the archetypes EVALUATION.health_risk or EVALUATION.exposure for this purpose.

Not to be used to record information about sources of income or income details for the individual. Use the EVALUATION.income_summary archetype for this purpose.

Not to be used to record information about the occupation of an individual at a specific point in time (for example, on June 16, 2014) or during a relative interval of time (for example 'in the past 30 days'. Use an appropriate OBSERVATION archetype for this purpose.

Not to be used to record periods of leave or sick leave in lieu of a Human Resources record.">
			copyright = <"© 2010 Australian Digital Health Agency, openEHR Foundation, Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Morten Aas, Oslo Universitetssykehus, Norway", "Tomas Alme, DIPS ASA, Norway", "Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Koray Atalag, University of Auckland, New Zealand", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (openEHR Editor)", "Marcus Baw, openGPSoC / BawMedical Ltd, United Kingdom", "Ivar Berge, Oslo Universitetssykehus, Norway", "Bjørn Christensen, Helse Bergen HF, Norway", "Lisbeth Dahlhaug, Helse Midt - Norge IT, Norway", "Jayne Donaldson, University of Stirling, United Kingdom", "Bjørg Eli Hollund, helse-bergen, Norway", "Stig Erik Hegrestad, Helse Førde, Norway", "Samuel Frade, Marand, Portugal", "Hildegard Franke, freshEHR Clinical Informatics Ltd., United Kingdom", "Sergio Freire, State University of Rio de Janeiro, Brazil", "Mikkel Gaup Grønmo, FSE, Helse Nord, Norway (Nasjonal IKT redaktør)", "Heather Grain, Llewelyn Grain Informatics, Australia", "Anne Gunn Haugland, Helse Bergen HF, Norway", "Kristian Heldal, Telemark Hospital Trust, Norway", "Jørn Henrik Vold, Helse Bergen, Avdeling for rusmedisin, Norway", "Anca Heyd, DIPS ASA, Norway", "Teresa Highway, Alberta Health Services, Canada", "Annette Hole Sjøborg, DIPS ASA, Norway", "Evelyn Hovenga, EJSH Consulting, Australia", "Susanna Jönsson, Landstinget i Värmland, Sweden", "Tone Klund, DIPS AS, Norway", "Harmony Kosola, Alberta Health Services, Canada", "Ron Krawec, Alberta Health Services, Canada", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Pedro Leuschner, Centro Hospitalar do Porto, Portugal", "Hallvard Lærum, Direktoratet for e-helse, Norway", "Siv Marie Lien, DIPS ASA, Norway", "Hildegard McNicoll, freshEHR Clinical Informatics Ltd., United Kingdom", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "John Meredith, NHS Wales Informatics Service, United Kingdom", "Lars Morgan Karlsen, DIPS ASA, Norway", "Erik Nissen, Cambio Healthcare Systems AB, Sweden", "Andrej Orel, Marand d.o.o., Slovenia", "Anne Pauline Anderssen, Helse Nord RHF, Norway", "Martin Paulson, Sykehuset i Vestfold, Norway", "Georg Reinhardt, Helse Fonna, Norway", "Tanja Riise, Nasjonal IKT HF, Norway", "Line Silsand, Universitetssykehuset i Nord-Norge, Norway", "Niclas Skyttberg, Capio St Görans Sjukhus, Sweden", "Norwegian Review Summary, Nasjonal IKT HF, Norway", "Nyree Taylor, Ocean Informatics, Australia", "Tesfay Teame, Folkehelseinstittutet, Norway", "Jon Tysdahl, Furst medlab AS, Norway", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Derived from: Employment Summary, Draft Archetype [Internet]. nehta, Australia, nehta Clinical Knowledge Manager [cited: 2016-01-11]. No longer available.">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"9EB12EBC665B3ACD84FF4C168F1F4631">
		["build_uid"] = <"9ca02cab-9acc-477c-84d8-7e12e4133ba9">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {	-- Sysselsettingsperiode
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Sysselsettingskategori
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0016] occurrences matches {0..1} matches {	-- Beskrivelse
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Dato for oppstart
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			ELEMENT[at0005] occurrences matches {0..*} matches {	-- Tittel/rolle
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0015] occurrences matches {0..1} matches {	-- Sysselsettingsandel
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0020, 	-- Heltid
							at0021]	-- Deltid
						}
					}
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0013] occurrences matches {0..1} matches {	-- Heltidsekvivalent
				value matches {
					DV_PROPORTION matches {
						numerator matches {|>=0.0|}
						type matches {1, 2}
					}
				}
			}
			ELEMENT[at0019] occurrences matches {0..1} matches {	-- Tid
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::709]>
						list = <
							["1"] = <
								units = <"h/d">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
							["2"] = <
								units = <"h/wk">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
							["3"] = <
								units = <"h/mo">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
							["4"] = <
								units = <"h/a">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
							["5"] = <
								units = <"d/wk">
								magnitude = <|>=0.0|>
							>
							["6"] = <
								units = <"d/mo">
								magnitude = <|>=0.0|>
							>
							["7"] = <
								units = <"wk/mo">
								magnitude = <|>=0.0|>
							>
							["8"] = <
								units = <"d/a">
								magnitude = <|>=0.0|>
							>
							["9"] = <
								units = <"wk/a">
								magnitude = <|>=0.0|>
							>
						>
					>
				}
			}
			allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Organisasjonsdetaljer
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.organisation(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.organisation(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- Bransje
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0006] occurrences matches {0..1} matches {	-- Yrke
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0018] occurrences matches {0..*} matches {	-- Ytterligere detaljer
				include
					archetype_id/value matches {/.*/}
			}
			ELEMENT[at0008] occurrences matches {0..1} matches {	-- Dato opphør
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			ELEMENT[at0014] occurrences matches {0..1} matches {	-- Kommentar
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Occupation record">
					description = <"A single activity, service trade or craft carried out by an individual during a specified period of time.">
					comment = <"The occupation may be paid or unpaid, full-time or part time.">
				>
				["at0001"] = <
					text = <"Occupation category">
					description = <"The occupation category for this record.">
					comment = <"For example: employed, seeking employment, student, home duties or retiree. Coding with a terminology is desirable, where possible.">
				>
				["at0002"] = <
					text = <"Industry">
					description = <"Classification of the industry in which the individual works.">
					comment = <"For example: Mining, manufacturing, construction.">
				>
				["at0004"] = <
					text = <"Organisation details">
					description = <"Details about the employer or institution.">
				>
				["at0005"] = <
					text = <"Title/role">
					description = <"Job title or role of the occupation episode.">
				>
				["at0006"] = <
					text = <"Occupation classification">
					description = <"Classification of the type of occupation undertaken by the individual.">
					comment = <"Coding with a terminology is desirable, where possible. For example: categorisation according to the ISCO-08 (http://www.ilo.org/public/english/bureau/stat/isco/isco08/).">
				>
				["at0007"] = <
					text = <"Date commenced">
					description = <"Date occupation episode commenced.">
				>
				["at0008"] = <
					text = <"Date ceased">
					description = <"Date occupation episode ceased.">
				>
				["at0013"] = <
					text = <"Full time equivalent">
					description = <"The position's fraction or percentage of a full time equivalent.">
					comment = <"The definition of \"full time\" may vary between professions. In some situations it may be appropriate to also record the actual number of hours worked in the element 'hours'.">
				>
				["at0014"] = <
					text = <"Comment">
					description = <"Additional narrative about the occupation episode not captured in other fields.">
				>
				["at0015"] = <
					text = <"Activity category">
					description = <"Category for the amount of time spent carrying out the occupation.">
					comment = <"Coding with a terminology is desirable, where possible.">
				>
				["at0016"] = <
					text = <"Description">
					description = <"Narrative description of the occupation activities carried out by the individual during the specified time interval.">
				>
				["at0018"] = <
					text = <"Additional details">
					description = <"Further detail about the occupation episode.">
					comment = <"For example: workplace location and conditions;  or combat zone experience.">
				>
				["at0019"] = <
					text = <"Time">
					description = <"The amount of time spent per specified time period carrying out the occupation.">
				>
				["at0020"] = <
					text = <"Full-time">
					description = <"The individual carries out this occupation for equal to or more than the amount of time that is officially regarded as 'full-time' for the occupation.">
				>
				["at0021"] = <
					text = <"Part-time">
					description = <"The individual carries out this occupation for less than the amount of time that is officially regarded as 'full-time' for the occupation.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Sysselsettingsperiode">
					description = <"En enkelt aktivitet, yrke eller jobb for et individ i en spesifisert periode.">
					comment = <"Sysselsettingen kan være betalt eller ubetalt, deltid eller fulltid.">
				>
				["at0001"] = <
					text = <"Sysselsettingskategori">
					description = <"Kategorien for denne sysselsettingsperioden.">
					comment = <"For eksempel i arbeid, arbeidsløs, student, pensjonert, etc. Koding med en terminologi er ønskelig, der det er mulig.">
				>
				["at0002"] = <
					text = <"Bransje">
					description = <"Klassifisering av bransje eller sektor der individet har sin sysselsetting.">
					comment = <"For eksempel: \"Jordbruk, skogbruk og fiske\", \"Varehandel\", \"Bygg- og anleggsvirksomhet\".">
				>
				["at0004"] = <
					text = <"Organisasjonsdetaljer">
					description = <"Detaljer om arbeidsgiver eller utdanningsinstitusjon.">
				>
				["at0005"] = <
					text = <"Tittel/rolle">
					description = <"Stillingstittel eller rolle for sysselsettingsperioden.">
				>
				["at0006"] = <
					text = <"Yrke">
					description = <"Klassifisering av yrket individet har.">
					comment = <"Koding med en terminologi er ønskelig om mulig. For eksempel kategorisering i henhold til STYRK-08 (https://www.ssb.no/arbeid-og-lonn/artikler-og-publikasjoner/standard-for-yrkesklassifisering-styrk-08).">
				>
				["at0007"] = <
					text = <"Dato for oppstart">
					description = <"Datoen sysselsettingsperioden startet.">
				>
				["at0008"] = <
					text = <"Dato for opphør">
					description = <"Datoen sysselsettingsperioden opphørte.">
				>
				["at0013"] = <
					text = <"Heltidsekvivalent">
					description = <"Stillingens andel eller prosent av en heltidsstilling.">
					comment = <"Definisjonen av \"heltid\" kan variere mellom ulike yrker. I noen situasjoner kan det være passende å registrere faktisk antall arbeidstimer i elementet \"Tid\".">
				>
				["at0014"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst til sysselsettingsperioden som ikke passer i andre felt.">
				>
				["at0015"] = <
					text = <"Sysselsettingskategori">
					description = <"Kategori for andel tid brukt til sysselsettingen.">
					comment = <"Koding med en terminologi er ønskelig, der det er mulig.">
				>
				["at0016"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse om sysselsettingperioden.">
				>
				["at0018"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere detaljer om sysselsettingsperioden.">
					comment = <"For eksempel: Forhold på arbeidsplassen eller erfaring fra krigssoner.">
				>
				["at0019"] = <
					text = <"Tid">
					description = <"Mengde tid sysselsettingen omfatter i den spesifiserte perioden.">
				>
				["at0020"] = <
					text = <"Heltid">
					description = <"Individet bruker lik eller lengre tid på sysselsettingen enn hva full stilling er definert i enten lov- eller avtaleverk (tariffavtale).">
				>
				["at0021"] = <
					text = <"Deltid">
					description = <"Individet bruker kortere tid på sysselsettingen enn hva full stilling er definert i enten lov- eller avtaleverk (tariffavtale).">
				>
			>
		>
	>

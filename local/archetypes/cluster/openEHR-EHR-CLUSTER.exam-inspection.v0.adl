archetype (adl_version=1.4; uid=35cb5327-9bc8-43af-ab02-142ea2f83b7d)
	openEHR-EHR-CLUSTER.exam-inspection.v0
specialise
	openEHR-EHR-CLUSTER.exam.v1

concept
	[at0000.1]	-- Inspection findings
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Silje Ljosland Bakke">
				["organisation"] = <"Nasjonal IKT HF">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2019-02-14">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"*For recording a narrative description and clinical interpretation of the findings observed during visual inspection of a body system or anatomical structure.(en)">
			use = <"*Use to record a narrative description and clinical interpretation of the findings observed during visual inspection of a body system or anatomical structure, identified as part of this archetype, where the findings will be recorded in isolation of other types of examination. This archetype consists only of the core inspection pattern data elements, and can be expanded using other CLUSTER archetypes or specialised for inspection of specified body parts, such as: inspection of the cervix using a speculum; or inspection of the rectum using a proctoscope.

Findings in more advanced examinations, such as endoscopy, can be recorded using this archetype. In those cases, information on which device used can be recorded in the element 'Device details' in the OBSERVATION.exam, in the element 'Procedure detail' in the ACTION.procedure, or in an equivalent element in an appropriate ENTRY or ACTION archetype.

This archetype has been specifically designed to be used in the 'Examination detail' SLOT within the OBSERVATION.exam or ACTION.procedure archetype, but can also be used within other ENTRY or CLUSTER archetypes, where clinically appropriate.

Use to provide a framework in which other CLUSTER archetypes can be nested in the 'Examination findings' SLOT to record additional structured inspection findings.

The CLUSTER.exclusion_exam archetype can be nested within the 'Examination not done' SLOT to optionally record explicit details about the inspection not being performed.

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical Description' data element.(en)">
			misuse = <"*Not to be used to record other types of clinical examination. Use CLUSTER.exam or CLUSTER.exam-palpation and related specialisations for this purpose.

Not to be used to record stand-alone clinical measurements or test results - use specific OBSERVATION archetypes. For example OBSERVATION.head_circumference or OBSERVATION.glasgow_coma_scale.

Not to be used for recording the clinical history - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom_sign.(en)">
			copyright = <"© openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording a narrative description and clinical interpretation of the findings observed during visual inspection of a body system or anatomical structure.">
			use = <"Use to record a narrative description and clinical interpretation of the findings observed during visual inspection of a body system or anatomical structure, identified as part of this archetype, where the findings will be recorded in isolation of other types of examination. This archetype consists only of the core inspection pattern data elements, and can be expanded using other CLUSTER archetypes or specialised for inspection of specified body parts, such as: inspection of the cervix using a speculum; or inspection of the rectum using a proctoscope.

Findings in more advanced examinations, such as endoscopy, can be recorded using this archetype. In those cases, information on which device used can be recorded in the element 'Device details' in the OBSERVATION.exam, in the element 'Procedure detail' in the ACTION.procedure, or in an equivalent element in an appropriate ENTRY or ACTION archetype.

This archetype has been specifically designed to be used in the 'Examination detail' SLOT within the OBSERVATION.exam or ACTION.procedure archetype, but can also be used within other ENTRY or CLUSTER archetypes, where clinically appropriate.

Use to provide a framework in which other CLUSTER archetypes can be nested in the 'Examination findings' SLOT to record additional structured inspection findings.

The CLUSTER.exclusion_exam archetype can be nested within the 'Examination not done' SLOT to optionally record explicit details about the inspection not being performed.

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical Description' data element.">
			misuse = <"Not to be used to record other types of clinical examination. Use CLUSTER.exam or CLUSTER.exam-palpation and related specialisations for this purpose.

Not to be used to record stand-alone clinical measurements or test results - use specific OBSERVATION archetypes. For example OBSERVATION.head_circumference or OBSERVATION.glasgow_coma_scale.

Not to be used for recording the clinical history - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom_sign.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (openEHR Editor)", "Hildegard Franke, freshEHR Clinical Informatics Ltd., United Kingdom", "Mikkel Gaup Grønmo, FSE, Helse Nord, Norway (Nasjonal IKT redaktør)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "Bjørn Næss, DIPS ASA, Norway", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)", "Anca Heyd, DIPS ASA, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"D8574E7E9F540A1FA75D6630128E92E7">
		["build_uid"] = <"5154e1d0-2068-4042-ad83-4f856a6dfe3c">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000.1] matches {	-- Inspection findings
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001.1] matches {	-- System or structure inspected
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0012] occurrences matches {0..1} matches {	-- Body site
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0011] occurrences matches {0..*} matches {	-- Structured body site
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_circle(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- No abnormality detected
				value matches {
					DV_BOOLEAN matches {
						value matches {True}
					}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Examination findings
				include
					archetype_id/value matches {/.*/}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Multimedia representation
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {	-- Clinical interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0008] occurrences matches {0..*} matches {	-- Examination not done
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000.1"] = <
					text = <"Inspection findings">
					description = <"Findings observed during visual inspection of a body system or anatomical structure.">
				>
				["at0001"] = <
					text = <"System or structure examined">
					description = <"Identification of the examined body system or anatomical structure.">
					comment = <"For example: the very generic term \"skin\", which will likely require additional qualification using one of the 'Body site' data elements, or the complete phrase \"skin of right knee\". Coding of the system or structure examined with a terminology is preferred, where possible.">
				>
				["at0001.1"] = <
					text = <"System or structure inspected">
					description = <"Identification of the body system or anatomical structure.">
					comment = <"For example: the very generic term \"skin\", which will likely require additional qualification using one of the 'Body site' data elements, or the complete phrase \"skin of right knee\". Coding of the system or structure examined with a terminology is preferred, where possible.">
				>
				["at0002"] = <
					text = <"No abnormality detected">
					description = <"Statement that no abnormality was detected (NAD) on physical examination.">
					comment = <"Record as True if no abnormality was detected on examination. Specific statements about the examination can be included in the 'Clinical Interpretation' data element. If 'No abnormality detected' is selected, then recording of other examination data elements becomes redundant, with the exception of only the 'Clinical interpretation' data element, which may be useful if a normal statement is desired for recording, for example 'Normal skin examination'.">
				>
				["at0003"] = <
					text = <"Clinical description">
					description = <"Narrative description of the overall findings observed during the physical examination.">
				>
				["at0004"] = <
					text = <"Examination findings">
					description = <"Structured details about the physical examination findings.">
				>
				["at0005"] = <
					text = <"Multimedia representation">
					description = <"Digital image, video or diagram representing the physical examination findings.">
				>
				["at0006"] = <
					text = <"Clinical interpretation">
					description = <"Single word, phrase or brief description that represents the clinical meaning and significance of the physical examination findings.">
					comment = <"Coding of the 'Clinical interpretation' with a terminology is preferred, where possible.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the physical examination findings, not captured in other fields.">
				>
				["at0008"] = <
					text = <"Examination not done">
					description = <"Details to explicitly record that this examination was not performed.">
				>
				["at0011"] = <
					text = <"Structured body site">
					description = <"A structured description of the area of the body under examination.">
					comment = <"If the body site has been fully identified in the 'System or structure examined' or the 'Body site' data element, this SLOT becomes redundant.">
				>
				["at0012"] = <
					text = <"Body site">
					description = <"Identification of the area of the body under examination.">
					comment = <"For example: \"entire body\", \"face\" or \"right cheek\". If the body site has been fully identified in the 'System or structure examined' data element, this data element becomes redundant.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000.1"] = <
					text = <"*Inspection findings(en)">
					description = <"*Findings observed during visual inspection of a body system or anatomical structure.(en)">
				>
				["at0001"] = <
					text = <"Undersøkt organsystem eller struktur">
					description = <"Identifisering av det undersøkte organsystemet eller anatomiske strukturen.">
					comment = <"For eksempel den generiske termen \"Hud\" - som mest sannsynlig vil trenge en ytterligere spesifikasjon ved å benytte dataelementet \"Anatomisk lokalisasjon\", eller en fullstendig frase, som \"Hud på høyre kne\". Det er anbefalt å kode det undersøkte systemet eller strukturen med en terminologi, der det er mulig.">
				>
				["at0001.1"] = <
					text = <"*System or structure inspected(en)">
					description = <"*Identification of the body system or anatomical structure.(en)">
					comment = <"*For example: the very generic term \"skin\", which will likely require additional qualification using one of the 'Body site' data elements, or the complete phrase \"skin of right knee\". Coding of the system or structure examined with a terminology is preferred, where possible.(en)">
				>
				["at0002"] = <
					text = <"Uten anmerkning">
					description = <"Utsagn om at ingen avvik var oppdaget ved undersøkelsen (U.a.).">
					comment = <"Registrer som Sann dersom ingen avvik ble oppdaget ved undersøkelsen. Spesifikke utsagn om undersøkelsen kan registreres i dataelementet \"Fortolkning\". Registrerer man \"Uten anmerkning\", vil registrering av andre dataelementer i arketypen være overflødig med unntak av dataelementet \"Fortolkning\", som kan være nyttig om en har behov for en normalbeskrivelse.">
				>
				["at0003"] = <
					text = <"Klinisk beskrivelse">
					description = <"Fritekstbeskrivelse av de overordnede funnene ved den fysiske undersøkelsen.">
				>
				["at0004"] = <
					text = <"Spesifikke funn">
					description = <"Ytterligere strukturerte detaljer om undersøkelsesfunnene.">
				>
				["at0005"] = <
					text = <"Multimediarepresentasjon">
					description = <"Digitale bilder, video eller diagram som representerer undersøkelsesfunnene.">
				>
				["at0006"] = <
					text = <"Fortolkning">
					description = <"Enkelt ord, setning, frase eller kort beskrivelse som representerer den kliniske betydning og viktigheten av funnene ved den kliniske undersøkelsen.">
					comment = <"Koding med terminologi foretrekkes når mulig. For eksempel \"normal undersøkelse\" eller \"tympanisk membran perforasjon\".">
				>
				["at0007"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om funn ved undersøkelsen, som ikke dekkes av andre elementer.">
				>
				["at0008"] = <
					text = <"Undersøkelse ikke utført">
					description = <"Detaljer for å eksplisitt registrere at denne undersøkelsen ikke ble utført.">
				>
				["at0011"] = <
					text = <"Strukturert anatomisk lokalisasjon">
					description = <"Angivelse av en strukturert anatomisk lokalisering av det undersøkte organsystemet eller anatomiske strukturen.">
					comment = <"Hvis anatomisk lokalisasjon er entydig identifisert i elementet \"Undersøkt organsystem eller struktur\" er dette SLOTet ikke nødvendig å benytte.">
				>
				["at0012"] = <
					text = <"Anatomisk lokalisasjon">
					description = <"Identifisering av et enkelt fysisk sted enten på eller i menneskekroppen.">
					comment = <"For eksempel \"hele kroppen\", \"ansiktet\" eller \"høyre kinn\". Hvis navn på sted på kroppen er fullstendig identifisert i elementet \"Strukturert anatomisk lokalisasjon\", er dette elementet unødvendig å ta med.">
				>
			>
		>
	>

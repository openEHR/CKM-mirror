archetype (adl_version=1.4; uid=0b1e5145-b0fd-4844-ae78-5a102fed7928)
	openEHR-EHR-CLUSTER.genomic_copy_number_variant.v0

concept
	[at0000]	-- Genomic copy number variant
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Aurelie Tomczak, Natalia Strauch">
				["organisation"] = <"Institute of Pathology, University Hospital Heidelberg, Germany; Medizinische Hochschule Hannover">
				["email"] = <"au.tomczak@yahoo.com, Strauch.Natalia@mh-hannover.de">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Liv Laugen">
				["organisation"] = <"​Oslo University Hospital, Norway">
				["email"] = <"liv.laugen@ous-hf.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Gideon Giacomelli">
		["organisation"] = <"Charité Berlin, Germany">
		["email"] = <"gideon.giacomelli@charite.de">
		["date"] = <"2019-02-01">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Zur Darstellung von Informationen zu der in einer Sequenz beobachteten \"Copy number variant\" menschlicher DNA.">
			use = <"Verwenden Sie diesen Archetyp, um die Details zu einer \"Copy number variant\" menschlicher DNA darzustellen, die in einer genomischen Sequenz beobachtet wurden.

Dieser Archetyp wurde speziell für die Verwendung im SLOT \"Structured variant\" innerhalb des Archetyps CLUSTER.genetic_variant entwickelt, kann aber gegebenenfalls auch in anderen ENTRY- oder CLUSTER-Archetypen verwendet werden.">
			keywords = <"Copy number, Kopienzahl, Variation, genetisch, Genom, genomisch", ...>
			misuse = <"Nicht zur Darstellung von Informationen über Varianten nicht-menschlicher DNA oder jeglicher Art von RNA oder Protein.

Nicht zur Darstellung einer Änderung verwenden, bei der die Größe des DNA-Segments kürzer als 1 kb ist. Verwenden Sie zu diesem Zweck den Archetyp CLUSTER.repeated_sequence_variant.">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere detaljer om en kopitallvariant (CNV) funnet i en sekvensiering av humant DNA i en genomisk sekvens.
">
			use = <"For å registrere detaljer om en kopitallvariant (CNV) funnet i en sekvensiering av humant DNA i en genomisk sekvens.

Denne arketypen har blitt laget for å brukes i SLOT'et \"'Strukturert variantbeskrivelse'\" i arketypen CLUSTER.genomic_variant_result. Den kan også brukes i andre ENTRY- eller CLUSTER-arketyper der det er klinisk hensiktsmessig.">
			keywords = <"kopitall", "variasjon", "genetikk", "genom", "mutasjon", "nukleotide", "CNV">
			misuse = <"Skal ikke brukes til å registrere informasjon om varianter av ikke-humant DNA, eller noen form for RNA eller protein.

Skal ikke brukes til å registrere en endring der størrelsen på DNA-segmentet er kortere enn 1kb. Bruk arketypen CLUSTER.repeated_sequence_variant til dette formålet.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the details about a copy number variant of human DNA, observed in a genomic sequence.">
			use = <"Use to record the details about a copy number variant of human DNA, observed in a genomic sequence.

This archetype has been specifically designed to be used in the 'Structured variant' SLOT within the CLUSTER.genetic_variant archetype, but can also be used within other ENTRY or CLUSTER archetypes, where clinically appropriate.">
			keywords = <"copy number, variation, genetic, genomic", ...>
			misuse = <"Not to be used to record information about variants of non-human DNA, or any kind of RNA or protein.

Not to be used to record a change where the size of the DNA segment is shorter than 1kb. Use the CLUSTER.repeated_sequence_variant archetype for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Cecilia Mascia, CRS4, Italy", "Christina Jaeger-Schmidt, Heidelberg University Hospital, Germany", "Florian Kaercher, Charité Berlin, Germany", "Francesca Frexia, CRS4, Italy", "Gianluigi Zanetti, CRS4, Italy", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Paolo Uva, CRS4, Italy", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (openEHR Editor)", "Simon Schumacher, HiGHmed, Germany", "Gunnar Houge, Haukeland University Hospital, Norway", "Gyri Aasland Gradek, Haukeland University Hospital, Norway", "Asbjørg Stray-Pedersen, Oslo University Hospital, Norway", "Toril Fagerheim, University Hospital of North Norway, Norway", "Camilla F. Skjelbred, Telemark HF Hospital, Norway", "Dag Erik Undlien, Oslo University Hospital, Norway", "Rune Østern, St Olavs Hospital, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"50E7358EB3BBF77B9BFBD27AE0CEC796">
		["build_uid"] = <"ad22ae83-1282-4da6-bd2c-e94b6439ecc5">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {    -- Genomic copy number variant
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {    -- Start
				value matches {
					DV_COUNT matches {*}
					DV_INTERVAL<DV_COUNT> matches {*}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {    -- End
				value matches {
					DV_COUNT matches {*}
					DV_INTERVAL<DV_COUNT> matches {*}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {    -- Total copy number
				value matches {
					DV_COUNT matches {
						magnitude matches {|>1|}
					}
				}
			}
			ELEMENT[at0005] occurrences matches {0..1} matches {    -- Copy number change type
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0006,    -- Gain
							at0007]    -- Loss
						}
					}
				}
			}
			allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {    -- Reference sequence
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.reference_sequence(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Genomic copy number variant">
					description = <"A human genetic sequence change where, compared to a genomic reference sequence, a DNA segment, that is 1 kilobases (kb) or larger, was deleted or duplicated.">
				>
				["at0001"] = <
					text = <"Start">
					description = <"Position or range of possible positions of the first nucleotide of the CNV.">
				>
				["at0002"] = <
					text = <"End">
					description = <"Position or range of possible positions of the last nucleotide of the CNV.">
				>
				["at0003"] = <
					text = <"Total copy number">
					description = <"Number of appearance of the allele.">
				>
				["at0004"] = <
					text = <"Reference sequence">
					description = <"The sequence file used as a reference to describe this variant.">
					comment = <"Should be a specific chromosome most of the time.">
				>
				["at0005"] = <
					text = <"Copy number change type">
					description = <"Type of sequence alteration.">
					comment = <"Different type of impact, such as low-level amplification or whole gene deletion, should be recorded using the Reported impact Cluster within the CLUSTER.genetic_variant.v0 archetype.">
				>
				["at0006"] = <
					text = <"Gain">
					description = <"A sequence alteration whereby the copy number of a given region is greater than the reference sequence.">
				>
				["at0007"] = <
					text = <"Loss">
					description = <"A sequence alteration whereby the copy number of a given region is less than the reference sequence.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Genetisk variant - kopitall">
					description = <"En endring av gensekvensen til et menneske hvor et DNA-segment, som er 1 kilobaser (kb) eller større, blir slettet eller duplisert, sammenliknet med referansesekvensen. Også kalt CNV. Engelsk: copy number.">
				>
				["at0001"] = <
					text = <"Start">
					description = <"Startposisjon eller området til mulige posisjoner til det første nukleotidet av kopitallvarianten (CNV).">
				>
				["at0002"] = <
					text = <"Slutt">
					description = <"Sluttposisjonen eller området til mulige posisjoner til det siste nukleotidet av kopitallvarianten (CNV).">
				>
				["at0003"] = <
					text = <"Totalt kopitall">
					description = <"Antall ganger allelet forekommer (antall kopier).">
				>
				["at0004"] = <
					text = <"Referansesekvens">
					description = <"Sekvensfilen som har blitt brukt som en referanse for å beskrive varianten.">
					comment = <"Vanligvis vil dette være et spesifikt kromosom.">
				>
				["at0005"] = <
					text = <"Kopitall endringstype">
					description = <"Type sekvensendring.">
					comment = <"Ulike typer betydning (impact), for eksempel lav-nivå forsterkning eller sletting av hele genet, skal registreres ved hjelp av den rapporterte betydning clusteret i arketypen genomic_variant_result.">
				>
				["at0006"] = <
					text = <"Tillegg">
					description = <"En sekvensendring der kopitallet til en gitt region er større enn referansesekvensen.">
				>
				["at0007"] = <
					text = <"Tap">
					description = <"En sekvensendring der kopitallet til en gitt region er mindre enn referansesekvensen.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Genomic copy number variant">
					description = <"Eine menschliche genetische Sequenzänderung, bei der im Vergleich zu einer genomischen Referenzsequenz ein DNA-Segment, das 1 Kilobasen (kb) oder größer ist, gelöscht oder dupliziert wurde.">
				>
				["at0001"] = <
					text = <"Start">
					description = <"Position or range of possible positions of the first nucleotide of the CNV.">
				>
				["at0002"] = <
					text = <"End">
					description = <"Position or range of possible positions of the last nucleotide of the CNV.">
				>
				["at0003"] = <
					text = <"Total copy number">
					description = <"Number of appearance of the allele.">
				>
				["at0004"] = <
					text = <"Reference sequence">
					description = <"The sequence file used as a reference to describe this variant.">
					comment = <"Should be a specific chromosome most of the time.">
				>
				["at0005"] = <
					text = <"Copy number change type">
					description = <"Type of sequence alteration.">
					comment = <"Different type of impact, such as low-level amplification or whole gene deletion, should be recorded using the Reported impact Cluster within the CLUSTER.genetic_variant.v0 archetype.">
				>
				["at0006"] = <
					text = <"Gain">
					description = <"A sequence alteration whereby the copy number of a given region is greater than the reference sequence.">
				>
				["at0007"] = <
					text = <"Loss">
					description = <"A sequence alteration whereby the copy number of a given region is less than the reference sequence.">
				>
			>
		>
	>

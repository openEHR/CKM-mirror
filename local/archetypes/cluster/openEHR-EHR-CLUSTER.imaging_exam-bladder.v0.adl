archetype (adl_version=1.4; uid=a3c1df47-c06c-48aa-a0e3-cfd7d976481b)
	openEHR-EHR-CLUSTER.imaging_exam-bladder.v0
specialise
	openEHR-EHR-CLUSTER.imaging_exam.v0

concept
	[at0000.1]	-- Imaging examination of the bladder
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2021-11-24">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record a narrative description, structured details and clinical impression of the findings observed in an imaging examination targeting the bladder.">
			use = <"Use to record a narrative description, structured details and clinical impression of the findings observed in an imaging examination targeting the bladder.

This archetype has been designed to be nested in the 'Structured imaging findings' SLOT within the OBSERVATION.imaging_exam_result archetype, but can also be used within other ENTRY or CLUSTER archetypes, where clinically appropriate.

Use to provide a framework in which CLUSTER archetypes can be nested in the 'Additional details' SLOT within this archetype to record additional nested levels of structured imaging findings for related body substructures or site-specified findings.
">
			misuse = <"Not to be used to record findings outside the specified body structure or region. Use other archetypes from the CLUSTER.imaging_exam family for this purpose.

Not to be used to record details related to the overall findings, context or technical details related to a complete imaging examination test result. Use the OBERSERVATION.imaging_exam_result for this purpose. For example, comments on the study quality, differential diagnoses, and overall impression.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital", "Liv Laugen, Oslo University Hospital", "Peter Fedorcsak, Oslo University Hospital">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"971B57507C2F3F6F75BB93B4D7F9B7C3">
		["build_uid"] = <"2deffddb-8dc7-4499-b9dd-71baf7ee78b1">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact https://www.snomed.org/snomed-ct/get-snomed or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000.1] matches {    -- Imaging examination of the bladder
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001.1] matches {    -- Body structure
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::at0.1]    -- Urinary bladder
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {    -- Anatomical location
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0003] occurrences matches {0..*} matches {    -- Structured anatomical location
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v2|openEHR-EHR-CLUSTER\.anatomical_location_circle(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0004] occurrences matches {0..1} matches {    -- Imaging findings
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {    -- SLOT: Additional details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.imaging_finding(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-lymph_node(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-lymph_node_group(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-abnormality(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0006] occurrences matches {0..1} matches {    -- Impression
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {    -- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.2] occurrences matches {0..1} matches {    -- Length
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::122]>
						list = <
							["1"] = <
								units = <"mm">
								magnitude = <|>=0.0|>
								precision = <|0|>
							>
							["2"] = <
								units = <"cm">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
						>
					>
				}
			}
			ELEMENT[at0.3] occurrences matches {0..1} matches {    -- Width
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::122]>
						list = <
							["1"] = <
								units = <"mm">
								magnitude = <|>=0.0|>
								precision = <|0|>
							>
							["2"] = <
								units = <"cm">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
						>
					>
				}
			}
			ELEMENT[at0.4] occurrences matches {0..1} matches {    -- Depth
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::122]>
						list = <
							["1"] = <
								units = <"mm">
								magnitude = <|>=0.0|>
								precision = <|0|>
							>
							["2"] = <
								units = <"cm">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
						>
					>
				}
			}
		}
	}



ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000.1"] = <
					text = <"Imaging examination of the bladder">
					description = <"Findings observed in an imaging examination targeting the bladder, using radiographic techniques.">
				>
				["at0001.1"] = <
					text = <"Body structure">
					description = <"Identification of the body structure or region examined.">
				>
				["at0.1"] = <
					text = <"Urinary bladder">
					description = <"The urinary bladder was examined.">
				>
				["at0.2"] = <
					text = <"Length">
					description = <"Measurement of the longest axis of the bladder.">
				>
				["at0.3"] = <
					text = <"Width">
					description = <"Measurement of the widest axis of the bladder, perpendicular to the length.">
				>
				["at0.4"] = <
					text = <"Depth">
					description = <"Measurement of the depth of the bladder, perpendicular to the length and width.">
				>
				["at0000"] = <
					text = <"Imaging examination">
					description = <"Findings observed in an imaging examination targeting a specified structure or region, using radiographic techniques.">
				>
				["at0004"] = <
					text = <"Imaging findings">
					description = <"Narrative description of the imaging findings observed during this examination.">
				>
				["at0005"] = <
					text = <"SLOT: Additional details">
					description = <"Additional structured details about imaging findings for the identified body structure or region, or findings for related substructures.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the imaging findings, not captured in other fields.">
				>
				["at0001"] = <
					text = <"Body structure">
					description = <"Identification of the body structure or region examined.">
				>
				["at0003"] = <
					text = <"Structured anatomical location">
					description = <"Structured details about the anatomical location of the identified body structure.">
					comment = <"If the anatomical location of the identified body structure is known or it has been identified in the 'Anatomical location' data element, this data element is redundant.">
				>
				["at0006"] = <
					text = <"Impression">
					description = <"Narrative concise, clinically relevant impression of all imaging findings for the identified body structure.">
					comment = <"Also referred to as 'Opinion'. May include a comparison with previous studies of the identified body structure, where appropriate.">
				>
				["at0002"] = <
					text = <"Anatomical location">
					description = <"Simple body site for the identified structure.">
					comment = <"If the anatomical location of the identified body structure is known, this data element is redundant.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0.1"] = <[SNOMED-CT::89837001]>
			>
		>
	>

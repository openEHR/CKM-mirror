archetype (adl_version=1.4; uid=ebe48cf8-4c29-47bf-8df7-60f815bcbaf7)
	openEHR-EHR-CLUSTER.specimen_transport.v0

concept
	[at0000]	-- Specimen transportation
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Natalia Strauch">
				["organisation"] = <"Medizinische Hochschule Hannover">
				["email"] = <"Strauch.Natalia@mh-hannover.de">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Liv Laugen">
				["organisation"] = <"​Oslo University Hospital, Norway">
				["email"] = <"liv.laugen@ous-hf.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Ian McNicoll">
		["organisation"] = <"freshEHR">
		["email"] = <"ian@freshehr.com">
		["date"] = <"2018-11-20">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"*To record details of the transportation process of a tissue specimen or a specimen container between two or more sites. (en)">
			use = <"*Use to record details of the transportation process of a tissue specimen or a specimen container between two or more sites.
This archetype is intended to be nested within the 'Transport details' slot in the CLUSTER.specimen archetype or 'Additional details' in the CLUSTER.specimen_container archetype, where appropriate. If it is necessary to record details about individual component steps within a multistep transportation process, this archetype can also be nested in the 'Transportation step' SLOT within this archetype.

This archetype can be used in the following use cases:
- within INSTRUCTION.service_request or another laboratory-related INSTRUCTION archetype to describe the required transportation for the specimen /specimen container.
- within a laboratory-related ACTION archetype to describe how a specimen/specimen container has been transported, particularly where this is relevant to potential issues with the quality of the specimen for analysis.
- within OBSERVATION.laboratory_test_result or other laboratory reporting OBSERVATION archetypes to record a summary of the specimen/specimen container transportation. (en)">
			keywords = <"*specimen, transport, collection, delivery, container (en)", ...>
			misuse = <"Nicht zur Beschreibung des Transports von nicht biologischem Material zu verwenden.">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere et sammendrag om transporten av et prøvemateriale eller en prøvebeholder mellom to eller flere destinasjoner.">
			use = <"Brukes for å registrere et sammendrag om kritiske deler av transportprosessen til et prøvemateriale eller en prøvebeholder mellom to eller flere steder. 
Denne arketypen vil vanligvis bli satt inn i SLOT'et \"Transportdetaljer\" i arketypen CLUSTER.specimen (Prøvemateriale) eller i SLOT'et \"Tilleggsinformasjon\" i arketypen CLUSTER.specimen_container (Prøvebeholder). Hvis det er nødvendig å registrere detaljer om individuelle transportetapper i en flertrinns transportprosess, kan denne arketypen nøstes i SLOTet 'Transportsekvens' i denne arketypen. 

Denne arketypen kan brukes i: 
- INSTRUCTION.service_request (Helsetjenesteforespørsel) eller en annen laboratorierelatert INSTRUCTION-arketype for å beskrive nødvendig transport for prøven/prøvebeholderen.
- en laboratorierelatert ACTION-arketype for å beskrive hvordan en prøve/prøvebeholder har blitt transportert, spesielt der dette er relevant for potensielle problemer med kvaliteten på prøven for analyse.
- OBSERVATION.laboratory_test_result (Laboratoriresultat) eller andre laboratorieresultat OBSERVATION-arketyper for å registrere et sammendrag av prøven/prøvetransporten.">
			keywords = <"prøve, transport, prøvemottak, prøvesending, beholder", ...>
			misuse = <"Skal ikke brukes for å beskrive transport av ikke biologisk prøvemateriale.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details of the transportation process of a tissue specimen or a specimen container between two or more sites.">
			use = <"Use to record details of the transportation process of a tissue specimen or a specimen container between two or more sites.
This archetype is intended to be nested within the 'Transport details' slot in the CLUSTER.specimen archetype or 'Additional details' in the CLUSTER.specimen_container archetype, where appropriate. If it is necessary to record details about individual component steps within a multistep transportation process, this archetype can also be nested in the 'Transportation step' SLOT within this archetype.

This archetype can be used in the following use cases:
- within INSTRUCTION.service_request or another laboratory-related INSTRUCTION archetype to describe the required transportation for the specimen /specimen container.
- within a laboratory-related ACTION archetype to describe how a specimen/specimen container has been transported, particularly where this is relevant to potential issues with the quality of the specimen for analysis.
- within OBSERVATION.laboratory_test_result or other laboratory reporting OBSERVATION archetypes to record a summary of the specimen/specimen container transportation.">
			keywords = <"specimen, transport, collection, delivery, container", ...>
			misuse = <"Not to be used to describe the transport of non-biological material.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Michael Anywar, Universitätsklinikum SH, Germany", "Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "Paulo Bandiera-Paiva, Federal University of Sao Paulo, Brazil", "Keisha Barwise, MOHW/IDB HSSP Project, Jamaica", "SB Bhattacharyya, Bhattacharyyas Clinical Records Research & Informatics LLP, India", "Marand d.o.o", "Candice de Lisser, Ministry of Health and Wellness, Jamaica", "Rosane Gotardo, Systema Ltda., Brazil", "Heather Grain, Llewelyn Grain Informatics, Australia", "Martin Grundberg, Cambio Healthcare Systems, Sweden", "Liv Laugen, ​Oslo University Hospital, Norway, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Neranga Liyanaarachchi, Ministry of Health, Postgraduate Institute of Medicine, Sri Lanka", "Pramil Liyanage, Ministry of Health, Sri Lanka", "Manisha Mantri, C-DAC, India", "Cecilia Mascia, CRS4, Italy", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Arunakiry Natarajan, medondo, Germany", "Jayashree Panickar, Karolinska Institute, Sweden", "Vanessa Pereira, Better - Pathfinder, Portugal", "Saeed Samie, Pars Hospital, Iran", "Natalia Strauch, Medizinische Hochschule Hannover, Germany", "Norwegian Review Summary, Nasjonal IKT HF, Norway", "Anders Thurin, VGR, Sweden", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)", "Wouter Zanen, Eurotranplant, Netherlands", "Lin Zhang, Taikang Insurance Group, China">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"4D4C6A85954A48E11B857A7D781E4C04">
		["build_uid"] = <"5e212114-920c-4705-924b-e387e99c42ea">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {    -- Specimen transportation
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {    -- Transport status
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0027] occurrences matches {0..1} matches {    -- Transport instruction
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0010] occurrences matches {0..*} matches {    -- Transport description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0008] occurrences matches {0..1} matches {    -- Origin site
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0023] occurrences matches {0..1} matches {    -- Origin site details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.person(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.organisation(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.address(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0004] occurrences matches {0..1} matches {    -- Departure date/time
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			ELEMENT[at0009] occurrences matches {0..1} matches {    -- Delivery site
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0022] occurrences matches {0..*} matches {    -- Delivery site details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.person(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.organisation(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.address(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {    -- Arrival date/time
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			ELEMENT[at0002] occurrences matches {0..*} matches {    -- Transporter identifier
				value matches {
					DV_IDENTIFIER matches {*}
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0017] occurrences matches {0..*} matches {    -- Transporter details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.person(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.organisation(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.address(-[a-zA-Z0-9_]+)*\.v1/}
			}
			CLUSTER[at0025] occurrences matches {0..1} matches {    -- Transportation step
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0015] occurrences matches {0..1} matches {    -- Transport sequence
						value matches {
							DV_COUNT matches {*}
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0026] occurrences matches {0..*} matches {    -- Transport step detail
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.specimen_transport\.v0/}
					}
				}
			}
			allow_archetype CLUSTER[at0024] occurrences matches {0..*} matches {    -- Additional details
				include
					archetype_id/value matches {/.*/}
			}
			ELEMENT[at0005] occurrences matches {0..1} matches {    -- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Specimen transportation">
					description = <"Details of the transportation process of a tissue specimen or specimen container between two or more sites.">
				>
				["at0001"] = <
					text = <"Transport status">
					description = <"The status of the transportation process for the specimen or the speciment container.">
					comment = <"For example: planned, in transit, or completed.">
				>
				["at0002"] = <
					text = <"Transporter identifier">
					description = <"Identifier of person or agency responsible for transporting the specimen or specimen container.">
				>
				["at0004"] = <
					text = <"Departure date/time">
					description = <"The date/time when the specimen or specimen container departed the site of origin.">
				>
				["at0005"] = <
					text = <"Comment">
					description = <"Additional narrative about the transportation process not captured in other fields.">
				>
				["at0007"] = <
					text = <"Arrival date/time">
					description = <"The date/time when the specimen or the specimen container arrived at the delivery.">
				>
				["at0008"] = <
					text = <"Origin site">
					description = <"Narrative description about the place where the specimen or specimen container is located prior to transport.">
					comment = <"For example: the site where the specimen was collected; or the site where the specimen has been stored.">
				>
				["at0009"] = <
					text = <"Delivery site">
					description = <"Narrative description about the place where the specimen or specimen container is located after transport.">
					comment = <"The site where the specimen/specimen container is to be delivered or has been delivered.">
				>
				["at0010"] = <
					text = <"Transport description">
					description = <"Narrative description about the actual transport process and handling of the specimen or specimen container.">
					comment = <"Record only details that may affect the result of the analyses or durability for the specimen. For example: 'The specimen was preserved on ice', 'The specimen was transported in room temperature' or 'the specimen container was left several hour in the sun'.">
				>
				["at0015"] = <
					text = <"Transport sequence">
					description = <"The transport sequence/step number.">
					comment = <"For example: 2 or step 2 of 3. If there is only one transport event or if this instance is the overview of all transports, then this data element is redundant.">
				>
				["at0017"] = <
					text = <"Transporter details">
					description = <"Structured details about the transporter.">
				>
				["at0022"] = <
					text = <"Delivery site details">
					description = <"Structured details about the the person and/or organisational recipient at delivery.">
					comment = <"For example: the name of a recipient or the address of an organisation receiving the specimen/specimen container.">
				>
				["at0023"] = <
					text = <"Origin site details">
					description = <"Structured details about the person and/or organisation sending/has sent the specimen or specimen container.">
					comment = <"For example: the name of the sender, or the address or the organisation sending the specimen/specimen container.">
				>
				["at0024"] = <
					text = <"Additional details">
					description = <"Additional details about the transportation process.">
				>
				["at0025"] = <
					text = <"Transportation step">
					description = <"">
				>
				["at0026"] = <
					text = <"Transport step detail">
					description = <"Details about a single component step within a multistep transportation process.">
				>
				["at0027"] = <
					text = <"Transport instruction">
					description = <"Narrative description about the intended transport process for the specimen or specimen container.">
					comment = <"For example: instructions about the need for temperature regulation.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Prøvetransport">
					description = <"Detaljer om transporten av et prøvemateriale eller en prøvebeholder mellom to eller flere destinasjoner.">
				>
				["at0001"] = <
					text = <"Transportstatus">
					description = <"Statusen til transporten av prøven eller prøvebeholderen.
">
					comment = <"For eksempel: planlagt, under transport eller fullført,">
				>
				["at0002"] = <
					text = <"Transportør Id">
					description = <"Identifikasjon til personen eller firmaet som er ansvarlig for å transportere prøven eller prøvebeholderen til laboratoriet.">
				>
				["at0004"] = <
					text = <"Transport start">
					description = <"Datoen/klokkeslettet da prøven eller prøvebeholderen forlot opprinnelsesstedet/hentestedet.">
				>
				["at0005"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om transporten som ikke dekkes av andre felt.">
				>
				["at0007"] = <
					text = <"Transport slutt">
					description = <"Datoen/klokkeslettet da prøven eller prøvebeholderen ankom bestemmelsesstedet.">
				>
				["at0008"] = <
					text = <"Hentested">
					description = <"Fritekstbeskrivelse av lokalisasjonen til prøvematerialet eller prøvebeholderen før transporten startet.">
					comment = <"For eksempel: lokalisasjonen der prøven ble tatt, eller stedet hvor prøven har blitt lagret.">
				>
				["at0009"] = <
					text = <"Leveringssted">
					description = <"Fritekstbeskrivelse av lokalisasjonen til prøvematerialet eller prøvebeholderen etter transporten.">
					comment = <"Lokalisasjonen hvor prøvemateriale eller prøvebeholderen skal leveres eller har blitt levert.">
				>
				["at0010"] = <
					text = <"Transportbeskrivelse">
					description = <"Fritekstbeskrivelse om selve transportprosessen og behandlingen av prøvematerialet eller prøvebeholderen.">
					comment = <"Registrer kun detaljer som kan påvirke resultatet av analysene eller holdbarheten for prøven.
Kommentar: For eksempel: \"Prøven ble oppbevart på is\", \"Prøven ble transportert i romtemperatur\" eller \"Prøvebeholderen sto flere timer i solen\".">
				>
				["at0015"] = <
					text = <"Transportetappe">
					description = <"Transportsekvens/trinn nummer.">
					comment = <"For eksempel: 2 eller etappe 2 av 3. Hvis det kun er én transportetappe/sekvens eller hvis denne forekomsten er et sammendrag av alle transportetappene, så er dette dataelementet overflødig.">
				>
				["at0017"] = <
					text = <"Transportør detaljer">
					description = <"Strukturerte detaljer om transportøren.">
				>
				["at0022"] = <
					text = <"Leveringssted detaljer">
					description = <"Strukturerte detaljer om mottaker, person og/eller organisasjon, av prøvemateriale og prøvebeholder.">
					comment = <"For eksempel: navnet til mottakeren eller adressen til organisasjonen som har mottatt prøvematerialet eller prøvebeholderen.">
				>
				["at0023"] = <
					text = <"Hentested detaljer">
					description = <"Strukturerte detaljer om personen og/eller organisasjonen som sender/har sendt prøvematerialet eller prøvebeholderen.">
					comment = <"For eksempel: navnet til avsenderen, eller en adresse til organisasjonen som sender prøven eller prøvebeholderen.">
				>
				["at0024"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere detaljer om transporten.">
				>
				["at0025"] = <
					text = <"Transportsekvens">
					description = <"">
				>
				["at0026"] = <
					text = <"Transportetappe detaljer">
					description = <"Detaljer om et enkelt trasnsportsekvens/etappe i en flertrinns transportprosess.">
				>
				["at0027"] = <
					text = <"Transportinstruksjon">
					description = <"Ytterligere fritekst om transporten av prøven eller prøvebeholderen som ikke dekkes av andre felt.">
					comment = <"For eksempel: instruksjon om behov for temperaturregulering.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"*Specimen transportation (en)">
					description = <"*Details of the transportation process of a tissue specimen or specimen container between two or more sites. (en)">
				>
				["at0001"] = <
					text = <"Transportstatus">
					description = <"Der Status des Transportprozesses für die Probe oder den Probenbehälter.">
					comment = <"Zum Beispiel: geplant, unterwegs oder abgeschlossen.">
				>
				["at0002"] = <
					text = <"Identifikator des Transporter">
					description = <"Kennung der Person oder Stelle, die für den Transport der Probe oder des Probenbehälters verantwortlich ist.">
				>
				["at0004"] = <
					text = <"Zeitpunkt des Ausgangs">
					description = <"Das Datum und die Uhrzeit, wann die Probe oder der Probenbehälter den Ursprungsort verlassen hat.">
				>
				["at0005"] = <
					text = <"Kommentar">
					description = <"Zusätzliche Beschreibung über den Transportprozess, die in anderen Feldern nicht erfasst wird.">
				>
				["at0007"] = <
					text = <"Zeitpunkt des Eingangs">
					description = <"Das Datum und die Uhrzeit des Eingangs der Probe oder des Probenbehälters bei der Lieferung.">
				>
				["at0008"] = <
					text = <"Ausgangsort">
					description = <"*Narrative description about the place where the specimen or specimen container is located prior to transport. (en)">
					comment = <"*For example: the site where the specimen was collected; or the site where the specimen has been stored. (en)">
				>
				["at0009"] = <
					text = <"Eingangsort">
					description = <"*Narrative description about the place where the specimen or specimen container is located after transport. (en)">
					comment = <"*The site where the specimen/specimen container is to be delivered or has been delivered. (en)">
				>
				["at0010"] = <
					text = <"Transportbeschreibung">
					description = <"Beschreibung über den eigentlichen Transportablaufs und die Handhabung der Probe oder des Probenbehälters.">
					comment = <"Notieren Sie nur Informationen, die das Ergebnis der Analysen oder die Haltbarkeit der Probe beeinflussen können. Zum Beispiel: „Die Probe wurde auf Eis gelagert“, „Die Probe wurde bei Raumtemperatur transportiert“ oder „Der Probenbehälter wurde mehrere Stunden in der Sonne gelassen“.">
				>
				["at0015"] = <
					text = <"Transportschrittfolge">
					description = <"Die Transportsequenz/Schrittnummer.">
					comment = <"Beispiel: 2 oder Schritt 2 von 3. Wenn es nur ein Transportereignis gibt oder diese Instanz die Übersicht aller Transporte ist, dann ist dieses Datenelement redundant.">
				>
				["at0017"] = <
					text = <"Angaben zum Transporter">
					description = <"Strukturierte Details zum Transporter.">
				>
				["at0022"] = <
					text = <"Details zum Eingangsort">
					description = <"Strukturierte Angaben zur Person und/oder Organisation als Empfänger der Zustellung.">
					comment = <"*For example: the name of a recipient or the address of an organisation receiving the specimen/specimen container. (en)">
				>
				["at0023"] = <
					text = <"Details zum Ausgangsort">
					description = <"*Structured details about the person and/or organisation sending/has sent the specimen or specimen container. (en)">
					comment = <"*For example: the name of the sender, or the address or the organisation sending the specimen/specimen container. (en)">
				>
				["at0024"] = <
					text = <"Zusätzliche Details">
					description = <"Zusätzliche Details zum Transportprozess.">
				>
				["at0025"] = <
					text = <"Transportschritt">
					description = <"">
				>
				["at0026"] = <
					text = <"Angabe zum Transportschritt">
					description = <"Details zu einem einzelnen Schritt innerhalb eines mehrstufigen Transportprozesses.">
				>
				["at0027"] = <
					text = <"Transportanweisung">
					description = <"Beschreibung über den beabsichtigten Transportablauf für die Probe oder den Probenbehälter.">
					comment = <"Zum Beispiel: Anweisungen zur Notwendigkeit einer Temperaturregelung.">
				>
			>
		>
	>

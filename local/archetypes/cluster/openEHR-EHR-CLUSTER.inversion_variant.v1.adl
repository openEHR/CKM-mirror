archetype (adl_version=1.4; uid=ee9f2fe2-e652-4324-a289-07eb3b720b89)
	openEHR-EHR-CLUSTER.inversion_variant.v1

concept
	[at0000]	-- Genomic inversion variant
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Aurelie Tomczak">
				["organisation"] = <"Institute of Pathology, University Hospital Heidelberg, Germany">
				["email"] = <"au.tomczak@yahoo.com">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Liv Laugen, Silje Ljosland Bakke">
				["organisation"] = <"Oslo University Hospital, Norway, Helse Vest IKT AS">
				["email"] = <"liv.laugen@ous-hf.no, silje.ljosland.bakke@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Cecilia Mascia">
		["organisation"] = <"CRS4, Italy">
		["email"] = <"cecilia.mascia@crs4.it">
		["date"] = <"2017-02-23">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Zur Beschreibung der in einer Sequenz beobachteten 'inversion variant' gemäß der HGVS-Nomenklatur.">
			use = <"*Use to record the details about an inversion variant of human DNA, observed in a genomic sequence.

This archetype has been specifically designed to be used in the ‘Structured variant' SLOT within the CLUSTER.genetic_variant archetype, but can also be used within other ENTRY or CLUSTER archetypes, where clinically appropriate.

In the examples in this archetype, lower case nucleotides are only intended to highlight the changed positions of the DNA sequence. (en)">
			keywords = <"Inversion", "Variante", "genetisch", "genomisch", "Variation">
			misuse = <"">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å dokumentere en inversjonsvariant funnet i en DNA- eller RNA-sekvensering, basert på HGVS nomenklaturen.">
			use = <"*Use to record the details about an inversion variant of human DNA, observed in a genomic sequence.

This archetype has been specifically designed to be used in the ‘Structured variant' SLOT within the CLUSTER.genetic_variant archetype, but can also be used within other ENTRY or CLUSTER archetypes, where clinically appropriate.

In the examples in this archetype, lower case nucleotides are only intended to highlight the changed positions of the DNA sequence. (en)">
			keywords = <"inversjon", "genetisk variasjon", "genetikk", "genom", "variant", "DNA", "RNA", "kromosom", "mutasjon", "nukleotid">
			misuse = <"Skal ikke brukes for å dokumentere en inversjon av et enkelt nukleotid (enkeltbaseforskjell), bruk CLUSTER.substitution_variant (Genetisk substitusjonvariant) arketypen til dette.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the details about an inversion variant of human DNA, observed in a genomic sequence.">
			use = <"Use to record the details about an inversion variant of human DNA, observed in a genomic sequence.

This archetype has been specifically designed to be used in the ‘Structured variant' SLOT within the CLUSTER.genetic_variant archetype, but can also be used within other ENTRY or CLUSTER archetypes, where clinically appropriate.

In the examples in this archetype, lower case nucleotides are only intended to highlight the changed positions of the DNA sequence.">
			keywords = <"inversion", "variation", "genetic", "genomic", "variant">
			misuse = <"Not to be used to record information about variants of non-human DNA, or any kind of RNA or protein.

Not to be used to record a one nucleotide inversion. Use the CLUSTER.substitution_variant archetype for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Silje Ljosland Bakke, Helse Vest IKT AS, Norway (openEHR Editor)", "Francesca Frexia, CRS4, Italy", "Gideon Giacomelli, Charité Berlin, Germany", "Christina Jaeger-Schmidt, Heidelberg University Hospital, Germany", "Florian Kaercher, Charité Berlin, Germany", "Liv Laugen, ​Oslo University Hospital, Norway, Norway", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Cecilia Mascia, CRS4, Italy (openEHR Editor)", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Andrej Orel, Marand d.o.o., Slovenia", "Simon Schumacher, HiGHmed, Germany", "Aurelie Tomczak, Uniklinikum Heidelberg, Germany (openEHR Editor)", "Paolo Uva, CRS4, Italy", "Gianluigi Zanetti, CRS4, Italy", "Gyri Aasland Gradek, Haukeland University Hospital, Norway", "Asbjørg Stray-Pedersen, Oslo University Hospital, Norway", "Toril Fagerheim, University Hospital of Northern Norway, Norway", "Camilla F. Skjelbred, Telemark Hospital HF, Norway", "Dag Erik Undlien, Oslo University Hospital, Norway", "Rune Østern, St Olavs Hospital, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"den Dunnen JT, Dalgleish R, Maglott DR, Hart RK, Greenblatt MS, McGowan-Jordan J, Roux AF, Smith T, Antonarakis SE, Taschner PE. HGVS Recommendations for the Description of Sequence Variants: 2016 Update. Hum Mutat. 2016 Jun;37(6):564-9. doi: 10.1002/humu.22981. Epub 2016 Mar 25. PubMed PMID: 26931183.

Sequence Variant Nomenclature - DNA Recommendations - Inversion Variant [Internet]. Human Genome Variation Society. [2019] - [cited 2020-02-26]. Available from: https://varnomen.hgvs.org/recommendations/DNA/variant/inversion/">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"D397897523A7D5BF82FC4F8E89C390E6">
		["build_uid"] = <"2b72c66c-9698-484e-a4d8-c4785d26b3da">
		["revision"] = <"1.1.0">
	>

definition
	CLUSTER[at0000] matches {    -- Genomic inversion variant
		items cardinality matches {2..*; ordered} matches {
			ELEMENT[at0001] matches {    -- Start position
				value matches {
					DV_COUNT matches {*}
				}
			}
			ELEMENT[at0004] matches {    -- End position
				value matches {
					DV_COUNT matches {*}
				}
			}
			ELEMENT[at0006] occurrences matches {0..1} matches {    -- Inverted sequence
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0007] occurrences matches {0..*} matches {    -- Reference sequence
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.reference_sequence(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Genomic inversion variant">
					description = <"A human genetic sequence change where more than one nucleotide replaces the original sequence with the reverse complement of the original sequence, compared to a genomic reference sequence.">
					comment = <"For example: changing 'TCAG' to 'CTGA'.">
				>
				["at0001"] = <
					text = <"Start position">
					description = <"Position of the first nucleotide of the inverted range, in relation to the reference sequence.">
					comment = <"For example: for a variant where the reference sequence 'CAtcagCCT' is changed to 'CActgaCCT', the start position is 3.">
				>
				["at0004"] = <
					text = <"End position">
					description = <"Position of the last nucleotide of the inverted range, in relation to the reference sequence.">
					comment = <"For example: for a variant where the reference sequence 'CAtcagCCT' is changed to 'CActgaCCT', the end position is 6.">
				>
				["at0006"] = <
					text = <"Inverted sequence">
					description = <"The inverted sequence.">
					comment = <"For example: for a variant where the reference sequence  'CAtcagCCT' is changed to 'CActgaCCT', the inverted sequence is 'TCAG'.">
				>
				["at0007"] = <
					text = <"Reference sequence">
					description = <"The sequence file used as a reference to describe this variant.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Genetisk variant - Inversjon">
					description = <"En endring i gensekvensen hvor mer enn ett nukleotid (et DNA eller RNA-segment) er tatt ut og satt inn igjen i motsatt orientering (reversering av rekkefølgen i et kromosomsegment), sammenliknet med referansesekvensen.">
					comment = <"For eksempel: endring fra 'TCAG' til 'CTGA'.">
				>
				["at0001"] = <
					text = <"Startposisjonen">
					description = <"Posisjonen til det første nukleotidet i inversjonen i forhold til referansesekvensen.">
					comment = <"*For example: for a variant where the reference sequence 'CAtcagCCT' is changed to 'CActgaCCT', the start position is 3. (en)">
				>
				["at0004"] = <
					text = <"Sluttposisjonen">
					description = <"Posisjonen til det siste nukleotidet i inversjonen i forhold til referansesekvensen.">
					comment = <"*For example: for a variant where the reference sequence 'CAtcagCCT' is changed to 'CActgaCCT', the end position is 6. (en)">
				>
				["at0006"] = <
					text = <"Inverterte sekvensen">
					description = <"Den omvendte/inverterte nukleotidsekvensen på referansesekvensen.">
					comment = <"*For example: for a variant where the reference sequence  'CAtcagCCT' is changed to 'CActgaCCT', the inverted sequence is 'TCAG'. (en)">
				>
				["at0007"] = <
					text = <"Referansesekvens">
					description = <"Sekvensfilen som har blitt brukt som en referanse for å beskrive varianten.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Genetic inversion variant">
					description = <"A genetic sequence change where, compared to a reference sequence, more than one nucleotide replacing the original sequence are the reverse complement of the original sequence.">
					comment = <"*For example: changing 'TCAG' to 'CTGA'. (en)">
				>
				["at0001"] = <
					text = <"Start position">
					description = <"*Position of the first nucleotide of the inverted range, in relation to the reference sequence. (en)">
					comment = <"*For example: for a variant where the reference sequence 'CAtcagCCT' is changed to 'CActgaCCT', the start position is 3. (en)">
				>
				["at0004"] = <
					text = <"End position">
					description = <"*Position of the last nucleotide of the inverted range, in relation to the reference sequence. (en)">
					comment = <"*For example: for a variant where the reference sequence 'CAtcagCCT' is changed to 'CActgaCCT', the end position is 6. (en)">
				>
				["at0006"] = <
					text = <"Inverted sequence">
					description = <"*The nucleotide sequence of the reference sequence at the given positions. (en)">
					comment = <"*For example: for a variant where the reference sequence  'CAtcagCCT' is changed to 'CActgaCCT', the inverted sequence is 'TCAG'. (en)">
				>
				["at0007"] = <
					text = <"Reference sequence">
					description = <"The sequence file that has been used as a reference to describe the variant.">
				>
			>
		>
	>

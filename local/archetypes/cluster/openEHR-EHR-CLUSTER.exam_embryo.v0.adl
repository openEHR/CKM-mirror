archetype (adl_version=1.4; uid=b7abefdc-0e0e-4f72-abb0-07537b8dc16f)
	openEHR-EHR-CLUSTER.exam_embryo.v0

concept
	[at0000]	-- Examination of a cleavage-stage embryo
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Peter Fedorcsak">
				["organisation"] = <"University of Oslo">
				["email"] = <"peterfe@uio.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Peter Fedorcsak">
		["organisation"] = <"Oslo University Hospital">
		["email"] = <"peter.fedorcsak@medisin.uio.no">
		["date"] = <"2021-10-26">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere strukturerte observasjoner av et humant embryo i tidlige delingsstadier ved assistert befruktning.">
			use = <"Brukes til å registrere morfologiske observasjoner som passer for tidlige delingsstadier av embryoutviklingen ved assistert befruktning.

Arketypen er laget spesifikt for å brukes i \"Undersøkelsesdetaljer\" SLOT i arketypen 'OBSERVATION.embryo_assessment' eller 'ACTION.procedure', men kan også brukes innen andre ENTRY- og CLUSTER-arketyper der det er klinisk passende.">
			keywords = <"in vitro, befruktning, IVF, reproduksjon, zygote, fertilisert, befruktet, egg, eggcelle, oocytt, embryo, blastocyst, assistert", ...>
			misuse = <"Skal ikke brukes til å registrere morfologiske observasjoner av oocytter, zygoter og blastocyster - bruk spesifikke CLUSTER arketyper.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record structured observations of the human cleavage stage embryo during assisted reproduction treatment.">
			use = <"Use to record morphological observations appropriate for the cleavage stage of human embryo development during assisted reproduction treatment. 

This archetype has been specifically designed to be used in the 'Observations detail' SLOT within the 'OBSERVATION.embryo_assessment' or 'ACTION.procedure' archetype, but can also be used within other ENTRY or CLUSTER archetypes, where clinically appropriate.">
			keywords = <"in vitro, fertilization, IVF, reproductive medicine, zygote, fertilised, fertilized, oocyte, egg, fertilisation, embryo, reproduction, ART, blastocyst", ...>
			misuse = <"Not to be used for recording observations on oocytes, zygotes and blastocysts - use specific CLUSTER archetypes for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Peter Fedorcsak, University of Oslo, Norway (openEHR Editor)", "Liv Laugen, ​Oslo University Hospital, Norway, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Trine Skuland, Oslo University Hospital", "Gareth Greggains, Oslo University Hospital">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Alpha Scientists in Reproductive, M. and E.S.I.G.o. Embryology, The Istanbul consensus workshop on embryo assessment: proceedings of an expert meeting. Human Reproduction, 2011. 26(6): p. 1270-1283.

Prados, F.J., et al., The cleavage stage embryo. Human Reproduction, 2012. 27(suppl_1): p. i50-i71.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"361B371290E0C98C40552F488A56CC6C">
		["build_uid"] = <"c48a76e1-dd3a-4e8f-a9e0-a8be1014f3b2">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact https://www.snomed.org/snomed-ct/get-snomed or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {    -- Examination of a cleavage-stage embryo
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0028] occurrences matches {0..1} matches {    -- Number of cells
				value matches {
					DV_COUNT matches {*}
				}
			}
			ELEMENT[at0030] occurrences matches {0..1} matches {    -- Fragmentation
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0031,    -- None
							at0032,    -- Mild fragmentation
							at0033,    -- Moderate fragmentation
							at0034]    -- Severe fragmentation
						}
					}
					DV_TEXT matches {*}
					DV_PROPORTION matches {*}
				}
			}
			ELEMENT[at0035] occurrences matches {0..*} matches {    -- Blastomere size
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0036,    -- Equal
							at0037,    -- Unequal
							at0053,    -- Stage specific
							at0054]    -- Non-stage specific
						}
					}
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0038] occurrences matches {0..1} matches {    -- Nucleation
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0039,    -- No visible nuclei
							at0040,    -- Mononucleation
							at0041,    -- Binucleation
							at0051,    -- Multinucleation
							at0052]    -- Broad multinucleation
						}
					}
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0042] occurrences matches {0..1} matches {    -- Cytoplasmic morphology
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0043] occurrences matches {0..1} matches {    -- Spatial distribution of cells
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0044] occurrences matches {0..1} matches {    -- Compaction
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0045,    -- None
							at0046,    -- Minimal
							at0047,    -- Moderate
							at0048]    -- Complete
						}
					}
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0049] occurrences matches {0..*} matches {    -- Other morphological features
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0055] occurrences matches {0..1} matches {    -- Embryo morphology score
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination of a cleavage-stage embryo">
					description = <"Morphological findings obtained by microscopy of the human cleavage-stage embryo during assisted reproduction treatment.">
				>
				["at0028"] = <
					text = <"Number of cells">
					description = <"Number of cells in a cleavage-stage embryo.">
				>
				["at0030"] = <
					text = <"Fragmentation">
					description = <"Cytoplasmic fragmentation in a cleavage stage embryo.">
				>
				["at0031"] = <
					text = <"None">
					description = <"Absence of cytoplasmic fragments.">
				>
				["at0032"] = <
					text = <"Mild fragmentation">
					description = <"Cytoplasmic fragments cover < 10% of the total cytoplasmic volume.">
				>
				["at0033"] = <
					text = <"Moderate fragmentation">
					description = <"Cytoplasmic fragments cover 10 - 25% of the total cytoplasmic volume.">
				>
				["at0034"] = <
					text = <"Severe fragmentation">
					description = <"Cytoplasmic fragments cover > 25% of the total cytoplasmic volume.">
				>
				["at0035"] = <
					text = <"Blastomere size">
					description = <"Relative blastomere size.">
				>
				["at0036"] = <
					text = <"Equal">
					description = <"Equally sized blastomeres.">
				>
				["at0037"] = <
					text = <"Unequal">
					description = <"Unequally sized blastomeres.">
				>
				["at0038"] = <
					text = <"Nucleation">
					description = <"Presence or absence of nuclei in the blastomeres of the cleavage stage embryo.">
				>
				["at0039"] = <
					text = <"No visible nuclei">
					description = <"Nuclei are not visible.">
				>
				["at0040"] = <
					text = <"Mononucleation">
					description = <"Only a single visible nucleus in all blastomeres.">
				>
				["at0041"] = <
					text = <"Binucleation">
					description = <"Two visible nuclei in one blastomere.">
				>
				["at0042"] = <
					text = <"Cytoplasmic morphology">
					description = <"Morphology of the cytoplasm of cells in cleavage stage embryo.">
					comment = <"May contain description of abnomalies, such as granularity, pitting, and presence of vacuoles.">
				>
				["at0043"] = <
					text = <"Spatial distribution of cells">
					description = <"Spatial distribution of cells in a cleavage stage embryo.">
					comment = <"The cells of early embryo may be arranged in pyramidal, clover or irregular manner.">
				>
				["at0044"] = <
					text = <"Compaction">
					description = <"Degree of compaction in a cleavage stage embryo.">
				>
				["at0045"] = <
					text = <"None">
					description = <"Absence of compaction.">
				>
				["at0046"] = <
					text = <"Minimal">
					description = <"Less than 50% of the total embryonic volume is compacted.">
				>
				["at0047"] = <
					text = <"Moderate">
					description = <"Over 50% of the total embryonic volume is compacted.">
				>
				["at0048"] = <
					text = <"Complete">
					description = <"The complete volume of the embryo is compacted.">
				>
				["at0049"] = <
					text = <"Other morphological features">
					description = <"Additional morphological features not captured by specific parameters.">
				>
				["at0051"] = <
					text = <"Multinucleation">
					description = <"Multiple visible nuclei in one blastomere.">
				>
				["at0052"] = <
					text = <"Broad multinucleation">
					description = <"Two or more visible nuclei in two or more blastomeres.">
				>
				["at0053"] = <
					text = <"Stage specific">
					description = <"Stage specific cells size.">
				>
				["at0054"] = <
					text = <"Non-stage specific">
					description = <"Non-stage specific cells size.">
				>
				["at0055"] = <
					text = <"Embryo morphology score">
					description = <"Overall grading or score based on recorded morphological features the cleavage-stage embryo.">
					comment = <"Use of an established morphology assessment classification is preferred.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Undersøkelse av et humant embryo i tidlige delingsstadier">
					description = <"Morfologiske funn ved mikroskopisk undersøkelse av et humant embryo i tidlige delingsstadier ved assistert befruktning.">
				>
				["at0028"] = <
					text = <"Antall celler">
					description = <"Antall celler i embryoet.">
				>
				["at0030"] = <
					text = <"Fragmenteringsgrad">
					description = <"Grad av cytoplasmatisk fragmentering hos embryoet.">
				>
				["at0031"] = <
					text = <"Ingen">
					description = <"Ingen cytoplasmatiske fragmenter.">
				>
				["at0032"] = <
					text = <"Mild fragmentering">
					description = <"Cytoplasmatiske fragmenter dekker mindre enn 10% av totalt cytoplasmatisk volum.">
				>
				["at0033"] = <
					text = <"Moderat fragmentering">
					description = <"Cytoplasmatiske fragmenter dekker 10 - 25% av totalt cytoplasmatisk volum.">
				>
				["at0034"] = <
					text = <"Alvorlig fragmentering">
					description = <"Cytoplasmatiske fragmenter dekker mer enn 25% av totalt cytoplasmatisk volum.">
				>
				["at0035"] = <
					text = <"Cellestørrelse">
					description = <"Relativ størrelse på cellene i embryoet.">
				>
				["at0036"] = <
					text = <"Lik">
					description = <"Celler av lik størrelse.">
				>
				["at0037"] = <
					text = <"Ulik">
					description = <"Celler med ulik størrelse.">
				>
				["at0038"] = <
					text = <"Kjerner">
					description = <"Tilstedeværelse eller fravær av kjerner i embryoets celler.">
				>
				["at0039"] = <
					text = <"Ingen synlige kjerner">
					description = <"Kjernene er ikke synlige.">
				>
				["at0040"] = <
					text = <"Enkeltkjernede celler">
					description = <"Bare én kjerne sees i alle cellene i embryoet.">
				>
				["at0041"] = <
					text = <"Tokjernet celle">
					description = <"To synlige kjerner i en celle.">
				>
				["at0042"] = <
					text = <"Morfologi av cytoplasma">
					description = <"Morfologi av cytoplasmaet i cellene til embryoet.">
					comment = <"Kan brukes for å beskrive avvikende cytoplasmamorfologi, slik som granulering, dannelse av groper og vakuoler.">
				>
				["at0043"] = <
					text = <"Organisering av celler">
					description = <"Geometrisk organisering av cellene i embryoet.">
					comment = <"Cellene i et embryo kan for eksempel være organisert som en tetraedre eller pyramide, som et kløverblad eller andre irregulære former.">
				>
				["at0044"] = <
					text = <"Kompaktering">
					description = <"Grad av kompaktering i embryo i delingsstadiene.">
				>
				["at0045"] = <
					text = <"Ingen">
					description = <"Ingen kompaktering.">
				>
				["at0046"] = <
					text = <"Minimal">
					description = <"Mindre enn 50% av embryoets totalt volum er kompaktert.">
				>
				["at0047"] = <
					text = <"Moderat">
					description = <"Over 50% av embryoets totalt volum er kompaktert.">
				>
				["at0048"] = <
					text = <"Komplett">
					description = <"Alle embryoets celler er kompaktert.">
				>
				["at0049"] = <
					text = <"Andre morfologiske observasjoner">
					description = <"Ytterligere morfologiske observasjoner av embryoet som ikke dekkes av de andre spesifiserte elementene.">
				>
				["at0051"] = <
					text = <"Flerkjernet celle">
					description = <"Flere synlige kjerner i en celle.">
				>
				["at0052"] = <
					text = <"Flerkjernede celler.">
					description = <"To eller flere synlige kjerner i to eller flere celler.">
				>
				["at0053"] = <
					text = <"Stadium-spesifikk">
					description = <"Stadium-spesifikk cellestørrelse.">
				>
				["at0054"] = <
					text = <"Ikke stadium-spesifikk">
					description = <"Ikke stadium-spesifikk cellestørrelse.">
				>
				["at0055"] = <
					text = <"Embryo morfologisk skår">
					description = <"Overordnet gradering eller skår basert på embryoets registrerte, morfologiske egenskaper.">
					comment = <"Bruk av en etablert skala for vurdering av embryoets morfologi anbefales.">
				>
			>
		>
	>

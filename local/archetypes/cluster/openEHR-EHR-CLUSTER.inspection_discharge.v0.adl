archetype (adl_version=1.4; uid=45219794-a2b2-42f5-b39d-7cc753b81465)
	openEHR-EHR-CLUSTER.inspection_discharge.v0

concept
	[at0000]	-- Inspection of discharge
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2015-06-25">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording a narrative description and clinical interpretation of the findings observed during the inspection of a discharge from an identified body site.">
			use = <"Use to record a narrative description of the overall findings observed during the inspection of a discharge from an identified body site. This initial draft archetype consists only of the core examination pattern data elements (as per CLUSTER.exam_pattern) and will likely be expanded further over time as specific clinical requirements are identified.

This archetype has been specifically designed to be used in the 'Examination findings' SLOT within the CLUSTER.exam_cervix, CLUSTER.exam.wound and similar archetypes, but can also be used within other OBSERVATION or CLUSTER archetypes, where clinically appropriate..

The CLUSTER.exclusion_exam archetype can be nested within the 'Examination not done' SLOT to optionally record explicit details about the examination not being performed.

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical Description' data element.">
			misuse = <"Not to be used for recording data not related to the inspection of a discharge from an identified body site.

Not to be used for recording the clinical history - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom.

Not to be used to record stand-alone clinical observations or measurements or test results - use specific OBSERVATION archetypes. For example OBSERVATION.blood_pressure or OBSERVATION.imaging_exam.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Bergen Hospital Trust, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "John Tore Valand, Haukeland Universitetssjukehus, Norway", "Heather Leslie, Ocean Informatics, Australia">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics<heather.leslie@oceaninformatics.com>">
		["revision"] = <"0.0.1-alpha">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["build_uid"] = <"5a2e6afa-2c5f-44f6-8a40-c1e34ca27c57">
		["original_publisher"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"88BBA116B4B105F68120170A06298937">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	CLUSTER[at0000] matches {	-- Inspection of discharge
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Body site
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0017] occurrences matches {0..*} matches {	-- Structured body site
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_clock(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- Presence
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0011, 	-- Present
							at0012, 	-- Absent
							at0013]	-- Indeterminate
						}
					}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0014] occurrences matches {0..*} matches {	-- Amount
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0015] occurrences matches {0..1} matches {	-- Appearance
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0016] occurrences matches {0..1} matches {	-- Odour
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Multimedia representation
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {	-- Clinical interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Inspection of discharge">
					description = <"Findings observed during the inspection of a discharge from an identified body site.">
				>
				["at0001"] = <
					text = <"Body site">
					description = <"Identification of the body site under examination.">
					comment = <"If the body site has previously been identified in another archetype in which this archetype is nested, this data element becomes redundant.">
				>
				["at0002"] = <
					text = <"Presence">
					description = <"Statement that no abnormality was detected (NAD) on inspection at the identified body site.">
				>
				["at0003"] = <
					text = <"Clinical description">
					description = <"Narrative description of the overall findings observed during the inspection at the identified body site.">
				>
				["at0005"] = <
					text = <"Multimedia representation">
					description = <"Digital image, video or diagram representing the inspection findings.">
				>
				["at0006"] = <
					text = <"Clinical interpretation">
					description = <"Single word, phrase or brief description that represents the clinical meaning and significance of the inspection findings.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the inspection findings, not captured in other fields.">
				>
				["at0011"] = <
					text = <"Present">
					description = <"Discharge is observed at the identified body site.">
				>
				["at0012"] = <
					text = <"Absent">
					description = <"Discharge is not observed at the identified body site.">
				>
				["at0013"] = <
					text = <"Indeterminate">
					description = <"It is not possible to tell if discharge is present at the identified body site.">
				>
				["at0014"] = <
					text = <"Amount">
					description = <"Description of the amount of discharge at the identified body site.">
					comment = <"Can be a free text narrative or coded to a value set such as small/medium/profuse or normal/increased in a template.">
				>
				["at0015"] = <
					text = <"Appearance">
					description = <"Description of the appearance of discharge at the identified body site.">
				>
				["at0016"] = <
					text = <"Odour">
					description = <"Description of the smell of discharge at the identified body site.">
				>
				["at0017"] = <
					text = <"Structured body site">
					description = <"A structured anatomical location of the finding.">
					comment = <"If the body site has previously been identified in another archetype in which this archetype is then nested, this SLOT becomes redundant.">
				>
			>
		>
	>

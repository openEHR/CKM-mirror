archetype (adl_version=1.4; uid=aa04836a-789a-4ceb-8dd9-b0415b7629e8)
	openEHR-EHR-CLUSTER.imaging_series.v0

concept
	[at0000]	-- Imaging series
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2022-01-25">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about a DICOM series.">
			use = <"Use to record details about a DICOM series.

This archetype has been designed to be nested within the 'Series details' and 'Comparison series details' SLOTs in the OBSERVATION.imaging_exam_result or other archetypes where clinically relevant.">
			keywords = <"DICOM", "radiology", "series", "image">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Digital imaging and Communications in Medicine (DICOM) [Internet]. Medical Imaging Technology Association (MITA); [cited 2022 Jan 25]. Available from: https://www.dicomstandard.org/current.

HL7 FHIR Resource - DiagnosticReport v4.0.1 R4 [Internet]. Health Level Seven International; [accessed 2021 Aug 04]. Available from: https://www.hl7.org/fhir/diagnosticreport.html.

HL7 FHIR Resource - ImagingStudy v4.0.1 R4 [Internet]. Health Level Seven International; [accessed 2021 Aug 04]. Available from: http://hl7.org/fhir/imagingstudy.html.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"3E5A09A665FE2306863572A4BF5F2363">
		["build_uid"] = <"446cfc60-18f0-4d7e-8ec0-96344fc6c25b">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {    -- Imaging series details
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0016] occurrences matches {0..1} matches {    -- Series instance identifier
				value matches {
					DV_IDENTIFIER matches {*}
				}
			}
			ELEMENT[at0017] occurrences matches {0..1} matches {    -- Series number
				value matches {
					DV_COUNT matches {*}
				}
			}
			ELEMENT[at0022] occurrences matches {0..1} matches {    -- Series description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0018] occurrences matches {0..1} matches {    -- Modality
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0010] occurrences matches {0..1} matches {    -- Number of instances
				value matches {
					DV_COUNT matches {*}
				}
			}
			ELEMENT[at0019] occurrences matches {0..*} matches {    -- Series end point
				value matches {
					DV_URI matches {*}
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0021] occurrences matches {0..*} matches {    -- Device
				include
					archetype_id/value matches {/.*/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Imaging series">
					description = <"Details about a series of images, as part of a DICOM study.">
				>
				["at0010"] = <
					text = <"Number of instances">
					description = <"The number of instances contained in the series.">
					comment = <"Equivalent to ImagingStudy.series.numberOfInstances in FHIR.">
				>
				["at0016"] = <
					text = <"Series instance identifier">
					description = <"Unique identifier for the imaging series assigned by the radiology service.">
					comment = <"Equivalent to FHIR ImagingStudy.identifier and DICOM Series Instance UID (0020,000E).">
				>
				["at0017"] = <
					text = <"Series number">
					description = <"The numeric identifier of this series in the study.">
					comment = <"Equivalent to ImagingStudy.series.number in FHIR.">
				>
				["at0018"] = <
					text = <"Modality">
					description = <"The type of diagnostic equipment, or function or technique of that equipment, that originally acquired, the data used to create the image series.">
					comment = <"Coding of the 'Modality' with a value set is recommended. For example, the DICOM CID 29 Acquisition Modality value set. Equivalent to ImagingStudy.series.modality in FHIR.">
				>
				["at0019"] = <
					text = <"Series end point">
					description = <"Location of where to query for imaging content and metadata (QIDO-RS, WADO-RS, or WADO-URI).">
					comment = <"The network service providing access (e.g., query, view, or retrieval) for the series. Equivalent to ImagingStudy.series.endpoint in FHIR.">
				>
				["at0021"] = <
					text = <"Device">
					description = <"Details about imaging device/s used to capture the series.">
				>
				["at0022"] = <
					text = <"Series description">
					description = <"Description about the series.">
					comment = <"Equivalent to Series description (0008, 103E) in DICOM.">
				>
			>
		>
	>

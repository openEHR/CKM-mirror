archetype (adl_version=1.4; uid=2790f093-d7dc-4ee8-8236-881f853ec9b7)
	openEHR-EHR-CLUSTER.address.v0

concept
	[at0000]	-- Address
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Natalia Strauch">
				["organisation"] = <"Medizinische Hochschule Hannover">
				["email"] = <"Strauch.Natalia@mh-hannover.de">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Liv Laugen, John Tore Valand">
				["organisation"] = <"​Oslo University Hospital, Norway, Helse Bergen">
				["email"] = <"liv.laugen@ous-hf.no, john.tore.valand@helse-bergen.no, john.tore.valand@helse-vest-ikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2021-04-14">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Zur Darstellung von Details zu einer Haus- oder Postadresse zur Verwendung in einer Gesundheitsakte.">
			use = <"Verwenden Sie diese Archetyp, um Details zu einer physischen oder postalischen Adresse darzustellen, die als Teil einer klinischen Aufzeichnung in einer Gesundheitsakte erforderlich ist. Beispiele beinhalten:
- die Adresse einer teilnehmenden Organisation für häusliche Pflege bei einer Überweisung an einen anderen Gesundheitsdienstleister; oder
- die Adresse einer Person oder Organisation, die eine physische Kopie einer Vorab-Pflegerichtlinie besitzt; oder
- die Adresse eines Sturz- oder Unfallortes.

Der Umfang dieses Archetyps wurde darauf beschränkt, nur die gebräuchlichsten Komponenten für eine einfache Adresse zu enthalten. Detailliertere Adresskomponenten sind im Archetyp CLUSTER.structured_address enthalten, der im SLOT \"Strukturierte Adressdaten\" innerhalb dieses Archetyps verschachtelt ist.

In den meisten Anwendungsfällen, in denen eine Adresse in eine Gesundheitsakte aufgenommen werden muss, reicht die Freitexteingabe in einem oder mehreren Elementen der \"Adresse unstrukturiert\" aus, um eine einfache Adresse zu erfassen. In Situationen, in denen detailliertere Details zu einer oder mehreren Adresskomponenten erforderlich sind, verwenden Sie die zusätzlichen strukturierten Datenelemente im Archetyp CLUSTER.structured_address. Wenn der Archetyp CLUSTER.structured_address innerhalb des SLOTs \"Strukturierte Adressdaten\" verschachtelt ist, können einige oder alle der strukturierten Adresselemente verkettet und im Element \"Adresse unstrukturiert\" dargestellt werden.

Dieser Archetyp könnte auch als Ersatz für formelle demografische Daten verwendet werden, wenn ein Template mit Domänenexperten überprüft wird – beispielsweise bei einer Bewertung, bei der Prüfer eine Adresse in einem Bewertungsformular erwarten würden. Dieser CLUSTER.address könnte auch als Stellvertreter für eine Adresse in formalen demografischen Adressdaten verwendet werden, wenn ein Template mit Klinikern überprüft wird – zum Beispiel bei einer Bewertung, bei der Prüfer erwarten würden, dass die vollständigen Kontaktdaten der Person oben auf dem Bewertungsformular angezeigt werden.">
			keywords = <"Adresse", "Anschrift", "postalisch", "Mailing", "Heimat", "Geschäft", "physisch">
			misuse = <"Darf nicht verwendet werden, um eine Adresse im Rahmen der Führung eines offiziellen demografischen Registers oder Index zu erfassen. Verwenden Sie zu diesem Zweck einen formalen Master Patient Index oder Health Provider Index oder Archetypen, die auf dem openEHR Demographic Information Mode basieren.">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere detaljer om en fysisk adresse eller postadresse i en pasientjournal.">
			use = <"Brukes for å registrere detaljer om en fysisk adresse eller postadresse i en pasientjournal.

For eksempel:
- Adressen til hjemmetjeneste eller en annen helsetjenesteyter.
- Adressen til en person eller organisasjon som oppbevarer fremtidige behandlingsvalg.
- Adressen til hendelsestedet til en ulykke eller et fall.

Målet for denne arketypen er å dekke de vanligste komponentene for en enkel adresse. Mer detaljerte adressekomponenter finnes i arketypen CLUSTER.structured_address, som er laget for bruk i SLOTet \"Strukturert adresse\" i denne arketypen.

I de mest vanlige brukscasene der navn skal registreres i pasientjournalen, vil fritekst registrering i ett eller flere \"Ustrukturert adresse\" elementer være tilstrekkelig.I tilfeller der man har behov for detaljer om en eller flere adressekomponenter, bruk arketypen CLUSTER.structured_address i SLOTet \"Strukturert adresse\". I tilfeller der arketypen CLUSTER.structured_address er lagt i SLOTet \"Strukturert adresse\", kan noen eller alle de strukturerte navnelementene kobles sammen og vises i dataelementet \"Ustrukturert adresse\".

Denne arketypen kan også brukes som en midlertidig erstatning for formelle demografiske data når man gjennomgår et templat med domeneeksperter - for eksempel en vurdering der man i templatet forventer å se detaljer om en adresse i eller på toppen av et vurderingsskjema.">
			keywords = <"adresse, postadresse, hjem, arbeid, fysisk", ...>
			misuse = <"Brukes ikke til å representere eller erstatte de formelle demografiske detaljene for formell identitetsforvaltning eller for å opprettholde et demografisk register eller en indeks. Bruk formelle pasientregister, helsepersonellregister eller arketyper basert på openEHR demografisk informasjonsmodell til dette formålet.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about a physical or postal address for use within a health record.">
			use = <"Use to record details about a physical or postal address required as part of clinical recording within a health record. Examples include:
- the address of an attending home care organisation in a referral to another healthcare provider; or
- the address of a person or organisation that holds a physical copy of an advance care directive; or
- the address of a location of a fall or accident.

The scope of this archetype has been constrained to carry only the most common components for a simple address. More detailed address components are included in the CLUSTER.structured_address archetype, designed to nest in the 'Structured address' SLOT within this archetype. 

In most common use cases where an address needs to be included within a health record, free text entry within one or more 'Unstructured address' elements will be sufficient to record a simple address. In situations when finer-grained detail about one or more address components is required, use the additional structured data elements in the CLUSTER.structured address archetype. If the CLUSTER.structured_address archetype is nested within the 'Structured address' SLOT, any or all of the structured name elements can be concatenated and represented in the 'Unstructured address' element.

This archetype could also be used as a proxy for formal demographic data when reviewing a template with domain experts - for example, an assessment where reviewers would expect to see an address within an assessment form. This CLUSTER.address could also be used as a proxy for an address in formal demographic address data when reviewing a template with clinicians - for example, an assessment where reviewers would expect to see the individual's full contact details at the top of the assessment form.">
			keywords = <"address", "postal", "mailing", "home", "business", "physical">
			misuse = <"Not to be used to record an address as part of maintaining an official demographic register or index. Use a formal Master Patient Index or Health Provider Index for this purpose, or archetypes based on the openEHR Demographic Information Model.">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Health Care Client Identification - AS 5017-2006. New South Wales (AU): Standards Australia; 2006.

Health Care Provider Identification - AS 4846-2006. New South Wales (AU): Standards Australia; 2006.

Health informatics — Identification of subjects of health care - ISO/TS 22220:2007. Geneva: International Organization for Standardization; 2007.

HL7 FHIR Resource - Person v4.0.1: R4 [Internet]. Health Level Seven International; [accessed 2021 05 04]. Available from: http://hl7.org/fhir/2021May/person.html.

HL7 FHIR Resource - Organization v4.0.1: R4 [Internet]. Health Level Seven International; [accessed 2021 05 04]. Available from: http://hl7.org/fhir/2021May/organization.html">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"8C2C056384179DB2FC5B1660934B5B12">
		["build_uid"] = <"72566cf8-2fa4-4ab5-9bd0-522bba720d8f">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {    -- Address
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {    -- Type
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0002,    -- Physical
							at0003,    -- Postal
							at0004]    -- Both
						}
					}
				}
			}
			ELEMENT[at0005] occurrences matches {0..1} matches {    -- Use
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0006,    -- Business
							at0007,    -- Residential
							at0008]    -- Temporary accommodation
						}
					}
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0009] occurrences matches {0..*} matches {    -- Unstructured address
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {    -- Structured address details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.structured_address\.v0|openEHR-EHR-CLUSTER\.structured_address(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0015] occurrences matches {0..*} matches {    -- Town
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0016] occurrences matches {0..*} matches {    -- District/County
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0017] occurrences matches {0..1} matches {    -- State/Territory/Province
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0018] occurrences matches {0..1} matches {    -- Postcode
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0019] occurrences matches {0..1} matches {    -- Country
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0020] occurrences matches {0..1} matches {    -- Postal delivery point
				value matches {
					DV_IDENTIFIER matches {*}
				}
			}
			allow_archetype CLUSTER[at0024] occurrences matches {0..*} matches {    -- Additional details
				include
					archetype_id/value matches {/.*/}
			}
			ELEMENT[at0023] occurrences matches {0..1} matches {    -- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Address">
					description = <"A physical or postal address for use within a health record.">
				>
				["at0001"] = <
					text = <"Type">
					description = <"The type of address.">
					comment = <"For example: physical or postal.">
				>
				["at0002"] = <
					text = <"Physical">
					description = <"A physical location that can be visited.">
				>
				["at0003"] = <
					text = <"Postal">
					description = <"Address used as a destination for mailing letters or parcels.">
				>
				["at0004"] = <
					text = <"Both">
					description = <"Address of a physical location, also used as a destination for mail.">
				>
				["at0005"] = <
					text = <"Use">
					description = <"The primary purpose or use for the address.">
				>
				["at0006"] = <
					text = <"Business">
					description = <"Address of the physical location of a business or office.">
				>
				["at0007"] = <
					text = <"Residential">
					description = <"Address of where a person is living on a regular basis.">
				>
				["at0008"] = <
					text = <"Temporary accommodation">
					description = <"Address of where a person is living on a temporary basis.">
				>
				["at0009"] = <
					text = <"Address">
					description = <"The unstructured address of the person or organisation.">
					comment = <"This address line represents a low level of geographical/physical description of a location that, used in conjunction with the other high-level address components i.e. ‘Suburb/Town/Locality’, ‘Postcode’, and ‘State/Territory/Province’, forms a complete geographical/physical address. This data element can be used to represent a landmark, such as \"The second house north of the general store\" or \"At the corner of Smith & Brown Streets.
The content of this data element may be derived from a concatenation of one or more components from CLUSTER.structured_address.
Multiple occurrences allow for as many 'Address' lines, as required. For example: 4 address lines represented as
Apartment 7A,
52 Davis Street,
Carlton North,
Victoria, AUSTRALIA 3042.">
				>
				["at0014"] = <
					text = <"Structured address details">
					description = <"Additional structured low level geographical/physical details about a location that can be concatenated to form one or more 'Unstructured address' lines.">
				>
				["at0015"] = <
					text = <"Town">
					description = <"The name of the suburb, town, city, village, community or lowest level locality of the address.">
					comment = <"Coding with an external terminology is preferred, where possible. For example: Fitzroy, Calgary, Bergen.">
				>
				["at0016"] = <
					text = <"District/County">
					description = <"The name of an internal political or geographic district or area within a state, territory or province containing the address.">
					comment = <"Coding with an external terminology is preferred, where possible.">
				>
				["at0017"] = <
					text = <"State/Territory/Province">
					description = <"The name of an internal political or geographic division of a country containing the address.">
					comment = <"Coding with an external terminology is preferred, where possible. For example: Victoria; Alberta.">
				>
				["at0018"] = <
					text = <"Postcode">
					description = <"The code for a postal delivery area containing the address, aligned with locality, suburb or place for an address, as defined by the relevant postal delivery service.">
					comment = <"Also known as ZIP code.
">
				>
				["at0019"] = <
					text = <"Country">
					description = <"The name of the country containing the address.">
					comment = <"Coding with an external terminology is preferred, where possible. For example: Australia; Canada">
				>
				["at0020"] = <
					text = <"Postal delivery point">
					description = <"A unique number assigned to a postal delivery point, as defined by the relevant postal delivery service.">
				>
				["at0023"] = <
					text = <"Comment">
					description = <"Additional narrative about the address not captured in other fields.">
				>
				["at0024"] = <
					text = <"Additional details">
					description = <"Additional details about the address.">
					comment = <"For example: Geolocation.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Adresse">
					description = <"En fysisk adresse eller postadresse for bruk i en pasientjournal.">
				>
				["at0001"] = <
					text = <"Type">
					description = <"Adressetypen.">
					comment = <"For eksempel: Fysisk adresse eller postadresse.">
				>
				["at0002"] = <
					text = <"Fysisk">
					description = <"Adressen til en fysisk lokalisering.">
				>
				["at0003"] = <
					text = <"Postadresse">
					description = <"Adresse for korrespondanse, pakker eller brev.">
				>
				["at0004"] = <
					text = <"Begge">
					description = <"Adresse både for en fysisk lokalisering og for korrespondanse.">
				>
				["at0005"] = <
					text = <"Bruk">
					description = <"Primærformål for bruk av adressen.">
				>
				["at0006"] = <
					text = <"Virksomhet">
					description = <"Adressen til den fysiske plasseringen av en bedrift eller et kontor.">
				>
				["at0007"] = <
					text = <"Bosted">
					description = <"Adressen hvor en person bor.">
				>
				["at0008"] = <
					text = <"Midlertidig bosted">
					description = <"Adressen hvor en person bor på midlertidig basis.">
				>
				["at0009"] = <
					text = <"Ustrukturert adresse">
					description = <"En ustrukturert adresse eller en sammenkobling av en eller flere komponenter fra CLUSTER.structured_adress">
					comment = <"Denne adresselinjen representerer en detaljert geografisk eller fysisk beskrivelse av et sted. Sammen med de mindre detaljerte adressekomponentene som \"By\", \"Postnummer\" og \"Stat/territorie/provins\" utgjør denne en komplett geografisk eller fysisk adresse. Elementet kan repeteres for å kunne legge til så mange adresselinjer som nødvendig. For eksempel vil tre adresselinjer kunne representere:
Leilighet 7A
Eksempelveien 64
0300 OSLO
Dette dataelementet kan også brukes til å representere et landemerke, for eksempel \"Det andre huset til høyre etter butikken\" eller \"på hjørnet mellom Storgaten og Lillegaten\".">
				>
				["at0014"] = <
					text = <"Strukturert adresse detaljer">
					description = <"Ytterligere detaljerte geografiske/fysiske detaljer om et sted som kan kobles sammen til en eller flere instanser av \"Ustrukturert adresse\".">
				>
				["at0015"] = <
					text = <"By">
					description = <"Navn på forstad, landsby, by, tettsted eller den laveste grad av lokalisering for en adresse.">
					comment = <"Koding med ekstern terminologi er ønskelig om mulig. For eksempel: Fitzroy, Calgary, Bergen.">
				>
				["at0016"] = <
					text = <"Distrikt/fylke">
					description = <"Navnet på et politisk eller geografisk distrikt, eller område i en stat, territorium eller fylke, som en del av adressen.">
					comment = <"Koding med ekstern terminologi er foretrukket om mulig.">
				>
				["at0017"] = <
					text = <"Stat/territorie/provins">
					description = <"Navnet på en politisk eller geografisk inndeling av et land som en del av adressen.">
					comment = <"Koding med ekstern terminologi er foretrukket om mulig. For eksempel: Victoria, Alberta.">
				>
				["at0018"] = <
					text = <"Postnummer">
					description = <"Koden for et postleveringsområde som er en del av adressen, og som er definert av den relevante postleveringstjenesten.">
					comment = <"I andre land kalles dette også ZIP kode.">
				>
				["at0019"] = <
					text = <"Land">
					description = <"Navnet på landet som en del av adressen.">
					comment = <"Koding med ekstern terminologi er foretrukket om mulig. For eksempel: Australia, Canada.">
				>
				["at0020"] = <
					text = <"Postutleveringssted">
					description = <"Et unikt nummer tildelt et postutleveringssted utstedt av den aktuelle postleveringstjenesten.">
				>
				["at0023"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om adressen som ikke er registrert i andre felt.">
				>
				["at0024"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere detaljer om adressen.">
					comment = <"For eksempel: geolokasjon.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Adresse">
					description = <"Eine physische oder postalische Adresse zur Verwendung in einer Gesundheitsakte.">
				>
				["at0001"] = <
					text = <"Art">
					description = <"Art der Adresse.">
					comment = <"Zum Beispiel: Hausadresse oder postalische Adresse.">
				>
				["at0002"] = <
					text = <"Physisch">
					description = <"Ein physischer Ort, der besucht werden kann.">
				>
				["at0003"] = <
					text = <"Postalisch">
					description = <"Adresse, die als Ziel für den Versand von Briefen oder Paketen verwendet wird.">
				>
				["at0004"] = <
					text = <"Beides">
					description = <"Adresse eines physischen Standorts, der auch als Ziel für E-Mails verwendet wird.">
				>
				["at0005"] = <
					text = <"Verwendung">
					description = <"Der primäre Zweck oder die primäre Verwendung der Adresse.">
				>
				["at0006"] = <
					text = <"Geschäftlich">
					description = <"Adresse des physischen Standorts eines Unternehmens oder Büros.">
				>
				["at0007"] = <
					text = <"Privat">
					description = <"Adresse, an der eine Person regelmäßig lebt.">
				>
				["at0008"] = <
					text = <"Vorrübergehende Unterkunft">
					description = <"Adresse, an der eine Person vorübergehend lebt.">
				>
				["at0009"] = <
					text = <"Adresse unstrukturiert">
					description = <"Eine unstrukturierte Adresse oder eine Verkettung einer oder mehrerer Komponenten aus CLUSTER.structured_address.">
					comment = <"Diese Adresszeile stellt eine niedrige geografische/physische Beschreibung eines Standorts dar, die in Verbindung mit den anderen übergeordneten Adresskomponenten, d. h. „Vorort/Stadt/Ort“, „Postleitzahl“ und „Staat/Land/Bundesland“, bildet eine vollständige geografische/physische Adresse. Mehrfaches Vorkommen erlaubt beliebig viele \"Adresszeilen\". Beispiel: 4 Adresszeilen dargestellt als
Wohnung 7A,
52 Davis-Straße,
Carlton Nord,
Victoria, Australien 3042.
Dieses Datenelement kann auch verwendet werden, um ein Wahrzeichen darzustellen, wie zum Beispiel \"Das zweite Haus nördlich des Hauptgeschäftes\" oder \"An der Ecke Smith & Brown Streets\".">
				>
				["at0014"] = <
					text = <"Strukturierte Adressdaten">
					description = <"Zusätzliche strukturierte geografische/physische Details auf niedriger Ebene zu einem Standort, die verkettet werden können, um eine oder mehrere Zeilen der „Adresse unstrukturiert“ zu bilden.">
				>
				["at0015"] = <
					text = <"Stadt">
					description = <"Der Name des Vorortes, der Stadt, des Dorfes, der Gemeinde oder des Ortes der untersten Ebene der Adresse.">
					comment = <"Die Codierung mit einer externen Terminologie wird nach Möglichkeit bevorzugt. Zum Beispiel: Fitzroy, Calgary, Bergen.">
				>
				["at0016"] = <
					text = <"Bezirk / Landkreis">
					description = <"Der Name eines internen politischen oder geografischen Bezirks oder Gebiets innerhalb eines Staates, Territoriums oder einer Provinz, der die Adresse enthält.">
					comment = <"Wenn möglich, wird die Codierung mit einer externen Terminologie bevorzugt.">
				>
				["at0017"] = <
					text = <"Staat / Land / Bundesland">
					description = <"Der Name einer internen politischen oder geografischen Abteilung eines Landes, das die Adresse enthält.">
					comment = <"Wenn möglich, wird die Codierung mit einer externen Terminologie bevorzugt. Zum Beispiel: Victoria, Alberta.">
				>
				["at0018"] = <
					text = <"Postleitzahl">
					description = <"Der Code für einen Postzustellungsbereich, der die Adresse enthält, ausgerichtet auf Ort, Vorort oder Ort für eine Adresse, wie vom jeweiligen Postzustelldienst definiert.">
					comment = <"Auch als Postleitzahl PLZ bekannt.">
				>
				["at0019"] = <
					text = <"Land">
					description = <"Der Name des Landes, in dem sich die Adresse befindet.">
					comment = <"Wenn möglich, wird die Codierung mit einer externen Terminologie bevorzugt. Zum Beispiel: Australien, Kanada.">
				>
				["at0020"] = <
					text = <"Postfach">
					description = <"Eine eindeutige Nummer, die einem Postzustellungspunkt zugewiesen ist, wie vom jeweiligen Postzustelldienst definiert.">
				>
				["at0023"] = <
					text = <"Kommentar">
					description = <"Zusätzliche Erläuterungen zur Adresse, die nicht in anderen Feldern erfasst werden.">
				>
				["at0024"] = <
					text = <"Zusätzliche Details">
					description = <"Weitere Angaben zur Adresse.">
					comment = <"Beispiel: Geolokalisierung.">
				>
			>
		>
	>

archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.family_prevalence.v1

concept
	[at0000]	-- Family prevalence
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2015-05-05">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record information about the prevalence of an identified risk factor or health condition within a family group.">
			use = <"Use to record information about the prevalence of an identified risk factor or health condition within a family group. 

This archetype has been designed to support assessment of risk in the subject of care for the identified 'Risk factor', if used within 'Family prevalence' SLOT in the EVALUATION.health_risk archetype.

It could also be used to document the presence of affected family in other archetypes in which the index data element has already been identified. For example, it could document the number of affected family members for an identified diagnosis, by insertion into the 'Specific details' SLOT of the EVALUATION.problem_diagnosis archetype.">
			keywords = <"family history", "prevalence", "affected", "genetic">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"OrganisationDraft">
	other_contributors = <"Tomas Alme, DIPS, Norway", "Rita Apelt, Department of Health,NT, Australia", "Vebjoern Arntzen, Oslo university hospital, Norway", "Koray Atalag, University of Auckland, New Zealand", "Silje Ljosland Bakke, Bergen Hospital Trust, Norway", "John Bennett, NEHTA, Australia", "Lars Bitsch-Larsen, Haukeland University hospital, Norway", "Diego Bosca, IBIME group, Spain", "Rong Chen, Cambio Healthcare Systems, Sweden", "Stephen Chu, Queensland Health, Australia", "David Evans, Queensland Health, Australia", "Shahla Foozonkhah, Ocean Informatics, Australia", "Einar Fosse, National Centre for Integrated Care and Telemedicine, Norway", "Samuel Frade, Marand, Portugal", "Tim Garden, NTG Department of Health, Australia", "Sebastian Garde, Ocean Informatics, Germany", "Jacquie Garton-Smith, Royal Perth Hospital and DoHWA, Australia", "Andrew Goodchild, NEHTA, Australia", "Sam Heard, Ocean Informatics, Australia (Editor)", "Lars Karlsen, DIPS ASA, Norway", "Mary Kelaher, NEHTA, Australia", "Shinji Kobayashi, Kyoto University, Japan", "Robert L'egan, NEHTA, Australia", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Hugh Leslie, Ocean Informatics, Australia", "Hallvard Lærum, Oslo University Hospital, Norway", "Mike Martyn, The Hobart Anaesthetic Group, Australia", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (Editor)", "Chris Mitchell, RACGP, Australia", "Stewart Morrison, NEHTA, Australia", "Bjoern Naess, DIPS ASA, Norway", "Jeremy Oats, NT Health, Australia", "Lynne Parsons, Primary and Community Health Services, Australia", "Jodie Pycroft, Adelaide Northern Division of General Practice Ltd, Australia", "Robyn Richards, NEHTA - Clinical Terminology, Australia", "Jussara Rotzsch, UNB, Brazil", "Anoop Shah, University College London, United Kingdom", "Elizabeth Stanick, Hobart Anaesthetic Group, Australia", "John Taylor, NEHTA, Australia", "Gordon Tomes, Australian Institute of Health and Welfare, Australia", "Richard Townley-O'Neill, NEHTA, Australia", "Donna Truran, ACCTI-UoW, Australia", "Jo Wright, NT Dept of Health, Australia (Editor)">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics<heather.leslie@oceaninformatics.com>">
		["references"] = <"Risk of condition based on family history, rejected archetype, openEHR Clinical Knowledge Manager [Internet]. openEHR Foundation. Authored: 2006 04 23. Available at: http://www.openehr.org/ckm/#showArchetype_1013.1.125 (last accessed 2015 03 05).">
		["MD5-CAM-1.0.1"] = <"44B193E88340069460FD27F273A41893">
	>

definition
	CLUSTER[at0000] matches {	-- Family prevalence
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0030] occurrences matches {0..1} matches {	-- Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0055] occurrences matches {0..1} matches {	-- Genetic predisposition?
				value matches {
					DV_BOOLEAN matches {
						value matches {True}
					}
				}
			}
			CLUSTER[at0031] occurrences matches {0..*} matches {	-- Affected ratio
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0032] occurrences matches {0..1} matches {	-- Relationship
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0037, 	-- First degree relative
									at0038, 	-- Second degree relative
									at0039, 	-- Third degree relative
									at0042, 	-- Genetic family
									at0050]	-- Non-genetic family
								}
							}
						}
					}
					ELEMENT[at0051] occurrences matches {0..1} matches {	-- Family line
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0040, 	-- Maternal line
									at0041]	-- Paternal line
								}
							}
						}
					}
					ELEMENT[at0033] occurrences matches {0..1} matches {	-- Biological sex
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0043, 	-- Male
									at0044, 	-- Female
									at0052]	-- Indeterminate/Intersex/Unspecified
								}
							}
						}
					}
					ELEMENT[at0034] occurrences matches {0..1} matches {	-- Number affected
						value matches {
							DV_COUNT matches {
								magnitude matches {|>=1|}
							}
						}
					}
					ELEMENT[at0054] occurrences matches {0..1} matches {	-- Total number
						value matches {
							DV_COUNT matches {
								magnitude matches {|>=1|}
							}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Family prevalence">
					description = <"Summary information about the prevalence of a risk factor, problem or diagnosis in family members.">
				>
				["at0030"] = <
					text = <"Description">
					description = <"Narrative description about occurrence in family members.">
				>
				["at0031"] = <
					text = <"Affected ratio">
					description = <"Details about the numbers of family members affected.">
					comment = <"This cluster will be repeated for each relationship, family line, sex value or combination of all three. For example, the cluster will be repeated for any or all of first degree relative (without sex specified), first degree relative from maternal line; first degree male relative or first degree female relative; or all males, all females.">
				>
				["at0032"] = <
					text = <"Relationship">
					description = <"The degree of relationship between the subject of care and a selected group of family members.">
				>
				["at0033"] = <
					text = <"Biological sex">
					description = <"The biological sex of the family member/s.">
				>
				["at0034"] = <
					text = <"Number affected">
					description = <"The number of family members known to be affected.">
					comment = <"Effectively this number is the numerator for the 'Affected ratio'.">
				>
				["at0037"] = <
					text = <"First degree relative">
					description = <"50% genetic share with the subject - for example, parent, sibling or child.">
				>
				["at0038"] = <
					text = <"Second degree relative">
					description = <"25% genetic share with the subject -  for example, grandparent, aunt, uncle, niece, nephew, grandchildren. and half siblings.">
				>
				["at0039"] = <
					text = <"Third degree relative">
					description = <"12.5% genetic share with the subject - for example, great grandparent, great aunt, great uncle, first cousin, children of nieces and nephews, and great grandchildren.">
				>
				["at0040"] = <
					text = <"Maternal line">
					description = <"Related through the subject's mother.">
				>
				["at0041"] = <
					text = <"Paternal line">
					description = <"Related through the subject's father.">
				>
				["at0042"] = <
					text = <"Genetic family">
					description = <"All genetically-related family members.">
				>
				["at0043"] = <
					text = <"Male">
					description = <"Family member who is biologically male.">
				>
				["at0044"] = <
					text = <"Female">
					description = <"Family member who is biologically female.">
				>
				["at0050"] = <
					text = <"Non-genetic family">
					description = <"All non-genetic family members.">
				>
				["at0051"] = <
					text = <"Family line">
					description = <"Identification of the maternal or paternal family line in the relationship.">
				>
				["at0052"] = <
					text = <"Indeterminate/Intersex/Unspecified">
					description = <"Family member who either has the biological attributes of both sexes or lacks some of the biological attributes considered necessary to be defined as one or the other sex.">
				>
				["at0054"] = <
					text = <"Total number">
					description = <"The number of eligible family members.">
					comment = <"This is the number of family members that may be affected AND fit selected relationship, family line and sex criteria. Effectively this number is the denominator for the 'Affected ratio'.">
				>
				["at0055"] = <
					text = <"Genetic predisposition?">
					description = <"Is there a genetic basis for the risk factor, problem or diagnosis?">
					comment = <"Optional to record as True if there is a recognised genetic predisposition. In many cases, this may be inferred from the risk factor, problem or diagnosis and not need to be recorded explicitly.">
				>
			>
		>
	>

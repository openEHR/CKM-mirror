archetype (adl_version=1.4; uid=cc4e9a57-bac9-4518-9926-9ef1e75f3d95)
	openEHR-EHR-CLUSTER.inspection_body_fluid.v0

concept
	[at0000]	-- Examination of body fluid
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2015-06-25">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording the findings observed during the examination of any body fluid at an identified body site or from an identified body source.">
			use = <"Use to record a narrative description and structured details about findings observed during the examination of any body fluid from an identified body site or source. In addition, use to record the clinical interpretation of the findings.

This archetype has been specifically designed to be used as the framework for recording any body fluid. Findings that are applicable for specified body fluid should be modelled into a separate CLUSTER archetype and nested in the 'Specific findings' SLOT found within this archetype.

Body fluid is a liquid that originates from inside the human body, and this archetype is intended to support detailed description of:
- normal physiological fluids; 
- physiological fluids that may be altered due to a pathological process; and
- fluids generated as part of a pathological process within the body.

Examples include, but are not limited to:
- Amniotic fluid;
- Blood and plasma;
- Cerebrospinal fluid;
- Discharge
- Excreta, including urine and liquid faeces;
- Pus;
- Saliva;
- Sputum;
- Synovial fluid;
- Sweat; and
- Vomitus.

While this archetype will most likely be used in the context of a living subject, it is also appropriate to use in recording autopsy findings.

Context of the body site or source will usually be carried in the archetypes in which it is nested, including but not limited to:
-  in the 'Examination findings' SLOT within many of the CLUSTER.exam family of archetypes and related inspection and palpation archetypes;
-  in the 'Additional details' SLOT within the OBSERVATION.urinalysis, the OBSERVATION.fluid_output archetypes;
- in the  and
- within other OBSERVATION or CLUSTER archetypes, where clinically appropriate.

The CLUSTER.exclusion_exam archetype can be nested within the 'Examination not done' SLOT to optionally record explicit details about the examination not being performed.

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical description' data element.">
			keywords = <"biofluid", "bodily fluid">
			misuse = <"Not to be used for recording measurements of fluid volume - use OBSERVATION archetypes for this purpose. For example OBSERVATION.fluid_output.

Not to be used for recording the clinical history - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom_sign.

Not to be used to record the results of a urinalysis test - use OBSERVATION.urinalysis. This archetype may be included in the 'Additional details' SLOT to provide additional context to the test result.

Not to be used to record stand-alone clinical observations or measurements or test results - use specific OBSERVATION archetypes. For example OBSERVATION.blood_pressure or OBSERVATION.imaging_exam.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Kerstin Adolfsson, Sweden", "Vebjoern Arntzen, Oslo university hospital, Norway", "Silje Ljosland Bakke, National ICT Norway, Norway (openEHR Editor)", "Lars Bitsch-Larsen, Haukeland University hospital, Norway", "Kate Bryan, Stalis Ltd, United Kingdom", "David Burns, East Kent Hospitals University Foundation NHS Trust, United Kingdom", "Peter Coates, NHS England, United Kingdom", "Heather Grain, Llewelyn Grain Informatics, Australia", "Lars Karlsen, DIPS ASA, Norway", "Dave Kilroy, Application Insight Ltd, United Kingdom", "Sabine Leh, Haukeland University Hospital, Department of Pathology, Norway", "Heather Leslie, Ocean Informatics, Australia (openEHR Editor)", "Kelvin Lok, Stalis Ltd, United Kingdom", "Mark McEvoy, SITS International, Sweden", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Colin Newell, OpusVL, United Kingdom", "Andrej Orel, Marand d.o.o., Slovenia", "Rob Organ, Yeovil District Hospital NHS Foundation Trust, United Kingdom", "Petra Palm, Region Östergötland, Sweden", "Mike Pargeter, Across Health Ltd, United Kingdom", "John Pyle, self, United Kingdom", "Navin Ramachandran, NHS, United Kingdom", "Cecilia Revelj, Landstinget i Kalmar län, Sweden", "Ingela Sammeli, Landstinget i Värmland, Sweden", "Elin Tell, Region Ostergotland, Sweden", "John Tore Valand, Helse Bergen, Norway", "Lina Öfverström, Region Östergötland, Sweden">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics<heather.leslie@oceaninformatics.com>">
		["revision"] = <"0.0.1-alpha">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["build_uid"] = <"c210d665-e6f6-4c10-9af2-4c2ce9993c43">
		["original_publisher"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"76BC8935CD2A7123ED9D5366EB242248">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	CLUSTER[at0000] matches {	-- Examination of body fluid
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Fluid name
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0015] occurrences matches {0..1} matches {	-- Body source
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0016] occurrences matches {0..*} matches {	-- Structured body source
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_clock(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- Presence?
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0009, 	-- Present
							at0010, 	-- Indeterminate
							at0011]	-- Not present
						}
					}
				}
			}
			ELEMENT[at0014] occurrences matches {0..*} matches {	-- Amount
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Specific findings
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exam_body_fluid(-[a-zA-Z0-9_]+)*\.v0/}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Multimedia representation
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {	-- Clinical interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0008] occurrences matches {0..*} matches {	-- Examination not done
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination of body fluid">
					description = <"Findings observed during the examination of body fluid at a body site or from an identified body source.">
				>
				["at0001"] = <
					text = <"Fluid name">
					description = <"The name of the body fluid.">
					comment = <"Coding of the 'Fluid name' with a coding system is desirable, if available.">
				>
				["at0002"] = <
					text = <"Presence?">
					description = <"Statement that the fluid was, or was not, observed at the identified body site.">
					comment = <"This data element is intended for examination of body fluid in wounds, or similar, and at a specific body site and is not applicable to body fluids that are being examined away from the source such as urine. It may also be used to record that bloody fluid or meconium was observed within amniotic fluid, by nesting another instance of this archetype inside the 'Additional findings' SLOT'.">
				>
				["at0003"] = <
					text = <"Clinical description">
					description = <"Narrative description about the fluid.">
				>
				["at0004"] = <
					text = <"Specific findings">
					description = <"Additional structured details about the body fluid.">
					comment = <"For example: inclusion of detailed CLUSTER archetypes for specific fluids such as urine; or inclusion of further CLUSTER.exam_body_fluid archetypes could describe components of substances such as required to describe a mixture of blood and meconium post childbirth.">
				>
				["at0005"] = <
					text = <"Multimedia representation">
					description = <"Digital image, video or diagram representing the inspection findings.">
				>
				["at0006"] = <
					text = <"Clinical interpretation">
					description = <"Single word, phrase or brief description that represents the clinical meaning and significance of the examination findings.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the inspection findings, not captured in other fields.">
				>
				["at0008"] = <
					text = <"Examination not done">
					description = <"Details to explicitly record that this examination was not performed.">
				>
				["at0009"] = <
					text = <"Present">
					description = <"The fluid was observed at the body site.">
				>
				["at0010"] = <
					text = <"Indeterminate">
					description = <"It was not possible to determine if the fluid was present at the body site.">
				>
				["at0011"] = <
					text = <"Not present">
					description = <"The fluid was not present at the body site.">
				>
				["at0014"] = <
					text = <"Amount">
					description = <"Description of the amount of fluid observed at the identified body site.">
					comment = <"The intent of this data element is to record either a narrative description of the amount, or to code with a value set such as small/medium/profuse or normal/increased where appropriate in a template. This data element is not intended for quantitative assessment of body fluids - use OBSERVATION.fluid_output for this purpose.">
				>
				["at0015"] = <
					text = <"Body source">
					description = <"Identification of the body site or source.">
					comment = <"Coding of the 'Body source' with a coding system is desirable, if available. If the body site has previously been identified in the 'Fluid name' data element or another archetype in which this archetype is nested, this data element becomes redundant. Please note: while this archetype will most likely be used in the context of a living subject, it is also appropriate to use in recording autopsy findings.">
				>
				["at0016"] = <
					text = <"Structured body source">
					description = <"A structured representation of the body source.">
					comment = <"If the body source has previously been identified in the 'Fluid name' data element or another archetype in which this archetype is nested, this SLOT becomes redundant.">
				>
			>
		>
	>

archetype (adl_version=1.4; uid=93fdd74a-b7b4-4d9d-bb2f-33850d87239f)
	openEHR-EHR-CLUSTER.imaging_exam-lesion.v0
specialise
	openEHR-EHR-CLUSTER.imaging_exam.v1

concept
	[at0000.1]	-- Imaging examination of an abnormality
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Natalia Strauch">
				["organisation"] = <"Medizinische Hochschule Hannover">
				["email"] = <"Strauch.Natalia@mh-hannover.de">
			>
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Peter Fedorcsak, Vebjørn Arntzen">
				["organisation"] = <"University of Oslo, Oslo University Hospital">
				["email"] = <"peterfe@uio.no, varntzen@ous-hf.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2021-11-24">
	>
	details = <
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Zur Darstellung der Befunde einer radiologischen Untersuchung einer bestimmten Körperstruktur oder -region und deren Interpretation.">
			use = <"Der Archetyp wird verwendet, um eine Beschreibung, strukturierte Angaben und einen klinischen Eindruck der Befunde einer morphologischen Anomalie darzustellen, die bei einer bildgebenden Untersuchung beobachtet wurde, die auf eine bestimmte Körperstruktur oder -region abzielt.

Dieser Archetyp wurde für die Verwendung im SLOT „Zusätzliche Angaben“ innerhalb eines Archetyps CLUSTER.imaging_exam entwickelt, der die Struktur identifiziert, die die morphologische Anomalie enthält, kann aber auch innerhalb anderer ENTRY- oder CLUSTER-Archetypen verwendet werden, sofern klinisch angemessen.

Wird verwendet, um einen Rahmen bereitzustellen, in dem CLUSTER-Archetypen im SLOT „Zusätzliche Angaben“ innerhalb dieses Archetyps verschachtelt werden können, um zusätzliche verschachtelte Ebenen strukturierter Bildgebungsbefunde für verwandte Körpersubstrukturen oder ortsspezifische Befunde darzustellen.">
			keywords = <"Befund, Ergebnis, Bildgebung, Körper, Organ, System, Struktur, Region, Beobachtung, Diagnostik, Radiologie, Bild, Anomalie", ...>
			misuse = <"Nicht zur Darstellung von Befunden verwenden, die außerhalb der identifizierten Körperstruktur oder -region beobachtet wurden. Verwenden Sie zu diesem Zweck eine separate Instanz eines oder mehrerer Archetypen aus der Familie CLUSTER.imaging_exam.

Darf nicht verwendet werden, um Details in Bezug auf die Gesamtbefunde, den Kontext oder technische Details in Bezug auf ein vollständiges Testergebnis einer bildgebenden Untersuchung darzustellen. Verwenden Sie zu diesem Zweck das OBSERVATION.imaging_exam_result. Zum Beispiel Kommentare zur Studienqualität, zu Differentialdiagnosen und zum Gesamteindruck.">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere en fritekstbeskrivelse, strukturerte detaljer og klinisk tolkning av funn eller observasjoner ved bildediagnostisk undersøkelse av et morfologisk avvik og som er rettet mot en spesifikk kroppsstruktur eller region.">
			use = <"Brukes for å registrere en fritekstbeskrivelse, strukturerte detaljer og klinisk tolkning av funn eller observasjoner ved bildediagnostisk undersøkelse av et morfologisk avvik og som er rettet mot en spesifikk kroppsstruktur eller region.

Denne arketypen ble utviklet til bruk i SLOT'et \"Strukturerte funn\" i arketypen OBSERVATION.imaging_exam_result (Bildediagnostisk svar), men kan også brukes innen andre ENTRY- og CLUSTER-arketyper der det er klinisk passende.

Brukes som et rammeverk hvor andre CLUSTER arketyper kan nøstes i SLOT'et \"Strukturerte detaljer\" for å registrere ytterligere strukturerte billedfunn for delstrukturer eller andre relevante funn.">
			misuse = <"Skal ikke brukes til å registrere funn utenfor det spesifiserte organ eller kroppsdel. Bruk andre arketyper fra gruppen av CLUSTER.imaging_exam-arketyper for dette.

Skal ikke brukes til å registrere overordnede undersøkelsesfunn, kontekst eller tekniske detaljer relatert til billedundersøkelsen. Bruk arketypen OBSERVATION.imaging_exam_result (Bildediagnistisk svar) for å registrere for eksempel kommentar om undersøkelseskvalitet, differensialdiagnose og overordnet konklusjon.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record a narrative description, structured details and clinical impression of the findings of a morphological abnormality observed in an imaging examination targeting a specified body structure or region.">
			use = <"Use to record a narrative description, structured details and clinical impression of the findings of a morphological abnormality observed in an imaging examination targeting a specified body structure or region.

This archetype has been designed to be used in the 'Structured imaging findings' SLOT within OBSERVATION.imaging_exam_result, which will identify the structure containing the morphological abnormality, but can also be used within other ENTRY or CLUSTER archetypes, where clinically appropriate. 

Use to provide a framework in which CLUSTER archetypes can be nested in the 'Additional details' SLOT within this archetype to record additional nested levels of structured imaging findings for related body substructures or site-specific findings.">
			misuse = <"Not to be used to record findings for a specified body structure or region. Use other archetypes from the CLUSTER.imaging_exam family for this purpose.

Not to be used to record details related to the overall findings, context or technical details related to a complete imaging examination test result. Use the OBERSERVATION.imaging_exam_result for this purpose. For example, comments on the study quality, differential diagnoses, and overall impression.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital", "Liv Laugen, Oslo University Hospital", "Peter Fedorcsak, Oslo University Hospital">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"B89D92BF94AC4C0BC4B7A20878E62174">
		["build_uid"] = <"35b2c220-26e3-4689-8780-94e1977e4f94">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact https://www.snomed.org/snomed-ct/get-snomed or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000.1] matches {    -- Imaging examination of an abnormality
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001.1] matches {    -- Body structure
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::at0.1]    -- Morphological abnormality
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {    -- Body site
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0003] occurrences matches {0..*} matches {    -- Structured body site
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v2|openEHR-EHR-CLUSTER\.anatomical_location_circle(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0004] occurrences matches {0..1} matches {    -- Imaging findings
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {    -- Additional details
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.imaging_exam-lymph_node(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-lymph_node_group(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.imaging_exam-abnormality(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {    -- Impression
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {    -- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.2] occurrences matches {0..1} matches {    -- Label
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.3] occurrences matches {0..1} matches {    -- Type
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.4] occurrences matches {0..3} matches {    -- Diameter
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::122]>
						list = <
							["1"] = <
								units = <"mm">
								magnitude = <|>=0.0|>
								precision = <|0|>
							>
							["2"] = <
								units = <"cm">
								magnitude = <|>=0.0|>
								precision = <|1|>
							>
						>
					>
				}
			}
			ELEMENT[at0.9] occurrences matches {0..1} matches {    -- Shape
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.10] occurrences matches {0..1} matches {    -- Texture
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.11] occurrences matches {0..1} matches {    -- Definition
				value matches {
					DV_TEXT matches {*}
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.12,    -- Well-defined
							at0.13]    -- Poorly-defined
						}
					}
				}
			}
			ELEMENT[at0.14] occurrences matches {0..1} matches {    -- Contour
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.15] occurrences matches {0..1} matches {    -- Margin
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.16] occurrences matches {0..1} matches {    -- Calcification
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.17,    -- Present
							at0.18]    -- Absent
						}
					}
				}
			}
			ELEMENT[at0.19] occurrences matches {0..1} matches {    -- Effect
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0.20] occurrences matches {0..1} matches {    -- Vascularisation
				value matches {
					DV_TEXT matches {*}
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0.21,    -- None
							at0.22,    -- Mild
							at0.23,    -- Moderate
							at0.24]    -- Marked
						}
					}
				}
			}
		}
	}


ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0.1"] = <
					text = <"Morphological abnormality">
					description = <"A morphological abnormality was examined.">
				>
				["at0.10"] = <
					text = <"Texture">
					description = <"The texture of the abnormality.">
				>
				["at0.11"] = <
					text = <"Definition">
					description = <"The definition of the abnormality.">
				>
				["at0.12"] = <
					text = <"Well-defined">
					description = <"The abnormality is well-defined.">
				>
				["at0.13"] = <
					text = <"Poorly-defined">
					description = <"The abnormality is poorly-defined.">
				>
				["at0.14"] = <
					text = <"Contour">
					description = <"The contour of the abnormality.">
				>
				["at0.15"] = <
					text = <"Margin">
					description = <"The margin of the abnormality.">
				>
				["at0.16"] = <
					text = <"Calcification">
					description = <"Presence of calcification in the abnormality.">
				>
				["at0.17"] = <
					text = <"Present">
					description = <"Calcification is observed.">
				>
				["at0.18"] = <
					text = <"Absent">
					description = <"Calcification is not observed.">
				>
				["at0.19"] = <
					text = <"Effect">
					description = <"Narrative description about the effect of the abnormality on surrounding structures.">
					comment = <"For example: 'pressing on blood vessel'.">
				>
				["at0.2"] = <
					text = <"Label">
					description = <"A label for the abnormality.">
					comment = <"Use to differentiate one abnormality from another when there is more than one present at the same site.">
				>
				["at0.20"] = <
					text = <"Vascularisation">
					description = <"Assessment of the blood flow to the abnormality.">
					comment = <"Often determined by colour doppler or similar.">
				>
				["at0.21"] = <
					text = <"None">
					description = <"No blood flow can be detected.">
				>
				["at0.22"] = <
					text = <"Mild">
					description = <"Minimal blood flow can be detected.">
				>
				["at0.23"] = <
					text = <"Moderate">
					description = <"Moderate blood flow can be detected.">
				>
				["at0.24"] = <
					text = <"Marked">
					description = <"Marked blood flow can be detected.">
				>
				["at0.3"] = <
					text = <"Type">
					description = <"The type of abnormality.">
					comment = <"Coding of the morphological type with an external terminology is preferred, where possible. For example, a cyst or nodule from the SNOMED 4147007 | Mass (morphologic abnormality) | hierarchy.">
				>
				["at0.4"] = <
					text = <"Diameter">
					description = <"A measured diameter of the abnormality.">
					comment = <"This data element has 3 occurrences to allow for 3 measurements in different axes.">
				>
				["at0.9"] = <
					text = <"Shape">
					description = <"The shape of the abnormality.">
				>
				["at0000"] = <
					text = <"Imaging examination of a body structure">
					description = <"Findings on radiological examination of a specified body structure or region.">
				>
				["at0000.1"] = <
					text = <"Imaging examination of an abnormality">
					description = <"Findings of a morphological abnormality observed in an imaging examination targeting a specified structure or region, using radiographic techniques.">
				>
				["at0001"] = <
					text = <"Body structure">
					description = <"Identification of the body structure or region examined.">
					comment = <"For example: 'Liver', 'Right ankle' or 'Lymph node group'. Coding of the body structure with an appropriate terminology, such as SNOMED CT, is recommended.">
				>
				["at0001.1"] = <
					text = <"Body structure">
					description = <"Identification of the body structure or region examined.">
				>
				["at0002"] = <
					text = <"Body site">
					description = <"Identification of the area of the body under examination.">
					comment = <"For example: a lymph node group (body structure) which found in the 'Right axilla'. Coding of 'Body site' with a terminology, such as SNOMED CT, is desirable. If the body site has been fully identified in the 'Body structure' data element, this data element becomes redundant.">
				>
				["at0003"] = <
					text = <"Structured body site">
					description = <"Structured details about the area of the body under examination.">
					comment = <"For example: details about a relative anatomical location, such as '2cm distal to the appendix'. If the body site has been fully identified in the 'Body structure' or 'Body site' data elements, then this SLOT becomes redundant.
">
				>
				["at0004"] = <
					text = <"Imaging findings">
					description = <"Narrative description of the imaging findings observed during this examination.">
				>
				["at0005"] = <
					text = <"Additional details">
					description = <"Additional structured details about imaging findings for the identified body structure or region, or findings of related structures.">
				>
				["at0006"] = <
					text = <"Impression">
					description = <"Single word, phrase or brief description that represents the clinical meaning and significance of all imaging findings for the identified body structure.">
					comment = <"Also referred to as 'Interpretation' or 'Opinion'. For example: 'No abnormality detected' or 'Free fluid present'. Coding of the 'Impression' with a terminology is permitted, if useful. Multiple occurrences allow for more than one coded impression. A narrative description may include a comparison with previous studies of the identified body structure, if appropriate.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the imaging findings of the body structure, not captured in other fields.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0.1"] = <
					text = <"Morfologisk avvik">
					description = <"Undersøkelse av et morfologisk avvik.">
				>
				["at0.10"] = <
					text = <"Tekstur">
					description = <"Avvikets tekstur.">
				>
				["at0.11"] = <
					text = <"Avgrensning">
					description = <"Avvikets avgrensning.">
				>
				["at0.12"] = <
					text = <"Godt avgrenset">
					description = <"Avviket er godt avgrenset.">
				>
				["at0.13"] = <
					text = <"Svakt avgrenset">
					description = <"Avviket er svakt avgrenset.">
				>
				["at0.14"] = <
					text = <"Kontur">
					description = <"Avvikets kontur.">
				>
				["at0.15"] = <
					text = <"Grenseflate">
					description = <"Avvikets grenseflate.">
				>
				["at0.16"] = <
					text = <"Forkalkning">
					description = <"Tilstedeværelse av forkalkning i avviket.">
				>
				["at0.17"] = <
					text = <"Tilstede">
					description = <"Forkalkning kan sees.">
				>
				["at0.18"] = <
					text = <"Fraværende">
					description = <"Forkalkning kan ikke sees.">
				>
				["at0.19"] = <
					text = <"Effekt">
					description = <"Fritekstbeskrivelse av avvikets effekt på omkringliggende strukturer.">
					comment = <"Eksempelvis 'presser mot blodkar'.">
				>
				["at0.2"] = <
					text = <"Identifisering">
					description = <"Identifisering eller betegnelse for avviket.">
					comment = <"Brukes for a skille mellom et avvik fra et annet når flere avvik er tilstede samtidig.">
				>
				["at0.20"] = <
					text = <"Blodforsyning">
					description = <"Undersøkelse av avvikets blodforsyning.">
					comment = <"Undersøkes ofte med fargedoppler eller lignende metode.">
				>
				["at0.21"] = <
					text = <"Ingen">
					description = <"Ingen blodforsyning kan sees.">
				>
				["at0.22"] = <
					text = <"Mild">
					description = <"Liten blodtilstrømning kan sees.">
				>
				["at0.23"] = <
					text = <"Moderat">
					description = <"Moderat blodtilstrømning kan sees.">
				>
				["at0.24"] = <
					text = <"Markert">
					description = <"Markert blodtilstrømning kan sees.">
				>
				["at0.3"] = <
					text = <"Type">
					description = <"Avvikets type.">
					comment = <"Det bør om mulig benyttes ekstern terminologi for å angi den morfologisk typen for avvik. Eksempelvis cyste eller knute fra SNOMED 4147007 | Oppfylning (morfologisk avvik) | hierarki.">
				>
				["at0.4"] = <
					text = <"Diameter">
					description = <"Avvikets diameter.">
					comment = <"Elementet har 3 forekomster for å tillate registrering av målinger i 3 retninger.">
				>
				["at0.9"] = <
					text = <"Form">
					description = <"Avvikets form.">
				>
				["at0000"] = <
					text = <"Bildediagnostisk undersøkelse - kroppsstruktur">
					description = <"Funn eller observasjoner ved radiologisk undersøkelse av et definert organ eller en kroppsdel.">
				>
				["at0000.1"] = <
					text = <"Bildediagnostisk undersøkelse - avvik">
					description = <"Funn eller observasjoner ved bildediagnostisk undersøkelse av et morfologisk avvik med bruk av radiologiske metoder rettet mot en spesifisert struktur eller område.">
				>
				["at0001"] = <
					text = <"Kroppsstruktur">
					description = <"Identifisering av organet eller kroppsdelen som svaret gjelder.">
					comment = <"For eksempel \"Lever\", \"Høyre ankel\" eller \"Lymfeknutegruppe\". Det er anbefalt å kode kroppsstruktur med en passende terminologi, som SNOMED CT.">
				>
				["at0001.1"] = <
					text = <"Struktur">
					description = <"Navn av undersøkt struktur eller område i kroppen.">
				>
				["at0002"] = <
					text = <"Anatomisk lokalisasjon">
					description = <"Identifisering av organ eller kroppsdel.">
					comment = <"For eksempel en lymfeknutegruppe (Kroppsstruktur) som er funnet i \"Høyre aksille\" (Anatomisk lokalisasjon). Det er ønskelig å kode \"Anatomisk lokalisasjon\" med en terminologi, som SNOMED CT. Dersom den anatomiske lokalisasjonen er fullt ut angitt i elementet \"Kroppsstruktur\" er det ikke nødvendig å bruke \"Anatomisk lokalisasjon\".">
				>
				["at0003"] = <
					text = <"Strukturert anatomisk lokalisasjon">
					description = <"Strukturerte detaljer om organ eller kroppsdel.">
					comment = <"For eksempel detaljert om en relativ anatomisk lokalisasjon, som \"2 cm distalt for appendix\". Dersom den nøyaktige eller relative anatomiske lokalisasjonen er fullt ut angitt i elementet \"Kroppsstruktur\" er det ikke nødvendig å bruke \"Strukturert anatomisk lokalisasjon\".">
				>
				["at0004"] = <
					text = <"Funn">
					description = <"Fritekstlig beskrivelse av funn eller observasjoner på bilder tatt under denne undersøkelsen.">
				>
				["at0005"] = <
					text = <"Ytterligere detaljer">
					description = <"Ytterligere strukturerte detaljer om funn eller observasjoner på bilder for det definerte organet eller kroppsdelen, eller om funn eller observasjoner hos tilhørende kroppsstrukturer.">
				>
				["at0006"] = <
					text = <"Resymé">
					description = <"Enkeltord, frase eller kortfattet beskrivelse som uttrykker den kliniske betydningen av alle funn og observasjoner for den definerte kroppsstrukturen.">
					comment = <"Kan også kalles \"Tolkning\" eller \"Inntrykk\". For eksempel \"Uten anmerkning\" eller \"Fri væske\". Dersom det er nyttig, er det åpent for å kode \"Resymé\" med en terminologi. Flere svar kan kodes ved at elementet er åpent for å være gjentakende. En beskrivelse i fritektst kan også brukes og kan inneholde en sammenligning med tidligere undersøkelser av samme kroppsstruktur.">
				>
				["at0007"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om funn eller observasjoner på bilder av kroppsstrukturen og som ikke er fanget i andre felt.">
				>
			>
		>
		["de"] = <
			items = <
				["at0.1"] = <
					text = <"Morphologische Anomalie">
					description = <"Eine morphologische Anomalie, die untersucht wurde.">
				>
				["at0.10"] = <
					text = <"Beschaffenheit">
					description = <"Beschaffenheit der Anomalie.">
				>
				["at0.11"] = <
					text = <"Definition">
					description = <"Die Definition der Anomalie.">
				>
				["at0.12"] = <
					text = <"Gut definiert">
					description = <"Die Anomalie ist gut definiert.">
				>
				["at0.13"] = <
					text = <"Schlecht definiert">
					description = <"Die Anomalie ist schlecht definiert.">
				>
				["at0.14"] = <
					text = <"Kontur">
					description = <"Die Kontur der Anomalie.">
				>
				["at0.15"] = <
					text = <"Rand">
					description = <"Der Rand der Anomalie.">
				>
				["at0.16"] = <
					text = <"Kalzifizierung">
					description = <"Vorhandensein von Verkalkung in der Anomalie.">
				>
				["at0.17"] = <
					text = <"Vorhanden">
					description = <"Kalzifizierung wird beobachtet.">
				>
				["at0.18"] = <
					text = <"Nicht vorhanden">
					description = <"Kalzifizierung wird nicht beobachtet.">
				>
				["at0.19"] = <
					text = <"Wirkung">
					description = <"Beschreibung über die Wirkung der Anomalie auf umliegende Strukturen.">
					comment = <"Zum Beispiel: „Drücken auf ein Blutgefäß“.">
				>
				["at0.2"] = <
					text = <"Label">
					description = <"Bezeichnung der Anomalie.">
					comment = <"Wird verwendet, um eine Anomalie von einer anderen zu unterscheiden, wenn mehr als eine Anomalie an derselben Stelle vorhanden ist.">
				>
				["at0.20"] = <
					text = <"Vaskularisierung">
					description = <"Beurteilung des Blutflusses zur Anomalie.">
					comment = <"Oft bestimmt durch Farbdoppler oder Ähnliches.">
				>
				["at0.21"] = <
					text = <"Keine">
					description = <"Es ist kein Blutfluss feststellbar.">
				>
				["at0.22"] = <
					text = <"Leicht">
					description = <"Ein minimaler Blutfluss kann festgestellt werden.">
				>
				["at0.23"] = <
					text = <"Mäßig">
					description = <"Ein mäßiger Blutfluss kann festgestellt werden.">
				>
				["at0.24"] = <
					text = <"Deutlich">
					description = <"Eine deutliche Durchblutung kann festgestellt werden.">
				>
				["at0.3"] = <
					text = <"Art">
					description = <"Art der Anomalie.">
					comment = <"Die Codierung des morphologischen Arts mit einer externen Terminologie wird nach Möglichkeit bevorzugt. Zum Beispiel eine Zyste oder ein Knötchen aus dem SNOMED 4147007 | Masse (morphologische Anomalie) | Hierarchie.">
				>
				["at0.4"] = <
					text = <"Durchmesser">
					description = <"Ein gemessener Durchmesser der Anomalie.">
					comment = <"Dieses Datenelement hat drei Vorkommen, um drei Messungen in verschiedenen Achsen zu ermöglichen.">
				>
				["at0.9"] = <
					text = <"Form">
					description = <"Die Form der Anomalie.">
				>
				["at0000"] = <
					text = <"Bildgebende Untersuchung einer Körperstruktur">
					description = <"Befunde zur radiologischen Untersuchung einer bestimmten Körperstruktur oder -region.">
				>
				["at0000.1"] = <
					text = <"Bildgebende Untersuchung einer Anomalie">
					description = <"Befunde einer morphologischen Anomalie, die bei einer bildgebenden Untersuchung beobachtet werden, die auf eine bestimmte Körperstruktur oder -region abzielt, wobei radiologische technische Verfahren verwendet werden.">
				>
				["at0001"] = <
					text = <"Körperstruktur">
					description = <"Identifizierung der untersuchten Körperstruktur oder Region.">
					comment = <"Zum Beispiel: „Leber“, „Rechtes Fußgelenk“ oder „Lymphknotengruppe“. Es wird empfohlen, die Körperstruktur mit einer geeigneten Terminologie, wie z. B. SNOMED CT, zu codieren.">
				>
				["at0001.1"] = <
					text = <"Körperstruktur">
					description = <"Identifizierung der untersuchten Körperstruktur oder -region.">
				>
				["at0002"] = <
					text = <"Körperstelle">
					description = <"Identifizierung des zu untersuchenden Bereichs des Körpers.">
					comment = <"Zum Beispiel: eine Lymphknotengruppe (Körperstruktur), die sich in der „rechten Achselhöhle“ befindet. Eine Kodierung von „Körperstelle“ mit einer Terminologie, wie z. B. SNOMED CT, ist wünschenswert. Wenn die Körperstelle im Datenelement „Körperstruktur“ vollständig identifiziert wurde, wird dieses Datenelement überflüssig.">
				>
				["at0003"] = <
					text = <"Körperstelle strukturiert">
					description = <"Strukturierte Angaben zum untersuchten Körperbereich.">
					comment = <"Zum Beispiel: Angaben zu einer relativen anatomischen Lokalisation, wie z. B. „2 cm distal zum Blinddarm“. Wenn die Körperstelle in den Datenelementen „Körperstruktur“ oder „Körperstelle“ vollständig identifiziert wurde, wird dieser SLOT überflüssig.">
				>
				["at0004"] = <
					text = <"Bildgebende Befunde">
					description = <"Beschreibung der bei dieser Untersuchung beobachteten bildgebenden Befunde.">
				>
				["at0005"] = <
					text = <"Zusätzliche Angaben">
					description = <"Zusätzliche strukturierte Angaben zu bildgebenden Befunden für die identifizierte Körperstruktur oder -region oder befundbezogene Strukturen.">
				>
				["at0006"] = <
					text = <"Interpretation">
					description = <"Einzelnes Wort, Satz oder kurze Beschreibung, die die klinische Bedeutung und Signifikanz aller bildgebenden Befunde für die identifizierte Körperstruktur darstellt.">
					comment = <"Auch als „Eindruck“ oder „Meinung“ bezeichnet. Zum Beispiel: „Keine Anomalie erkannt“ oder „Freie Flüssigkeit vorhanden“. Eine Kodierung des „Interpretation“ mit einer Terminologie ist zulässig, sofern sinnvoll. Mehrere Vorkommen ermöglichen mehr als eine codierte Interpretation. Eine einfache Beschreibung kann gegebenenfalls einen Vergleich mit früheren Studien der identifizierten Körperstruktur beinhalten.">
				>
				["at0007"] = <
					text = <"Kommentar">
					description = <"Zusätzlicher Text über die bildgebenden Befunde der Körperstruktur, der in anderen Bereichen nicht erfasst wurde.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0.1"] = <[SNOMED-CT::49755003]>
			>
		>
	>

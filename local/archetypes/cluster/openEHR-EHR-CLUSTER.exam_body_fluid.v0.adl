archetype (adl_version=1.4; uid=0f3452b4-5d45-4742-9324-40a684586494)
	openEHR-EHR-CLUSTER.exam_body_fluid.v0

concept
	[at0000]	-- Examination of body fluid
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2015-06-25">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording the findings observed during the examination of a body fluid at an identified body site or from an identified body source.">
			use = <"Use to record a narrative description of the overall findings observed during the  of a body fluid from an identified body source. This initial draft archetype consists only of the core examination pattern data elements (as per CLUSTER.exam_pattern) and will likely be expanded further over time as specific clinical requirements are identified.

Body fluids usually refer to fluids excreted or secreted from the body including, but not limited to:
- Amniotic fluid;
- Blood and plasma;
- Cerebrospinal fluid;
- Discharge;
- Excreta, including urine and liquid faeces
- Exudate;
- Joint fluid;
- Pus;
- Saliva; and
- Sweat.

This archetype has been specifically designed to be used in the 'Examination findings' SLOT within the CLUSTER.exam_cervix, CLUSTER.exam.wound and similar archetypes, and also in the 'Additional details' SLOT within the OBSERVATION.urinalysis archetype. It can also be used within other OBSERVATION or CLUSTER archetypes, where clinically appropriate.

The CLUSTER.exclusion_exam archetype can be nested within the 'Examination not done' SLOT to optionally record explicit details about the examination not being performed.

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical Description' data element.">
			misuse = <"Not to be used for recording measurements of fluid volume - use OBSERVATION archetypes for this purpose. For example OBSERVATION.fluid_output.

Not to be used for recording the clinical history - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom.

Not to be used to record the results of a urinalysis test - use OBSERVATION.urinalysis. This archetype may be included in the 'Additional details' SLOT to provide additional context to the test result.

Not to be used to record stand-alone clinical observations or measurements or test results - use specific OBSERVATION archetypes. For example OBSERVATION.blood_pressure or OBSERVATION.imaging_exam.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Bergen Hospital Trust, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "John Tore Valand, Haukeland Universitetssjukehus, Norway", "Heather Leslie, Ocean Informatics, Australia">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics<heather.leslie@oceaninformatics.com>">
		["revision"] = <"0.0.1-alpha">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["build_uid"] = <"8ab29f15-cb3c-454c-9f78-7a240a90d0a1">
		["original_publisher"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"8EB12C65E92182339BB145EAF5C46621">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	CLUSTER[at0000] matches {	-- Examination of body fluid
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Fluid name
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0015] occurrences matches {0..1} matches {	-- Body source
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0016] occurrences matches {0..*} matches {	-- Structured body source
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_clock(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- Presence?
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0009, 	-- Present
							at0010, 	-- Indeterminate
							at0011]	-- Not present
						}
					}
				}
			}
			ELEMENT[at0014] occurrences matches {0..*} matches {	-- Amount
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0018] occurrences matches {0..1} matches {	-- Colour
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0017] occurrences matches {0..1} matches {	-- Odour
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Additional findings
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exam_body_fluid(-[a-zA-Z0-9_]+)*\.v0/}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Multimedia representation
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {	-- Clinical interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0008] occurrences matches {0..*} matches {	-- Examination not done
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination of body fluid">
					description = <"Findings observed during the examination of body fluid at a body site or from an identified body source.">
				>
				["at0001"] = <
					text = <"Fluid name">
					description = <"The name of the body fluid.">
				>
				["at0002"] = <
					text = <"Presence?">
					description = <"Statement that the fluid was, or was not, observed at the identified body site.">
					comment = <"Record as true if the body fluid was observed. This data element is intended for examination of body fluid in wounds, or similar, and at a specific body site and is not applicable to body fluids that are being examined away from the source such as urine.">
				>
				["at0003"] = <
					text = <"Clinical description">
					description = <"Narrative description about the fluid, including appearance.">
				>
				["at0004"] = <
					text = <"Additional findings">
					description = <"Additional structured details about the body fluid.">
					comment = <"For example: inclusion of further CLUSTER.exam_body_fluid archetypes could describe components of substances such as required to describe a mixture of blood and meconium post childbirth.">
				>
				["at0005"] = <
					text = <"Multimedia representation">
					description = <"Digital image, video or diagram representing the inspection findings.">
				>
				["at0006"] = <
					text = <"Clinical interpretation">
					description = <"Single word, phrase or brief description that represents the clinical meaning and significance of the examination findings.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the inspection findings, not captured in other fields.">
				>
				["at0008"] = <
					text = <"Examination not done">
					description = <"Details to explicitly record that this examination was not performed.">
				>
				["at0009"] = <
					text = <"Present">
					description = <"The fluid was observed at the body site.">
				>
				["at0010"] = <
					text = <"Indeterminate">
					description = <"It was not possible to determine if the fluid was present at the body site.">
				>
				["at0011"] = <
					text = <"Not present">
					description = <"The fluid was not present at the body site.">
				>
				["at0014"] = <
					text = <"Amount">
					description = <"Description of the amount of fluid observed at the identified body site.">
					comment = <"This data element is intended for examination of body fluid in wounds, or similar, and at a specific body site and is not applicable to body fluids that are being examined away from the source such as urine. Can be described as a free text narrative or coded to a value set such as small/medium/profuse or normal/increased where appropriate in a template.">
				>
				["at0015"] = <
					text = <"Body source">
					description = <"Identification of the body site or source .">
					comment = <"If the body site has previously been identified in another archetype in which this archetype is nested, this data element becomes redundant.">
				>
				["at0016"] = <
					text = <"Structured body source">
					description = <"A structured representation of the body source.">
					comment = <"If the body site has previously been identified in another archetype in which this archetype is then nested, this SLOT becomes redundant.">
				>
				["at0017"] = <
					text = <"Odour">
					description = <"Description of the smell of the body fluid.">
				>
				["at0018"] = <
					text = <"Colour">
					description = <"Description of the colour of body fluid.">
				>
			>
		>
	>

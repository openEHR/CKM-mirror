archetype (adl_version=1.4; uid=90f5e49f-48be-4d04-ba90-257d24bcbcb1)
	openEHR-EHR-CLUSTER.exam_skin.v0

concept
	[at0000]	-- Examination of skin
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"John Tore Valand, Silje Ljosland Bakke, Vebjørn Arntzen">
				["organisation"] = <"Helse Bergen HF, Nasjonal IKT HF, Oslo universitetsykehus HF">
			>
		>
	>
description
	original_author = <
		["name"] = <"Silje Ljosland Bakke">
		["organisation"] = <"Nasjonal IKT HF, Norway">
		["date"] = <"2015-06-08">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"Brukes til å registrere en beskrivelse og klinisk tolkning av funn ved fysisk undersøkelse av et spesifikt hudområde.">
			use = <"Brukes til å registrere en beskrivelse og klinisk tolkning av funn ved fysisk undersøkelse av et spesifikt hudområde. 

Denne arketypen har blitt designet spesielt for å settes inn i SLOT'et \"Undersøkelsesdetaljer\" i en av de mange CLUSTER-arketypene for undersøkelse av ulike regioner eller anatomiske strukturer, for eksempel CLUSTER.exam_abdomen eller CLUSTER.exam_breast. Arketypen kan også benyttes direkte i arketypen OBSERVATION.exam (Norsk: Funn ved fysisk undersøkelse).
Brukes som rammeverk der andre CLUSTER-arketyper kan nøstes i SLOT'et \"Undersøkelsesdetaljer\" for å romme detaljerte strukturerte funn i tillegg, for eksempel CLUSTER.exam_lesion.

CLUSTER.exclusion_exam (Norsk: Eksklusjon av en fysisk undersøkelse) kan som en opsjon bli nøstet i SLOT'et for \"Undersøkelse ikke utført\" for å registrere detaljer om hvorfor undersøkelsen ikke ble gjennomført.

Bruk dataelementet \"Klinisk beskrivelse\" for å innlemme fritekstbeskrivelser av kliniske funn fra eksisterende eller historiske kliniske IT-systemer inn i et arketypeformat.">
			keywords = <"hudforandring", ...>
			misuse = <"Skal ikke brukes for å registrere data som ikke er knyttet til hudundersøkelse.

Skal ikke brukes for å registrere anamnestiske opplysninger - bruk spesifikke OBSERVATION- og CLUSTER-arketyper til dette, for eksempel OBSERVATION.story (Norsk: Anamnese) og CLUSTER.symptom_sign (Norsk: Symptom/Sykdomstegn).

Skal ikke brukes for å registrere frittstående kliniske observasjoner eller målinger eller testresultater. Bruk spesifikke OBSERVATION-arketyper, for eksempel OBSERVATION.blood_pressure eller OBSERVATION.imaging_exam.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording a description and clinical interpretation of the findings observed during the physical examination of an identified area of skin.">
			use = <"Use to record a description of the overall findings observed during the physical examination of an area of skin.

This archetype has been specifically designed to be used in the 'Examination findings' SLOT within many of the CLUSTER archetypes for examination of various regions or anatomical structures - for example, CLUSTER.exam_abdomen or CLUSTER.exam_breast - but can also be used within the OBSERVATION.exam.

Use to provide a framework in which CLUSTER archetypes can be nested in the 'Examination findings' SLOT to record additional structured physical examination findings. 

The CLUSTER.exclusion_exam archetype can be nested within the 'Examination not done' SLOT to optionally record explicit details about the examination not being performed.

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical Description' data element.">
			misuse = <"Not to be used for recording data not related to the physical examination of skin.

Not to be used for recording the clinical history - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom.

Not to be used to record stand-alone clinical observations or measurements or test results - use specific OBSERVATION archetypes. For example OBSERVATION.blood_pressure or OBSERVATION.imaging_exam.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo universitetssykehus HF, Norway (Nasjonal IKT redaktør)", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (Nasjonal IKT redaktør)", "Heather Leslie, Ocean Informatics, Australia (openEHR Editor)", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Nasjonal IKT redaktør)">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics<heather.leslie@oceaninformatics.com>">
		["revision"] = <"0.0.1-alpha">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["build_uid"] = <"e645f61b-54b7-4d83-8983-88b167bb2e52">
		["original_publisher"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"5033749CC05FB1D564E862C80E6D2633">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	CLUSTER[at0000] matches {	-- Examination of skin
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Body site
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0011] occurrences matches {0..*} matches {	-- Structured body site
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.anatomical_location_relative(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- No abnormality detected
				value matches {
					DV_BOOLEAN matches {
						value matches {True}
					}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0012] occurrences matches {0..*} matches {	-- Finding summary
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0013] occurrences matches {0..1} matches {	-- Finding
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0014] occurrences matches {0..1} matches {	-- Presence
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0018, 	-- Present
									at0019, 	-- Absent
									at0020]	-- Indeterminate
								}
							}
						}
					}
					ELEMENT[at0015] occurrences matches {0..1} matches {	-- Clinical description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0016] occurrences matches {0..1} matches {	-- Number
						value matches {
							DV_COUNT matches {*}
						}
					}
				}
			}
			allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Examination findings
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exam_skin(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.exam_wound(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.exam_lesion(-[a-zA-Z0-9_]+)*\.v0/}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Multimedia representation
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {	-- Clinical interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0008] occurrences matches {0..*} matches {	-- Examination not done
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}




ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination of skin">
					description = <"Findings observed during the physical examination of an area of skin.">
				>
				["at0001"] = <
					text = <"Body site">
					description = <"Identification of the area of skin under examination.">
					comment = <"For example, entire body, face or right cheek. If the body site has previously been identified in another archetype in which this archetype is nested, this data element becomes redundant.">
				>
				["at0002"] = <
					text = <"No abnormality detected">
					description = <"Statement that no abnormality was detected (NAD) on physical examination.">
					comment = <"Record as True if no abnormality was detected on examination. Specific statements about the examination can be included in the 'Clinical Interpretation' data element. If 'No abnormality detected' is selected, then recording of other examination data elements becomes redundant, with the exception of only the 'Clinical interpretation' data element, which may be useful if a normal statement is desired for recording, for example 'Normal skin examination'.">
				>
				["at0003"] = <
					text = <"Clinical description">
					description = <"Narrative description of the overall findings observed during the skin examination.">
				>
				["at0004"] = <
					text = <"Examination findings">
					description = <"Structured details about one or more specific findings on physical examination of the identified skin site.">
					comment = <"For example, allowing fractal examination of a more specific area of the skin site. It could also be used to record the examination of a specific lesion or wound, for example by using the 'Alias' data element in CLUSTER.exam_burn or CLUSTER.exam_lesion.">
				>
				["at0005"] = <
					text = <"Multimedia representation">
					description = <"Digital image, video or diagram representing the physical examination findings.">
				>
				["at0006"] = <
					text = <"Clinical interpretation">
					description = <"Single word, phrase or brief description that represents the clinical meaning and significance of the physical examination findings.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the physical examination findings, not captured in other fields.">
				>
				["at0008"] = <
					text = <"Examination not done">
					description = <"Details to explicitly record that this examination was not performed.">
				>
				["at0011"] = <
					text = <"Structured body site">
					description = <"A structured description of the area of skin under examination.">
					comment = <"If the body site has previously been identified in another archetype in which this archetype is then nested, this SLOT becomes redundant.">
				>
				["at0012"] = <
					text = <"Finding summary">
					description = <"Summary information about findings on the skin at the identified body site.">
					comment = <"Use this cluster of data elements where it makes sense to record summary information only. For example: the presence/absence of the identified finding (such as psoriasis), or the number of items (such as number of condyloma present).">
				>
				["at0013"] = <
					text = <"Finding">
					description = <"Identification of the clinical finding, by name or category.">
					comment = <"'Finding' should be coded with a terminology, where possible.">
				>
				["at0014"] = <
					text = <"Presence">
					description = <"The presence or absence of the specified finding.">
				>
				["at0015"] = <
					text = <"Clinical description">
					description = <"Narrative description about the specified finding.">
				>
				["at0016"] = <
					text = <"Number">
					description = <"The number of the specified finding observed in the identified body site.">
					comment = <"For example, papilloma or rash.">
				>
				["at0018"] = <
					text = <"Present">
					description = <"The finding is observed to be present.">
				>
				["at0019"] = <
					text = <"Absent">
					description = <"The finding is not observed to be present.">
				>
				["at0020"] = <
					text = <"Indeterminate">
					description = <"It is not able to be determined if the finding is present or absent..">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Undersøkelse av huden">
					description = <"Funn ved hudundersøkelse.">
				>
				["at0001"] = <
					text = <"Navn på kroppssted">
					description = <"Identifikasjon av hudområdet som er undersøkt.">
					comment = <"For eksempel: Hele kroppen, ansiktet eller høyre kinn. Hvis navn på kroppssted allerede er identifisert i en annen arketype hvor denne CLUSTER-arketypen er nøstet inn blir dette dataelementet overflødig.">
				>
				["at0002"] = <
					text = <"Uten anmerkning">
					description = <"Utsagn om at ingen avvik var oppdaget ved undersøkelsen (U.a.).">
					comment = <"Registrer som Sann dersom ingen avvik ble oppdaget ved undersøkelsen. Spesifikke utsagn om undersøkelsen kan registreres i dataelementet \"Klinisk fortolkning\". Registrerer man \"Uten anmerkning\", vil registrering av andre dataelementer i arketypen være overflødig med unntak av dataelementet \"Klinisk fortolkning\", som kan være nyttig om en har behov for en normalbeskrivelse, for eksempel \"Normal hudundersøkelse\".">
				>
				["at0003"] = <
					text = <"Klinisk beskrivelse">
					description = <"Fritekstbeskrivelse om det spesifikke hudfunnet.">
				>
				["at0004"] = <
					text = <"Spesifikke enkeltfunn">
					description = <"Ytterligere strukturerte detaljer om et eller flere enkeltfunn fra undersøkelsen.">
					comment = <"For eksempel en mer detaljert undersøkelse av en del av hudområdet. Kan også brukes for å registrere undersøkelse av en spesifikk lesjon eller sår, f.eks. ved å bruke dataelementet \"Alias\" i CLUSTER.exam_burn eller CLUSTER.exam_lesion.">
				>
				["at0005"] = <
					text = <"Multimedia">
					description = <"Digitale bilder, video eller diagram som representerer funn ved undersøkelsen.">
				>
				["at0006"] = <
					text = <"Klinisk fortolkning">
					description = <"Enkelt ord, frase eller kort beskrivelse som representerer klinisk betydning og signifikans ved funnene.">
				>
				["at0007"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om funn ved undersøkelsen ikke registrert i andre felt.">
				>
				["at0008"] = <
					text = <"Undersøkelse ikke utført">
					description = <"Detaljer for å eksplisitt registrere at denne undersøkelsen ikke ble utført.">
				>
				["at0011"] = <
					text = <"Strukturert kroppssted">
					description = <"En strukturert beskrivelse av et spesifikt sted på huden som er undersøkt.">
					comment = <"Hvis strukturert kroppssted allerede er identifisert i en annen arketype hvor denne CLUSTER-arketypen er nøstet inn blir dette dataelementet overflødig.">
				>
				["at0012"] = <
					text = <"Sammendrag av funn">
					description = <"Sammendrag av funn på hudområdet.">
					comment = <"Bruk denne gruppen dataelementer der det gir mening å registrere kun oppsummerende informasjon, for eksempel tilstedeværelse eller fravær av funnet (f.eks. psoriasis), eller antallet av et funn (f.eks. antallet kondylomer).">
				>
				["at0013"] = <
					text = <"Hudfunn">
					description = <"Hudfunnets navn eller kategori.">
					comment = <"\"Hudfunn\" bør om mulig kodes med en terminologi.">
				>
				["at0014"] = <
					text = <"Tilstedeværelse">
					description = <"Tilstedeværelse eller fravær av det spesifikke hudfunnet.">
				>
				["at0015"] = <
					text = <"Klinisk beskrivelse">
					description = <"Fritekstbeskrivelse om det spesifikke hudfunnet.">
				>
				["at0016"] = <
					text = <"Antall">
					description = <"Antall av det spesifikke funnet på kroppsstedet.">
					comment = <"For eksempel: Papillom eller utslett.">
				>
				["at0018"] = <
					text = <"Tilstede">
					description = <"Hudfunnet er til stede.">
				>
				["at0019"] = <
					text = <"Fraværende">
					description = <"Hudfunnet er ikke tilstede.">
				>
				["at0020"] = <
					text = <"Ubestemmelig">
					description = <"Det er ikke mulig å bestemme om funnet er tilstede.">
				>
			>
		>
	>

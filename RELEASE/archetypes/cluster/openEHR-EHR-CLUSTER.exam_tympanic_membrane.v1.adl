archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.exam_tympanic_membrane.v1

concept
	[at0000]	-- Examination of External Auditory Canal and Tympanic Membrane
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2012-09-03">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording the findings on physical examination of the external auditory canal and tympanic membrane by a clinician.">
			use = <"Use to record the findings on physical examination of the external auditory canal and tympanic membrane by a clinician., including details of any perforation.

Specifically designed to be used within the OBSERVATION.exam-ear archetype, but may be used within any clinically appropriate ENTRY or CLUSTER archetype.

Please note: this archetype has been designed specifically to reflect current practice within the NT Hearing Health program. It is recognised that this is not currently reflecting a maximal dataset and may need to be extended in future revisions as more specific requirements are identified.">
			keywords = <"inspection", "ear", "tympanic", "perforation", "drum", "canal", "otoscopic", "exam", "examination", "pneumatic", "otoscopy", "membrane">
			misuse = <"Not to be used to record findings of the physical examination of other parts of the ear, such as pinna and mastoid region - use other purpose-built archetypes for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"CommitteeDraft">
	other_contributors = <"Anthony Leech, Hearing Health, Australia", "Stephen Chu, NEHTA, Australia", "Marissa Cookson, Dept of Health, Australia", "Graeme Crossland, Northern Territory, Australia", "Kathy Currie, Northern Territory Health, Australia", "Jacques de Haan, Alice Springs Hospital, Australia", "Jade Frederiksen, Hearing Health Program, DoH, Australia", "Sam Heard, Ocean Informatics, Australia", "Paul Hickey, Hearing Health Program, Australia", "Oliver Hosking, Remote Health NT, Australia", "Amanda Leach, Menzies School of Health Research, Australia", "Kerrie Lee, Ngaanyatjarra Health Service, Australia (Editor)", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Ian McNicoll, Ocean Informatics, United Kingdom", "Hemi Patel, Royal Darwin Hospital, Australia", "Fiona Wake, RAHC, Australia", "Christine Wigger, Menzies School of Health Research, Australia">
	other_details = <
		["MD5-CAM-1.0.1"] = <"8F6D4152A87D3B1BC1640C989B5CC83C">
		["references"] = <"Communication with clinical domain experts associated with Australia's Northern Territory Hearing Health Program.

Northern Territory Government, Department of Health: Hearing Health Program ENT Specialist Consultation Form 2011 12 07.

Northern Territory Government, Department of Health: Northern Territory Hearing Health Program Teleotology Nurse Consultation Form 2011 12 07.

Northern Territory Government, Department of Health: Northern Territory Hearing Health Program Audiologist Consultation Form 2011 12 07.

OATSIH:  The Recommendations for Clinical Care Guidelines on the Management of Otitis Media in Aboriginal and Torres Strait Islander Populations (2010). Available at: http://www.health.gov.au/internet/publications/publishing.nsf/Content/CA2578620005D57ACA25792800821BE8?OpenDocument.">
	>

definition
	CLUSTER[at0000] matches {	-- Examination of External Auditory Canal and Tympanic Membrane
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0097] occurrences matches {0..1} matches {	-- Ear Examined
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0098, 	-- Left
							at0099]	-- Right
						}
					}
				}
			}
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Clinical Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0106] occurrences matches {0..1} matches {	-- Consistency of Wax
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0107, 	-- Hard
							at0108]	-- Soft
						}
					}
				}
			}
			ELEMENT[at0109] occurrences matches {0..1} matches {	-- State of Wax
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0110, 	-- Impacted
							at0111]	-- Not impacted
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- View of Tympanic Membrane
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0003, 	-- Full view
							at0004, 	-- Partial occlusion
							at0005]	-- Total occlusion
						}
					}
				}
			}
			ELEMENT[at0006] occurrences matches {0..1} matches {	-- Reason for Occlusion of View
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0139] occurrences matches {0..1} matches {	-- Tenderness of Canal
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0140, 	-- Present
							at0141]	-- Absent
						}
					}
				}
			}
			ELEMENT[at0132] occurrences matches {0..1} matches {	-- Oedema of Canal
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0133, 	-- Present
							at0134]	-- Absent
						}
					}
				}
			}
			ELEMENT[at0124] occurrences matches {0..1} matches {	-- Erythema of Canal
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0125, 	-- Present
							at0126]	-- Absent
						}
					}
				}
			}
			ELEMENT[at0143] occurrences matches {0..1} matches {	-- Offensive Odour
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0145, 	-- Present
							at0144]	-- Absent
						}
					}
				}
			}
			ELEMENT[at0157] occurrences matches {0..1} matches {	-- Discharge Type
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0090, 	-- Serous
							at0091, 	-- Bloody
							at0092, 	-- Purulent
							at0093, 	-- Mucous
							at0094, 	-- Mucopurulent
							at0095]	-- Haemoserous
						}
					}
				}
			}
			ELEMENT[at0151] occurrences matches {0..1} matches {	-- Discharge Amount
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0152, 	-- None
							at0153, 	-- Scant
							at0154, 	-- Moderate
							at0155]	-- Profuse
						}
					}
				}
			}
			ELEMENT[at0156] occurrences matches {0..1} matches {	-- Discharge Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0166] occurrences matches {0..1} matches {	-- Ventilation Tube Position
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0167, 	-- In situ
							at0168, 	-- Partially extruded
							at0169, 	-- Extruded - remains in canal
							at0170, 	-- Extruded - missing
							at0171]	-- Unknown
						}
					}
				}
			}
			CLUSTER[at0064] occurrences matches {0..1} matches {	-- Abnormal Features
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0112] occurrences matches {0..1} matches {	-- Polyps
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0113, 	-- Present
									at0114]	-- Absent
								}
							}
						}
					}
					ELEMENT[at0147] occurrences matches {0..1} matches {	-- Fungal Spores
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0148, 	-- Present
									at0149]	-- Absent
								}
							}
						}
					}
					ELEMENT[at0159] occurrences matches {0..1} matches {	-- Foreign Body
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0160, 	-- Present
									at0161]	-- Absent
								}
							}
						}
					}
					ELEMENT[at0163] occurrences matches {0..1} matches {	-- Location of Foreign Body
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0164, 	-- Outer canal
									at0165]	-- Deep canal
								}
							}
						}
					}
				}
			}
			CLUSTER[at0104] occurrences matches {0..1} matches {	-- Tympanic Membrane Details
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0007] occurrences matches {0..1} matches {	-- Membrane Intact?
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0008, 	-- Intact
									at0009, 	-- Perforated
									at0182]	-- Indeterminate
								}
							}
						}
						null_flavour existence matches {0..1} matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[openehr::
									253, 
									271, 
									273, 
									272]
								}
							}
						}
					}
					ELEMENT[at0018] occurrences matches {0..1} matches {	-- Appearance
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0019, 	-- Translucent
									at0021]	-- Opaque
								}
							}
						}
					}
					ELEMENT[at0011] occurrences matches {0..1} matches {	-- Normal Light Reflex
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0012, 	-- Present
									at0013]	-- Absent
								}
							}
						}
					}
					ELEMENT[at0022] occurrences matches {0..*} matches {	-- Surface Features
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0023, 	-- Blistering present
									at0024, 	-- Scarring present
									at0025, 	-- Granulation present
									at0026, 	-- Thickening present
									at0175]	-- Tympanosclerosis present
								}
							}
						}
					}
					ELEMENT[at0028] occurrences matches {0..1} matches {	-- Colour
						value matches {
							DV_TEXT matches {*}
						}
					}
					CLUSTER[at0010] occurrences matches {0..1} matches {	-- Landmark Anatomy
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0014] occurrences matches {0..1} matches {	-- Annulus
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0015] occurrences matches {0..1} matches {	-- Malleus Handle
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0016] occurrences matches {0..1} matches {	-- Malleus Head
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0017] occurrences matches {0..1} matches {	-- Umbo
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0061] occurrences matches {0..1} matches {	-- Incus
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0060] occurrences matches {0..1} matches {	-- Stapes
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
					ELEMENT[at0034] occurrences matches {0..*} matches {	-- Location of Tympanosclerosis
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0035] occurrences matches {0..1} matches {	-- Position of Tympanic Membrane
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0037, 	-- Bulging
									at0036, 	-- Neutral
									at0038]	-- Retracted
								}
							}
						}
					}
					ELEMENT[at0040] occurrences matches {0..1} matches {	-- Description of Retraction
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0044] occurrences matches {0..*} matches {	-- Retraction Grade
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.sade_classification(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.tos_classification(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0048] occurrences matches {0..1} matches {	-- Fluid Level Presence
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0049, 	-- Present
									at0050]	-- Absent
								}
							}
						}
					}
					ELEMENT[at0051] occurrences matches {0..1} matches {	-- Fluid Level Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0052] occurrences matches {0..1} matches {	-- Mobility
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0053, 	-- Immobile
									at0054, 	-- Hypomobile
									at0055, 	-- Mobile
									at0056]	-- Hypermobile
								}
							}
						}
					}
					CLUSTER[at0062] occurrences matches {0..1} matches {	-- Perforation Details
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0065] occurrences matches {0..1} matches {	-- Estimation of Size
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0066, 	-- Pinhole
											at0067, 	-- Intermediate
											at0068, 	-- Subtotal
											at0069]	-- Total
										}
									}
								}
							}
							allow_archetype CLUSTER[at0070] occurrences matches {0..*} matches {	-- Measurement
								include
									archetype_id/value matches {/openEHR-EHR-CLUSTER\.dimensions(-[a-zA-Z0-9_]+)*\.v1/}
							}
							ELEMENT[at0074] occurrences matches {0..1} matches {	-- Region
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0180, 	-- Pars flaccida
											at0181]	-- Pars tensa
										}
									}
								}
							}
							ELEMENT[at0071] occurrences matches {0..1} matches {	-- Marginal?
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0072, 	-- Central
											at0073]	-- Marginal
										}
									}
								}
							}
							ELEMENT[at0076] occurrences matches {0..1} matches {	-- Anterior?
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0077, 	-- Present
											at0078]	-- Absent
										}
									}
								}
							}
							ELEMENT[at0079] occurrences matches {0..1} matches {	-- Edge
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
				}
			}
			ELEMENT[at0172] occurrences matches {0..*} matches {	-- Image
				value matches {
					DV_MULTIMEDIA matches {
						media_type matches {
							[openEHR::
							425, 
							426, 
							427, 
							428, 
							429, 
							401, 
							402, 
							404, 
							405, 
							406, 
							407, 
							414]
						}
					}
				}
			}
			ELEMENT[at0105] occurrences matches {0..*} matches {	-- Clinical Interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0173] occurrences matches {0..1} matches {	-- Comments
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination of External Auditory Canal and Tympanic Membrane">
					description = <"Physical examination of the external auditory canal and tympanic membrane
 by a clinician.">
				>
				["at0001"] = <
					text = <"Clinical Description">
					description = <"Narrative description of findings observed by a clinician during a physical examination of the external auditory canal and tympanic membrane.">
					comment = <"Use this data element to provide additional, narrative description of any data elements related to the external auditory canal and tympanic membrane examination as a whole, that are not represented by structured values. For example, the relative size and surface of the auditory canal; increased vascularity of the tympanic membrane; presence of abnormalities not captured in specific structured data elements, such as presence of canal stenosis, exostoses, mastoid bowl/cavity or tumours.">
				>
				["at0002"] = <
					text = <"View of Tympanic Membrane">
					description = <"View of the tympanic membrane.">
				>
				["at0003"] = <
					text = <"Full view">
					description = <"All of the tympanic membrane was visualised.">
				>
				["at0004"] = <
					text = <"Partial occlusion">
					description = <"Part of the tympanic membrane was not clearly visualised, but sufficient to permit a clinical opinion to be formed.">
				>
				["at0005"] = <
					text = <"Total occlusion">
					description = <"None of the tympanic membrane visualised, such that a clinical opinion of the state of the tympanic membrane could not be formed.">
				>
				["at0006"] = <
					text = <"Reason for Occlusion of View">
					description = <"Description of the reason for occlusion to the view of the tympanic membrane.">
					comment = <"For example, soft wax or foreign body.">
				>
				["at0007"] = <
					text = <"Membrane Intact?">
					description = <"Is the tympanic membrane intact?">
				>
				["at0008"] = <
					text = <"Intact">
					description = <"The tympanic membrane appears intact.">
				>
				["at0009"] = <
					text = <"Perforated">
					description = <"The tympanic membrane appears perforated.">
				>
				["at0010"] = <
					text = <"Landmark Anatomy">
					description = <"Details about visibility of anatomical landmarks of the middle ear at the tympanic membrane.">
				>
				["at0011"] = <
					text = <"Normal Light Reflex">
					description = <"Presence of a normal light reflex noted.">
					comment = <"In most situations, this data element would only be recorded if the tympanic membrane is intact. A 'normal' light reflex may be described as : a complete, bright triangular area of reflected light seen in the anteroinferior portion of the tympanic membrane.">
				>
				["at0012"] = <
					text = <"Present">
					description = <"A normal light reflex is visible.">
				>
				["at0013"] = <
					text = <"Absent">
					description = <"A normal light reflex is not visible.">
				>
				["at0014"] = <
					text = <"Annulus">
					description = <"Narrative description of the Annulus.">
				>
				["at0015"] = <
					text = <"Malleus Handle">
					description = <"Narrative description of the Malleus Handle.">
				>
				["at0016"] = <
					text = <"Malleus Head">
					description = <"Narrative description of the Malleus Head.">
				>
				["at0017"] = <
					text = <"Umbo">
					description = <"Narrative description of the Umbo.">
				>
				["at0018"] = <
					text = <"Appearance">
					description = <"Category describing the appearance of the tympanic membrane.">
					comment = <"In most situations, this data element would only be recorded if the tympanic membrane is intact.">
				>
				["at0019"] = <
					text = <"Translucent">
					description = <"The tympanic membrane appears healthy.">
				>
				["at0021"] = <
					text = <"Opaque">
					description = <"The tympanic membrane appears dull and opaque.">
				>
				["at0022"] = <
					text = <"Surface Features">
					description = <"Features observed on the surface of the tympanic membrane.">
					comment = <"Data element has multiple occurrences to enable multiple selections, if required. In most situations, this data element would only be recorded if the tympanic membrane is intact.">
				>
				["at0023"] = <
					text = <"Blistering present">
					description = <"Blistering is observed on the surface of the tympanic membrane.">
				>
				["at0024"] = <
					text = <"Scarring present">
					description = <"Scarring is observed on the surface of the tympanic membrane.">
				>
				["at0025"] = <
					text = <"Granulation present">
					description = <"Granulation is observed on the surface of the tympanic membrane.">
				>
				["at0026"] = <
					text = <"Thickening present">
					description = <"Thickening is observed on the surface of the tympanic membrane.">
				>
				["at0028"] = <
					text = <"Colour">
					description = <"Description of the overall colour of the tympanic membrane.">
					comment = <"For example: red, bluish, or yellow. ">
				>
				["at0034"] = <
					text = <"Location of Tympanosclerosis">
					description = <"Description of the location of any tympanosclerosis observed on the tympanic membrane.">
					comment = <"May be coded with an external terminology, if desired, and be expressed as multiple occurrences for multiple sites.">
				>
				["at0035"] = <
					text = <"Position of Tympanic Membrane">
					description = <"Description of the position of the tympanic membrane.">
				>
				["at0036"] = <
					text = <"Neutral">
					description = <"The tympanic membrane is in a normal, neutral position - neither bulging or retracted.">
				>
				["at0037"] = <
					text = <"Bulging">
					description = <"The tympanic membrane appears to be bulging.">
				>
				["at0038"] = <
					text = <"Retracted">
					description = <"The tympanic membrane appears to be retracted.">
				>
				["at0040"] = <
					text = <"Description of Retraction">
					description = <"Description of retraction of the tympanic membrane.">
				>
				["at0044"] = <
					text = <"Retraction Grade">
					description = <"Description of retraction using formal classification/grading.">
					comment = <"For example: Sade Classification or Tos Classification, typically used by ENT specialists.">
				>
				["at0048"] = <
					text = <"Fluid Level Presence">
					description = <"Presence of a fluid level behind the tympanic membrane.">
				>
				["at0049"] = <
					text = <"Present">
					description = <"A fluid level is observed behind the tympanic membrane.">
				>
				["at0050"] = <
					text = <"Absent">
					description = <"A fluid level is not observed behind the tympanic membrane.">
				>
				["at0051"] = <
					text = <"Fluid Level Description">
					description = <"Narrative description of the fluid level and other related features observed behind the tympanic membrane.">
					comment = <"For example, presence of bubbles.">
				>
				["at0052"] = <
					text = <"Mobility">
					description = <"Description of mobility of the tympanic membrane, usually as determined by pneumatic otoscopy.">
				>
				["at0053"] = <
					text = <"Immobile">
					description = <"There is minimal or no movement of the tympanic membrane when positive pressure is applied.">
				>
				["at0054"] = <
					text = <"Hypomobile">
					description = <"The tympanic membrane moves with less excursion and in a more sluggish fashion when positive pressure is applied.">
				>
				["at0055"] = <
					text = <"Mobile">
					description = <"The tympanic membrane shows a crisp inward (medial) movement (approx 1mm) with positive pressure.">
				>
				["at0056"] = <
					text = <"Hypermobile">
					description = <"There is exaggerated movement of the tympanic membrane when positive pressure is applied.">
				>
				["at0060"] = <
					text = <"Stapes">
					description = <"Narrative description of the Stapes.">
				>
				["at0061"] = <
					text = <"Incus">
					description = <"Narrative description of the Incus.">
				>
				["at0062"] = <
					text = <"Perforation Details">
					description = <"Details about the tympanic membrane perforation.">
				>
				["at0064"] = <
					text = <"Abnormal Features">
					description = <"Details about abnormal features noted during the examination of the external auditory canal.">
				>
				["at0065"] = <
					text = <"Estimation of Size">
					description = <"Estimation of the size of the tympanic membrane perforation, based on anatomical landmarks.">
					comment = <"Many clinicians record perforations between the well defined 'pinhole' and 'subtotal' perforations using a variety of terms with inconsistent usage and definitions, such as small, medium and large. 'Intermediate' has been proposed as a means to minimise inter-clinician variability for documenting the size of all perforations falling between the 'pinhole' and 'subtotal' definitions. If more detail is required, then measurement of the perforation should be recorded.">
				>
				["at0066"] = <
					text = <"Pinhole">
					description = <"A pinhole perforation is present in the tympanic membrane.">
				>
				["at0067"] = <
					text = <"Intermediate">
					description = <"A perforation is present in the tympanic membrane, that is larger than a pinhole but smaller than a subtotal perforation.">
				>
				["at0068"] = <
					text = <"Subtotal">
					description = <"A subtotal perforation is present in the tympanic membrane: the pars tensa is absent but the perforation does not include the annulus.">
				>
				["at0069"] = <
					text = <"Total">
					description = <"A total perforation is present in the tympanic membrane; the tympanic membrane is essentially absent.">
				>
				["at0070"] = <
					text = <"Measurement">
					description = <"Direct measurement of the size of the tympanic membrane perforation, usually using a photograph as the source for direct measurement.">
				>
				["at0071"] = <
					text = <"Marginal?">
					description = <"Location of the tympanic membrane perforation.">
					comment = <"To document explicitly whether the perforation is located marginally, and therefore might be clinically unsafe. If 'Total' is selected in 'Estimation of Size' data element then this data element becomes redundant.">
				>
				["at0072"] = <
					text = <"Central">
					description = <"The perforation is not involving the annulus and is bounded on all sides by the remnant of the tympanic membrane.">
				>
				["at0073"] = <
					text = <"Marginal">
					description = <"The perforation involves the annulus of the tympanic membrane.">
				>
				["at0074"] = <
					text = <"Region">
					description = <"Description of the region of the tympanic membrane perforation.">
					comment = <"To document if the perforation is in the Pars flaccida and may be potentially unsafe. If 'Total' or 'Subtotal' is selected in 'Estimation of Size' data element then this data element becomes redundant. Coding with a terminology is preferred, if possible.">
				>
				["at0076"] = <
					text = <"Anterior?">
					description = <"Is an anterior perforation present?">
					comment = <"To document the presence of perf in the anterior portions of the pars tensa can be more difficult to manage. If 'Total' or 'Subtotal' is selected in 'Estimation of Size' data element then this data element becomes redundant.">
				>
				["at0077"] = <
					text = <"Present">
					description = <"An anterior perforation is present in the pars tensa.">
				>
				["at0078"] = <
					text = <"Absent">
					description = <"An anterior perforation is not present in the pars tensa.">
				>
				["at0079"] = <
					text = <"Edge">
					description = <"Narrative description of the edge of the perforation.">
				>
				["at0090"] = <
					text = <"Serous">
					description = <"The discharge appears to consist mostly of clear, watery fluid.">
				>
				["at0091"] = <
					text = <"Bloody">
					description = <"The discharge appears to consist mostly of blood.">
				>
				["at0092"] = <
					text = <"Purulent">
					description = <"The discharge appears to consist mostly of pus.">
				>
				["at0093"] = <
					text = <"Mucous">
					description = <"The discharge appears to consist mostly of a thick, mucoid substance.">
				>
				["at0094"] = <
					text = <"Mucopurulent">
					description = <"The discharge appears to consist of both mucous and pus.">
				>
				["at0095"] = <
					text = <"Haemoserous">
					description = <"The discharge appears to consist of both blood and clear, watery fluid.">
				>
				["at0097"] = <
					text = <"Ear Examined">
					description = <"Identification of the ear under examination.">
				>
				["at0098"] = <
					text = <"Left">
					description = <"The left external auditory canal and tympanic membrane.">
				>
				["at0099"] = <
					text = <"Right">
					description = <"The right external auditory canal and tympanic membrane.">
				>
				["at0104"] = <
					text = <"Tympanic Membrane Details">
					description = <"Details about findings on examination of the tympanic membrane.">
				>
				["at0105"] = <
					text = <"Clinical Interpretation">
					description = <"Single word, phrase or brief description representing a summary of the examination findings.">
					comment = <"Coding with a terminology is preferred, if possible. For example, normal examination or chronic otitis media.">
				>
				["at0106"] = <
					text = <"Consistency of Wax">
					description = <"Description of the type of wax in the external auditory canal.">
					comment = <"Presence of hard wax has implications on treatment decisions. Presence of soft wax may be a causative factor in visual occlusion.">
				>
				["at0107"] = <
					text = <"Hard">
					description = <"The wax is hard.">
				>
				["at0108"] = <
					text = <"Soft">
					description = <"The wax is soft.">
				>
				["at0109"] = <
					text = <"State of Wax">
					description = <"Description of the state of the wax observed in the external auditory canal.">
				>
				["at0110"] = <
					text = <"Impacted">
					description = <"Ear wax is tightly packed in the external auditory canal, such that it may be hard to remove or cause difficulty with hearing.">
				>
				["at0111"] = <
					text = <"Not impacted">
					description = <"Ear wax is not tightly packed in the external auditory canal.">
				>
				["at0112"] = <
					text = <"Polyps">
					description = <"Presence of polyp/polyps in the external auditory canal.">
				>
				["at0113"] = <
					text = <"Present">
					description = <"Polyps are observed in the external auditory canal.">
				>
				["at0114"] = <
					text = <"Absent">
					description = <"No polyps are observed in the external auditory canal.">
				>
				["at0124"] = <
					text = <"Erythema of Canal">
					description = <"Presence of erythema or redness in the external auditory canal.">
				>
				["at0125"] = <
					text = <"Present">
					description = <"Erythema, or redness, is observed in the wall of the external auditory canal.">
				>
				["at0126"] = <
					text = <"Absent">
					description = <"There is no erythema or redness observed in the wall of the external auditory canal.">
				>
				["at0132"] = <
					text = <"Oedema of Canal">
					description = <"Presence of oedema in the external auditory canal.">
				>
				["at0133"] = <
					text = <"Present">
					description = <"Oedema is observed in the external auditory canal wall.">
				>
				["at0134"] = <
					text = <"Absent">
					description = <"Oedema is not observed in the external auditory canal wall.">
				>
				["at0139"] = <
					text = <"Tenderness of Canal">
					description = <"Presence of tenderness in the external auditory canal.">
				>
				["at0140"] = <
					text = <"Present">
					description = <"Tenderness is noted in the external auditory canal wall.">
				>
				["at0141"] = <
					text = <"Absent">
					description = <"Tenderness is not noted in the external auditory canal wall.">
				>
				["at0143"] = <
					text = <"Offensive Odour">
					description = <"Presence of any offensive odour originating from the external auditory canal.">
				>
				["at0144"] = <
					text = <"Absent">
					description = <"No odour is noted as originating from the external auditory canal.">
				>
				["at0145"] = <
					text = <"Present">
					description = <"An offensive odour is noted as originating from the external auditory canal.">
				>
				["at0147"] = <
					text = <"Fungal Spores">
					description = <"Presence of fungal spores in the external auditory canal.">
				>
				["at0148"] = <
					text = <"Present">
					description = <"Fungal spores are observed in the external auditory canal.">
				>
				["at0149"] = <
					text = <"Absent">
					description = <"No fungal spores are observed in the external auditory canal.">
				>
				["at0151"] = <
					text = <"Discharge Amount">
					description = <"Amount of discharge observed in the external auditory canal or at the tympanic membrane perforation.">
				>
				["at0152"] = <
					text = <"None">
					description = <"No discharge is observed in the external auditory canal.">
				>
				["at0153"] = <
					text = <"Scant">
					description = <"A small amount of discharge is observed in the external auditory canal.">
				>
				["at0154"] = <
					text = <"Moderate">
					description = <"A moderate amount of discharge is observed in the external auditory canal.">
				>
				["at0155"] = <
					text = <"Profuse">
					description = <"A profuse amount of discharge is observed in the external auditory canal.">
				>
				["at0156"] = <
					text = <"Discharge Description">
					description = <"Narrative description of the discharge observed in the external auditory canal or at the tympanic membrane perforation.">
				>
				["at0157"] = <
					text = <"Discharge Type">
					description = <"Type of discharge observed in the external auditory canal or at the tympanic membrane perforation.">
				>
				["at0159"] = <
					text = <"Foreign Body">
					description = <"Presence of a foreign body in the external auditory canal.">
				>
				["at0160"] = <
					text = <"Present">
					description = <"A foreign body is present in the external auditory canal.">
				>
				["at0161"] = <
					text = <"Absent">
					description = <"A foreign body is present in the external auditory canal.">
				>
				["at0163"] = <
					text = <"Location of Foreign Body">
					description = <"Location of the foreign body within the external auditory canal.">
				>
				["at0164"] = <
					text = <"Outer canal">
					description = <"The foreign body is located in the outer part of the external auditory canal.">
				>
				["at0165"] = <
					text = <"Deep canal">
					description = <"The foreign body is located in the deep, inner part of the external auditory canal.">
				>
				["at0166"] = <
					text = <"Ventilation Tube Position">
					description = <"Observed position of the ventilation tube (or grommet) in the tympanic membrane or the auditory canal.">
					comment = <"There is value in identifying the presence of ventilation tubes in all examinations. There is no value in recording the absence of ventilation tubes if they have never been inserted. Note, however, that it is useful to record the extrusion of ventilation tubes if there is a known history of previous ventilation tube insertion.">
				>
				["at0167"] = <
					text = <"In situ">
					description = <"The ventilation tube is located in situ in the tympanic membrane.">
				>
				["at0168"] = <
					text = <"Partially extruded">
					description = <"The ventilation tube is partially extruded from it's original position in the tympanic membrane.">
				>
				["at0169"] = <
					text = <"Extruded - remains in canal">
					description = <"The ventilation tube has been extruded from the tympanic membrane but is visualised within the external auditory canal.">
				>
				["at0170"] = <
					text = <"Extruded - missing">
					description = <"The ventilation tube has been extruded from the tympanic membrane and is not visualised anywhere within the external auditory canal.">
				>
				["at0171"] = <
					text = <"Unknown">
					description = <"The position of the ventilation tube cannot be determined because the tympanic membrane is obscured and the ventilation tube is not observed within the external auditory canal.">
				>
				["at0172"] = <
					text = <"Image">
					description = <"Multimedia image taken during the physical examination of the external auditory canal and tympanic membrane.">
				>
				["at0173"] = <
					text = <"Comments">
					description = <"Additional narrative about the physical examination findings of the external auditory canal and tympanic membrane, not captured in other fields.">
				>
				["at0175"] = <
					text = <"Tympanosclerosis present">
					description = <"White patches, consistent with tympanosclerosis, are observed on surface of the tympanic membrane.">
				>
				["at0180"] = <
					text = <"Pars flaccida">
					description = <"The tympanic membrane perforation is located in the pars flaccida.">
				>
				["at0181"] = <
					text = <"Pars tensa">
					description = <"The tympanic membrane perforation is located in the pars tensa.">
				>
				["at0182"] = <
					text = <"Indeterminate">
					description = <"It is not possible to tell if the tympanic membrane is intact or perforated.">
				>
			>
		>
	>
